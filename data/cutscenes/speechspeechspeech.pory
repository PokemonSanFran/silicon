const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA
const KAI = 1
const TALA = 3
const MAGNUS = 2
const FRANK = 6
const ELLEN = 5
const VIGRIM = 7
const ADAORA = 8
const GRUNTA = 9
const GRUNTB = 10
const GRUNTC = 11
const GRUNTD = 12

const FLAG_HIDE_KAI = FLAG_TEMP_1
const FLAG_HIDE_ELITES = FLAG_TEMP_2
const FLAG_HIDE_GARBODOR = FLAG_TEMP_4
const FLAG_HIDE_FRANK = FLAG_TEMP_5
const FLAG_HIDE_VIGRIM = FLAG_TEMP_6
const FLAG_HIDE_GRUNTS = FLAG_TEMP_7
const FLAG_HIDE_ELLEN = FLAG_TEMP_8
const FLAG_HIDE_ADAORA = FLAG_TEMP_9

script SpeechSpeechSpeech_MoveObject_Script {
    setflag(FLAG_HIDE_GARBODOR)
    setflag(FLAG_HIDE_ELLEN)

    setobjectxy(KAI,10,8)
    setobjectxyperm(KAI,10,8)
    setobjectmovementtype(KAI,MOVEMENT_TYPE_FACE_UP)

    setobjectxy(TALA,7,3)
    setobjectxyperm(TALA,7,3)

    setobjectxy(MAGNUS,8,3)
    setobjectxyperm(MAGNUS,8,3)
}

script SpeechSpeechSpeech_MovePlayer_Script {
    setobjectxy(PLAYER,9,9)
        TeleportCamera(9,9)
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(PLAYER)

        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,13,8,8)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,14,11,8)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,15,7,9)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,16,9,10)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,17,10,9)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,18,7,10)

        call(SpeechSpeechSpeech_ResetFacing_Script)
}

script SpeechSpeechSpeech_CrowdLookAround_Script {
        turnvobject(13,DIR_WEST)
        delay(16)
        turnvobject(15,DIR_EAST)
        delay(16)
        turnvobject(16,DIR_EAST)
        delay(16)
        turnvobject(18,DIR_EAST)
        delay(16)
}

script SpeechSpeechSpeech_ResetFacing_Script {
        turnvobject(13,DIR_NORTH)
        turnvobject(14,DIR_NORTH)
        turnvobject(15,DIR_NORTH)
        turnvobject(16,DIR_NORTH)
        turnvobject(17,DIR_NORTH)
        turnvobject(18,DIR_NORTH)
}

script SpeechSpeechSpeech_ProjectAssignment_ObjectSetUp_Script {

    setflag(FLAG_HIDE_GARBODOR)
    setflag(FLAG_HIDE_KAI)
    setflag(FLAG_HIDE_VIGRIM)

    setobjectxy(TALA,11,17)
    setobjectxyperm(TALA,11,17)
    setobjectmovementtype(TALA,MOVEMENT_TYPE_FACE_DOWN)

    setobjectxy(MAGNUS,17,15)
    setobjectxyperm(MAGNUS,17,15)
    setobjectmovementtype(MAGNUS,MOVEMENT_TYPE_FACE_LEFT)

    setobjectxy(ADAORA,16,15)
    setobjectxyperm(ADAORA,16,15)
    setobjectmovementtype(ADAORA,MOVEMENT_TYPE_FACE_RIGHT)

    setobjectxy(FRANK,19,6)
    setobjectxyperm(FRANK,19,6)
    setobjectmovementtype(FRANK,MOVEMENT_TYPE_FACE_DOWN)

    setobjectxy(ELLEN,4,6)
    setobjectxyperm(ELLEN,4,6)
    setobjectmovementtype(ELLEN,MOVEMENT_TYPE_FACE_DOWN)

    setobjectmovementtype(GRUNTA,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTA,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTA,11,18)
    setobjectxyperm(GRUNTA,11,18)

    setobjectmovementtype(GRUNTB,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTB,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTB,17,16)
    setobjectxyperm(GRUNTB,17,16)

    setobjectmovementtype(GRUNTC,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTC,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTC,19,7)
    setobjectxyperm(GRUNTC,19,7)

    setobjectmovementtype(GRUNTD,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTD,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTD,4,7)
    setobjectxyperm(GRUNTD,4,7)
}

script SpeechSpeechSpeech_PositionPlayer_Script {
        lock
        startcutscene(JUMPPLAYER_SPEECHSPEECHSPEECH)

        applymovement(PLAYER,Common_Movement_WalkRight)
        applymovement(PLAYER,Common_Movement_WalkRight)
        fadescreen(FADE_TO_BLACK)

        call(SpeechSpeechSpeech_MovePlayer_Script)
        fadescreen(FADE_FROM_BLACK)

        goto(SpeechSpeechSpeech_Intro_Dialogue)
}

script SpeechSpeechSpeech_Intro_Dialogue {

        msgbox(format("Thank you all for coming. For all your hard work. All your support. I have a few announcements for you all."),,SPEAKER_VIGRIM)
        msgbox(format("First of all, I will be taking a leave of absence."),,SPEAKER_VIGRIM)
	closemessage

	call(SpeechSpeechSpeech_CrowdLookAround_Script)
        msgbox(format("Wait what?"),,SPEAKER_ROCKET_MEMBER_A,,EMOTE_SHOCK)
	closemessage

	msgbox(format("As our duly elected second in command, Adaora will succeed me as the Leader of The Tide for the remainder of this term."),,SPEAKER_VIGRIM)
        msgbox(format("Given her track record of service to the cause, she has my full trust and support, and I know you all feel the same."),,SPEAKER_VIGRIM,,EMOTE_HAPPY)
        msgbox(format("If there any objection, speak now!"),,SPEAKER_VIGRIM)
	closemessage 
	call(SpeechSpeechSpeech_CrowdLookAround_Script)
        msgbox(format("Then it is so."),,SPEAKER_VIGRIM)
	closemessage 
        call(SpeechSpeechSpeech_ResetFacing_Script)

        msgbox(format("Thank you for all you’ve done for us, Vigrim, I hope to live up to your example."),,SPEAKER_ADAORA)

        closemessage

        turnvobject(16,DIR_EAST)
        delay(16)
        turnvobject(18,DIR_EAST)
        msgbox(format("Can't wait to see what changes under Adaora! Are we finally going to start going on the offensive?"),SPEAKER_ROCKET_MEMBER_B,TAIL_WHISPER,EMOTE_SHOCK)
        closemessage

        turnvobject(14,DIR_EAST)
        delay(16)
        turnvobject(12,DIR_WEST)
        msgbox(format("How will The Tide feel with a public leader...?"),SPEAKER_ROCKET_MEMBER_B,TAIL_WHISPER,EMOTE_CONFUSE)
        closemessage

        msgbox(format("Yes, Adaora has my full confidence, as well as the backing of…"),,SPEAKER_VIGRIM)

        closemessage
        goto(SpeechSpeechSpeech_StartTransformation_Script)
}

script SpeechSpeechSpeech_StartTransformation_Script {

        clearflag(FLAG_HIDE_FRANK)
        special(SpawnCameraObject)
        applymovement(CAMERA, moves(walk_up * 4, step_end))
        applymovement(VIGRIM, moves(walk_up * 3, step_end))
        waitmovement(CAMERA)
        delay(32)
        applymovement(VIGRIM, moves(face_left, face_down, face_right, face_up, face_left, face_down, face_right, face_up, step_end))
        waitmovement(VIGRIM)
        applymovement(ELLEN, moves(face_left, face_down, face_right, face_up, step_end))
        waitmovement(VIGRIM)
        removeobject(VIGRIM)
        clearflag(FLAG_HIDE_ELLEN)
        addobject(ELLEN)
        delay(32)

        applymovement(TALA,SpeechSpeechSpeech_WalkOutElite)
        applymovement(MAGNUS,SpeechSpeechSpeech_WalkOutElite)
        applymovement(FRANK,SpeechSpeechSpeech_WalkOutElite)
        applymovement(ELLEN,SpeechSpeechSpeech_WalkOutElite)
        applymovement(ADAORA, moves(lock_facing_direction, walk_right * 2, unlock_facing_direction, step_end))
        applymovement(CAMERA, moves(walk_down * 3, step_end))
        waitmovement(CAMERA)

        special(RemoveCameraObject)
        goto(SpeechSpeechSpeech_EliteReveal_Dialogue)
}

script SpeechSpeechSpeech_EliteReveal_Dialogue {

        addvar(VAR_STORYLINE_STATE,1)

        msgbox(format("… US! The Elite Four of Resido!"),,SPEAKER_TALA)

        closemessage

        msgbox(format("Holy shit ok, good enough for me!"),,SPEAKER_ROCKET_MEMBER_A)
        closemessage

        call(Earthquake_PlayFake_EventScript)

        playse(SE_PIN)
        applymovement(GRUNTA,Common_Movement_ExclamationMark)
        applymovement(GRUNTB,Common_Movement_ExclamationMark)
        applymovement(GRUNTC,Common_Movement_ExclamationMark)
        applymovement(GRUNTD,Common_Movement_ExclamationMark)
        waitmovement(GRUNTD)

        call(Earthquake_WaitOutShake)

        msgbox(format("Is anybody hurt? …no? Wonderful. Let’s continue. Kai’s restoration project has been going excellently, and we’re going to use that momentum for our next stage. It’s time to take the fight to SharpRise Capital!"),,SPEAKER_ADAORA)

        closemessage

        msgbox(format("Yeah!"),,SPEAKER_ROCKET_MEMBER_A)

        msgbox(format("Hell yes! Woo! Wait, where did Vigrim go?"),,SPEAKER_ROCKET_MEMBER_B,,EMOTE_CONFUSE)
        closemessage
        applymovement(GRUNTD,SpeechSpeechSpeech_LookAround)
        waitmovement(GRUNTD)

        msgbox(format("You all know how companies like Presto and Buzzr have ruined our lives and families. But with the combined popularity of the Champion, Elite Four and our manpower, I think we can hurt these people where it counts: their wallets. We can’t drive these parasitic companies out of the region… but all that money has to go somewhere. Let’s use that money to strengthen the region and create a brighter future for us all."),,SPEAKER_ADAORA)

        closemessage

        msgbox(format("We’re finally doing it…"),,SPEAKER_ROCKET_MEMBER_A)
        closemessage

        applymovement(GRUNTD,SpeechSpeechSpeech_LookAround)
        msgbox(format("This is insane yo. But seriously, did Vigrim just disappear?"),,SPEAKER_ROCKET_MEMBER_B,,EMOTE_CONFUSE)
        closemessage
        waitmovement(GRUNTD)

        applymovement(MAGNUS,Common_Movement_WalkDown)
        waitmovement(MAGNUS)
        //Magnus steps forward. Adaora steps aside.

        msgbox(format("We want to begin by attacking the biggest players in Resido. Each of us will handle a different offensive plan."),,SPEAKER_MAGNUS)

        closemessage

        applymovement(ELLEN,Common_Movement_WalkDown)
        waitmovement(ELLEN)
        //Ellen steps forward.

        msgbox(format("Ellen will lead the charge against Presto."),,SPEAKER_MAGNUS)

        closemessage

        applymovement(ELLEN,SpeechSpeechSpeech_WalkBackwards)
        applymovement(FRANK,Common_Movement_WalkDown)
        waitmovement(FRANK)
        //Ellen steps back. Frank steps forward.

        msgbox(format("Frank will lead the charge against Arriba."),,SPEAKER_MAGNUS)

        closemessage

        applymovement(FRANK,SpeechSpeechSpeech_WalkBackwards)
        applymovement(TALA,Common_Movement_WalkDown)
        waitmovement(TALA)
        //Ellen steps back. Tala steps forward.

        msgbox(format("Tala will lead the charge against Buzzr."),,SPEAKER_MAGNUS)

        closemessage

        applymovement(TALA,SpeechSpeechSpeech_WalkBackwards)
        waitmovement(TALA)

        //Tala steps back.

        msgbox(format("And I’ll be handling negotiations with the government, and operations here. Adaora will be giving each of you assignments under one of the Elite Four members, so listen up. "),,SPEAKER_MAGNUS)

        closemessage

        fadescreen(FADE_TO_BLACK)
        setvar(VAR_ARANTRAZ_STATE,POST_SPEECHSPEECH)

        TeleportCamera(9,9)
        callnative(DestroyVirtualObjects)
        call(SpeechSpeechSpeech_ProjectAssignment_ObjectSetUp_Script)

        applymovement(KAI, moves(walk_left, face_down, step_end))
        fadescreen(FADE_FROM_BLACK)
        waitmovement(KAI)

        //Screen fades to black. Screen fades back in. Elite Four members are spaced around the room, surrounded by groups of TheTide members. Kai walks up to the player.
        msgbox(format("Damn, this shit is so crazy! The air is electric!"),,SPEAKER_KAI_TIDE)

        msgbox(format("It feels really awesome to start making a difference around here."),,SPEAKER_ALICIA)

        msgbox(format("Yeah!"),,SPEAKER_KAI_TIDE)

        if (var(VAR_STORYLINE_STATE) != STORY_RAVE_AND_SPEECH_COMPLETE)
        {
            msgbox(format("Also, are you heading to that party in CurenoPort now? I'll catch up with you in a second, I just gotta talk to Adaora first."),,SPEAKER_KAI_TIDE)
        }
        else
        {
            msgbox(format("By the way, you and I are free to work with whoever we want. Just talk to the Elite Four member you want to team up with. I gotta go, I told Adaora I’d help set up something."),,SPEAKER_KAI_TIDE)
        }
        msgbox(format("Peace!"),,SPEAKER_KAI_TIDE)

        closemessage

        applymovement(KAI, moves(walk_left * 7, step_end))
        waitmovement(KAI)
        removeobject(KAI)
    call(TryAutoSave)
        endcutscene
        release
        end

}

movement SpeechSpeechSpeech_WalkOutElite{
    walk_down * 2
	step_end
}

movement SpeechSpeechSpeech_WalkBackwards{
    lock_facing_direction
    walk_up
    unlock_facing_direction
	step_end
}

movement SpeechSpeechSpeech_LookAround {
    face_left
    delay_16
    face_right
    delay_16
    face_up
    step_end
}
