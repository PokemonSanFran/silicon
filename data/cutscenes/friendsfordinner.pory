const PLAYER = OBJ_EVENT_ID_PLAYER
const KEIYING = 1

script FriendsForDinner_BlockMansionAccess_Script {
    if (var(VAR_FORT_YOBU_STATE) < MET_KEIYING_OUTSIDE_GYM){
        setobjectxy(2,14,11)
            setobjectxyperm(2,14,11)
    }
}

script FriendsForDinner_HideObjects_Script {
    if (var(VAR_FORT_YOBU_STATE) != DEFEATED_NERIENE){
    setflag(FLAG_TEMP_1)
}
return
}

script FriendsForDinner_InteractKeiying_Script {
    lock
        applymovement(KEIYING,Common_Movement_FacePlayer)
        goto(FriendsForDinner_Intro_Dialogue)
}

script FriendsForDinner_Trigger_Script {
    lock
        call(FriendsForDinner_WalkToKeiying_Script)
        applymovement(KEIYING,Common_Movement_FacePlayer)
        goto(FriendsForDinner_Intro_Dialogue)
}

script FriendsForDinner_WalkToKeiying_Script {
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

        switch(var(VAR_TEMP_0)){
            case 7: applymovement(PLAYER,FriendsForDinner_WalkFrom7)
            case 8: applymovement(PLAYER,FriendsForDinner_WalkFrom8)
            case 9: applymovement(PLAYER,FriendsForDinner_WalkFrom9)
            case 10: applymovement(PLAYER,FriendsForDinner_WalkFrom10)
        }
    waitmovement(0)
}


script FriendsForDinner_Intro_Dialogue {
    //PSF TODO change this str_var the player's favorite food, which is defined in the character creation process
    lock
    startcutscene(JUMPPLAYER_FRIENDSFORDINNER)
    bufferitemname(STR_VAR_1,ITEM_LAVA_COOKIE)

    playbgm(MUS_MT_PYRE,FALSE)

    applymovement(KEIYING,Common_Movement_FaceRight)
    waitmovement(KEIYING)

        msgbox(gText_FriendsForDinner_SorryToInterrupt,,SPEAKER_KEIYING)

        closemessage

    applymovement(PLAYER,Common_Movement_FaceLeft)
    waitmovement(PLAYER)

    applymovement(KEIYING,FriendsForDinner_KeiyingWalksToPlayer_Movement)
    waitmovement(KEIYING)

        msgbox(gText_FriendsForDinner_YeahWhatsUp,,SPEAKER_ALICIA)

        msgbox(gText_FriendsForDinner_GladIWasAbleToRun,,SPEAKER_KEIYING)

        msgbox(gText_FriendsForDinner_IGuessYouDoToo,,SPEAKER_ALICIA)

        msgbox(gText_FriendsForDinner_ChiefOperatingOfficer,,SPEAKER_KEIYING)
        reveal(REVEAL_KEI_YING)

        msgbox(gText_FriendsForDinner_IKeepHearingThat,,SPEAKER_ALICIA)

        goto(FriendsForDinner_BadgeJudge_Script)
}

script FriendsForDinner_BadgeJudge_Script {

    special(GetNumberOfBadges)
        if (var(VAR_RESULT) == 8){
            msgbox(gText_FriendsForDinner_FormallSharpriseCapitalongratulate,,SPEAKER_KEIYING)
        } else {
            msgbox(gText_FriendsForDinner_CanBeAHandful,,SPEAKER_KEIYING)
        }
        goto(FriendsForDinner_AcceptInvite_Dialogue)
}

script FriendsForDinner_AcceptInvite_Dialogue {
        msgbox(gText_FriendsForDinner_ItsBeenAnInterestingRide,,SPEAKER_ALICIA)
        msgbox(gText_FriendsForDinner_WaitDidntISeeYou,,SPEAKER_ALICIA)

        msgbox(gText_FriendsForDinner_WhereDidYou,,SPEAKER_KEIYING)
        msgbox(gText_FriendsForDinner_IImagineYoureProbably,,SPEAKER_KEIYING)

        msgbox(gText_FriendsForDinner_ItHasBeenAWhile,,SPEAKER_ALICIA)

        msgbox(gText_FriendsForDinner_WontBeAnIssue,,SPEAKER_KEIYING)

        closemessage

        applymovement(KEIYING,Common_Movement_FaceLeft)
        waitmovement(0)

        msgbox(gText_FriendsForDinner_WestOfHere,,SPEAKER_KEIYING)

        closemessage

        applymovement(KEIYING,Common_Movement_FacePlayer)
        waitmovement(0)

        msgbox(gText_FriendsForDinner_MyHouseHas,,SPEAKER_KEIYING)

        msgbox(gText_FriendsForDinner_IllSeeYouForDinner,,SPEAKER_KEIYING)

        msgbox(gText_FriendsForDinner_YesThanksAgain,,SPEAKER_ALICIA)

        closemessage

        call(FriendsForDinner_WalkAway_Script)
        waitmovement(0)
        fadedefaultbgm
        removeobject(KEIYING)
        setvar(VAR_FORT_YOBU_STATE,MET_KEIYING_OUTSIDE_GYM)
        call(TryAutoSave)
        endcutscene
        release
        end
}

script FriendsForDinner_WalkAway_Script {
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

        if (var(VAR_TEMP_0) == 6){
            applymovement(KEIYING,FriendsForDinner_WalkAwayFrom6)
        } else {
            applymovement(KEIYING,FriendsForDinner_WalkAwayFrom7)
        }
}


text gText_FriendsForDinner_SorryToInterrupt {
    format("Sorry to interrupt - are you {PLAYER}?")
}

text gText_FriendsForDinner_YeahWhatsUp {
    format("Yeah, what's up?")
}

text gText_FriendsForDinner_GladIWasAbleToRun {
    format("Ah, Ramesh was right, glad I was able to run into you. I'm Keiying.")
}

text gText_FriendsForDinner_IGuessYouDoToo {
    format("Ramesh... they work for SharpriseCapital, right? So I guess you do too?")
}

text gText_FriendsForDinner_ChiefOperatingOfficer {
    format("Yes sorry, I'm the SharpriseCapitalChief Operating Officer, and Adelaide's right hand. It's a pleasure to meet you, we're all very impressed with you.")
}

text gText_FriendsForDinner_IKeepHearingThat {
    format("Yeah, I keep hearing that! It's a little overwhelming.")
}

text gText_FriendsForDinner_WaitDidntISeeYou{
    format("Wait, didn't I see you talking to Adaora in CurenoPort the other day?")
}

text gText_FriendsForDinner_WhereDidYou{
    format("Where did you hear that name? You shouldn't repeat that around other members of SharpriseCapital. Besides, that couldn't have been me.")
}

text gText_FriendsForDinner_IKnowYoureRacing{
    format("I know you're racing against the other Trainers, but I'd love to have you over to my place for dinner. I bet you haven't had a home-cooked meal in a while, what do you say? I'm very confident one dinner won't set you back terribly far.")
}

text gText_FriendsForDinner_CanBeAHandful {
    format("So far, you're certainly living up to your title! Congratulations on defeating Neriene, I know they can be... a handful.")
}

text gText_FriendsForDinner_ItsBeenAnInterestingRide {
    format("It's been an interesting ride, but I'm going to keep giving it my all. Thanks.")
}

text gText_FriendsForDinner_IImagineYoureProbably {
    format("I imagine you're probably exhausted. Why don't you come over to my place for dinner? I bet you haven't had a home-cooked meal in a while, what do you say?")
}

text gText_FriendsForDinner_FormallSharpriseCapitalongratulate {
    format("Well, you're certainly living up to your legend. Let me be the first to formally congratulate you on your qualification in the Championship!")
}

text gText_FriendsForDinner_ItHasBeenAWhile {
    format("Oh! You're right, it has been a while, are you sure that's fine on such short notice?")
}

text gText_FriendsForDinner_WontBeAnIssue {
    format("Won't be an issue at all! I'd love to really get to know the prodigy Trainer we're investing in. ")
}

text gText_FriendsForDinner_WestOfHere {
    format("I live in PetarosaBorough, west of here. ")
}

text gText_FriendsForDinner_MyHouseHas {
    format("My house has the teal shutters, it'll be easy to spot. My kids mentoned you love to eat {STR_VAR_1}, so we'll make sure you have that tonight.")
}

text gText_FriendsForDinner_IllSeeYouForDinner {
    format("I'll see you for dinner then?")
}

text gText_FriendsForDinner_YesThanksAgain {
    format("Uh... yeah. Sounds good.")
}

movement FriendsForDinner_WalkFrom7 {
    walk_up
        face_left
        step_end
}

movement FriendsForDinner_WalkFrom8 {
    walk_up * 2
        walk_left
        step_end
}

movement FriendsForDinner_WalkFrom9 {
    walk_up * 3
        walk_left * 2
        step_end
}

movement FriendsForDinner_WalkFrom10 {
    walk_up * 4
        walk_left * 3
        step_end
}

movement FriendsForDinner_WalkAwayFrom6 {
    walk_right
        walk_down * 6
        step_end
}

movement FriendsForDinner_WalkAwayFrom7 {
    walk_down * 6
        step_end
}

movement FriendsForDinner_KeiyingWalksToPlayer_Movement{
    walk_right * 3
    step_end
}
