const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_1

script BeachBattle_ObjectVisibility_Script
{
    if (var(VAR_BEACH_BATTLE_STATE) != SAVE_CHARLOTTE_BEACH)
    {
        call(BeachBattle_HideCharlotte_Script)
    }
    else
    {
        call(BeachBattle_BattleSetUp_Script)
    }
}

script BeachBattle_HideCharlotte_Script
{
    setflag(FLAG_HIDE_CHARLOTTE)
}

script BeachBattle_BattleSetUp_Script
{
    setobjectxyperm(CHARLOTTE,10,24)
}

script BeachBattle_CharlotteWalkToPlayer
{
    lock
        msgbox(format("Hey, {PLAYER}!!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
        closemessage

        playse(SE_PIN)
        clearflag(FLAG_HIDE_CHARLOTTE)
        addobject(CHARLOTTE)

        applymovement(PLAYER, moves(emote_exclamation_mark, face_down, step_end))
        waitmovement(0)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        applymovement(CHARLOTTE, moves(walk_up * 2, walk_right * 2, walk_up * 2, walk_right * 2, walk_up * 2, walk_right * 2, face_up, step_end))
        waitmovement(0)
        goto(BeachBattle_Approach_Dialogue)
}

script BeachBattle_Interact_Script
{
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        faceplayer
        goto(BeachBattle_StartBattle_Script)
}

script BeachBattle_Approach_Dialogue
{
        setvar(VAR_BEACH_BATTLE_STATE,SAVE_CHARLOTTE_BEACH)
        setobjectxyperm(CHARLOTTE,10,24)
        msgbox(format("I'm heading north for my last Badge. How's your challenge holding up?"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)

        special(GetNumberOfBadges)

        switch (var(VAR_RESULT)) {
            case 8: msgbox(format("Oh shit! You beat me to eight badges - guess I underestimated you!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE,TAIL_TALK,EMOTE_SHOCK)
            case 7: goto(BeachBattle_SevenBadges_Dialogue)
            case 6: msgbox(format("Two left, that's what I figured."),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
            case 5: msgbox(format("Hmm, only five? Guessed you'd be further along by now!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
            case 4: goto(BeachBattle_FourBadges_Dialogue)
        }
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_SevenBadges_Dialogue
{
    msgbox(format("Oh nice, last one too!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE,TAIL_TALK,EMOTE_HAPPY)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_FourBadges_Dialogue
{
        msgbox(format("{PLAYER}, I'm not sure that slow and steady always win the race… you gotta pick it up!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)

        msgbox(format("I've been a little… distracted! But I'll make it up for sure, don't worry."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_SaveProgress
{
    closemessage
    startmenu_savescreen
    goto(BeachBattle_StartBattle_Script)
}

script BeachBattle_StartBattle_Script
{
    lock
        msgbox(format("My brain's scrambled from running around all day. Bet I can still beat you, though! See if you can even make me sweat!"),MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
        closemessage
        trainerbattle_no_intro(TRAINER_CHARLOTTE_BEACHBATTLE,format("I'm legit shook. You're even stronger than I thought!"))
        closemessage

        goto(BeachBattle_PostBattle_Dialogue)
}

script BeachBattle_PostBattle_Dialogue
{
    lock
        startcutscene(JUMPPLAYER_BEACHBATTLE)
        addvar(VAR_STORYLINE_STATE,1)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        setvar(VAR_BEACH_BATTLE_STATE,POST_BEACH_BATTLE)

        msgbox(format("Well, I'm not surprised you've already got seven Badges, that was tough!"),,SPEAKER_ALICIA)

        msgbox(format("Ugh, I should be stronger than that."),,SPEAKER_CHARLOTTE)

        if (flag(FLAG_BADGE05_GET)){
            msgbox(format("If it takes a team as good as yours to beat Neriene, then I might need to rethink things…"),,SPEAKER_CHARLOTTE,TAIL_WHISPER)
        }

    msgbox(format("Next time, I'll have you figured out!"),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(CHARLOTTE, moves(walk_down * 7, step_end))
        waitmovement(0)
        fadedefaultbgm
        removeobject(CHARLOTTE)
        call(TryAutoSave)
        endcutscene
        release
        end
}

