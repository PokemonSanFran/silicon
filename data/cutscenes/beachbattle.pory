const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_1

script BeachBattle_ObjectVisibility_Script
{
    if (var(VAR_BEACH_BATTLE_STATE) != SAVE_CHARLOTTE_BEACH)
    {
        call(BeachBattle_HideCharlotte_Script)
    }
    else
    {
        call(BeachBattle_BattleSetUp_Script)
    }
}

script BeachBattle_HideCharlotte_Script
{
    setflag(FLAG_HIDE_CHARLOTTE)
}

script BeachBattle_BattleSetUp_Script
{
    setobjectxyperm(CHARLOTTE,10,24)
}

script BeachBattle_CharlotteWalkToPlayer
{
    lock
        msgbox(gText_BeachBattle_HeyPlayer,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
        closemessage

        playse(SE_PIN)
        clearflag(FLAG_HIDE_CHARLOTTE)
        addobject(CHARLOTTE)

        applymovement(PLAYER,BeachBattle_ExclaimAndTurn)
        waitmovement(0)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        applymovement(CHARLOTTE,BeachBattle_ApproachFromSouth)
        waitmovement(0)
        goto(BeachBattle_Approach_Dialogue)
}

script BeachBattle_Interact_Script
{
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        faceplayer
        goto(BeachBattle_StartBattle_Script)
}

script BeachBattle_Approach_Dialogue
{
        setvar(VAR_BEACH_BATTLE_STATE,SAVE_CHARLOTTE_BEACH)
        setobjectxyperm(CHARLOTTE,10,24)
        msgbox(gText_BeachBattle_ImHeadingNorth,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)

        special(GetNumberOfBadges)

        switch (var(VAR_RESULT)) {
            case 8: msgbox(gText_BeachBattle_OhShit,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE,TAIL_TALK,EMOTE_SHOCK)
            case 7: goto(BeachBattle_SevenBadges_Dialogue)
            case 6: msgbox(gText_BeachBattle_TwoLeft,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
            case 5: msgbox(gText_BeachBattle_HmmOnlyFive,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
            case 4: goto(BeachBattle_FourBadges_Dialogue)
        }
        goto(BeachBattle_SaveProgress)
}


script BeachBattle_SevenBadges_Dialogue
{
    msgbox(gText_BeachBattle_OhNiceLastOne,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE,TAIL_TALK,EMOTE_HAPPY)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_FourBadges_Dialogue
{
        msgbox(gText_BeachBattle_ImNotSureThatSlow,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)

        msgbox(gText_BeachBattle_IveBeenALittleDistracted,MSGBOX_DEFAULT,SPEAKER_ALICIA)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_SaveProgress
{
    closemessage
    startmenu_savescreen
    goto(BeachBattle_StartBattle_Script)
}

script BeachBattle_StartBattle_Script
{
    lock
        msgbox(gText_BeachBattle_MyBrainIsScrambled,MSGBOX_DEFAULT,SPEAKER_CHARLOTTE)
        closemessage
        trainerbattle_no_intro(TRAINER_CHARLOTTE_BEACHBATTLE,gText_BeachBattle_ImLegitShook)
        closemessage

        goto(BeachBattle_PostBattle_Dialogue)
}

script BeachBattle_PostBattle_Dialogue
{
    lock
        startcutscene(JUMPPLAYER_BEACHBATTLE)
        addvar(VAR_STORYLINE_STATE,1)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        setvar(VAR_BEACH_BATTLE_STATE,POST_BEACH_BATTLE)

        msgbox(gText_BeachBattle_WellImNotSurprised,,SPEAKER_ALICIA)

        msgbox(gText_BeachBattle_UghIShouldBe,,SPEAKER_CHARLOTTE)

        if (flag(FLAG_BADGE05_GET)){
            msgbox(gText_BeachBattle_IfItTakesATeam,,SPEAKER_CHARLOTTE,TAIL_WHISPER)
        }

    msgbox(gText_BeachBattle_NextTimeIllHave,,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(CHARLOTTE,BeachBattle_WalkAway)
        waitmovement(0)
        fadedefaultbgm
        removeobject(CHARLOTTE)
        call(TryAutoSave)
        endcutscene
        release
        end
}

text gText_BeachBattle_HeyPlayer
{
    format("Hey, {PLAYER}!!")
}

text gText_BeachBattle_ImHeadingNorth
{
    format("I'm heading north for my last Badge. How's your challenge holding up?")
}

text gText_BeachBattle_OhShit
{
    format("Oh shit! You beat me to eight badges - guess I underestimated you!")
}

text gText_BeachBattle_OhNiceLastOne
{
    format("Oh nice, last one too!")
}

text gText_BeachBattle_TwoLeft
{
    format("Two left, that's what I figured.")
}

text gText_BeachBattle_HmmOnlyFive
{
    format("Hmm, only five? Guessed you'd be further along by now!")
}

text gText_BeachBattle_ImNotSureThatSlow
{
    format("{PLAYER}, I'm not sure that slow and steady always win the race… you gotta pick it up!")
}

text gText_BeachBattle_IveBeenALittleDistracted
{
    format("I've been a little… distracted! But I'll make it up for sure, don't worry.")
}

text gText_BeachBattle_MyBrainIsScrambled
{
    format("My brain is scrambled from running around all day. Let's see if I can beat you without breaking a sweat!")
}

text gText_BeachBattle_ImLegitShook
{
    format("I'm legit shook. You're even stronger than I thought!")
}

text gText_BeachBattle_WellImNotSurprised
{
    format("Well, I'm not surprised you've already got seven Badges, that was tough!")
}

text gText_BeachBattle_UghIShouldBe
{
    format("Ugh, I should be stronger than that.")
}

text gText_BeachBattle_IfItTakesATeam
{
    format("If it takes a team as good as yours to beat Neriene, then I might need to rethink things…")
}

text gText_BeachBattle_NextTimeIllHave
{
    format("Next time, I'll have you figured out!")
}

movement BeachBattle_ExclaimAndTurn
{
    emote_exclamation_mark
        face_down
        step_end
}
movement BeachBattle_ApproachFromSouth
{
    walk_up * 2
    walk_right * 2
    walk_up * 2
    walk_right * 2
    walk_up * 2
    walk_right * 2
    face_up
    step_end
}
movement BeachBattle_WalkAway
{
    walk_down * 7
    step_end
}
