const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const ADAORA = 2
const VIGRIM = 3
const BAIYA = 4
const PKMNA = 5
const PKMNB = 6
const FLAG_HIDE_POLICE = FLAG_TEMP_1
const FLAG_HIDE_VIGRIM_ADAORA = FLAG_TEMP_2
const FLAG_HIDE_BAIYA = FLAG_TEMP_3
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_4

script WhyAreYouHelpingThem_HideObject_Script {
    setflag(FLAG_HIDE_BAIYA)
}

script WhyAreYouHelpingThem_ObjectCleanUp_Script {
    setflag(FLAG_HIDE_CHARLOTTE)
    setflag(FLAG_HIDE_BAIYA)
    setflag(FLAG_HIDE_VIGRIM_ADAORA)
    setflag(FLAG_HIDE_POLICE)
}

script WhyAreYouHelpingThem_MoveObject_Script {
    setobjectxy(CHARLOTTE,2,16)
    setobjectxyperm(CHARLOTTE,2,16)
    setflag(FLAG_HIDE_BAIYA)
    setflag(FLAG_HIDE_POLICE)
}

script WhyAreYouHelpingThem_MoveObjectBoss_Script {
    setobjectxy(CHARLOTTE,2,16)
    setobjectxyperm(CHARLOTTE,2,16)
    setobjectxy(VIGRIM,7,3)
    setobjectxyperm(VIGRIM,7,3)
}

script WhyAreYouHelpingThem_MoveObjectAdaora_Script {
    setflag(FLAG_HIDE_VIGRIM_ADAORA)
    setobjectxy(ADAORA,1,9)
    setobjectxyperm(ADAORA,1,9)
    setobjectxy(CHARLOTTE,2,16)
    setobjectxyperm(CHARLOTTE,2,16)
    turnobject(ADAORA,DIR_NORTH)
}

script WhyAreYouHelpingThem_BlowHoleWall {
    setmetatile(12,1,0x3F2,FALSE)
    special(DrawWholeMapView)
}

script WhyAreYouHelpingThem_ObjectSetUpPostSave_Script {
    setflag(FLAG_HIDE_POLICE)
    setflag(FLAG_HIDE_VIGRIM_ADAORA)
    setobjectxy(BAIYA,6,3)
    setobjectxyperm(BAIYA,6,3)
    turnobject(BAIYA,DIR_WEST)
    setobjectxy(CHARLOTTE,2,16)
    setobjectxyperm(CHARLOTTE,2,16)
    return
}

script WhyAreYouHelpingThem_ObjectDebriefSetUp_Script {
    setflag(FLAG_HIDE_POLICE)
    setflag(FLAG_HIDE_BAIYA)
    setflag(FLAG_HIDE_CHARLOTTE)
    setobjectxyperm(4,26,4)
    setobjectxyperm(5,25,6)
    setobjectxyperm(6,27,6)
    setobjectxyperm(7,25,7)
    setobjectxyperm(8,27,7)
    return
}

script WhyAreYouHelpingThem_BackAtCompound_ObjectSetUp_Script {
    clearflag(FLAG_HIDE_POLICE)
    clearflag(FLAG_HIDE_VIGRIM_ADAORA)
    setflag(FLAG_HIDE_BAIYA)

    setobjectxy(1,6,6)
    setobjectxy(2,6,5)
    setobjectxyperm(1,6,6)
    setobjectxyperm(2,6,5)
    turnobject(2,DIR_SOUTH)

    setobjectmovementtype(2,MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(1,MOVEMENT_TYPE_FACE_UP)
    return
}

script WhyAreYouHelpingThem_StartRaid {
    lock
    setvar(VAR_NAVAL_BASE_STATE,NAVAL_BASE_RAID_START)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    applymovement(CHARLOTTE,Common_Movement_FaceDown)
    applymovement(7,WhyAreYouHelpingThem_PoliceRunIn)
    applymovement(8,WhyAreYouHelpingThem_PoliceRunIn)
    applymovement(9,WhyAreYouHelpingThem_PoliceRunIn)
    waitmovement(0)
    msgbox(format("STRUCTURAL BREACH DETECTED. BASE IS NOW SHIFTING TO DEFENSE MODE. EVACUATE IMMEDIATELY."),MSGBOX_AUTOCLOSE,SPEAKER_SPEAKER)
    waitmovement(0)

    msgbox(format("“Defense mode” is pretty advanced for some abandoned buildings."),,SPEAKER_CHARLOTTE)

    msgbox(format("We should redefine abandoned…"),,SPEAKER_ALICIA)

    msgbox(format("Let's split up. I'm faster than you and can cut off the leaders before they escape."),,SPEAKER_CHARLOTTE)

    msgbox(format("wha - fine, I'll try to find the source of the hack and shut it down."),,SPEAKER_ALICIA)

    msgbox(format("See ya."),,SPEAKER_CHARLOTTE)

    closemessage

    applymovement(CHARLOTTE,WhyAreYouHelpingThem_PoliceRunIn)
    waitmovement(0)
    removeobject(7)
    removeobject(8)
    removeobject(9)
    call(WhyAreYouHelpingThem_MoveObject_Script)
    call(TryAutoSave)
    release
    end
}

script WhyAreYouHelpingThem_WitnessBattle {
    setvar(VAR_NAVAL_BASE_STATE,CHASE_VIGRIM)
        lock
        applymovement(VIGRIM, moves(walk_up * 4, step_end))
        waitmovement(0)
        call(WhyAreYouHelpingThem_MoveObjectBoss_Script)

        msgbox(format("Go after Vigrim! I'll hold this one off."),,SPEAKER_CHARLOTTE)

        closemessage
        release
        end
}

script WhyAreYouHelpingThem_PreBattleBoss_Script {
    lock
        applymovement(VIGRIM,Common_Movement_FaceLeft)
        applymovement(PLAYER, moves(walk_up * 2, walk_right * 4, step_end))
        waitmovement(0)
        msgbox(format("It's a shame that the police have totally infiltrated our youth. "),,SPEAKER_VIGRIM)

        closemessage

        applymovement(VIGRIM,Common_Movement_WalkLeft)
        waitmovement(0)

        msgbox(format("But beating you is going to be the best route out of here."),,SPEAKER_VIGRIM)

        closemessage

        trainerbattle_continue_after_lose(TRAINER_VIGRIM,format("Wait a minute…"))
        goto(WhyAreYouHelpingThem_PostBattleBoss_Dialogue)
}

script WhyAreYouHelpingThem_PostBattleBoss_Dialogue{
        setvar(VAR_NAVAL_BASE_STATE,SAVE_BAIYA_NAVAL_BASE)
        addobject(ADAORA)
        call(WhyAreYouHelpingThem_MoveObjectAdaora_Script)

        if (defeated(TRAINER_VIGRIM)){

                msgbox(format("You're incredible. Or a cheater. Can't quite tell. Either way, you can have this."),,SPEAKER_VIGRIM)
                closemessage

                applymovement(VIGRIM,Common_Movement_WalkInPlaceLeft)
                waitmovement(0)
                giveitem(ITEM_MASTER_BALL,1)

        }else {

                msgbox(format("…I didn't even touch them…"),,SPEAKER_ALICIA)
        }

        msgbox(format("I'm sorry you have to experience this. I hope you eventually understand what's at stake here, {PLAYER}. Let me help you."),,SPEAKER_VIGRIM)

        closemessage

        applymovement(ADAORA, moves(walk_up * 4, walk_right * 5, walk_up, step_end))
        applymovement(VIGRIM, moves(walk_in_place_slow_left, delay_16, delay_16, face_down, step_end))
        waitmovement(0)
        call(Common_EventScript_OutOfCenterPartyBypassHeal)

        msgbox(format("I handled the brat. I'm guessing you beat -"),,SPEAKER_ADAORA)

        closemessage

        playse(SE_PIN)
        waitse
        applymovement(ADAORA, moves(emote_exclamation_mark, face_left, step_end))
        waitmovement(0)

        msgbox(format("Damn, you too, {PLAYER}?"),,SPEAKER_ADAORA)

        msgbox(format("What was all that shit you tried to tell me before?! What the hell is The Tide doing?"),,SPEAKER_ALICIA)

        msgbox(format("We've got to help more comrades escape. Another time."),,SPEAKER_VIGRIM)

        closemessage

        applymovement(VIGRIM,WhyAreYouHelpingThem_BeginEscape)
        applymovement(ADAORA,WhyAreYouHelpingThem_BeginEscape)
        waitmovement(0)
        goto(WhyAreYouHelpingThem_ExplodeWall_Script)
}

script WhyAreYouHelpingThem_ExplodeWall_Script {

    playse(SE_M_EXPLOSION)
        waitse
        setvar(VAR_0x8004,1)
        setvar(VAR_0x8005,2)
        setvar(VAR_0x8006,8)
        setvar(VAR_0x8007,5)
        special(ShakeCamera)
        waitstate
        call(WhyAreYouHelpingThem_BlowHoleWall)
        clearflag(FLAG_HIDE_BAIYA)
        playse(SE_EXIT)
        waitse
        addobject(BAIYA)
        applymovement(BAIYA, moves(walk_down, walk_left * 3, face_down, step_end))
        waitmovement(0)

        msgbox(format("This place is swarming with cops. You two need to get the hell out, right now. This goes straight out."),,SPEAKER_BAIYA_TIDE)

        msgbox(format("We can't leave people behind. "),,SPEAKER_VIGRIM)

        msgbox(format("We don't have time for this! If you get arrested, this all falls apart! You. Need. To. Go."),,SPEAKER_BAIYA_TIDE)

        msgbox(format("I think they're right."),,SPEAKER_ADAORA)

        closemessage
        applymovement(VIGRIM, moves(walk_right * 3, walk_up * 2, step_end))
        applymovement(ADAORA, moves(walk_right * 3, walk_up * 3, step_end))
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(ADAORA)
        removeobject(VIGRIM)
        applymovement(BAIYA, moves(walk_down, walk_left * 3, step_end))
        waitmovement(0)

        msgbox(format("You're working with The Tide too?!?"),,SPEAKER_ALICIA)
        closemessage
        startmenu_savescreen
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_InteractBattle_Script {
    lock
        faceplayer
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_StartBattle_Script{
    msgbox(format("Always have been."),,SPEAKER_BAIYA_TIDE)
    closemessage
    trainerbattle_no_intro(TRAINER_BAIYA_WHYAREYOUHELPINGTHEM,format("Another victory for the establishment.")) //PSF TODO EMOTE sigh
    goto(WhyAreYouHelpingThem_PostBattle_Dialogue)
}

script WhyAreYouHelpingThem_PostBattle_Dialogue {
        startcutscene(JUMPPLAYER_WHYAREYOUHELPINGTHEM)

        setvar(VAR_NAVAL_BASE_STATE,DEFEATED_BAIYA_NAVAL_BASE)
        msgbox(format("Adaora tells me you're all good guys… now all this shit? And you? None of this makes any sense!"),,SPEAKER_ALICIA)

        msgbox(format("Why would it? You don't see what your bosses are doing to Resido. You don't see what you're doing to Resido."),,SPEAKER_BAIYA_TIDE)

        msgbox(format("{PLAYER}!"),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(PLAYER,Common_Movement_FaceLeft)
        applymovement(BAIYA, moves(walk_right * 6, walk_up * 2, step_end))
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(BAIYA)
        setobjectxyperm(CHARLOTTE,1,8)
        addobject(CHARLOTTE)
        applymovement(CHARLOTTE, moves(walk_up * 5, walk_right * 3, step_end))
        waitmovement(0)

        msgbox(format("I got knocked out. Did you stop Vigrim?"),,SPEAKER_CHARLOTTE)

        msgbox(format("No, I lost too."),,SPEAKER_ALICIA)

        closemessage
        setvar(VAR_HALAI_ISLAND_STATE,WHY_ARE_YOU_HELPING_THEM)
        fadeblackandsetremovewarpfadeflag
        warpsilent(MAP_HALAI_ISLAND,255,26,6)
}

script WhyAreYouHelpingThem_MissionDebrief_Dialogue {
        setvar(VAR_HALAI_ISLAND_STATE,SLEPT_AFTER_RAID) //advance HalaiIsland forward to clean up raid damage
        setvar(VAR_STORYLINE_STATE,STORY_COMPLETED_NAVAL_BASE)
        setvar(VAR_PLAYER_HOME_STATE,POST_NAVAL_BASE_RAID)
        lock
        applymovement(PLAYER,Common_Movement_FaceUp)

        msgbox(format("Great work in there, team. Andre, what's the sitrep regarding The Tide leadership?"))
        closemessage

        applymovement(6,Common_Movement_WalkUp)
        waitmovement(0)

        msgbox(format("We spotted Vigrim and their lieutenant escaping on Pokemon by using the move Fly. We were unable to track them given our resources."))
        closemessage

        applymovement(6, moves(lock_facing_direction, walk_down, unlock_facing_direction, step_end))
        waitmovement(0)

        msgbox(format("A shame. Still, we've shut down their primary base of operations, and we arrested a bunch of these shitheads. This was an absolute win!"))
        closemessage

        applymovement(4,Common_Movement_WalkDown)
        waitmovement(0)

        msgbox(format("Not bad in there. Oliver wants you kids back at CuconuTown Compound."))

        msgbox(format("{PLAYER}, I'm gonna head over in a bit. Wanna come?"),MSGBOX_YESNO,,SPEAKER_CHARLOTTE)
        endcutscene

        if (var(VAR_RESULT) == YES){
            closemessage
            fadescreen(FADE_TO_BLACK)
            warpsilent(MAP_CUCONU_TOWN_SHARPRISE_COMPOUND_1F,0)
        } else {
            closemessage
                release
                end
        }

    }

script WhyAreYouHelpingThem_BackAtCompound_Dialogue {
        lock
        startcutscene(JUMPPLAYER_WHYAREYOUHELPINGTHEM)
        setvar(VAR_PLAYER_HOME_STATE,NEXT_DAY_CHAMPIONSHIP)
        applymovement(PLAYER, moves(walk_up * 3, step_end))
        waitmovement(0)

        msgbox(format("What… a day. I know that was a lot of pressure, but from what I hear, you were both fantastic. Thank you."),,SPEAKER_OLIVER)

        msgbox(format("…it was a day."),,SPEAKER_ALICIA)

        msgbox(format("Good news - while you were training in LeaverraForest, construction on SharpRise Stadium finished up. The Group Stage will start in 3 days. Hopefully, that's enough time to rest up and get ready."),,SPEAKER_OLIVER)

        msgbox(format("Oh, that was fast. How did you…"),,SPEAKER_CHARLOTTE)

        msgbox(format("Don't worry about it! You two just focus on winning! "),,SPEAKER_OLIVER)

        msgbox(format("Now, I've got a lot of work to do between now and then. I'll see you both in a few days!"),,SPEAKER_OLIVER)

        closemessage

        applymovement(2, moves(walk_right, walk_down * 4, walk_left, walk_in_place_down, face_down, step_end))
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(2)

        msgbox(format("We've all got some work to do! I'll see you around, {PLAYER}."),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(CHARLOTTE, moves(walk_down * 3, step_end))
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(1)
        endcutscene
        release
        end
}

script WhyAreYouHelpingThem_StartChampionship_Dialogue {
    msgbox(format("Are you ready to begin the Championship?"),MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO){
            closemessage
                return
        } else {
            setvar(VAR_STORYLINE_STATE,STORY_START_CHAMPIONSHIP)
                fadescreen(FADE_TO_BLACK)
                warpsilent(MAP_SHARPRISE_STADIUM_LOCKERS,9,6)
        }
}

movement WhyAreYouHelpingThem_PoliceRunIn {
    walk_left * 8
        step_end
}

movement WhyAreYouHelpingThem_BeginEscape {
    walk_right * 3
        step_end
}

movement WhyAreYouHelpingThem_ApproachBossAndPlayer {
    walk_left
        walk_up * 8
        step_end
}

movement WhyAreYouHelpingThem_WalkToPlayerFormation {
    walk_right
        walk_down
        step_end
}

