const PLAYER = OBJ_EVENT_ID_PLAYER
const BELEN = 1

script EnterBelen_StartGymBattle_ObjectSetup_Script {
    if (var (VAR_GYM_1_STATE)== GYM_SAVE_BEFORE_LEADER) {
        setobjectxyperm(BELEN, 6, 5)
        setobjectmovementtype(BELEN, MOVEMENT_TYPE_FACE_LEFT)
    }
    return
}

script EnterBelen_PutMeatInOven_Dialogue {
  lockall
  setvar(VAR_GYM_1_STATE,GYM_SAVE_BEFORE_LEADER)
  msgbox(format("Hold on! I've got one brushing of Pinap glaze, and then into the oven..."),,SPEAKER_BELEN)
    closemessage
  applymovement(BELEN, Common_Movement_Delay48)
  waitmovement(0)
  applymovement(BELEN, Common_Movement_WalkInPlaceUp)
  waitmovement(0)
  closemessage
  msgbox(format("Cool, cake's in the oven. "), MSGBOX_NPC,SPEAKER_BELEN)
    closemessage
  call(EnterBelen_DetermineMovementToBattlefield_Script)
  goto(EnterBelen_Intro_Dialogue)
}

script EnterBelen_DetermineMovementToBattlefield_Script {
  getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
  if (var (VAR_TEMP_1) == 2) {
    applymovement(BELEN, moves(walk_down * 3, walk_left * 2, face_left, step_end))

    if (var (VAR_TEMP_0) == 7) {
      applymovement(PLAYER, moves(delay_16, delay_16, delay_16, walk_left * 4, walk_down * 3, face_right, step_end))
    } else {
      applymovement(PLAYER, moves(delay_16, delay_16, delay_16, walk_left * 6, walk_down * 3, face_right, step_end))
    }
  } else {
    applymovement(BELEN, moves(walk_left * 2, walk_down * 3, face_left, step_end))
    applymovement(PLAYER, moves(delay_16, delay_16, delay_16, walk_left * 5, walk_down * 2, face_right, step_end))
  }
  waitmovement(0)
}

script EnterBelen_Intro_Dialogue
{
  lock
  msgbox(format("Ticket said you're {PLAYER}, right? I'm Belen, head baker of Poc-Chuc and the Gym Leader here. After you lose, I'll treat you to my latest creation: crepe cake with a Pinap glaze. It's my first time making it, but from that amazing smell, it's gonna be great."),,SPEAKER_BELEN)
  reveal(REVEAL_BELEN)
  msgbox(format("Did you know that a lot of your sense of taste actually is because of your sense of smell? All of your senses work together to create your culinary experiences. I'll show how I do that in battle too! Let's see if I can overwhelm your palette before my cake burns!"),,SPEAKER_BELEN,,EMOTE_HAPPY)
  startmenu_savescreen
  goto(EnterBelen_StartGymBattle_Script)
}

script EnterBelen_StartGymBattle_Script
{
    lock
    faceplayer
    msgbox(format("I think you talk more about food than you do battling..."),,SPEAKER_ALICIA,,EMOTE_SWEAT)
    closemessage
    trainerbattle_no_intro(TRAINER_BELEN,format("Woah, super sour! I'll add some Micle Berry."))
    goto(EnterBelen_BelenDefeated_Script)
}

script EnterBelen_BelenDefeated_Script
{
    lockall
    cleargymtrainersandgetbadge(GYM_BELEN)
    setvar(VAR_GYM_1_STATE,GYM_DEFEATED_LEADER)
    setvar(VAR_BELEN_STATE,BATTLE_1_COMPLETE)
    addvar(VAR_STORYLINE_STATE, 1)
    call(WowYoureStrong_CheckCutsceneScript)
    msgbox(format("That was delicious! I'm absolutely satiated."),,SPEAKER_BELEN)
    closemessage
    call(EnterBelen_ExplainBuzzr_Dialogue)
    tweetfanfare(TWEET_GYM_MERMEREZA_CITY_BADGE,MUS_OBTAIN_BADGE)
    giveitem(ITEM_TM01)
    msgbox(format("Oh, the cake!"),,SPEAKER_BELEN)
    closemessage
    call(EnterBelen_DetermineMovementBackToStove_Script)
    waitmovement(0)
    setvar(VAR_TEMP_0,0)
    call(TryAutoSave)
    call(AaandWereBack_Monologue)
    releaseall
    end
}

script EnterBelen_ExplainBuzzr_Dialogue{
    special(GetNumberOfBadges)

    if(var(VAR_RESULT) != 1)
    {
        return
    }

    msgbox(format("I'll serve up that tweet now!"),,SPEAKER_BELEN)
    msgbox(format("What's a tweet?"),,SPEAKER_ALICIA)
    msgbox(format("You haven't tasted the wonders of Buzzr?"),,SPEAKER_BELEN)
    msgbox(format("Everybody in the region uses the app! You can send messages and post updates about your world. In the SharpRise Capital League, when you defeat a Gym Leader, we celebrate with a post on Buzzr! When all the leaders have posted about you, you've qualified for the Championship."),,SPEAKER_BELEN)
    call(EnterBelen_DownloadBuzzr_Dialogue)
    msgbox(format("Now let's try this dish!"),,SPEAKER_BELEN)
    closemessage
    return
}

script EnterBelen_DownloadBuzzr_Dialogue{
    msgbox(format("Let me download it real quick..."),,SPEAKER_ALICIA)
        closemessage
        fadescreen(FADE_TO_BLACK)
        call(EventScript_InstallBuzzrApp)
        fadescreen(FADE_FROM_BLACK)
        return
}

script EnterBelen_DetermineMovementBackToStove_Script {
  getplayerxy(VAR_TEMP_0, VAR_TEMP_1)

  if (var (VAR_TEMP_0) == 7) {
    applymovement(BELEN, moves(emote_exclamation_mark, walk_up * 3, walk_right * 3, walk_right, face_up, walk_left, face_up, step_end))
  } else {
    applymovement(BELEN, moves(emote_exclamation_mark, walk_right * 3, walk_up * 3, walk_right, face_up, walk_left, face_up, step_end))
  }
}

script EnterBelen_PostDefeat1_Dialogue {
  lockall
  setvar(VAR_GYM_1_STATE,GYM_DEFEATED_LEADER)
	bufferitemname(STR_VAR_1,ITEM_TM01)
	buffermovename(STR_VAR_2,MOVE_FLAMETHROWER)
  msgbox(format("STR_VAR_1 is one of my most delicious moves, it allows your Pok√©mon to use STR_VAR_2."),MSGBOX_NPC,SPEAKER_BELEN)
      applymovement(BELEN,Common_Movement_FaceUp)
    waitmovement(0)
  closemessage
  releaseall
  end
}

script EnterBelen_PostDefeat2_Dialogue {
  lockall
  msgbox(format("Let's bake together again soon! Now if you'll excuse me..."),,SPEAKER_BELEN)
    closemessage
  releaseall
  end
}

