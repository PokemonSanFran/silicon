const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA
const KAI_HOUSE = 1
const KAI = 3 //outside house
const ADAORA = 13
const GRUNTA = 15
const GRUNTB = 16
const GRUNTC = 17

const FLAG_HIDE_KAI_HOUSE = FLAG_TEMP_1

const FLAG_HIDE_TRANSPORT = FLAG_TEMP_1F

script Earthquake_HideObject_Script {
        setflag(FLAG_TEMP_4)
        setflag(FLAG_TEMP_2)
        setobjectxyperm(KAI,28,33)
        turnobject(KAI,DIR_NORTH)
}

script Earthquake_HideTransport_Script{
    if (var(VAR_HALAI_ISLAND_STATE) > START_CHAMPIONSHIP_CLEAN_RAID && var(VAR_HALAI_ISLAND_STATE) < MEET_AT_ROUTE4){
        setflag(FLAG_HIDE_TRANSPORT)
    }
}

script Earthquake_PostRescue_Objects_Script {
    clearflag(FLAG_TEMP_1)
    clearflag(FLAG_TEMP_3)
    clearflag(FLAG_TEMP_4)

        addobject(KAI)
        addobject(ADAORA)

        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,15,32,10)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,16,31,12)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,17,31,11)

        turnvobject(16,DIR_EAST)
        turnvobject(17,DIR_EAST)

        setobjectxy(PLAYER,32,13)
        setobjectxy(KAI,33,12)
        setobjectxy(ADAORA,33,11)
        turnobject(PLAYER,DIR_NORTH)

        applymovement(ADAORA,Common_Movement_FaceLeft)
        turnobject(KAI,DIR_WEST)

        TeleportCamera(32,13)
}

script Earthquake_ThatNight_Dialogue
{
    startcutscene(JUMPPLAYER_EARTHQUAKE_INSIDE)

    clearflag(FLAG_HIDE_KAI_HOUSE)
    addobject(KAI_HOUSE)
    applymovement(KAI_HOUSE,Common_Movement_FaceDown)
    waitmovement(KAI_HOUSE)

    msgbox(format("That night…"))
    closemessage
    fadescreen(FADE_FROM_BLACK)

    call(Earthquake_PlayFake_EventScript)

    playse(SE_PIN)
    applymovement(PLAYER,Common_Movement_ExclamationMark)
    applymovement(KAI_HOUSE,Common_Movement_ExclamationMark)

    call(Earthquake_Fake_Aftershock)

    msgbox(format("{PLAYER}, are you okay?"),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)
            closemessage

    msgbox(format("Oh no!"),MSGBOX_DEFAULT,SPEAKER_RESIDENT_A,TAIL_SHOUT)
    closemessage

    applymovement(PLAYER,Earthquake_WalkOutOfBed)

    msgbox(format("Help me!"),MSGBOX_DEFAULT,SPEAKER_RESIDENT_B,TAIL_SHOUT,EMOTE_SHOCK)

    msgbox(format("I'm fine. Do you hear people screaming?"),MSGBOX_DEFAULT,SPEAKER_ALICIA)
    closemessage

    applymovement(KAI_HOUSE,Earthquake_WalkToWall)
    waitmovement(KAI_HOUSE)

    msgbox(format("The power's dead. This… isn't good."),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)
    closemessage

    applymovement(KAI_HOUSE,Earthquake_RunOutside)
    waitmovement(KAI_HOUSE)
    playse(SE_EXIT)
    waitse
    removeobject(KAI_HOUSE)

    applymovement(PLAYER,Earthquake_RunFromBedOutside)
    waitmovement(PLAYER)

    setvar(VAR_HALAI_ISLAND_STATE,POST_EARTHQUAKE)
    fadeblackandsetremovewarpfadeflag
    warp(MAP_HALAI_ISLAND,2)
}

script Earthquake_ShowDestroyedCity_Script {
    lock
        msgbox(format("Holy shit."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
        closemessage

        fadescreenswapbuffers(FADE_TO_BLACK)
        TeleportCamera(11,7)
        special(SpawnCameraObject)
        fadescreenswapbuffers(FADE_FROM_BLACK)

        applymovement(CAMERA,Earthquake_PanCameraDown)
        waitmovement(CAMERA)

        fadescreenswapbuffers(FADE_TO_BLACK)
        TeleportCamera(33,26)
        special(SpawnCameraObject)
        fadescreenswapbuffers(FADE_FROM_BLACK)

        applymovement(CAMERA,Earthquake_PanCameraUp)
        waitmovement(CAMERA)

        fadescreenswapbuffers(FADE_TO_BLACK)
        TeleportCamera(29,33)

        fadescreenswapbuffers(FADE_FROM_BLACK)
        goto(Earthquake_StartRescue_Dialogue)
}

script Earthquake_StartRescue_Dialogue {
    //PSF TODO Prevent player leaving the city during this mission

    special(RemoveCameraObject)

        addobject(ADAORA)

        applymovement(ADAORA,Earthquake_WalkToPair)
        waitmovement(ADAORA)

        msgbox(format("Oh, you're okay!"),MSGBOX_DEFAULT,SPEAKER_ADAORA)

        msgbox(format("Yeah, why didn't you just call?"),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)

        msgbox(format("Cell towers were destroyed… like everything else. Emily just told me that the docks are destroyed too."),MSGBOX_DEFAULT,SPEAKER_ADAORA)

        msgbox(format("Sounds like we're on our own."),MSGBOX_DEFAULT,SPEAKER_ALICIA)

        msgbox(format("I'm going to make another lap around HalaiIsland and see who else I can help, and I've asked other TheTide members to do the same. Could you two -"),MSGBOX_DEFAULT,SPEAKER_ADAORA)

        msgbox(format("Of course. Let's meet back in the town center in a bit."),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)
        closemessage

        applymovement(KAI,Earthquake_WalkAwayA)
        applymovement(ADAORA,Earthquake_WalkAwayB)
        waitmovement(KAI)
        waitmovement(ADAORA)
        removeobject(KAI)
        removeobject(ADAORA)

        setvar(VAR_HALAI_ISLAND_STATE,START_EARTHQUAKE_RESCUE)

        call(TryAutoSave)
        endcutscene
        release
        end
}


script Earthquake_PostRescue_Dialogue {
    lock
        startcutscene(JUMPPLAYER_EARTHQUAKE_OUTSIDE)

        msgbox(format("Okay… things seem to be stabilizing. I should go meet Adaora in the center of town."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                closemessage

        fadescreenswapbuffers(FADE_TO_BLACK)
        call(Earthquake_PostRescue_Objects_Script)
        fadescreenswapbuffers(FADE_FROM_BLACK)

        msgbox(format("We've managed to re-establish cell communications. The authorities claim they're on their way… Who knows how useful they'll actually be. Ina reports that our ranks suffered heavy casualties in the quake. Mark says our supply caches were also hit hard."),MSGBOX_DEFAULT,SPEAKER_ADAORA)
                closemessage

        applymovement(ADAORA,Earthquake_Pace)
        waitmovement(ADAORA)

        msgbox(format("I really don't know how TheTide is going to hold up under this stress."),MSGBOX_DEFAULT,SPEAKER_ADAORA)

        playse(SE_POKENAV_CALL)
        waitse

        msgbox(format("My phone got a message from Tide Events? Now?"),MSGBOX_DEFAULT,SPEAKER_ADAORA,TAIL_TALK,EMOTE_CONFUSE)
                closemessage

        playse(SE_PIN)
        waitse
        applymovement(PLAYER,Common_Movement_ExclamationMark)
        waitmovement(PLAYER)

        msgbox(format("That's a trap."),MSGBOX_DEFAULT,SPEAKER_ALICIA,TAIL_SHOUT,EMOTE_SHOCK)

        msgbox(format("What do you mean? That doesn't make any sense."),MSGBOX_DEFAULT,SPEAKER_ADAORA,,EMOTE_CONFUSE)

        msgbox(format("Shit, we never got a chance to tell you."),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)
                closemessage

        applymovement(KAI,Common_Movement_FaceUp)
        waitmovement(KAI)

        msgbox(format("Kai and I got a message from that account a few days ago. I went to a warehouse in Cureno Port, and Kei-ying trapped me there and attacked me."),MSGBOX_DEFAULT,SPEAKER_ALICIA)

        applymovement(ADAORA,Earthquake_TurnToPlayer)
        waitmovement(ADAORA)

        msgbox(format("Wait what?"),MSGBOX_DEFAULT,SPEAKER_ADAORA,,EMOTE_SHOCK)

        msgbox(format("It gets crazier."),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)

        msgbox(format("We battled but… he threw the battle. And then told me to run. It was like he wasn't even trying."),MSGBOX_DEFAULT,SPEAKER_ALICIA)

        msgbox(format("Let's check this message out."),MSGBOX_DEFAULT,SPEAKER_ADAORA,EMOTE_CONFUSE)

        msgbox(format("“TM15@18030122@37.75030467013338, -122.3798002138461” It's just a bunch of nonsense. Maybe a glitch?"),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)

        msgbox(format("…I don't think so. Kei-ying was really trying to help me. Even when I had dinner with him, their energy was different from everybody else in SharpRise Capital."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                closemessage

        applymovement(KAI,Common_Movement_FaceDown)
        waitmovement(KAI)

        msgbox(format("How can you trust them after ALL of this?"),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE,,EMOTE_SHOCK)

        msgbox(format("The second half of that message are GPS coordinates, if that counts for anything."),MSGBOX_DEFAULT,SPEAKER_ADAORA)

        msgbox(format("So somebody wants us to go somewhere. Where is that?"),MSGBOX_DEFAULT,SPEAKER_ALICIA)

        msgbox(format("I think it's on the mainland, by Route4. Let's take a small team to go investigate."),MSGBOX_DEFAULT,SPEAKER_ADAORA)
                closemessage

        applymovement(KAI,Common_Movement_FaceUp)
        waitmovement(KAI)

        msgbox(format("Fine. The three of us and maybe a few more… nobody can take all of us at once."),MSGBOX_DEFAULT,SPEAKER_KAI_TIDE)
                closemessage
        setvar(VAR_STORYLINE_STATE,STORY_POST_EARTHQUAKE)
        setvar(VAR_HALAI_ISLAND_STATE,MEET_AT_ROUTE4)
        endcutscene
        goto(Earthquake_AskPlayerWarp_Script)
}

script Earthquake_AskPlayerWarp_Script {
    applymovement(KAI,Earthquake_TurnToPlayer)
        waitmovement(KAI)

        msgbox(format("I'm gonna head over to Route4 right now. Want to come with me?"),MSGBOX_YESNO,SPEAKER_KAI_TIDE)
        closemessage

        if (var(VAR_RESULT) == YES){
            fadeblackandsetremovewarpfadeflag
            warpsilent(MAP_ROUTE4,255,10,24)
                end
        }else {
                msgbox(format("Okay, we've all got our orders. Let's stay in touch."),MSGBOX_DEFAULT,SPEAKER_ADAORA)
                closemessage
                        }

    fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(GRUNTA)
        removeobject(GRUNTB)
        removeobject(GRUNTC)
        removeobject(KAI)
        removeobject(ADAORA)
        fadescreenswapbuffers(FADE_FROM_BLACK)

        call(TryAutoSave)
        release
        end
}

movement Earthquake_WalkToWall {
    walk_up * 2
        step_end
}

movement Earthquake_RunOutside {
    player_run_right
        player_run_up
        step_end
}

movement Earthquake_RunFromBedOutside {
    player_run_right * 5
        player_run_up * 3
        step_end
}

movement Earthquake_WalkOutOfBed {
    walk_right
        step_end
}

movement Earthquake_PanCameraDown {
    walk_down * 10
        step_end
}

movement Earthquake_PanCameraUp {
    walk_up * 10
        step_end
}

movement Earthquake_PanCameraLeft {
    walk_left * 10
        step_end
}

movement Earthquake_WalkToPair {
    walk_right * 5
        walk_down * 2
        walk_right * 3
        face_up
        step_end
}

movement Earthquake_WalkAwayA {
    walk_left
        walk_up * 6
        step_end
}

movement Earthquake_WalkAwayB {
    walk_left * 2
        walk_up * 7
        step_end
}

movement Earthquake_Pace {
    walk_right
        walk_up * 2
        walk_down * 2
        walk_up * 2
        walk_down * 2
        step_end
}

movement Earthquake_TurnToPlayer {
    walk_left
        face_down
        step_end
}
