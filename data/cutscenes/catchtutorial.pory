const PLAYER = OBJ_EVENT_ID_PLAYER
const PROF = 4
const FLAG_HIDE_PROF = FLAG_TEMP_2

script CatchTutorial_HideProfessor_Script
{
    getgamestat(GAME_STAT_POKEMON_CAPTURES)
    if (var(VAR_RESULT) > 0)
    {
        setflag(FLAG_HIDE_PROF)
    }
}

script CatchTutorial_CheckRun_Script
{
    if (flag(FLAG_PLAYED_CATCH_TUTORIAL))
    {
        release
        return
    }
    else
    {
        goto(CatchTutorial_ProfIntro_Script)
    }
}

script CatchTutorial_ProfIntro_Script
{
    lock
    startcutscene(JUMPPLAYER_CATCHTUTORIAL)
    playbgm(MUS_FOLLOW_ME,0)
    playse(SE_PIN)
    applymovement(PROF,Common_Movement_ExclamationMark)
    applymovement(PROF,CatchTutorial_WalkToPlayer_Movement)
    waitmovement(PROF)

    msgbox(gText_CatchTutorial_HeyThere,,SPEAKER_PROF_TRACHY)
    msgbox(gText_CatchTutorial_IDontRecognize,,SPEAKER_PROF_TRACHY)
    msgbox(gText_CatchTutorial_YeahImNew,,SPEAKER_ALICIA)
    msgbox(gText_CatchTutorial_ItsUsuallyEasyTo,,SPEAKER_PROF_TRACHY)
    closemessage

    call(Earthquake_PlayNatural_EventScript)

    playse(SE_PIN)
    applymovement(PLAYER,Common_Movement_ExclamationMark)
    waitmovement(PLAYER)
    applymovement(PROF,Common_Movement_ExclamationMark)

    call(Earthquake_Natural_Aftershock)

    msgbox(gText_CatchTutorial_WoahWoah,,SPEAKER_ALICIA,TAIL_SHOUT,EMOTE_SHOCK)
    msgbox(gText_CatchTutorial_ThatsAnother,,SPEAKER_PROF_TRACHY,,EMOTE_LAUGH)
    msgbox(gText_CatchTutorial_HowOften,,SPEAKER_ALICIA,,EMOTE_CONFUSE)
    msgbox(gText_CatchTutorial_TheyreNot,,SPEAKER_PROF_TRACHY)

    bufferpartymonspecies(STR_VAR_1,0)
    msgbox(gText_CatchTutorial_IWasGoing,,SPEAKER_PROF_TRACHY)
    goto(CatchTutorial_AskPlayer_Script)
}

script CatchTutorial_InteractProf_Script
{
    lock
    faceplayer
    playbgm(MUS_FOLLOW_ME,0)
    goto(CatchTutorial_AskPlayer_Script)
}

script CatchTutorial_AskPlayer_Script
{
    msgbox(gText_CatchTutorial_DoYouWant,MSGBOX_YESNO,SPEAKER_PROF_TRACHY)
    goto(CatchTutorial_HandleAnswer_Script)
}

script CatchTutorial_HandleAnswer_Script
{
    if (var(VAR_RESULT) == YES)
    {
        goto(CatchTutorial_StartCatchingScene_Script)
    }
    else
    {
        goto(CatchTutorial_RefuseCatching_Script)
    }
}

script CatchTutorial_StartCatchingScene_Script
{
    msgbox(gText_CatchTutorial_OkayLetSee,,SPEAKER_PROF_TRACHY)
    closemessage
    applymovement(PROF,Common_Movement_FaceAwayPlayer)
    waitmovement(PROF)
    msgbox(gText_CatchTutorial_HeresOne,,SPEAKER_PROF_TRACHY)
    closemessage
    special(SavePlayerParty)
    special(LoadWallyZigzagoon)
    special(StartWallyTutorialBattle) // PSF TODO update this to use the professor's backsprite and a gen 9 pokemon
    waitstate
    goto(CatchTutorial_PostCatching_Script)
}

script CatchTutorial_PostCatching_Script
{
    special(LoadPlayerParty)
    applymovement(PROF,Common_Movement_FacePlayer)
    waitmovement(PROF)
    msgbox(gText_CatchingTutorial_ThatsReally,,SPEAKER_PROF_TRACHY)
    call(CatchTutorial_GiveBallsSetFlag_Script)
    goto(CatchTutorial_End_Script)
}

script CatchTutorial_SetFlagSave_Script
{
    setflag(FLAG_PLAYED_CATCH_TUTORIAL)
    call(TryAutoSave)
}

script CatchTutorial_End_Script
{
    closemessage
    fadedefaultbgm
    msgbox(gText_CatchingTutorial_IfYouWant,,SPEAKER_PROF_TRACHY)
    closemessage
    call_if_unset(FLAG_PLAYED_CATCH_TUTORIAL,CatchTutorial_SetFlagSave_Script)
    endcutscene
    release
    end
}

script CatchTutorial_GiveBallsSetFlag_Script
{
    if(flag(FLAG_PLAYED_CATCH_TUTORIAL))
    {
        return
    }
    msgbox(gText_CatchingTutorial_AsAMatter,,SPEAKER_PROF_TRACHY)
    closemessage
    playfanfare(MUS_OBTAIN_ITEM)
    additem(ITEM_REPEAT_BALL,2)
    additem(ITEM_DUSK_BALL,1)
    additem(ITEM_QUICK_BALL,1)
    additem(ITEM_DREAM_BALL,1)
    msgbox(gText_CatchTutorial_SomeBalls)
    waitmessage
    waitfanfare
}

script CatchTutorial_RefuseCatching_Script
{
    msgbox(gText_CatchTutorial_NoProblem,,SPEAKER_PROF_TRACHY)
    goto(CatchTutorial_End_Script)
}
text gText_CatchTutorial_HeyThere
{
	format("Hey there!")
}
text gText_CatchTutorial_IDontRecognize
{
	format("I don't recognize you... and you're coming out of CuconuTown - you must be one of those SharpRise Trainers!")

}
text gText_CatchTutorial_YeahImNew
{
	format("Yeah! How were you able to tell?")
}
text gText_CatchTutorial_ItsUsuallyEasyTo
{
	format("It's usually easy to pick out transplants. You all have thi -")
}
text gText_CatchTutorial_WoahWoah
{
	format("Woah woah what was that?!?! Is that a Pokémon attack?")
}
text gText_CatchTutorial_ThatsAnother
{
	format("That's another easy way you identify 'em... how they react to earthquakes.")
}
text gText_CatchTutorial_HowOften
{
	format("You're not freaking out - how often do those happen?")
}
text gText_CatchTutorial_TheyreNot
{
	format("They're not super rare, but it doesn't happen every day. They're largely harmless though.")
}
text gText_CatchTutorial_IWasGoing
{
	format("I was going to offer to teach you how to catch Pokémon! I always offer to help out new Trainers, but it looks like you've already got a {STR_VAR_1}! But in case you need a refresher...")
}
text gText_CatchTutorial_DoYouWant
{
	format("Do you want me to teach you how to catch a Pokémon?")
}
text gText_CatchTutorial_OkayLetSee
{
	format("Okay, let's see, you walk around here until one attacks...")
}
text gText_CatchTutorial_HeresOne
{
	format("Aha!")
}
text gText_CatchingTutorial_ThatsReally
{
	format("That's really it! Different Pokéballs will have more effective based on what Pokémon, or where you are... sometimes what time of day... it can be complicated.")
}
text gText_CatchingTutorial_IfYouWant
{
	format("If you want me to show you in the future, just come to talk to me!")
}
text gText_CatchTutorial_NoProblem
{
	format("No problem!")
}

text gText_CatchingTutorial_AsAMatter
{
    format("As a matter of fact, I've got a few types here. Here you go!")
}

text gText_CatchTutorial_SomeBalls
{
    format("{PLAYER} obtained some PokéBalls!")
}
movement CatchTutorial_WalkToPlayer_Movement
{
	walk_left
}
