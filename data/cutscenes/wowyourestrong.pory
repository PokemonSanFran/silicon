const ADELAIDE = VAR_LAST_TALKED
const KAI = VAR_0x8006
const CAMERA = OBJ_EVENT_ID_CAMERA
const FLAG_HIDE_ADELAIDE = FLAG_TEMP_1E
const FLAG_HIDE_KAI = FLAG_TEMP_1F

script WowYourStrong_AssignObjectId_Script
{
    specialvar(VAR_RESULT,GetCurrentMap)
    switch (var(VAR_RESULT))
    {
        case MAP_TORA_TOWN:
            setvar(ADELAIDE,6)
            setvar(KAI,7)
        case MAP_PERLACIA_CITY:
            setvar(ADELAIDE,1)
            setvar(KAI,9)
        case MAP_MERMEREZA_CITY:
            setvar(ADELAIDE,10)
            setvar(KAI,11)
    }
    return
}

script WowYoureStrong_HideObject_Script
{
    special(GetNumberOfBadges)
    if(var(VAR_RESULT)!= 3 || (var(VAR_SHARPRISESPIRE_CONFERENCE_STATE) > DEFEATED_THIRD_LEADER))
    {
        setflag(FLAG_HIDE_ADELAIDE)
        setflag(FLAG_HIDE_KAI)
    }

    if (var(VAR_ROUTE10_STATE) == SAVED_BEFORE_KAI_ROUTE10)
    {
        setflag(FLAG_HIDE_KAI)
    }
}

script WowYoureStrong_CheckCutsceneScript
{
    //special(GetNumberOfBadges)
    //if (var(VAR_RESULT) == 3)
    //{
        //setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,DEFEATED_THIRD_LEADER)
    //}
    //return
	callnative(WowYoureStrong_GetBadgesAndSetTowerState)
}

script WowYoureStrong_WalkToPlayer_Script
{
    lockall
    call(WowYourStrong_AssignObjectId_Script)
    startcutscene(JUMPPLAYER_WOWYOURESTRONG_PERLACIA_CITY)
    setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,POST_WOW_YOURE_STRONG)
    playse(SE_PIN)
    playbgm(MUS_RG_ENCOUNTER_DEOXYS,TRUE)
    applymovement(ADELAIDE,Common_Movement_ExclamationMark)
    waitmovement(ADELAIDE)
    applymovement(ADELAIDE,WowYoureStrong_WalkToPlayer_Movement)
    waitmovement(ADELAIDE)
    setobjectxyperm(ADELAIDE,28,25)
    goto(WowYoureStrong_Intro_Dialogue)
}

script WowYoureStrong_Intro_Dialogue
{
    msgbox(gText_WowYoureStrong_WhatAShow,,SPEAKER_ADELAIDE)

    msgbox(gText_WowYoureStrong_OhUhThanksALot,,SPEAKER_ALICIA)

    msgbox(gText_WowYoureStrong_WhereMyManners,,SPEAKER_ADELAIDE)

    msgbox(gText_WowYoureStrong_ShouldHaveKnown,,SPEAKER_ALICIA)

    msgbox(gText_WowYoureStrong_DreamBig,,SPEAKER_ADELAIDE)

    goto(WowYoureStrong_ShowTower_Script)
}

script WowYoureStrong_ShowTower_Script
{
    bufferstring(STR_VAR_2,gText_PerlaciaCity)
    specialvar(VAR_RESULT,GetCurrentMap)
    if (var(VAR_RESULT) == MAP_PERLACIA_CITY)
    {
        call(WowYoureStrong_MoveCameraToTower_Script)
        bufferstring(STR_VAR_2,"this city")
    }

    msgbox(gText_WowYoureStrong_YouShouldComeBy,,SPEAKER_ADELAIDE)
    closemessage

    if (var(VAR_RESULT) == MAP_PERLACIA_CITY)
    {
        call(WowYoureStrong_TakeCameraFromTower_Script)
    }
    goto (WowYoureStrong_End_Dialouge)
}

script WowYoureStrong_MoveCameraToTower_Script
{
    fadescreen(FADE_TO_BLACK)
    TeleportCamera(7,14)
    special(SpawnCameraObject)
    fadescreen(FADE_FROM_BLACK)
    applymovement(CAMERA,WowYoureStrong_PanCameraUp_Movement)
    return
}

script WowYoureStrong_TakeCameraFromTower_Script
{
    fadescreen(FADE_TO_BLACK)
    TeleportCamera(28,24)
    special(RemoveCameraObject)
    fadescreen(FADE_FROM_BLACK)
    waitmovement(CAMERA)
    return
}

script WowYoureStrong_End_Dialouge
{
    msgbox(gText_WowYoureStrong_RunToMeeting,,SPEAKER_ADELAIDE)

    closemessage
    applymovement(ADELAIDE,WowYoureStrong_WalkToMeeting_Movement)
    if (var(VAR_ROUTE10_STATE) != SAVED_BEFORE_KAI_ROUTE10)
    {
        delay(30)
        applymovement(KAI,WowYoureStrong_KaiToPlayer_Movement)
    }
    fadedefaultbgm

    if (var(VAR_ROUTE10_STATE) != SAVED_BEFORE_KAI_ROUTE10)
    {
        goto(WowYourStrong_KaiConfrontation_Dialogue)
    }
    goto(WowYourStrong_RemoveAdelaide_Script)
}

script WowYourStrong_RemoveAdelaide_Script
{
    waitmovement(ADELAIDE)
    removeobject(ADELAIDE)
    release
    end
}

script WowYourStrong_KaiConfrontation_Dialogue
{
    msgbox(gText_WowYoureStrong_IDontGetIt,,SPEAKER_KAI)

    if (var(VAR_ROUTE10_STATE) == DEFEATED_KAI_ROUTE10)
    {
        msgbox(gText_WowYoureStrong_YouAgain,,SPEAKER_ALICIA)
    }
    else
    {
        msgbox(gText_WowYoureStrong_WhatAreYouTalking,,SPEAKER_ALICIA)
    }
    msgbox(gText_WowYoureStrong_IJustCant,,SPEAKER_KAI)
    closemessage

    applymovement(KAI,WowYoureStrong_WalkToMeeting_Movement)
    waitmovement(KAI)
    removeobject(KAI)

    if (var(VAR_ROUTE10_STATE) == DEFEATED_KAI_ROUTE10)
    {
        msgbox(gText_WowYoureStrong_IHopeThats,,SPEAKER_ALICIA,TAIL_THOUGHT,EMOTE_SWEAT)
    }
    else
    {
        msgbox(gText_WowYoureStrong_ThisRegionHasSome,,SPEAKER_ALICIA)
    }
    closemessage

    endcutscene
    call(TryAutoSave)
    goto(WowYourStrong_RemoveAdelaide_Script)
}

movement WowYoureStrong_WalkToPlayer_Movement {
    walk_up
    walk_right * 5
    face_up
    step_end
}

movement WowYoureStrong_PanCameraUp_Movement{
    walk_up * 9
        step_end
}

movement WowYoureStrong_WalkToMeeting_Movement {
    walk_left * 8
    step_end
}

movement WowYoureStrong_KaiToPlayer_Movement{
    walk_up * 2
    walk_right * 2
    face_up
    step_end
}

text gText_WowYoureStrong_WhatAShow {
    format("{PLAYER}! What a show! That was marvelous! Flying you in from Orre was absolutely worth it, for that battle alone.")
}

text gText_WowYoureStrong_OhUhThanksALot {
    format("Oh uh, thanks a lot, that's very nice. I'm guessing you work at SharpriseCapitalâ€¦?")
}

text gText_WowYoureStrong_WhereMyManners {
    format("Hah! Oh my, I'm sorry. Where are my manners? My name is Adelaide, and I'm the SharpriseCapitalLeague Commissioner, appointed by our corporate sponsors, Buzzr, Presto, and Arriba.")
}

text gText_WowYoureStrong_ShouldHaveKnown {
    format("Oh! I guess I should have known that. Well um, thanks so much for the opportunity! I'm excited to be the region's next Champion.")
}

text gText_WowYoureStrong_DreamBig {
    format("Dream big {PLAYER}! That's the attitude we wanna see. We're big dreamers over at SharpriseCapitaltoo. That's why we built the Sharprise Spire! I wrote a stunning email introduction after it was completed... I'll have to forward it to you, it's really some of my best writing.")
}

text gText_WowYoureStrong_YouShouldComeBy {
    format("You should come by once you're done exploring! My office is on the 38th floor of the SharpriseSpire in {STR_VAR_2}.")
}

text gText_WowYoureStrong_RunToMeeting {
    format("I have to run to a meeting. Again, excellent job in your Gym battle. I hope to see what else you can do!")
}

text gText_WowYoureStrong_IDontGetIt{
	format("I don't get it. Do all of you transplants just worship the ground these tycoons walk on?")
}
text gText_WowYoureStrong_YouAgain{
	format("You again? Are you following me? What is your deal?")
}
text gText_WowYoureStrong_WhatAreYouTalking{
	format("What are you talking about?")
}
text gText_WowYoureStrong_IJustCant{
	format("I just can't understand why you would willingly buy into a system that just makes these people more money. Do you think they need MORE money?")
}
text gText_WowYoureStrong_IHopeThats{
	format("I hope that this isn't going to be a regular thing...")
}
text gText_WowYoureStrong_ThisRegionHasSome{
	format("The region has some... characters.")
}
