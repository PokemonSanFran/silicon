const LUCREZIA = VAR_LAST_TALKED
const BAIYA = VAR_0x8006
const CAMERA = OBJ_EVENT_ID_CAMERA
const FLAG_HIDE_LUCREZIA = FLAG_TEMP_1E
const FLAG_HIDE_BAIYA = FLAG_TEMP_1F

script WowYourStrong_AssignObjectId_Script
{
    specialvar(VAR_RESULT,GetCurrentMap)
    switch (var(VAR_RESULT))
    {
        case MAP_TORA_TOWN:
            setvar(LUCREZIA,6)
            setvar(BAIYA,7)
        case MAP_PERLACIA_CITY:
            setvar(LUCREZIA,1)
            setvar(BAIYA,9)
        case MAP_MERMEREZA_CITY:
            setvar(LUCREZIA,10)
            setvar(BAIYA,11)
    }
    return
}

script WowYoureStrong_HideObject_Script
{
    special(GetNumberOfBadges)
    if(var(VAR_RESULT)!= 3 || (var(VAR_SHARPRISESPIRE_CONFERENCE_STATE) > DEFEATED_THIRD_LEADER))
    {
        setflag(FLAG_HIDE_LUCREZIA)
        setflag(FLAG_HIDE_BAIYA)
    }

    if (var(VAR_ROUTE10_STATE) == SAVED_BEFORE_BAIYA_ROUTE10)
    {
        setflag(FLAG_HIDE_BAIYA)
    }
}

script WowYoureStrong_CheckCutsceneScript
{
    //special(GetNumberOfBadges)
    //if (var(VAR_RESULT) == 3)
    //{
        //setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,DEFEATED_THIRD_LEADER)
    //}
    //return
	callnative(WowYoureStrong_GetBadgesAndSetTowerState)
}

script WowYoureStrong_WalkToPlayer_Script
{
    lockall
    call(WowYourStrong_AssignObjectId_Script)
    startcutscene(JUMPPLAYER_WOWYOURESTRONG_PERLACIA_CITY)
    setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,POST_WOW_YOURE_STRONG)
    playse(SE_PIN)
    playbgm(MUS_RG_ENCOUNTER_DEOXYS,TRUE)
    applymovement(LUCREZIA,Common_Movement_ExclamationMark)
    waitmovement(LUCREZIA)
    applymovement(LUCREZIA, moves(walk_up, walk_right * 5, face_up, step_end))
    waitmovement(LUCREZIA)
    setobjectxyperm(LUCREZIA,28,25)
    goto(WowYoureStrong_Intro_Dialogue)
}

script WowYoureStrong_Intro_Dialogue
{
    msgbox(format("{PLAYER}! What a show! That was marvelous! Flying you in from Orre was absolutely worth it, for that battle alone."),,SPEAKER_LUCREZIA)

    msgbox(format("Oh uh, thanks a lot, that's very nice. I'm guessing you work at SharpRise Capitalâ€¦?"),,SPEAKER_ALICIA)

    msgbox(format("Hah! Oh my, I'm sorry. Where are my manners? My name is Lucrezia, and I'm the SharpRise Capital League Commissioner, appointed by our corporate sponsors, Buzzr, Presto, and Arriba."),,SPEAKER_LUCREZIA)

    msgbox(format("Oh! I guess I should have known that. Well um, thanks so much for the opportunity! I'm excited to be the region's next Champion."),,SPEAKER_ALICIA)

    msgbox(format("Dream big {PLAYER}! That's the attitude we wanna see. We're big dreamers over at SharpRise Capital too. That's why we built the SharpRise  Spire! I wrote a stunning email introduction after it was completed... I'll have to forward it to you, it's really some of my best writing."),,SPEAKER_LUCREZIA)

    goto(WowYoureStrong_ShowTower_Script)
}

script WowYoureStrong_ShowTower_Script
{
	buffermapname(STR_VAR_2,MAP_PERLACIA_CITY)
    specialvar(VAR_RESULT,GetCurrentMap)
    if (var(VAR_RESULT) == MAP_PERLACIA_CITY)
    {
        call(WowYoureStrong_MoveCameraToTower_Script)
        bufferstring(STR_VAR_2,"this city")
    }

    msgbox(format("You should come by once you're done exploring! My office is on the 38th floor of the SharpRise Spire in {STR_VAR_2}."),,SPEAKER_LUCREZIA)
    closemessage

    if (var(VAR_RESULT) == MAP_PERLACIA_CITY)
    {
        call(WowYoureStrong_TakeCameraFromTower_Script)
    }
    goto (WowYoureStrong_End_Dialouge)
}

script WowYoureStrong_MoveCameraToTower_Script
{
    fadescreen(FADE_TO_BLACK)
    TeleportCamera(7,14)
    special(SpawnCameraObject)
    fadescreen(FADE_FROM_BLACK)
    applymovement(CAMERA, moves(walk_up * 9, step_end))
    return
}

script WowYoureStrong_TakeCameraFromTower_Script
{
    fadescreen(FADE_TO_BLACK)
    TeleportCamera(28,24)
    special(RemoveCameraObject)
    fadescreen(FADE_FROM_BLACK)
    waitmovement(CAMERA)
    return
}

script WowYoureStrong_End_Dialouge
{
    msgbox(format("I have to run to a meeting. Again, excellent job in your Gym battle. I hope to see what else you can do!"),,SPEAKER_LUCREZIA)

    closemessage
    applymovement(LUCREZIA,WowYoureStrong_WalkToMeeting_Movement)
    if (var(VAR_ROUTE10_STATE) != SAVED_BEFORE_BAIYA_ROUTE10)
    {
        delay(30)
        applymovement(BAIYA, moves(walk_up * 2, walk_right * 2, face_up, step_end))
    }
    fadedefaultbgm

    if (var(VAR_ROUTE10_STATE) != SAVED_BEFORE_BAIYA_ROUTE10)
    {
        goto(WowYourStrong_BaiyaConfrontation_Dialogue)
    }
    goto(WowYourStrong_RemoveLucrezia_Script)
}

script WowYourStrong_RemoveLucrezia_Script
{
    waitmovement(LUCREZIA)
    removeobject(LUCREZIA)
    release
    end
}

script WowYourStrong_BaiyaConfrontation_Dialogue
{
    msgbox(format("I don't get it. Do all of you transplants just worship the ground these tycoons walk on?"),,SPEAKER_BAIYA)

    if (var(VAR_ROUTE10_STATE) == DEFEATED_BAIYA_ROUTE10)
    {
        msgbox(format("You again? Are you following me? What is your deal?"),,SPEAKER_ALICIA)
    }
    else
    {
        msgbox(format("What are you talking about?"),,SPEAKER_ALICIA)
    }
    msgbox(format("I just can't understand why you would willingly buy into a system that just makes these people more money. Do you think they need MORE money?"),,SPEAKER_BAIYA)
    closemessage

    applymovement(BAIYA,WowYoureStrong_WalkToMeeting_Movement)
    waitmovement(BAIYA)
    removeobject(BAIYA)

    if (var(VAR_ROUTE10_STATE) == DEFEATED_BAIYA_ROUTE10)
    {
        msgbox(format("I hope that this isn't going to be a regular thing..."),,SPEAKER_ALICIA,TAIL_THOUGHT,EMOTE_SWEAT)
    }
    else
    {
        msgbox(format("The region has some... characters."),,SPEAKER_ALICIA)
    }
    closemessage

    endcutscene
    call(TryAutoSave)
    goto(WowYourStrong_RemoveLucrezia_Script)
}

movement WowYoureStrong_WalkToMeeting_Movement {
    walk_left * 8
    step_end
}

