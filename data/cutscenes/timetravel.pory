const FLAG_HIDE_ADAM = FLAG_TEMP_1
const FLAG_HIDE_FRANK = FLAG_TEMP_2
const FLAG_HIDE_ELEANOR = FLAG_TEMP_3
const FLAG_HIDE_SNOM = FLAG_TEMP_4
const FLAG_HIDE_EMRYS = FLAG_TEMP_5

script TimeTravel_Script_HideObjects
{
	setflag(FLAG_HIDE_SNOM)
	if (flag(FLAG_USING_WORMHOLE))
	{
		setflag(FLAG_HIDE_EMRYS)
		if (var(VAR_TIME_TRAVEL_STATE) == TIME_TRAVEL_HAS_BATTLED_ONCE)
		{
			setobjectxyperm(LOCALID_ADAM,7,8)
			setobjectxyperm(LOCALID_FRANK,6,11)
			setobjectxyperm(LOCALID_ELEANOR,7,11)
		}
		return
	}
	setflag(FLAG_HIDE_ADAM)
	setflag(FLAG_HIDE_FRANK)
	setflag(FLAG_HIDE_ELEANOR)
	return
}

script TimeTravel_Script_RoutePlayer
{
	switch(var(VAR_TIME_TRAVEL_STATE))
	{
		case TIME_TRAVEL_FIRST_WORMHOLE_USE:
			call(TimeTravel_Script_ResetIfNotWormhole)
		case TIME_TRAVEL_HAS_BATTLED_ONCE:
			call(TimeTravel_Script_ResetIfNotWormhole)
		default: return
	}
}

script TimeTravel_Script_ResetIfNotWormhole
{
	if (flag(FLAG_USING_WORMHOLE))
	{
		return
	}
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_SACRIFICE_COMPLETE)
}

script TimeTravel_Script_StartTravel
{
	setflag(FLAG_USING_WORMHOLE)
	checktrainerdiscovered(TRAINER_ADAM)
	if (var(VAR_RESULT) == TRUE)
	{
		setvar(VAR_TIME_TRAVEL_STATE, TIME_TRAVEL_HAS_BATTLED_ONCE)
		warp(MAP_OROLAND_COLISEUM_OUTSIDE,WARP_ID_NONE,7,7)
	}
	else
	{
		setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_FIRST_WORMHOLE_USE)
		warp(MAP_OROLAND_COLISEUM_OUTSIDE,WARP_ID_NONE,7,5)
	}
}

script TimeTravel_Script_BumpIntoChampion
{
	callnative(StoreElenaorMonSpecies)
	msgbox(format("...Where am I?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	applymovement(LOCALID_PLAYER,Common_Movement_FaceRight)
	waitmovement(LOCALID_PLAYER)
	applymovement(LOCALID_PLAYER,Common_Movement_FaceLeft)
	waitmovement(LOCALID_PLAYER)
	applymovement(LOCALID_PLAYER,Common_Movement_FaceUp)
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Or when?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	msgbox(format("Did the machine bring me to the right place?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage

	applymovement(LOCALID_PLAYER,Common_Movement_WalkInPlaceDown)
	waitmovement(LOCALID_PLAYER)
	playse(SE_BALL_OPEN)
	waitse
	clearflag(FLAG_HIDE_SNOM)
	callnative(BufferEleanorMonSprite)
	addobject(LOCALID_SNOM)
	callnative(PlayEleanorMonCry)
	waitmoncry

	callnative(BufferEleanorMonName)
	closemessage
	callnative(PlayEleanorMonCry)
	applymovement(LOCALID_SNOM,FollowerHopFast)
	waitmovement(LOCALID_SNOM)
	applymovement(LOCALID_SNOM,moves(emote_heart))
	callnative(PlayEleanorMonCry)
	waitmoncry
	msgbox(format("{STR_VAR_1} looks excited!"))
	closemessage	
	delay(8)
	applymovement(LOCALID_ADAM,moves(walk_up * 2))
	applymovement(LOCALID_SNOM,moves(walk_fast_down * 3, lock_facing_direction))
	waitmovement(LOCALID_SNOM)
	playse(SE_BIKE_HOP)
	applymovement(LOCALID_SNOM,moves(jump_up, unlock_facing_direction, walk_fast_left * 8))
	waitmovement(LOCALID_SNOM)
	delay(8)
	applymovement(LOCALID_PLAYER,moves(face_left))
	msgbox(format("Wait - where are you going?"),,SPEAKER_ALICIA,TAIL_SHOUT,EMOTE_SHOCK)
	closemessage
	msgbox(format("Hey! Is that your poorly trained Pokemon running into me?"),,SPEAKER_ADAM_UNKNOWN,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("Definately still in Resido, plenty of rude jerks..."),,SPEAKER_ALICIA,TAIL_WHISPER)
	closemessage
	applymovement(LOCALID_PLAYER,moves(walk_down * 3))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Hey sorry. That {STR_VAR_1} -"),,SPEAKER_ALICIA)
	msgbox(format("You should be sorry! You're lucky I'm in a celebrating mood - it wouldn't look good for the new Champion of Resido completely destroy some chump!"),,SPEAKER_ADAM_UNKNOWN)
	closemessage
	applymovement(LOCALID_PLAYER,moves(face_left))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("I don't have time -"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	applymovement(LOCALID_PLAYER,moves(face_down))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Wait."),,SPEAKER_ALICIA,TAIL_THOUGHT)
	applymovement(LOCALID_PLAYER,moves(face_down))
	msgbox(format("You're the Champion? Of Resido?"),,SPEAKER_ALICIA,EMOTE_CONFUSE)
	closemessage
	applymovement(LOCALID_ADAM,moves(walk_up))
	msgbox(format("Do you live under a rock? I'm Champion Adam - latest winner of the Resido Pokemon League!"),,SPEAKER_ADAM)
	msgbox(format("...he didn't say SharpRise. So either I'm in a future where SharpRise doesn't own the League anymore, or..."),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	applymovement(LOCALID_FRANK,moves(walk_up * 3))
	applymovement(LOCALID_ELEANOR,moves(walk_up * 3))
	waitmovement(LOCALID_FRANK)
	goto(TimeTravel_Script_StartBattle)
}

script TimeTravel_Script_StartBattle
{
	if (var(VAR_TIME_TRAVEL_STATE) == TIME_TRAVEL_HAS_BATTLED_ONCE)
	{
		msgbox(format("Back for another lesson? Perfect, I'll be here all day."),,SPEAKER_ADAM)
	}
	else
	{
			msgbox(format("You know what? Maybe you need a lesson in paying attention to current events!"),,SPEAKER_ADAM)
	}
	closemessage
		trainerbattle_no_intro(TRAINER_ADAM,format("They told me I would be invincible..."))
		goto(TimeTravel_Script_PostBattle)
}

script TimeTravel_Script_PostBattle
{
	lock
	msgbox(format("...how are you this strong?"),,SPEAKER_ADAM,TAIL_WHISPER,EMOTE_CONFUSE)
	msgbox(format("She told me I would be... invincible!"),,SPEAKER_ADAM,,EMOTE_ANGRY)
	msgbox(format("That if I repped them, I would never lose again!"),,SPEAKER_ADAM,,EMOTE_ANGRY)	
	msgbox(format("I have best Pokemon...the best items... access to any move!"),,SPEAKER_ADAM,TAIL_WHISPER)
	msgbox(format("I'm supposed to be spokesperson... no, the FACE of SharpRise!"),,SPEAKER_ADAM,,EMOTE_ANGRY)
	msgbox(format("But I lost... to garbage like you?"),,SPEAKER_ADAM)
	closemessage
	delay(8)
	msgbox(format("But nobody will ever believe I lost anyways! I'm invincible. I'm the Champion."),,SPEAKER_ADAM)
	msgbox(format("I'm going to go back in that party. The party where all of the strongest Trainers in Resido are celebrating me!"),,SPEAKER_ADAM)
	closemessage
	waitmovement(LOCALID_ADAM)
	special(SpawnCameraObject)
	applymovement(LOCALID_CAMERA,moves(walk_down * 2))
	waitmovement(LOCALID_CAMERA)
	special(RemoveCameraObject)
	delay(16)
	applymovement(LOCALID_ADAM,moves(face_down))
	playse(SE_PIN)
	waitse
	applymovement(LOCALID_ADAM,moves(emote_exclamation_mark))
	waitmovement(LOCALID_ADAM)
	msgbox(format("...Did you two see that battle?"),,SPEAKER_ADAM)
	msgbox(format("You said she. Are you talking about Adelaide?"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Oh no... they're going to ruin me - "),,SPEAKER_ADAM)
	closemessage
	msgbox(format("We won't say a word."),,SPEAKER_ELLEN_PAST)
	closemessage
	playse(SE_PIN)
	applymovement(LOCALID_ADAM,moves(emote_exclamation_mark))
	applymovement(LOCALID_FRANK,moves(emote_exclamation_mark))
	waitmovement(LOCALID_FRANK)
	waitse
	msgbox(format("With a condition."),,SPEAKER_ELLEN_PAST)
	msgbox(format("You're going to go back inside."),,SPEAKER_ELLEN_PAST)
msgbox(format("Magnus is going to bring you into the hall, and probably apologize that the party is smaller than normal."),,SPEAKER_ELLEN_PAST)
msgbox(format("I'm going to give a speech. You're going to act totally normal."),,SPEAKER_ELLEN_PAST)
msgbox(format("You talk, you drink, you mingle, and finally, you leave the party."),,SPEAKER_ELLEN_PAST)
	msgbox(format("And once you leave the party..."),,SPEAKER_ELLEN_PAST)
	msgbox(format("you will never be seen in Resido again."),,SPEAKER_ELLEN_PAST)
	msgbox(format("Disobey me... you will never have a career ever again."),,SPEAKER_ELLEN_PAST)
	closemessage
	delay(8)
	msgbox(format("Now go."),,SPEAKER_ELLEN_PAST)
	closemessage
	goto(TimeTravel_Dialogue_ReturnToPresent)
}

script TimeTravel_Dialogue_ReturnToPresent
{
applymovement(LOCALID_ADAM,moves(walk_left*2,walk_down * 8))
	waitmovement(LOCALID_ADAM)
	removeobject(LOCALID_ADAM)
	applymovement(LOCALID_ELEANOR,moves(face_left))
	applymovement(LOCALID_FRANK,moves(face_right))
	waitmovement(LOCALID_FRANK)
	msgbox(format("Okay Ellen, look -"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("He was a plant? Are you serious? We lost to that?!?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("Turning BD against us.\pBuying our League.\pTaking us apart piece of piece.\pNone of that was enough? They weren't happy with owning it, so they had to INVENT a Golden Boy?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("And we were so stupid!\pCompetitive integrity? Who cares!\pAs long as the PRODUCT looks good, right?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("That's what it looks like but - "),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Armando, we have to fight now. We don't have a choice!"),,SPEAKER_ELLEN_PAST)
	msgbox(format("Ellen, we don't have anything yet. Do you just want to do what Tala and Magnus did? What's the plan?"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("And you think its okay to just wait for our magical oppurtunity?\pLike some Mythical Pokemon is going to come along and help make it right? What's your plan?"),,SPEAKER_ELLEN_PAST)
	msgbox(format("...I don't have one."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("I know."),,SPEAKER_ELLEN_PAST)
	closemessage
	applymovement(LOCALID_ELEANOR,moves(walk_down * 8))
	waitmovement(LOCALID_ELEANOR)
	special(SpawnCameraObject)
	applymovement(LOCALID_CAMERA,moves(walk_up * 2))
	waitmovement(LOCALID_CAMERA)
	special(RemoveCameraObject)
	applymovement(LOCALID_FRANK,moves(walk_up * 3, face_right))
	waitmovement(LOCALID_FRANK)
	applymovement(LOCALID_PLAYER,moves(face_left))
	msgbox(format("I'm sorry you had to deal with all of that. But I am surprised you were able to so expertly defeat Adam."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Who are you?"),,SPEAKER_ARMANDO_PAST)
	closemessage
	call(Common_EventScript_PhoneRinging)
	callnative(BufferEleanorMonName)
	msgbox(format("{PLAYER}! Did you get seperated from that {STR_VAR_1}? The futher it gets from you, the weaker your connection gets! I have to pull you back!"),,SPEAKER_MADRONE,TAIL_SHOUT,EMOTE_SHOCK,PHONE_ON)
	closemessage
	playse(SE_POKENAV_HANG_UP)
	msgbox(format("We'll meet again Armando! And for what its worth, you're part of the reason I'm as strong as I am."),,SPEAKER_ALICIA,,EMOTE_HAPPY)
	closemessage
	clearflag(FLAG_USING_WORMHOLE)
	callnative(DeleteEleanorSnom)
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_COMPLETE)
	warp(MAP_SHARPRISE_SPIRE_LAB,0)
	waitstate
	release
	end
}

script TimeTravel_CheckSnom_Script
{
	if (var(VAR_ELLEN_STATE) != TAKEN_ELITE_MON)
	{
		return
	}


	if (var(VAR_TIME_TRAVEL_STATE) > TIME_TRAVEL_COMPLETE)
	{
		return
	}

	specialvar(VAR_RESULT,Quest_Ultrawormholeresearch_CountRemainingSubquests)
	if (var(VAR_RESULT) > 8)
	{
		return
	}

	callnative(BufferEleanorMonName)
	callnative(Script_IsEleanorSnomInParty)
	if (var(VAR_RESULT) == FALSE)
	{
		msgbox(format("I should bring Eleanor's {STR_VAR_1} to Madrone..."),,SPEAKER_ALICIA,TAIL_THOUGHT)
		closemessage
		return
	}

	switch (var(VAR_TIME_TRAVEL_STATE))
	{
		case TIME_TRAVEL_SACRIFICE_COMPLETE:
			msgbox("Should I ask him about {STR_VAR_1}?",MSGBOX_YESNO,SPEAKER_ALICIA,TAIL_THOUGHT)
			if (var(VAR_RESULT) == YES)
			{
				goto(TimeTravel_Dialogue_FirstTalkEllenMadrone)
			}
			else
			{
				closemessage
				return
			}
		case TIME_TRAVEL_ASKED_ABOUT_SNOM:
			goto(TimeTravel_Dialogue_OfferTravelMadrone)
		case TIME_TRAVEL_FIRST_WORMHOLE_USE:
			goto(TimeTravel_Dialogue_OfferTravelMadrone)
		case TIME_TRAVEL_HAS_BATTLED_ONCE:
			goto(TimeTravel_Dialogue_OfferTravelMadrone)
		default:
			return
	}
	return
}

script TimeTravel_Dialogue_FirstTalkEllenMadrone
{
	lock
	msgbox(format("Professor, a friend gave me this Pokemon..."),,SPEAKER_ALICIA)
	closemessage
	playse(SE_PIN)
	applymovement(LOCALID_MADRONE,moves(face_player,lock_facing_direction,jump_in_place_left,emote_exclamation_mark))
	waitmovement(LOCALID_MADRONE)
	waitse
	msgbox(format("Yikes! I can feel the wormhole energy seeping off of it from here!"),,SPEAKER_MADRONE,,EMOTE_SHOCK)
	msgbox(format("Wait, really? Is it not from here?"),,SPEAKER_ALICIA)
	closemessage
	applymovement(LOCALID_MADRONE,moves(face_up))
	waitmovement(LOCALID_MADRONE)
	playse(SE_ROTATING_GATE)
	msgbox(format("'Here' is a tricky word. My readings tell me its from this dimension.\pBut beyond that? No idea.\pIf you're that curious, I can calibrate the machine to where that {STR_VAR_1} is from... or at least close."),,SPEAKER_MADRONE)
	msgbox(format("You can only go if you have that {STR_VAR_1} with you... Using the portal with a target can get messy, so I'll warp you from here."),,SPEAKER_MADRONE)
	closemessage
	goto(TimeTravel_Dialogue_OfferTravelMadrone)
}

script TimeTravel_Dialogue_OfferTravelMadrone
{
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_ASKED_ABOUT_SNOM)
	msgbox(format("Do you want me to send you to when {STR_VAR_1} is from?"),MSGBOX_YESNO,SPEAKER_MADRONE)
	if(var(VAR_RESULT) == NO)
	{
		msgbox(format("No? Come talk to me when you're ready."),,SPEAKER_MADRONE)
		closemessage
		release
		end
	}
	goto(TimeTravel_Script_StartTravel)
}
