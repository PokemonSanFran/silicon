const FLAG_HIDE_ADAM = FLAG_TEMP_1
const FLAG_HIDE_FRANK = FLAG_TEMP_2
const FLAG_HIDE_ELEANOR = FLAG_TEMP_3
const FLAG_HIDE_SNOM = FLAG_TEMP_4
const FLAG_HIDE_EMRYS = FLAG_TEMP_5

script TimeTravel_Script_HideObjects
{
	setflag(FLAG_HIDE_SNOM)
	if (flag(FLAG_USING_WORMHOLE))
	{
		setflag(FLAG_HIDE_EMRYS)
		return
	}
	setflag(FLAG_HIDE_ADAM)
	setflag(FLAG_HIDE_FRANK)
	setflag(FLAG_HIDE_ELEANOR)
	return
}

script TimeTravel_Script_RoutePlayer
{
	switch(var(VAR_TIME_TRAVEL_STATE))
	{
		case TIME_TRAVEL_FIRST_WORMHOLE_USE:
			call(TimeTravel_Script_ResetIfNotWormhole)
		default: return
	}
}

script TimeTravel_Script_ResetIfNotWormhole
{
	if (flag(FLAG_USING_WORMHOLE))
	{
		return
	}
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_SACRIFICE_COMPLETE)
}

script TimeTravel_Script_StartTravel
{
	setflag(FLAG_USING_WORMHOLE)
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_FIRST_WORMHOLE_USE)
	warp(MAP_OROLAND_COLISEUM_OUTSIDE,WARP_ID_NONE,7,5)
}

script TimeTravel_Script_BumpIntoChampion
{
	callnative(StoreElenaorMonSpecies)
	msgbox(format("...Where am I?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	applymovement(LOCALID_PLAYER,Common_Movement_FaceRight)
	waitmovement(LOCALID_PLAYER)
	applymovement(LOCALID_PLAYER,Common_Movement_FaceLeft)
	waitmovement(LOCALID_PLAYER)
	applymovement(LOCALID_PLAYER,Common_Movement_FaceUp)
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Or when?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	msgbox(format("Did the machine bring me to the right place?"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	call(TimeTravel_Script_MoveCameraDown)
	msgbox(format("Why are you guys asking all these questions?"),SPEAKER_ADAM,,EMOTE_CONFUSE)
	msgbox(format("Aren't we suppsoed to be inside, celebrating?"),SPEAKER_ADAM)
	msgbox(format("You're such a strong Trainer, Adam. We just want to understand more about your training methods!"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Especially since you seemed to come out of nowhere..."),,SPEAKER_ELLEN_PAST)
	msgbox(format("I'm just stronger than you guys, you know? Hard work means a lot, but some people just have generational talent!"),,SPEAKER_ADAM,EMOTE_LAUGH)
	closemessage
	msgbox(format("Huh?"),,SPEAKER_ALICIA,TAIL_THOUGHT,EMOTE_CONFUSE)
	closemessage
	call(TimeTravel_Script_MoveCameraUp)
	playse(SE_PIN)
	applymovement(LOCALID_PLAYER,moves(face_down, emote_exclamation_mark))
	waitmovement(LOCALID_PLAYER)
	waitse
	playse(SE_BALL_OPEN)
	waitse
	clearflag(FLAG_HIDE_SNOM)
	callnative(BufferEleanorMonSprite)
	addobject(LOCALID_SNOM)
	callnative(PlayEleanorMonCry)
	waitmoncry

	callnative(BufferEleanorMonName)
	closemessage
	callnative(PlayEleanorMonCry)
	applymovement(LOCALID_SNOM,FollowerHopFast)
	waitmovement(LOCALID_SNOM)
	applymovement(LOCALID_SNOM,moves(emote_heart))
	callnative(PlayEleanorMonCry)
	waitmoncry
	msgbox(format("{STR_VAR_1} jumped out of its Ball!\n{STR_VAR_1} looks excited!"))
	closemessage	
	delay(8)
	applymovement(LOCALID_SNOM,moves(walk_fast_down * 3, lock_facing_direction))
	waitmovement(LOCALID_SNOM)
	playse(SE_BIKE_HOP)
	applymovement(LOCALID_SNOM,moves(jump_up, unlock_facing_direction, walk_fast_left * 4, face_right))
	waitmovement(LOCALID_SNOM)
	delay(8)
	applymovement(LOCALID_PLAYER,moves(face_left))
	msgbox(format("Wait - where are you going?"),,SPEAKER_ALICIA,TAIL_SHOUT,EMOTE_SHOCK)
	closemessage
	applymovement(LOCALID_ADAM,moves(face_up))
	waitmovement(LOCALID_ADAM)
	msgbox(format("Hey! Is that your poorly trained Pokemon running into me?"),,SPEAKER_ADAM_UNKNOWN,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("Definately still in Resido, plenty of rude jerks..."),,SPEAKER_ALICIA,TAIL_WHISPER)
	closemessage
	applymovement(LOCALID_PLAYER,moves(walk_down * 3))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Hey sorry. That {STR_VAR_1} -"),,SPEAKER_ALICIA)
	msgbox(format("You should be sorry! You're lucky I'm in a celebrating mood - it wouldn't look good for the new Champion of Resido completely destroy some chump!"),,SPEAKER_ADAM_UNKNOWN)
	closemessage
	applymovement(LOCALID_PLAYER,moves(face_left))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("I don't have time -"),,SPEAKER_ALICIA,TAIL_THOUGHT)
	applymovement(LOCALID_PLAYER,moves(face_down))
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Wait."),,SPEAKER_ALICIA,TAIL_THOUGHT)
	applymovement(LOCALID_PLAYER,moves(face_down))
	msgbox(format("You're the Champion? Of Resido?"),,SPEAKER_ALICIA,EMOTE_CONFUSE)
	closemessage
	applymovement(LOCALID_ADAM,moves(walk_up))
	msgbox(format("Do you live under a rock? I'm Champion Adam - latest winner of the Resido Pokemon League!"),,SPEAKER_ADAM)
	msgbox(format("...he didn't say SharpRise. So either I'm in a future where SharpRise doesn't own the League anymore, or..."),,SPEAKER_ALICIA,TAIL_THOUGHT)
	closemessage
	goto(TimeTravel_Script_StartBattle)
}

script TimeTravel_Script_StartBattle
{
	msgbox(format("You know what? Maybe you need a lesson in paying attention to current events!"),,SPEAKER_ADAM)
	closemessage
	trainerbattle_no_intro(TRAINER_ADAM,format("They told me I would be invincible..."))
	goto(TimeTravel_Script_PostBattle)
}

script TimeTravel_Script_PostBattle
{
	lock
	applymovement(LOCALID_SNOM,moves(walk_left * 4))
	applymovement(LOCALID_PLAYER,moves(face_left,delay_16))
	waitmovement(LOCALID_PLAYER)
	applymovement(LOCALID_PLAYER,moves(face_down))
	msgbox(format("...how are you this strong?"),,SPEAKER_ADAM,TAIL_WHISPER,EMOTE_CONFUSE)
	msgbox(format("She told me I would be... invincible!"),,SPEAKER_ADAM,,EMOTE_ANGRY)
	msgbox(format("That if I repped them, I would never lose again!"),,SPEAKER_ADAM,,EMOTE_ANGRY)	
	msgbox(format("I have best Pokemon...the best items... access to any move!"),,SPEAKER_ADAM,TAIL_WHISPER)
	msgbox(format("I'm supposed to be spokesperson... no, the FACE of SharpRise!"),,SPEAKER_ADAM,,EMOTE_ANGRY)
	msgbox(format("But I lost... to garbage like you?"),,SPEAKER_ADAM)
	closemessage
	delay(8)
	waitmovement(LOCALID_ADAM)
	call(TimeTravel_Script_MoveCameraDown)
	delay(16)
	applymovement(LOCALID_ADAM,moves(face_down))
	playse(SE_PIN)
	waitse
	applymovement(LOCALID_ADAM,moves(emote_exclamation_mark))
	waitmovement(LOCALID_ADAM)
	msgbox(format("...And you both saw that..."),,SPEAKER_ADAM)
	msgbox(format("You said she. Are you talking about Adelaide?"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Oh no... they're going to ruin me - "),,SPEAKER_ADAM)
	closemessage
	msgbox(format("I'll give you the choice to save yourself."),,SPEAKER_ELLEN_PAST)
	msgbox(format("If you want to maintain your illusion of the prodigy, undefeated Champion, you're going to go back inside to the party."),,SPEAKER_ELLEN_PAST)
msgbox(format("You'll talk, you'll drink, you'll mingle, and finally, you'll leave the party."),,SPEAKER_ELLEN_PAST)
	msgbox(format("And once you leave the party..."),,SPEAKER_ELLEN_PAST)
	msgbox(format("you will never be seen in Resido again."),,SPEAKER_ELLEN_PAST)
	closemessage
	playse(SE_PIN)
	applymovement(LOCALID_FRANK,moves(emote_exclamation_mark))
	applymovement(LOCALID_PLAYER,moves(emote_exclamation_mark))
	waitse
	waitmovement(LOCALID_PLAYER)
	msgbox(format("Ellen..."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Disobey me... and I will make sure to expose you."),,SPEAKER_ELLEN_PAST)
	msgbox(format("..."),,SPEAKER_ADAM)
	closemessage
	delay(8)
	msgbox(format("Time's up, Champion."),,SPEAKER_ELLEN_PAST)
	closemessage
	goto(TimeTravel_Dialogue_ReturnToPresent)
}

script TimeTravel_Dialogue_ReturnToPresent
{
applymovement(LOCALID_ADAM,moves(walk_left*2,walk_down * 8))
	waitmovement(LOCALID_ADAM)
	removeobject(LOCALID_ADAM)
	applymovement(LOCALID_ELEANOR,moves(face_left))
	applymovement(LOCALID_FRANK,moves(face_right))
	waitmovement(LOCALID_FRANK)
	msgbox(format("I knew we were onto somethin -"),,SPEAKER_ARMANDO_PAST)
	msgbox(format("He was a plant? Are you serious? We lost to that?!?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("Turning BD against us.\pBuying our League.\pTaking us apart piece of piece.\pNone of that was enough? They weren't happy with owning it, so they had to INVENT a Golden Boy?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("And we were so stupid!\pCompetitive integrity? Who cares!\pAs long as the PRODUCT looks good, right?"),,SPEAKER_ELLEN_PAST,TAIL_SHOUT,EMOTE_ANGRY)
	msgbox(format("That's what it looks like but - "),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Armando, we have to fight now. We don't have a choice! We heard it directly!"),,SPEAKER_ELLEN_PAST)
	msgbox(format("Ellen, we don't have anything concrete.\pWe need more information!\pThis isn't Arantraz. You aren't going to be able to rally the region like Magnus did... that was decades ago. We have a much steeper hill to climb!\pWe need a plan."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("So what, we just wait around for our magical oppurtunity?\pLike some Mythical Pokemon is going to come along and give us the clear cut evidence?"),,SPEAKER_ELLEN_PAST)
	msgbox(format("Look, I just want to lay down and look for a better oppurtunity to strike."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Armando, the tides are shifting NOW, whether we act or not. I just want to make sure they flow in our direction."),,SPEAKER_ELLEN_PAST)
	closemessage
	applymovement(LOCALID_ELEANOR,moves(walk_down * 8))
	waitmovement(LOCALID_ELEANOR)
	call(TimeTravel_Script_MoveCameraUp)
	applymovement(LOCALID_FRANK,moves(walk_up * 3, face_right))
	waitmovement(LOCALID_FRANK)
	applymovement(LOCALID_PLAYER,moves(face_left))
	msgbox(format("I'm sorry you had to deal with all of that. But I am surprised you were able to so expertly defeat Adam."),,SPEAKER_ARMANDO_PAST)
	msgbox(format("Who are you?"),,SPEAKER_ARMANDO_PAST)
	closemessage
	call(Common_EventScript_PhoneRinging)
	waitbuttonpress
	waitmessage
	callnative(BufferEleanorMonName)
	msgbox(format("{PLAYER}! Did you get seperated from that {STR_VAR_1}? The futher it gets from you, the weaker your connection gets! I have to pull you back!"),,SPEAKER_MADRONE,TAIL_SHOUT,EMOTE_SHOCK,PHONE_ON)
	closemessage
	playse(SE_POKENAV_HANG_UP)
	msgbox(format("We'll meet again Armando! And for what its worth, you're part of the reason I'm as strong as I am."),,SPEAKER_ALICIA,,EMOTE_HAPPY)
	closemessage
	clearflag(FLAG_USING_WORMHOLE)
	callnative(DeleteEleanorSnom)
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_COMPLETE)
	warp(MAP_SHARPRISE_SPIRE_LAB,0)
	waitstate
	release
	end
}

script TimeTravel_CheckSnom_Script
{
	if (var(VAR_ELLEN_STATE) != TAKEN_ELITE_MON)
	{
		return
	}


	if (var(VAR_TIME_TRAVEL_STATE) > TIME_TRAVEL_COMPLETE)
	{
		return
	}

	specialvar(VAR_RESULT,Quest_Ultrawormholeresearch_CountRemainingSubquests)
	if (var(VAR_RESULT) > 8)
	{
		return
	}

	callnative(BufferEleanorMonName)
	callnative(Script_IsEleanorSnomInParty)
	if (var(VAR_RESULT) == FALSE)
	{
		msgbox(format("I should bring Eleanor's {STR_VAR_1} to Madrone..."),,SPEAKER_ALICIA,TAIL_THOUGHT)
		closemessage
		return
	}

	switch (var(VAR_TIME_TRAVEL_STATE))
	{
		case TIME_TRAVEL_SACRIFICE_COMPLETE:
			msgbox("Should I ask him about {STR_VAR_1}?",MSGBOX_YESNO,SPEAKER_ALICIA,TAIL_THOUGHT)
			if (var(VAR_RESULT) == YES)
			{
				goto(TimeTravel_Dialogue_FirstTalkEllenMadrone)
			}
			else
			{
				closemessage
				return
			}
		case TIME_TRAVEL_ASKED_ABOUT_SNOM:
			goto(TimeTravel_Dialogue_OfferTravelMadrone)
		case TIME_TRAVEL_FIRST_WORMHOLE_USE:
			goto(TimeTravel_Dialogue_OfferTravelMadrone)
		default:
			return
	}
	return
}

script TimeTravel_Dialogue_FirstTalkEllenMadrone
{
	lock
	msgbox(format("Professor, a friend gave me this Pokemon..."),,SPEAKER_ALICIA)
	closemessage
	playse(SE_PIN)
	applymovement(LOCALID_MADRONE,moves(face_player,lock_facing_direction,jump_in_place_left,emote_exclamation_mark))
	waitmovement(LOCALID_MADRONE)
	waitse
	msgbox(format("Yikes! I can feel the wormhole energy seeping off of it from here!"),,SPEAKER_MADRONE,,EMOTE_SHOCK)
	msgbox(format("Wait, really? Is it not from here?"),,SPEAKER_ALICIA)
	closemessage
	applymovement(LOCALID_MADRONE,moves(face_up))
	waitmovement(LOCALID_MADRONE)
	playse(SE_ROTATING_GATE)
	msgbox(format("'Here' is a tricky word. My readings tell me its from this dimension.\pBut beyond that? No idea.\pIf you're that curious, I can calibrate the machine to where that {STR_VAR_1} is from... or at least close."),,SPEAKER_MADRONE)
	msgbox(format("You can only go if you have that {STR_VAR_1} with you... Using the portal with a target can get messy, so I'll warp you from here."),,SPEAKER_MADRONE)
	closemessage
	goto(TimeTravel_Dialogue_OfferTravelMadrone)
}

script TimeTravel_Dialogue_OfferTravelMadrone
{
	setvar(VAR_TIME_TRAVEL_STATE,TIME_TRAVEL_ASKED_ABOUT_SNOM)
	msgbox(format("Do you want me to send you to when {STR_VAR_1} is from?"),MSGBOX_YESNO,SPEAKER_MADRONE)
	if(var(VAR_RESULT) == NO)
	{
		msgbox(format("No? Come talk to me when you're ready."),,SPEAKER_MADRONE)
		closemessage
		release
		end
	}
	goto(TimeTravel_Script_StartTravel)
}

script TimeTravel_Script_MoveCameraDown
{
	special(SpawnCameraObject)
	applymovement(LOCALID_CAMERA,moves(walk_down * 2))
	waitmovement(LOCALID_CAMERA)
	special(RemoveCameraObject)
}

script TimeTravel_Script_MoveCameraUp
{
	special(SpawnCameraObject)
	applymovement(LOCALID_CAMERA,moves(walk_up * 2))
	waitmovement(LOCALID_CAMERA)
	special(RemoveCameraObject)
}
