const PLAYER = OBJ_EVENT_ID_PLAYER
const GRUNT1 = 1
const GRUNT2 = 2
const GRUNT3 = 3
const GRUNT4 = 4
const GRUNT5 = 5
const GRUNT6 = 6
const DAVID = 7

script GroupOfAssholesAppears_GroupAssault_Dialogue
{
    lock
    playbgm(MUS_RG_ENCOUNTER_ROCKET,FALSE)

    cueobjecttalking(GRUNT1)
    msgbox(format("You should be ashamed!"),,SPEAKER_ROCKET_MEMBER_A)
    cueobjecttalking(GRUNT2)
    msgbox(format("Haven't you done enough already?"),,SPEAKER_ROCKET_MEMBER_A)
    cueobjecttalking(GRUNT3)
    msgbox(format("Give Jelena back her store!"),,SPEAKER_ROCKET_MEMBER_A)

    applymovement(DAVID,GroupOfAssholesAppears_LookAround_Movement)
    waitmovement(0)
    msgbox(format("I don't know who Jelena is or what you want, or what I did to any of you!"),,SPEAKER_DAVID)

    cueobjecttalking(GRUNT4)
    msgbox(format("We don't want to hear it! How many lives are you going to try to ruin?"),,SPEAKER_ROCKET_MEMBER_A)

    applymovement(DAVID,Common_Movement_FaceLeft)
    waitmovement(0)
    msgbox(format("Ruin lives? I'm opening a store!"),,SPEAKER_DAVID)

    cueobjecttalking(GRUNT5)
    msgbox(format("Not if we have anything to say about it."),,SPEAKER_ROCKET_MEMBER_B)
    closemessage

    call(GroupOfAssholesAppears_PlayerToDavid_Script)
    goto(GroupOfAssholesAppears_PlayerIntervene_Dialogue)
}

script GroupOfAssholesAppears_PlayerToDavid_Script
{
    playse(SE_PIN)
    applymovement(PLAYER,Common_Movement_ExclamationMark)
    waitmovement(0)
    getplayerxy(VAR_TEMP_1,VAR_TEMP_2)

    if (var(VAR_TEMP_1) > 76)
    {
        applymovement(PLAYER, moves(walk_left * 2, walk_down * 2, walk_left * 3, walk_up, step_end))
    }
    else
    {
        applymovement(PLAYER, moves(walk_up, walk_right * 5, walk_up * 1, step_end))
    }
    waitmovement(0)
}

script GroupOfAssholesAppears_PlayerIntervene_Dialogue {
        msgbox(format("What the hell is going on here? Why are you ganging up on this guy?"),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                closemessage
        applymovement(GRUNT2,Common_Movement_FaceRight)
        applymovement(GRUNT3,Common_Movement_FaceRight)
        applymovement(GRUNT5,Common_Movement_FaceLeft)
        applymovement(GRUNT4,Common_Movement_FaceLeft)
        waitmovement(0)
        msgbox(format("Nothing wrong with a little protest, kid. And where do you get off, defending this guy?"),,SPEAKER_ROCKET_MEMBER_A)
        msgbox(format("This isn't a protest. This is straight-up intimidation. Leave him alone!"),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                applymovement(DAVID,Common_Movement_FaceDown)
        waitmovement(0)
        msgbox(format("I don't know what they were going to do or what I did to them... I don't have anything to give you yet! Please, just leave me alone!"),,SPEAKER_DAVID)
        closemessage
        applymovement(PLAYER,GroupOfAssholesAppears_LookAround_Movement)
        waitmovement(0)
        msgbox(format("He might be asking, but I'm not. Leave."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                setvar(VAR_GROUP_OF_ASSHOLES_STATE,SAVE_THE_TIDE_POPIDORA_PIER)
        closemessage
        startmenu_savescreen
        goto(GroupOfAssholes_StartBattle_Script)
}

script GroupOfAssholes_PostSaveTrigger_Script {
    lockall
    applymovement(DAVID,Common_Movement_FaceDown)
        waitmovement(0)
    applymovement(PLAYER,Common_Movement_FaceRight)
    waitmovement(0)
    applymovement(3,Common_Movement_FaceRight)
    applymovement(4,Common_Movement_FaceLeft)
    waitmovement(0)
    goto(GroupOfAssholes_StartBattle_Script)
}

script GroupOfAssholesAppears_GruntInteract_Script {
    lockall
        msgbox(format("Go ahead, defend this clown!"),MSGBOX_NPC,SPEAKER_ROCKET_MEMBER_B)
        end
}

script GroupOfAssholes_StartBattle_Script {
        lockall
        applymovement(DAVID,Common_Movement_FaceDown)
        waitmovement(0)
        msgbox(format("I don't know who the hell you think you are, but anybody defending this trash clearly isn't going to be reasoned with. We'll show you!"),,SPEAKER_ISHAN)
        closemessage
        trainerbattle_no_intro(TRAINER_IHSAN_AND_EMMITT,format("Woah, who did we pick a fight with?"))
        goto(GroupOfAssholesAppears_GroupLeaves_Dialogue)
}

script GroupOfAssholesAppears_GroupLeaves_Dialogue {
        lockall
        startcutscene(JUMPPLAYER_GROUPOFASSHOLESAPPEARS)
        setvar(VAR_GROUP_OF_ASSHOLES_STATE,PLAYER_HAS_DEFEATED_THE_TIDE)
        msgbox(format("Pfft, looks like the League sent by some enforcers to clear us out. Lame as hell."),SPEAKER_ROCKET_MEMBER_B)
        msgbox(format("You both make me sick. Leave our people alone! *spits*"),SPEAKER_ISHAN)
        closemessage
        applymovement(GRUNT1, moves(walk_down * 2, walk_left * 3, walk_down * 3, walk_left * 3, step_end))
        applymovement(GRUNT2, moves(walk_down, walk_left * 3, walk_down * 3, walk_left * 3, step_end))
        applymovement(GRUNT3, moves(walk_down, walk_left * 4, walk_down * 3, walk_left * 3, step_end))
        applymovement(GRUNT4, moves(walk_down, walk_left * 6, walk_down * 3, walk_left * 3, step_end))
        applymovement(GRUNT5, moves(walk_down, walk_left * 7, walk_down * 3, walk_left * 3, step_end))
        applymovement(GRUNT6, moves(walk_down * 2, walk_left * 7, walk_down * 3, walk_left * 3, step_end))
        waitmovement(0)
        removeobject(GRUNT1)
        removeobject(GRUNT2)
        removeobject(GRUNT3)
        removeobject(GRUNT4)
        removeobject(GRUNT5)
        removeobject(GRUNT6)
        fadedefaultbgm
        msgbox(format("Yeah! And don't come back assholes!"),SPEAKER_DAVID,TAIL_SHOUT,EMOTE_ANGRY)
        goto(GroupOfAssholesAppears_ExplainTeam_Dialogue)
}

script GroupOfAssholesAppears_ExplainTeam_Dialogue {
        applymovement(PLAYER,Common_Movement_FaceUp)
        msgbox(format("Thank you so much..."),SPEAKER_DAVID,,EMOTE_LOVE)
        msgbox(format("Are you going to be okay?"),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                msgbox(format("Honestly? Gonna need to pop several Xanax after I come down from this panic attack, but I'll survive for sure."),,SPEAKER_DAVID,,EMOTE_SWEAT)
        msgbox(format("Why were they bullying you? Who are they?"),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                msgbox(format("Uhh... I think A Wave or something stupid like that. I don't know what they want from me, they're like this band of idiots that just roll around everywhere, getting high and telling people they can't do things. Guess it was my turn."),,SPEAKER_DAVID)
        msgbox(format("That doesn't make any sense! You're just trying to open a.... Uh..."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                msgbox(format("Memento Boutique!"),,SPEAKER_DAVID,,EMOTE_HAPPY)
        msgbox(format("A souvenir shop?"),MSGBOX_DEFAULT,SPEAKER_ALICIA,,EMOTE_CONFUSE)
                msgbox(format("When I pitched it to the League, it was a little more...upscale than some tacky place to buy snow globes and magnets. We're talking handcrafted items from the people of Resido! "),,SPEAKER_DAVID)
        msgbox(format("Oh, yeah... that makes sense."),MSGBOX_DEFAULT,SPEAKER_ALICIA)
                goto(GroupOfAssholesAppears_GiveRewards_Dialogue)
}

script GroupOfAssholesAppears_GiveRewards_Dialogue {
    msgbox(format("You'll have to come by once I open! And here, take this!"),,SPEAKER_DAVID,,EMOTE_LOVE)
        closemessage
        giveitem(ITEM_DOWSING_MACHINE,1)
        msgbox(format("If you put that on, it'll point you towards hidden shit on the ground. My sister gave that to me... but I'm like literally never going to use it. Me? Dig around in the dirt? Wearing that? Absolutely not."),,SPEAKER_DAVID)
        closemessage
        addvar(VAR_STORYLINE_STATE,1)
        setvar(VAR_TEMP_0,0)
        endcutscene
        call(TryAutoSave)
        call(AaandWereBack_Monologue)
        releaseall
        end
}

script GroupOfAssholesAppears_LookForXanax_Dialogue {
        lockall
        applymovement(DAVID,GroupOfAssholesAppears_LookAround_Movement)

        if (var(VAR_GROUP_OF_ASSHOLES_STATE) == SAVE_THE_TIDE_POPIDORA_PIER)   {
            msgbox(format("Somebody help me..."),,SPEAKER_DAVID,,EMOTE_SAD)
        }else{
            msgbox(format("Now, where did I put those pills...?"),,SPEAKER_DAVID,TAIL_WHISPER)
        }
        closemessage
        releaseall
        end
}

movement GroupOfAssholesAppears_LookAround_Movement {
    face_left
        delay_16
        face_right
        delay_16
        step_end
}

