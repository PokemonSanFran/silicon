const PLAYER = OBJ_EVENT_ID_PLAYER
const KAI = 1
const ADAORA = 2
const MAGNUS = 3

const FLAG_HIDE_SEARCH = FLAG_TEMP_1
const VAR_UNDERGROUND_DOOR = VAR_TEMP_C

script ThisIsntRandom_CheckDoor_Script
{
    if (var(VAR_PARC_STATE) != PLAYER_DISCOVERED_LAB)
    {
        return
    }

    if (var(VAR_STORYLINE_STATE) != STORY_POST_EARTHQUAKE)
    {
        return
    }
    call(ThisIsntRandom_UnlockDoor_Dialogue)
}

script ThisIsntRandom_HideObject_AboveGround_Script
{
    if (var(VAR_PARC_STATE) != LAB_NOT_DISCOVERED || (var(VAR_STORYLINE_STATE) != STORY_POST_EARTHQUAKE)){
        setflag(FLAG_HIDE_SEARCH)
    }
    return
}

script ThisIsntRandom_ObjectSetUp_Script
{
    fadescreen(FADE_TO_BLACK)

        setobjectxy(PLAYER,13,18)
        TeleportCamera(13,18)
        applymovement(PLAYER,Common_Movement_FaceDown)

        clearflag(FLAG_HIDE_SEARCH)

        addobject(KAI)
        setobjectxy(KAI,12,19)
        applymovement(KAI,Common_Movement_FaceUp)

        addobject(ADAORA)
        setobjectxy(ADAORA,13,19)
        applymovement(ADAORA,Common_Movement_FaceUp)

        addobject(MAGNUS)
        setobjectxy(MAGNUS,14,19)
        applymovement(MAGNUS,Common_Movement_FaceUp)

        fadescreen(FADE_FROM_BLACK)
}

script ThisIsntRandom_UnlockDoor_Dialogue
{
    lock
        startcutscene(JUMPPLAYER_THISISNTRANDOM_DISCOVER)
        call(ThisIsntRandom_ObjectSetUp_Script)
        setvar(VAR_PARC_STATE,THETIDE_DISCOVERED_LAB)

        playse(SE_PIN)
        waitse
        applymovement(KAI,Common_Movement_ExclamationMark)
        waitmovement(0)

        msgbox(format("Oh,  “TM15” is literally the 15th Technical Machine! I feel pretty dumb, but damn good work."),,SPEAKER_ADAORA)

        closemessage

        applymovement(KAI,ThisIsRandom_WalkDownStairsA)
        playse(SE_EXIT)
        waitse

        applymovement(MAGNUS,ThisIsRandom_WalkDownStairsC)
        playse(SE_EXIT)
        waitse
        applymovement(ADAORA,ThisIsRandom_WalkDownStairsB)
        playse(SE_EXIT)
        waitse
        waitmovement(ADAORA)

        removeobject(ADAORA)
        removeobject(KAI)
        removeobject(MAGNUS)

        applymovement(PLAYER,Common_Movement_WalkUp)
        playse(SE_EXIT)
        endcutscene
        fadeblackandsetremovewarpfadeflag
        warp(MAP_SECRET_PATH,0)
        end
}

script ThisIsntRandom_UnlockElectronicDoor_Script
{
    setmetatile(7,0,0x269,TRUE)
        setmetatile(7,1,0x271,TRUE)
        setmetatile(7,2,0x22F,FALSE)
        setmetatile(7,3,0x242,FALSE)
        special(DrawWholeMapView)
}

script ThisIsntRandom_UndergroundDoor_Dialogue
{
    lock

        startcutscene(JUMPPLAYER_THISISNTRANDOM_MON1)
        msgbox(format("And now my turn…"),,SPEAKER_MAGNUS)

        closemessage

        applymovement(MAGNUS,ThisIsntRandom_WalkToKeypad)
        waitmovement(MAGNUS)

        playse(SE_DING_DONG)
        waitse
        playse(SE_BANG)
        waitse

        call(ThisIsntRandom_UnlockElectronicDoor_Script)

        msgbox(format("How did you…?"),,SPEAKER_ADAORA)

        closemessage

        applymovement(MAGNUS,Common_Movement_FaceLeft)
        waitmovement(MAGNUS)


        msgbox(format("The second set of numbers was the door code."),,SPEAKER_MAGNUS)

        closemessage

        applymovement(PLAYER,Common_Movement_FaceRight)

        msgbox(format("So… is Keiying actually trying to help us?"),,SPEAKER_ALICIA)

        msgbox(format("About to find out. Let’s rage!"),,SPEAKER_KAI_TIDE)

        msgbox(format("Hmm. If we’re all going, we should be okay. Let’s see."),,SPEAKER_ADAORA)

        closemessage

        applymovement(MAGNUS,ThisIsntRandom_WalkDownHallwayA)
        applymovement(ADAORA,ThisIsntRandom_WalkDownHallwayB)
        applymovement(KAI,ThisIsntRandom_WalkDownHallwayC)
        waitmovement(KAI)

        setflag(FLAG_HIDE_SEARCH)
        removeobject(MAGNUS)
        removeobject(ADAORA)
        removeobject(KAI)

        setvar(VAR_PARC_STATE,MAGNUS_UNLOCKED_DOOR)
        call(TryAutoSave)
        release
        end
}

script ThisIsntRandom_WalkIntoLab_Dialogue
{
    lock
        setvar(VAR_PARC_STATE,TEAM_INSIDE_LAB)

        msgbox(format("Looks old as hell. And empty. Let’s split up and see what we turn up."),,SPEAKER_KAI_TIDE)

        msgbox(format("Take these."),,SPEAKER_ADAORA)

        closemessage

        applymovement(PLAYER,Common_Movement_FaceLeft)

        msgbox(format("Walkie talkies?"),,SPEAKER_ALICIA)

        msgbox(format("To stay in touch in case we find anything. "),,SPEAKER_ADAORA)

        msgbox(format("Yup!"),,SPEAKER_KAI_TIDE)

        closemessage

        applymovement(KAI,ThisIsntRandom_WalkIntoLabA)
        applymovement(MAGNUS,ThisIsntRandom_WalkIntoLabB)
        applymovement(ADAORA,ThisIsntRandom_WalkIntoLabC)

        waitmovement(ADAORA)

        removeobject(ADAORA)
        removeobject(KAI)
        removeobject(MAGNUS)

        call(TryAutoSave)
        endcutscene
        release
        end
}

script ThisIsntRandom_CheckProgressA_Script
{
    if (!flag(FLAG_DEFEATED_LAB_MON_A))
    {
        goto(ThisIsntRandom_StopProgress_Script)
    }
    else
    {
        call(ThisIsntRandom_DefeatedFirst_Script)
    }
}

script ThisIsntRandom_CheckProgressB_Script
{
    if (!flag(FLAG_DEFEATED_LAB_MON_B))
    {
        goto(ThisIsntRandom_StopProgress_Script)
    }
    else
    {
        call(ThisIsntRandom_DefeatedSecond_Script)
    }
}

script ThisIsntRandom_StopProgress_Script
{
    lock
        random(2)

        switch (var(VAR_RESULT))
{
            case 0: msgbox(format("That Pokemon looks likes its in pain..."),,SPEAKER_ALICIA)
            case 1: msgbox(format("I wonder if I can help that Pokemon..."),,SPEAKER_ALICIA)
        }


    closemessage

        applymovement(PLAYER,Common_Movement_WalkDown)
        waitmovement(PLAYER)
        release
        end
}

script ThisIsntRandom_CoreEncounter_Script
{
    startmenu_savescreen
        faceplayer

        if (!flag(FLAG_DEFEATED_LAB_MON_A))
{
            setwildbattle(SPECIES_DONPHAN, 60, ITEM_NONE)
                playmoncry(SPECIES_DONPHAN, CRY_MODE_ENCOUNTER)
                }
        elif (!flag(FLAG_DEFEATED_LAB_MON_B))
{
            setwildbattle(SPECIES_GOLURK, 60, ITEM_NONE)
                playmoncry(SPECIES_GOLURK, CRY_MODE_ENCOUNTER)
        }

        delay(40)
        waitmoncry
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        dowildbattle
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch (var (VAR_RESULT))
{
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                goto(ThisIsntRandom_RemoveMon_Script)
        }
    release
        end
}

script ThisIsntRandom_RemoveMon_Script
{
    lock
        closemessage
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        call(TryAutoSave)
        release
        end
}

script ThisIsntRandom_DefeatedFirst_Script
{
    lock
        startcutscene(JUMPPLAYER_THISISNTRANDOM_MON1)
        setvar(VAR_PARC_STATE,FREED_LAB_POKEMON_1)
        msgbox(format("Are you guys seeing Pokémon strapped to machines?"),,SPEAKER_ALICIA)

        msgbox(format("Yeah, I was able to set the poor thing free though."),,SPEAKER_ADAORA)

        msgbox(format("I got attacked, but yeah, I dealt with it. I found some documentation too - this place is called the Infinity Energy Amplification Center. The goal was to use Water and Electric-type Pokémon to help combat fires and power outages."),,SPEAKER_KAI_TIDE)

        msgbox(format("Both huge problems in our area…"),,SPEAKER_ADAORA)

        msgbox(format("Right! Looks like they wanted to extract the energy from Pokémon, beef it up, and direct it to locations all over the region. Testing went well, but the extraction was painful and traumatic for the Pokémon, so they shut it down… 50 years ago."),,SPEAKER_KAI_TIDE)

        msgbox(format("But all of these machines are on! These Pokémon are suffering now!"),,SPEAKER_ADAORA)

        msgbox(format("I’m just telling you what I’m reading!"),,SPEAKER_KAI_TIDE)

        msgbox(format("Let’s keep looking, and freeing more Pokémon then."),,SPEAKER_MAGNUS)

        closemessage

        endcutscene
        release
        end
}

script ThisIsntRandom_DefeatedSecond_Script
{
    lock
        startcutscene(JUMPPLAYER_THISISNTRANDOM_MON2)
        setvar(VAR_PARC_STATE,FREED_LAB_POKEMON_2)
        msgbox(format("I started messing around with this computer and found a system access table!  Each row has a date attached to it... a bunch in 1968... two in 1971... and then three in the last week."),,SPEAKER_ADAORA)

        msgbox(format("Adaora… can you get me the exact dates of the ones in 1971?"),,SPEAKER_MAGNUS)

        msgbox(format("I think so, hold on… yeah! Oh, when I get into this screen, I can see all the data for each specific test. 1971… April 22nd, and June 4th."),,SPEAKER_ADAORA)

        msgbox(format("Those monsters. They were trying to kill us."),,SPEAKER_MAGNUS)

        msgbox(format("What? Are those dates special?"),,SPEAKER_KAI_TIDE)

        msgbox(format("The second date is the end of the Arantraz riots. What types of Pokémon did you guys fight down here?"),,SPEAKER_ADAORA)

        msgbox(format("Silicobra, Graveler, Larvitar…"),,SPEAKER_KAI_TIDE)

        msgbox(format("Numel, Piloswine, Nidoking…"),,SPEAKER_MAGNUS)

        msgbox(format("They’re all Ground-type Pokémon."),,SPEAKER_ALICIA)

        msgbox(format("So what?"),,SPEAKER_KAI_TIDE)

        msgbox(format("The documentation called out specific types of Pokémon are used for specific problems."),,SPEAKER_ADAORA)

        msgbox(format("Which means it would've be easy to modify the machinery to allow Ground-types to trigger Earthquakes. Those bastards used this system to attack Arantraz."),,SPEAKER_MAGNUS)

        msgbox(format("Oh no. I didn’t recognize the earlier names, but the activities in the last few days… their access signature is Adelaide."),,SPEAKER_ADAORA)
        msgbox(format("Oh... this table has more columns. You can see specific location names! MermerezaCity, PerlaciaCity, HodouCity... and then a bunch in a row on Arantraz... and the last one is HalaiIsland."),,SPEAKER_MAGNUS)

        msgbox(format("Wait... but they targeted HalaiIsland. Why attack innocent people?"),,SPEAKER_KAI_TIDE)


        msgbox(format("…Let’s keep going."),,SPEAKER_ADAORA)

        closemessage
        call(TryAutoSave)
        endcutscene
        release
        end
}

movement ThisIsRandom_WalkDownStairsA {
    walk_up * 2
        walk_right
        step_end
}
movement ThisIsRandom_WalkDownStairsB {
    walk_right
        walk_up * 2
        walk_left
        step_end
}
movement ThisIsRandom_WalkDownStairsC {
    walk_up * 2
        walk_left
        step_end
}

movement ThisIsntRandom_WalkToKeypad {
    walk_right * 4
        walk_up
        step_end
}

movement ThisIsntRandom_WalkDownHallwayA {
    walk_right * 15
        step_end
}
movement ThisIsntRandom_WalkDownHallwayB {
    walk_right * 20
        step_end
}
movement ThisIsntRandom_WalkDownHallwayC {
    walk_down
        walk_right * 18
        step_end
}

movement ThisIsntRandom_WalkIntoLabA {
    walk_down
        walk_left * 5
        walk_left * 4
        step_end
}

movement ThisIsntRandom_WalkIntoLabB {
    delay_16
        walk_right * 7
        delay_16
        delay_16
        step_end
}

movement ThisIsntRandom_WalkIntoLabC {
    walk_down
        walk_left * 4
        face_down
        delay_16
        walk_down
        walk_left * 4
        step_end
}
