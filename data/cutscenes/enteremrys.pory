const PLAYER = OBJ_EVENT_ID_PLAYER
const EMRYS = 1
const FLAG_HIDE_PUZZLE = FLAG_TEMP_1
const FLAG_HIDE_EMRYS = FLAG_TEMP_3

script EnterEmrys_LeaderPosition_Script{
    switch (var (VAR_GYM_3_STATE)) {
        case 0: return
        case 1: setflag(FLAG_TEMP_1)
        default: goto(EnterEmrys_StartBattle_ObjectSetup_Script)

    }
}

script EnterEmrys_ObjectSetUp_Script {
    setobjectmovementtype(EMRYS, MOVEMENT_TYPE_FACE_LEFT)
}

script EnterEmrys_StartBattle_ObjectSetup_Script {
    setflag(FLAG_TEMP_1)
    setobjectxy(EMRYS, 10, 6)
    setobjectxyperm(EMRYS, 10, 6)
    setobjectmovementtype(EMRYS, MOVEMENT_TYPE_LOOK_AROUND)
}

script AutomaticSolvePuzzle_Script {
    lock
    setvar(VAR_GYM_3_STATE,GYM_PUZZLE_COMPLETE)
    msgbox(format("Puzzle is solved. Leave and come back."), MSGBOX_NPC)
    closemessage
    release
    end
}

script EnterEmrys_WalkToBattlefield_Script {
    setvar(VAR_GYM_3_STATE, GYM_LEADER_ON_BATTLEFIELD)
    lock
    playse(SE_PIN)
    applymovement(EMRYS, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(EMRYS, EnterEmrys_WalkToBattlefield_Movement)
    waitmovement(0)
    releaseall
    end
}

script Emrys_Event_Script {
    lockall
    switch (var (VAR_GYM_3_STATE)) {
    case 2:
        goto(EnterEmrys_MuseumArrangement_Dialogue)
    case 3:
        goto(EnterEmrys_StartBattle_Script)
    case 4:
        goto(EnterEmrys_PostBattle_Dialogue)
    case 5:
        goto(EnterEmrys_PostBattle_Dialogue2)
    }
}

script EnterEmrys_MuseumArrangement_Dialogue {
    setvar(VAR_GYM_3_STATE,GYM_LEADER_ON_BATTLEFIELD)
    msgbox(gText_EnterEmrys_IWasThinking, MSGBOX_NPC,SPEAKER_EMRYS)
    reveal(REVEAL_EMRYS)
    applymovement(EMRYS, EmrysLookAround_Movement)
    waitmovement(0)
    msgbox(gText_EnterEmrys_HasYourVisit,,SPEAKER_EMRYS)
        msgbox(gText_EnterEmrys_UhYup,MSGBOX_DEFAULT,SPEAKER_ALICIA)
    msgbox(gText_EnterEmrys_ICanHearTheOrre,,SPEAKER_EMRYS)
    setvar(VAR_GYM_3_STATE,GYM_SAVE_BEFORE_LEADER)
        startmenu_savescreen
    goto(EnterEmrys_StartBattle_Script)
}

script EnterEmrys_StartBattle_Script {
    trainerbattle_single(TRAINER_EMRYS, gText_EnterEmrys_LetsSeeHow, gText_EnterEmrys_IfThisBattle, EnterEmrys_Defeated_Script, NO_MUSIC)
}

script EnterEmrys_Defeated_Script {
    lockall
    setflag(FLAG_BADGE03_GET)
    setvar(VAR_GYM_3_STATE,GYM_DEFEATED_LEADER)
    setvar(VAR_EMRYS_STATE,BATTLE_1_COMPLETE)
    addvar(VAR_STORYLINE_STATE, 1)
    call(WowYoureStrong_CheckCutsceneScript)
    msgbox(gText_EnterEmrys_WowIllReallyNeed,,SPEAKER_EMRYS)
    closemessage
    call(EnterEmrys_ExplainBuzzr_Dialogue)
    tweetfanfare(TWEET_GYM_PERLACIA_CITY_BADGE,MUS_GET_BADGE)
    giveitem(ITEM_TM34)
    setvar(VAR_TEMP_0,0)
    call(TryAutoSave)
    releaseall
    end
}

script EnterEmrys_ExplainBuzzr_Dialogue {
    special(GetNumberOfBadges)

    if(var(VAR_RESULT) != 1)
    {
        return
    }

    msgbox(gText_EnterEmrys_LetUsRecord,,SPEAKER_EMRYS)
    msgbox(gText_EnterBelen_WhatsBuzzr,,SPEAKER_ALICIA)
    msgbox(gText_EnterEmrys_NoWonderYouHavent,,SPEAKER_EMRYS)
    msgbox(gText_EnterEmrys_BuzzrIs,,SPEAKER_EMRYS)
    call(EnterBelen_DownloadBuzzr_Dialogue)
    msgbox(gText_EnterEmrys_AndNowFor,,SPEAKER_EMRYS)
    closemessage
    return
}

script EnterEmrys_PostBattle_Dialogue {
	bufferitemname(STR_VAR_1,ITEM_TM03)
	buffermovename(STR_VAR_2,MOVE_ICE_SHARD)
    setvar(VAR_GYM_3_STATE,GYM_EXPLAINED_BADGE_AND_TM)
    msgbox(gText_EnterEmrys_ThatPositiveSanction, MSGBOX_NPC,SPEAKER_EMRYS)
        closemessage
    releaseall
    end
}

script EnterEmrys_PostBattle_Dialogue2 {
    msgbox(gText_EnterEmrys_WellNeedToBattle, MSGBOX_NPC,SPEAKER_EMRYS)
        closemessage
    releaseall
    end
}

movement EnterEmrys_WalkToBattlefield_Movement {
    walk_right * 9
    walk_down
    face_left
    step_end
}

movement EmrysLookAround_Movement {
    face_up
    delay_16
    face_down
    delay_16
    face_right
    delay_16
    face_left
    step_end
}

text gText_EnterEmrys_IWasThinking {
    format("I was thinking about the arrangement of the museum. Museums tend to organize through etic categories, but I always wonder... how far off are we from their emic counterparts...? Sorry, I'm Emrys, Chief of the Perlacia City Gym!")
}
text gText_EnterEmrys_HasYourVisit {
    format("Has your visit been a positive time investment? There was a ton of qualitative research into curating the best possible experience for both patrons and challengers.")
}
text gText_EnterEmrys_UhYup {
    format("Uh... yup. Yeah. It's cool.")
}
text gText_EnterEmrys_ICanHearTheOrre {
    format("I can hear the Orre in you. Enchanting.")
}
text gText_EnterEmrys_LetsSeeHow {
    format("Let's see how your socialization impacted your battling ability!")
}
text gText_EnterEmrys_TimeToPay {
    format("Time to pay your tribute.")
}
text gText_EnterEmrys_IfThisBattle {
    format("If this battle is any indication, your assimilation is going perfectly!")
}
text gText_EnterEmrys_YouRecieved {
    format("You recieved the EmrysBadge Badge!")
}

text gText_EnterEmrys_WowIllReallyNeed {
    format("Wow, I'll really need to re-think my tactical core values. Awesome battle, {PLAYER}!")
}

text gText_EnterEmrys_ThatPositiveSanction {
    format("{STR_VAR_1} is like a shaman in your pocket and will teach {STR_VAR_2}.")
}
text gText_EnterEmrys_WellNeedToBattle {
    format("We'll need to battle again in the future. I want to increase our data set - a sample size of one isn't enough for any conclusions about our relative skill levels!")
}

text gText_EnterEmrys_LetUsRecord {
    format("Let us record this achievement for the masses on Buzzr to see!")
}

text gText_EnterEmrys_NoWonderYouHavent {
    format("No wonder you haven't properly accimlated!")
}

text gText_EnterEmrys_BuzzrIs {
    format("Buzzr is a medium by which one can tap into the zzeitgiest of the region - everybody can use their phones to write history! Your spoils of victory in Resido's Pok√©mon League are posts from the Gym Leader.When all the leaders have posted about you, you've qualified for the Championship.")
}

text gText_EnterEmrys_AndNowFor {
    format("And now, a record from the beginning of the {PLAYER} empire!")
}
