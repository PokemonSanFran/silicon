const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_1

script OldAssholeAppears_ControlVisibility_Script
{
    if (var(VAR_ANBEH_BEND_STATE) != SAVE_CHARLOTTE_LOMBARD)
    {
        call(OldAssholeAppears_HideCharlotte_Script)
    }
    else
    {
        call(OldAssholeAppears_InteractedObject_Script)
    }
}

script OldAssholeAppears_HideCharlotte_Script
{
    setflag(FLAG_HIDE_CHARLOTTE)
}

script OldAssholeAppears_InteractedObject_Script
{
    setobjectxyperm(CHARLOTTE,15,10)
    setobjectxy(CHARLOTTE,15,10)
}

script OldAssholeAppears_MoveCharlotteIntoPosition_Script
{
    clearflag(FLAG_HIDE_CHARLOTTE)
    addobject(CHARLOTTE)
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

    if (var(VAR_TEMP_0) != 20)
    {
        return
    }
    setobjectxyperm(CHARLOTTE,12,10)
    setobjectxy(CHARLOTTE,12,10)
}

script OldAssholeAppears_MoveCharlotteToPlayer
{
    playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
    applymovement(CHARLOTTE, moves(walk_right * 7))
    waitmovement(CHARLOTTE)
}

script OldAssholeAppear_TriggerCharlotte_Script
{
    lock
    applymovement(PLAYER,Common_Movement_FaceLeft)
    waitmovement(PLAYER)
    call(OldAssholeAppears_MoveCharlotteIntoPosition_Script)
    call(OldAssholeAppears_MoveCharlotteToPlayer)
    call(OldAssholeAppears_StartDialogue)
}

script OldAssholeAppears_Interact_Script
{
    lockall
    faceplayer
    goto(OldAssholeAppears_StartBattle_Script)
}

script OldAssholeAppears_StartDialogue
{
    setvar(VAR_ANBEH_BEND_STATE,SAVE_CHARLOTTE_LOMBARD)
    msgbox(format("You know, we're probably standing on a volcano, {PLAYER}."),,SPEAKER_CHARLOTTE)

    msgbox(format("I'm sorry, what?"),,SPEAKER_ALICIA)

    msgbox(format("Or, what used to be one at least. Turns out this region is close to a tectonic faultline, which when they shift, make a bunch of tiny volcanoes, all over the place. It's kind of magical when you think about it, huh?"),,SPEAKER_CHARLOTTE)

    msgbox(format("I don't know about magical, but it's pretty neat, yeah!"),,SPEAKER_ALICIA)
    msgbox(format("Tell you what - I do have something magical for you, but you'll have to beat me for it first!"),,SPEAKER_CHARLOTTE)
    startmenu_savescreen
    goto(OldAssholeAppears_StartBattle_Script)
}

script OldAssholeAppears_StartBattle_Script
{
    msgbox(format("Now, let's see how far behind you are!"),,SPEAKER_CHARLOTTE)
    closemessage
    trainerbattle_no_intro(TRAINER_CHARLOTTE_OLDASSHOLEAPPEARS, format("I'm starting to question all those years at the private battle academy..."))
    goto(OldAssholeAppears_PostBattle_Script)
}

script OldAssholeAppears_PostBattle_Script
{
    lockall
    startcutscene(JUMPPLAYER_OLDASSHOLEAPPEARS)
    applymovement(CHARLOTTE,Common_Movement_FacePlayer)
    msgbox(format("Good to see you're taking this all seriously, {PLAYER}! My rival should be strong to keep me sharp! Well, true to my word..."),,SPEAKER_CHARLOTTE)

    giveitem(ITEM_SS_TICKET)
    msgbox(format("Are you familiar with Arriba?"),MSGBOX_YESNO,SPEAKER_CHARLOTTE)
    if (var (VAR_RESULT) == 1){
        msgbox(format("Thought so. They're everywhere, right?"),,SPEAKER_CHARLOTTE)
    } else {
        msgbox(format("Don't have it in Orre? Arriba lets you call a ride from anywhere to anywhere else that you've been."),,SPEAKER_CHARLOTTE)
    }
    msgbox(format("That card lets you use Arriba for free! I went to the SharpRise Spire, and the League Commissioner gave me these for being the first Challenger to visit! It also lets you use the Subway System for free, but I think I'll stick to Arriba. Less homeless people to deal with, you know?"),,SPEAKER_CHARLOTTE)
    msgbox(format("Oh, okay... thanks!"),,SPEAKER_ALICIA)

    msgbox(format("That reminds me, Daddy shipped over way too many Full Restores and Max Elixirs, so I'll heal you up too."),,SPEAKER_CHARLOTTE)
    closemessage

    playse(SE_USE_ITEM)
    special(HealPlayerParty)

    msgbox(format("Now we're even after I ditched breakfast. All right, time to go challenge the FortYobu Gym. So glad Daddy sent me these Go-Goggles, because they're impossible to find new. And like hell I'm gonna get one from the locals..."),,SPEAKER_CHARLOTTE)

    closemessage
    goto(OldAssholeAppears_CharlotteExit_Script)
}

script OldAssholeAppears_CharlotteExit_Script
{
    setvar(VAR_ANBEH_BEND_STATE,DEFEATED_CHARLOTTE_LOMBARD)
    addvar(VAR_STORYLINE_STATE,1)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    applymovement(CHARLOTTE, moves(walk_left * 6, walk_down * 2, walk_left * 3, step_end))
    waitmovement(0)
    fadedefaultbgm
    removeobject(CHARLOTTE)
    setvar(VAR_TEMP_0,0)
    endcutscene
    call(TryAutoSave)
    call(AaandWereBack_Monologue)
    releaseall
    end
}

movement OldAssholeAppears_WalkNorthtoCharlotte_Movement {
    walk_right * 3
    walk_up * 3
    face_up
    step_end
}

movement OldAssholeAppears_WalkWesttoCharlotte_Movement {
    walk_left * 6
    face_up
    step_end
}

