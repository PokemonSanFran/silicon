const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const OLIVER = 2
const ALCMENE = 3
const ROCKET_A = 4
const ROCKET_B = 5
const CROWD_A = 6
const CROWD_B = 7
const CROWD_C = 8
const POLICE_A = 9
const POLICE_B = 10
const ADELAIDE = 11
const ORI = 12

const FLAG_HIDE_CHARLOTTE_COMPOUND = FLAG_TEMP_1
const FLAG_HIDE_OLIVER_COMPOUND = FLAG_TEMP_2
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_1

script VSGarbodor_Arrest_ObjectSetup_Script {
    setobjectxy(ROCKET_A, 26, 19)
        setobjectxy(ROCKET_B, 26, 20)
        setobjectxy(CHARLOTTE, 21, 21)
        setobjectxy(OLIVER, 21, 22)

        setobjectxyperm(ROCKET_A, 26, 19)
        setobjectxyperm(ROCKET_B, 26, 20)
        setobjectxyperm(CHARLOTTE, 21, 21)
        setobjectxyperm(OLIVER, 21, 22)
}

script VSGarbodor_ObjectCleanUp_Script{
    if ((var(VAR_STORYLINE_STATE) > STORY_FERRY_FIXED) && ((var(VAR_STORYLINE_STATE) != STORY_DEFEATED_GARBODOR))){
        setflag(FLAG_HIDE_CHARLOTTE_COMPOUND)
            setflag(FLAG_HIDE_OLIVER_COMPOUND)
            return
    }
}

script VSGarbodor_ConstructionObjectCleanUp_Script{
    if (var(VAR_CONSTRUCTION_STRIKE_STATE) != START_VS_GARBODOR){
        setflag(FLAG_HIDE_CHARLOTTE)
    }
    return
}

script VSGarbodor_Scouting_Dialogue {
    lock
        applymovement(PLAYER, Common_Movement_FaceRight)
        waitmovement(0)
        msgbox(format("I did some scouting while waiting for you. Both sides have two Garbodor and two Trainers guarding ‘em. Do you care which side you take?"),,SPEAKER_CHARLOTTE)
        dynmultichoice(18,6,TRUE,3,0,NULL,
            "Left",
            "Right",
            "Either one.",
        )

        goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_RandomSide_Script {
    random(2)
        goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_ChooseSide_Script {
    switch (var (VAR_RESULT)) {
        case 0:
            goto(VSGarbodor_ChooseLeft_Script)
        case 1:
                goto(VSGarbodor_ChooseRight_Script)
        case 2:
                    goto(VSGarbodor_RandomSide_Script)
    }
}

script VSGarbodor_ChooseLeft_Script {
    setvar(VAR_INNER_CONSTRUCTION_SITE_STATE,PLAYER_LEFT_SIDE)
        msgbox(format("Then I'll take the right side. See you on the other side. Don't take too long!"),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(CHARLOTTE, moves(walk_up * 8, walk_right * 8, step_end))
        delay(60)
        applymovement(PLAYER, moves(walk_up * 8, walk_left * 2, step_end))
        waitmovement(0)
        removeobject(CHARLOTTE)
        release
        end
}

script VSGarbodor_ChooseRight_Script {
    setvar(VAR_INNER_CONSTRUCTION_SITE_STATE, PLAYER_RIGHT_SIDE)
        msgbox(format("Cool, I wanted the left side anyway.  See you on the other side. Don't take too long!"),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(CHARLOTTE, moves(walk_up * 15, walk_left * 8, step_end))
        delay(60)
        applymovement(PLAYER, moves(walk_up * 8, walk_right * 2, step_end))
        waitmovement(0)
        removeobject(CHARLOTTE)
        release
        end
}

script VSGarbodor_TheTideGrunt1_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_A, format("Heh, we're TheTide! Screw this stadium and screw SharpRise Capital!"), format("Damn, they were right, you're strong…"), VSGarbodor_TheTideGrunt1_Battle_Script)
        msgbox(format("Welp, I did what I could! Good luck with that stinky shit."), MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_A)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VSGarbodor_TheTideGrunt2_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_B, format("Right on cue, here come the heroes…"), format("Hmm, I guess I get to be the bad guy today!"), VSGarbodor_TheTideGrunt2_Battle_Script)
        msgbox(format("Being bad was fun for a day!"), MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_A)
        goto(VSGarbodor_RemoveGrunt)
        end
}
script VSGarbodor_TheTideGrunt3_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_A, format("The Tide is the name, and shutting this shit down is our game!"), format("They didn't prepare us for this!"), VSGarbodor_TheTideGrunt3_Battle_Script)
        msgbox(format("Oh thank Giratina, I can go back to the office now…"), MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_B)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VSGarbodor_TheTideGrunt4_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_B, format("Look at you, thinking you're making any difference!"), format("That's right, dance puppets!"), VSGarbodor_TheTideGrunt4_Battle_Script)
        msgbox(format("Have fun cleaning up after us, kid."), MSGBOX_AUTOCLOSE)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VS_Garbodor_CoreEncounter_Script {
    startmenu_savescreen
        faceplayer
        setwildbattle(SPECIES_GARBODOR, 40, ITEM_NORMAL_GEM)
        playmoncry(SPECIES_GARBODOR, CRY_MODE_ENCOUNTER)
        delay(40)
        waitmoncry
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        closemessage
        dowildbattle
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch (var (VAR_RESULT)) {
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                goto(VSGarbodor_RemoveGrunt)
        }
    release
        end
}

script VSGarbodor_RemoveGrunt {
    lock
        closemessage
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        call(VSGarbodor_CheckDefeated_Script)
        release
        end
}

script VSGarbodor_CheckDefeated_Script {
    if (flag(FLAG_DEFEATED_CONSTRUCTION_ROCKET_B) && flag(FLAG_DEFEATED_CONSTRUCTION_ROCKET_A) && flag(FLAG_DEFEATED_CONSTRUCTION_MON_A) && flag(FLAG_DEFEATED_CONSTRUCTION_MON_B)) {
        goto(VSGarbodor_MoveCharlotte_Script)
    }
    end
}

script VSGarbodor_PreventCrossingSides_Script {
    lock
        msgbox(format("Feeling lonely over there? I got this side, you handle that side!"),,SPEAKER_CHARLOTTE)
        closemessage

        if (var (VAR_INNER_CONSTRUCTION_SITE_STATE) == PLAYER_RIGHT_SIDE) {
            applymovement(PLAYER, Common_Movement_WalkRight)
        } else {
            applymovement(PLAYER, Common_Movement_WalkLeft)
        }
    waitmovement(0)
        release
        end
}

script VSGarbodor_MoveCharlotte_Script {
    lock
startcutscene(JUMPPLAYER_VSGARBODOR)

        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        clearflag(FLAG_TEMP_1)
        addobject(CHARLOTTE)

        if (var (VAR_TEMP_0) < 10) {
            applymovement(PLAYER, Common_Movement_FaceRight)
                if (var (VAR_TEMP_1) > 9) {
                    setobjectxy(CHARLOTTE, 11, 12)
                        copyobjectxytoperm(CHARLOTTE)
                        applymovement(CHARLOTTE, moves(walk_left * 6, step_end))
                } else {
                    setobjectxy(CHARLOTTE, 13, 6)
                        copyobjectxytoperm(CHARLOTTE)
                        applymovement(CHARLOTTE, moves(walk_left * 6, step_end))
                }
        } else {
            if (var (VAR_TEMP_1) > 9) {
                applymovement(PLAYER, Common_Movement_FaceUp)
                    setobjectxy(CHARLOTTE, 22, 4)
                    copyobjectxytoperm(CHARLOTTE)
                    applymovement(CHARLOTTE, moves(walk_down * 6, step_end))
            } else {
                applymovement(PLAYER, Common_Movement_FaceLeft)
                    setobjectxy(CHARLOTTE, 10, 6)
                    copyobjectxytoperm(CHARLOTTE)
                    applymovement(CHARLOTTE, moves(walk_right * 7, step_end))
            }
        }
    waitmovement(0)
        goto(VSGarbodor_DefeatedAll_Dialogue)
}

script VSGarbodor_DefeatedAll_Dialogue {
    setvar(VAR_CONSTRUCTION_STRIKE_STATE,DEFEATED_GARBODOR)

        msgbox(format("God, you were taking forever. They don't teach efficiency in Orre, do they?"),,SPEAKER_CHARLOTTE)
        closemessage

        applymovement(CHARLOTTE, moves(face_up, delay_16, face_left, delay_16, face_right, delay_16, step_end))
        waitmovement(0)
        msgbox(format("Whatever. Mine were easy, they just both used Self-Destruct before I could really get a move in. I already texted Oliver, he said meet in front of the construction site."),,SPEAKER_CHARLOTTE)
        closemessage

        fadeblackandsetremovewarpfadeflag
        warpsilent(MAP_ROUTE2, 255, 20, 22)
        delay(100)
}

script VSGarbodor_Arrest_Script {
    setvar(VAR_CONSTRUCTION_STRIKE_STATE,THETIDE_ARRESTED)
        setvar(VAR_STORYLINE_STATE,STORY_DEFEATED_GARBODOR)

        applymovement(ADELAIDE,Common_Movement_FaceUp)
        applymovement(CHARLOTTE,Common_Movement_FaceUp)
        applymovement(OLIVER,Common_Movement_FaceUp)
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(0)

        applymovement(ROCKET_A, VSGarbodor_PerpWalk)
        applymovement(ROCKET_B, VSGarbodor_PerpWalk)
        applymovement(POLICE_A, VSGarbodor_PerpWalk)
        applymovement(POLICE_B, VSGarbodor_PerpWalk)
        waitmovement(0)

        call(VSGarbodor_CleanUpPerpWalk)
        applymovement(ADELAIDE, Common_Movement_FaceDown)
        applymovement(CHARLOTTE, Common_Movement_FaceDown)
        waitmovement(0)
        goto(VSGarbodor_Negotiating_Dialogue)
}

script VSGarbodor_CleanUpPerpWalk {
    removeobject(ROCKET_A)
        removeobject(ROCKET_B)
        removeobject(POLICE_A)
        removeobject(POLICE_B)
        removeobject(ALCMENE)
}

script VSGarbodor_Negotiating_Dialogue
{
    msgbox(format("As we were negotiating, things started to get a little rowdy. The police aren't taking any chances this time, so they've halted escalation… for now. I'm hoping to continue the conversations over email, which are a little more civilized. How's the SharpRise Capital Task Force?"),,SPEAKER_ADELAIDE)

        msgbox(format("Done. Like you guys said, TheTide up to their shit as usual."),,SPEAKER_ALICIA)

        msgbox(format("Thank you both, and I'm sorry you had to get dragged into this. I'll work with our team to figure out what to do about the Championship."),,SPEAKER_ADELAIDE)
        msgbox(format("To show my appreciation, I have these for you."),,SPEAKER_ADELAIDE)
        closemessage
        special(VSGarbodor_ChooseGemBasedOnStarter)
        giveitem(VAR_0x8003,1)

        msgbox(format("How did you get these? I've been trying to get daddy to buy me a set of these for YEARS!"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_SHOCK)
        msgbox(format("...Did I miss someting?"),,SPEAKER_ALICIA,,EMOTE_CONFUSE)
        msgbox(format("They're only found in Unova, but they're impossible to get a hold of. They've been banned everywhere for like six years!"),,SPEAKER_CHARLOTTE,,EMOTE_SHOCK)
        msgbox(format("They are indeed removed from circulation worldwide, but consider it a perk. SharpRise Capital is an investor in Clay's Driftveil mining operations, and you've more than earned it. Make sure one of you wins the Championship with one of these!"),,SPEAKER_ADELAIDE)
        msgbox(format("Now, I think you two should head back to the Compound and get some rest."),,SPEAKER_ADELAIDE)
        msgbox(format("Yeah, I'm beat. Do you want a ride back to the Compound?"), MSGBOX_YESNO,,SPEAKER_CHARLOTTE)

        if (var (VAR_RESULT) == NO) {

            msgbox(format("Weird, but okay. I'll see you around."),,SPEAKER_CHARLOTTE)

                closemessage
                call(VSGarbodor_FlyWithoutPlayer_Script)
                release
                end
        } else {

            msgbox(format("Let's bounce."),,SPEAKER_CHARLOTTE)

                closemessage
                fadeblackandsetremovewarpfadeflag
                warpsilent(MAP_CUCONU_TOWN_SHARPRISE_COMPOUND_1F, 7, 3)
                release
                end
        }
}

script VSGarbodor_FlyWithoutPlayer_Script {
    applymovement(CHARLOTTE, Common_Movement_WalkInPlaceFasterDown)
        waitmovement(0)
        delay(50)
        setfieldeffectargument(0,1)
        dofieldeffect(FLDEFF_NPCFLY_OUT)
        delay(15)
        removeobject(CHARLOTTE)
        waitfieldeffect(FLDEFF_NPCFLY_OUT)
}

script VSGarbodor_ReturnToTheCompound_ObjectSetUp {
    if (var(VAR_STORYLINE_STATE) == STORY_DEFEATED_GARBODOR){
        setobjectxy(CHARLOTTE,7,2)
            setobjectxyperm(CHARLOTTE,7,2)
            setobjectmovementtype(CHARLOTTE,MOVEMENT_ACTION_FACE_DOWN)
            setflag(FLAG_HIDE_OLIVER_COMPOUND)
            return
    }
}

script VSGarbodor_ReturnToTheCompound_Dialogue {
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
        if (var(VAR_TEMP_0) != 7){
            fadeblackandsetremovewarpfadeflag
            warpsilent(MAP_CUCONU_TOWN_SHARPRISE_COMPOUND_1F, 7, 3)
                waitstate
        }else{
            applymovement(PLAYER,Common_Movement_FaceUp)
                waitmovement(0)
                msgbox(format("I'm gonna get some sleep. Good shit on finishing your Badges. Can't wait to kick your ass at the Championship… whenever that is."),,SPEAKER_CHARLOTTE)

                msgbox(format("Heh, we'll see. Good night."),,SPEAKER_ALICIA)

                closemessage

                applymovement(CHARLOTTE, moves(walk_up, step_end))
                waitmovement(0)
                playse(SE_EXIT)
                waitse
                removeobject(CHARLOTTE)
                setvar(VAR_STORYLINE_STATE,STORY_NEED_SLEEP_BEFORE_ARMANDO)

        call(TryAutoSave)
                endcutscene
                release
                end
        }
}

movement VSGarbodor_PerpWalk {
    walk_left * 15
        step_end
}

