const PLAYER = OBJ_EVENT_ID_PLAYER
const KAI = 1
const FLAG_HIDE_KAI = FLAG_TEMP_1

script NewAssholeAppears_HideKai_Script{
    if (var(VAR_ROUTE10_STATE) > SAVED_BEFORE_KAI_ROUTE10){
        setflag(FLAG_HIDE_KAI)
    }
    return
}

script NewAssholeAppears_WalkToPlayer_Script {
    lockall
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
    applymovement(KAI,Common_Movement_ExclamationMark)
    playse(SE_PIN)
    playbgm(MUS_ENCOUNTER_CHAMPION,TRUE)
    waitmovement(0)

    switch(var(VAR_TEMP_0)){
        case 0: applymovement(KAI, moves(walk_left * 4, step_end))
        case 1: applymovement(KAI, moves(walk_left * 3, step_end))
        case 2: applymovement(KAI, moves(walk_left * 2, step_end))
        case 3: applymovement(KAI, moves(walk_left * 1, step_end))
        case 4: applymovement(KAI, moves(step_end))
    }
    waitmovement(0)

    switch(var(VAR_TEMP_1)){
        case 2: applymovement(KAI, moves(walk_up, step_end))
        case 6: applymovement(KAI, moves(walk_down, step_end))
    }
    waitmovement(0)
    goto(KaiAlpha_Event_Script)
}

script KaiAlpha_Event_Script{
    switch(var(VAR_ROUTE10_STATE)) {
        case HAS_NOT_MET_KAI: goto(NewAssholeAppears_Intro_Dialogue)
        case SAVED_BEFORE_KAI_ROUTE10: goto(NewAssholeAppears_StartBattle_Script)
    }
}

script NewAssholeAppears_Intro_Dialogue {
    setvar(VAR_ROUTE10_STATE,SAVED_BEFORE_KAI_ROUTE10)
    msgbox(format("You look familiar. You flew here to do the SharpRise Capital League Challenge, right?"),,SPEAKER_KAI)
    if (var(VAR_SHARPRISESPIRE_CONFERENCE_STATE) > DEFEATED_THIRD_LEADER){
        msgbox(format("Yeah, you had some weird rant in Perlacia City the other day. I'm {PLAYER}, and yes, I'm doing the challenge."),,SPEAKER_ALICIA)
    }else{
        msgbox(format("Yeah! Just started not too long ago. Name's {PLAYER}."),,SPEAKER_ALICIA)
    }
    goto(NewAssholeAppears_BadgeJudge_Dialogue)
}

script NewAssholeAppears_BadgeJudge_Dialogue {

    special(GetNumberOfBadges)

    switch (var(VAR_RESULT)){
        case 0: msgbox(format("Judging from your Badge count, the program isn't doing you much good."),,SPEAKER_KAI)
        case 1: msgbox(format("...yeah I thought I recognized you. Guess you transplants aren't all a total joke!"),,SPEAKER_KAI)
        default: msgbox(format("Yeah, I heard about your wins from the guys. The colonizers are really getting a head start, huh?"),,SPEAKER_KAI)
    }

    msgbox(format("Woah, what the hell is your problem?"),,SPEAKER_ALICIA)

    lockall
    startmenu_savescreen
    goto(NewAssholeAppears_StartBattle_Script)
}

script NewAssholeAppears_StartBattle_Script{
    lockall
    faceplayer
    msgbox(format("Go mind your business in your own region."),,SPEAKER_KAI)
    closemessage
    trainerbattle_no_intro(TRAINER_KAI_NEWASSHOLEAPPEARS,format("...you're tough."))
    goto(NewAssholeAppears_PostBattle_Script)
}

script NewAssholeAppears_PostBattle_Script {
    lockall
    startcutscene(JUMPPLAYER_NEWASSHOLEAPPEARS)
    setvar(VAR_ROUTE10_STATE,DEFEATED_KAI_ROUTE10)
    msgbox(format("Are you done being an ass, whateveryournameis?"),,SPEAKER_ALICIA)
    msgbox(format("My name is Kai."),,SPEAKER_KAI)
    reveal(REVEAL_KAI)
    msgbox(format("Everyone's falling over themselves to cater to transplants like you. The rest of us who are actually a part of this community get left in the cold. But I wouldn't expect you to understand that."),,SPEAKER_KAI)
    msgbox(format("You sound like a crazy person."),,SPEAKER_ALICIA)
    msgbox(format("I'm not crazy, you're just ignorant. I shouldn't be mad at you. The ignorance isn't entirely your fault. "),,SPEAKER_KAI)
    msgbox(format("See you around."),,SPEAKER_KAI)
    msgbox(format("I hope not."),,SPEAKER_ALICIA,TAIL_WHISPER,EMOTE_ANGRY)

    closemessage
    goto(NewAssholeAppears_KaiExit_Script)
}

script NewAssholeAppears_KaiExit_Script{
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

    switch(var(VAR_TEMP_1)){
        case 2: applymovement(KAI, moves(walk_down * 8, step_end))
        case 6: applymovement(KAI, moves(walk_up * 8, step_end))
    }
    endcutscene
	fadescreen(FADE_TO_BLACK)
    fadedefaultbgm
    removeobject(KAI)
	fadescreen(FADE_FROM_BLACK)
    addvar(VAR_STORYLINE_STATE,1)
    setvar(VAR_TEMP_0,0)
    call(TryAutoSave)
    call(AaandWereBack_Monologue)
    releaseall
    end
}

