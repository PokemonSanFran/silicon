const PLAYER = OBJ_EVENT_ID_PLAYER
const LUCREZIA = 1 //FLAG_TEMP_1
const CHARLOTTE = 2 //FLAG_TEMP_2
const KAI = 3 //FLAG_TEMP_3
const OLIVER = 4 //FLAG_TEMP_4

script WaitHeDidWhat_HideObject_Script {
    setflag(FLAG_TEMP_1)
}

script WaitHeDidWhat_ObjectSetUp_Script {
    call(Finals_HideObject_Script)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_6)
        setobjectxy(KAI,10,5)
        setobjectxyperm(KAI,10,5)
}

script WaitHeDidWhat_ObjectMidSetUp_Script {
    call(Finals_HideObject_Script)
        setflag(FLAG_TEMP_6)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_6)
}

script WaitHeDidWhat_MeltdownSetUp_Script {
    call(Finals_HideObject_Script)
        setflag(FLAG_TEMP_6)
        setflag(FLAG_TEMP_3)
        setobjectxy(2,2,7)
        setobjectxyperm(2,2,7)
        setobjectmovementtype(2,MOVEMENT_TYPE_WALK_SEQUENCE_RIGHT_LEFT_DOWN_UP)
}

script WaitHeDidWhat_MeltdownPostSaveSetUp_Script {
        setobjectxyperm(5,11,2)
        setobjectxy(5,11,2)
    call(Finals_HideObject_Script)
        setflag(FLAG_TEMP_3)
        setobjectxy(2,2,7)
        setobjectxyperm(2,2,7)
        setobjectmovementtype(2,MOVEMENT_TYPE_WALK_SEQUENCE_RIGHT_LEFT_DOWN_UP)
        setflag(FLAG_TEMP_6)
}

script WaitHeDidWhat_MovePlayerPostSave_Script {
    setobjectxy(PLAYER,4,6)
    turnobject(PLAYER,DIR_EAST)
    TeleportCamera(4,6)
}

script WaitHeDidWhat_RivalsGoToMatch_Script {
    lock
        startcutscene(JUMPPLAYER_WAITHEDIDWHAT)
        applymovement(5, moves(lock_facing_direction, walk_right, delay_16, delay_16, delay_16, delay_16, delay_16, walk_left, step_end))
        applymovement(PLAYER,WaitHeDidWhat_GetOutOfWay)
        applymovement(KAI, moves(walk_up * 4, delay_16, step_end))
        opendoor(10,1)
        waitdooranim
        waitmovement(0)
        playse(SE_EXIT)
        removeobject(KAI)
        closedoor(10,1)
        waitdooranim
        goto(WaitHeDidWhat_CalledToSkybox_Script)
}

script WaitHeDidWhat_CalledToSkybox_Script {
        playse(SE_POKENAV_CALL)
        waitse
        playse(SE_POKENAV_CALL)
        waitmessage
        msgbox(format("Congrats on making it to the Finals! Come to the Skybox to watch Charlotte's battle!"),,SPEAKER_OLIVER,,,PHONE_ON)

        playse(SE_POKENAV_HANG_UP)
        waitse
        setvar(VAR_STORYLINE_STATE,STORY_WATCH_SEMIFINAL)
        endcutscene
        release
        end
}

script WaitHeDidWhat_WatchOtherSemifinal_Dialogue {
        startcutscene(JUMPPLAYER_WAITHEDIDWHAT)
        setvar(VAR_STORYLINE_STATE,STORY_READY_FINALS)
        lock

        msgbox(format("Good shit down there! Time to crush this Kai kid. See you in the Finals!"),,SPEAKER_CHARLOTTE)

        closemessage

        applymovement(PLAYER,WaitHeDidWhat_GetOutOfWay)
        opendoor(9,1)
        waitdooranim
        applymovement(8, moves(walk_up * 2, step_end))
        waitmovement(0)
        playse(SE_EXIT)
        removeobject(8)
        closedoor(9,1)
        waitdooranim
        fadescreen(FADE_TO_BLACK)
        setobjectxy(PLAYER,9,6)
        turnobject(PLAYER,DIR_SOUTH)
        TeleportCamera(9,6)
        fadescreen(FADE_FROM_BLACK)

        msgbox(format("In a stunning upset, the hometown hero, Kai defeats Charlotte and moves onto the Finals! What a wild match!"))
        closemessage

        playse(SE_PIN)
        applymovement(5,Common_Movement_ExclamationMark)
        applymovement(1,Common_Movement_ExclamationMark)
        applymovement(7,Common_Movement_ExclamationMark)
        applymovement(PLAYER,Common_Movement_ExclamationMark)
        waitmovement(0)

        msgbox(format("(...holy shit!)"),,SPEAKER_ALICIA)

        msgbox(format("We'll take a quick break, and when we come back... your Championship match! Kai VS {PLAYER}, for all the marbles!"))
        closemessage

        applymovement(5, moves(walk_down, walk_right * 2, step_end))
        applymovement(7, Common_Movement_WalkLeft)
        waitmovement(0)

        applymovement(PLAYER, Common_Movement_FaceLeft)
        waitmovement(0)
        msgbox(format("I sent you a motivational email to get you ready for this match! Did you get it?"),,SPEAKER_LUCREZIA)
        msgbox(format("I don't know what email he's talking about... I don't even use my email."),,SPEAKER_ALICIA,TAIL_THOUGHT,EMOTE_SWEAT)
        closemessage

        applymovement(PLAYER, Common_Movement_FaceRight)
        waitmovement(0)
        msgbox(format("Good luck, {PLAYER}."),,SPEAKER_OLIVER)

        closemessage
        endcutscene
        release
        end
}

script WaitHeDidWhat_CharlotteMeltdown_Dialogue {
    random(5)

        switch (var(VAR_RESULT)) {
            case 0:msgbox(format("I lost to that absolute garbage Trainer? What happened? He must have cheated... right!?"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_SAD)
            case 1:msgbox(format("Get the fuck away from me!"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_ANGRY)
            case 2:msgbox(format("How could this happen? Now I have to win another match just to get THIRD?!?"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_SAD)
            case 3:msgbox(format("Absolute bullshit. Argh I cannot believe this shit…"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_ANGRY)
            case 4:msgbox(format("...I executed our plan perfectly! It's not my fault Daddy - he must have tricked me! Fix this now, Daddy!\p...\pWhat do you mean you're working on it? What are you going to do!?"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_SAD,PHONE_ON)
        }

        closemessage
}

script WaitHeDidWhat_PreparePlayer_Script {
    lock
        faceplayer
        msgbox(format("Are you ready to battle for the Championship?"),MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == YES){
            call(GroupStages_MoveObjects_Script)
                fadeblackandsetremovewarpfadeflag
                warpsilent(MAP_SHARPRISE_STADIUM_ARENA,0)
        }
    release
        end
}

script WaitHeDidWhat_WalkIntoChampionshipPosition {
    lock
        applymovement(PLAYER,WaitHeDidWhat_WalkToBattlefieldPosition)
        waitmovement(0)
        msgbox(format("{PLAYER}, I don't think you can beat me when I have home court advantage!"),,SPEAKER_KAI)
        msgbox(format("You and Charlotte and SharpRise might be able to outspend me, but you can't outwork me and my Pokémon!"),,SPEAKER_KAI)
        msgbox(format("I'm going to be the Resido Champion!"),,SPEAKER_KAI)
        msgbox(format("Kai, I'm super proud of you. I'm even happy you've gotten this far!"),,SPEAKER_ALICIA,,EMOTE_HAPPY)
        msgbox(format("But I've got an undefeated winning streak against you, and that isn't changing now."),,SPEAKER_ALICIA)

        setvar(VAR_STORYLINE_STATE,STORY_SAVE_FINALS)

        startmenu_savescreen
        goto(WaitHeDidWhat_StartBattle_Script)
}

script WaitHeDidWhat_StartBattle_Script{
    lock
        applymovement(PLAYER,Common_Movement_WalkRight)
        applymovement(1,Common_Movement_WalkLeft)
        waitmovement(0)
        msgbox(format("I'll beat you... for Resido!"),,SPEAKER_KAI,TAIL_SHOUT)
        trainerbattle_no_intro(TRAINER_KAI_FINALS,format("…so close."))
        closemessage

        goto(WelcomeToTheHallOfFame_PostBattle_Dialogue)
}

movement WaitHeDidWhat_GetOutOfWay {
    walk_left
        face_right
        step_end
}

movement WaitHeDidWhat_WalkToBattlefieldPosition {
    walk_left * 13
        walk_up * 3
        walk_right
        step_end
}

