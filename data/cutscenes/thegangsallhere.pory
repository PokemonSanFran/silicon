const PLAYER = OBJ_EVENT_ID_PLAYER
const ARNAV = 1
const TECHIE = 2
const OLIVER = 3
const CHARLOTTE = 4
const LUCREZIA = 5
const CHIEF = 6
const RAMESH = 7
const KEIYING = 8

const FLAG_HIDE_NERDS_COPS = FLAG_TEMP_1
const FLAG_HIDE_OLIVER = FLAG_TEMP_2
const FLAG_HIDE_CHARLOTTE = FLAG_TEMP_3
const FLAG_HIDE_LUCREZIA = FLAG_TEMP_4
const FLAG_HIDE_RAMESH = FLAG_TEMP_5
const FLAG_HIDE_KEIYING = FLAG_TEMP_6

script TheGangsAllHere_ObjectCleanUp_Script {
    call(SharpriseSpire_Leagueops_EmptyConferenceRoomOffices)
    return
}

script TheGangsAllHere_MidMission_Object_Script{
    setflag(FLAG_HIDE_NERDS_COPS)
    setflag(FLAG_HIDE_CHARLOTTE)
    setflag(FLAG_HIDE_RAMESH)
    setflag(FLAG_HIDE_KEIYING)
    setobjectxy(OLIVER,6,46)
    setobjectxy(LUCREZIA,5,56)
    setobjectxyperm(LUCREZIA,5,56)
    return
}

script TheGangsAllHere_ObjectSetup_Script{
    setflag(FLAG_HIDE_NERDS_COPS)
    setflag(FLAG_HIDE_CHARLOTTE)
    setflag(FLAG_HIDE_RAMESH)
    setflag(FLAG_HIDE_KEIYING)
    setobjectxy(LUCREZIA,5,56)
    setobjectxyperm(LUCREZIA,5,56)
    return
}

script TheGangsAllHere_MoveOliverToStairs_Script{
        addobject(OLIVER) //PSF TODO why is oliver invisible without this
        setobjectmovementtype(OLIVER,MOVEMENT_TYPE_FACE_DOWN)
        setobjectxy(OLIVER,16,9)
        setobjectxyperm(OLIVER,16,9)
        return
}

script TheGangsAllHere_OliverAppearStairs_Script{
    lock
    playse(SE_EXIT)
    waitse

    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

    if (var(VAR_TEMP_0) == 16){
        applymovement(PLAYER, moves(walk_down, walk_right, face_left, step_end))
        waitmovement(PLAYER)
        call(TheGangsAllHere_MoveOliverToStairs_Script)
        applymovement(OLIVER, moves(walk_down * 2, emote_exclamation_mark, face_right, step_end))
        waitmovement(OLIVER)
    }else{
        call(TheGangsAllHere_MoveOliverToStairs_Script)
        playse(SE_PIN)
        applymovement(PLAYER,Common_Movement_FaceLeft)
        applymovement(OLIVER, moves(walk_down, walk_right * 2, step_end))
        waitmovement(OLIVER)
    }

    goto(TheGangsAllHere_WhatBringsYou_Dialogue)
}

script TheGangsAllHere_WhatBringsYou_Dialogue{
    lock
    setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,START_THE_GANGS_ALL_HERE)
    playbgm(MUS_RG_FOLLOW_ME,FALSE)
    msgbox(format("{PLAYER}! What brings you to League HQ?"),,SPEAKER_OLIVER)
    msgbox(format("Commissioner Lucrezia told me to visit after I got my third badge!"),,SPEAKER_ALICIA)
    msgbox(format("Is this where you work?"),,SPEAKER_ALICIA)
    msgbox(format("My office is on this floor, yeah! This is League Ops, and everybody here is solely dedicated to making sure the SharpRise Capital Pokemon League is a success."),,SPEAKER_OLIVER)
    msgbox(format("I've an important delivery for the C.E.O., but the workers here in League Ops can be a little... rowdy. Do you mind if I tag along on your tour? I'll keep you in tip-top shape!"),,SPEAKER_OLIVER)
    msgbox(format("Of course not!"),,SPEAKER_ALICIA)

    closemessage
    special(HealPlayerParty)
    applymovement(OLIVER, moves(walk_left, walk_down * 20, step_end))
    waitmovement(OLIVER)
    setobjectxy(OLIVER,6,46)
    setobjectxyperm(OLIVER,6,46)
    fadedefaultbgm
    //loadword(0,NewScript)
    //setfollower(OLIVER,0x100) //PSF TODO hook up FollowMe scripts
    //https://www.pokecommunity.com/showthread.php?t=432529
    call(TryAutoSave)
    releaseall
    end
}

script TheGangsAllHere_OfficeIsntThatWay_Script{
    lockall
    startcutscene(JUMPPLAYER_THEGANGSALLHERE)
    msgbox(format("Hey {PLAYER}, the office isn't that way!"),,SPEAKER_OLIVER)

    closemessage
    applymovement(PLAYER, moves(walk_down, step_end))
}

script TheGangsAllHere_WalkHallway_Script{
    applymovement(OLIVER,Common_Movement_WalkDown)
    applymovement(PLAYER,Common_Movement_WalkDown)
    fadescreen(FADE_TO_BLACK)
    delay(30)
    setobjectxy(PLAYER,5,52)
    setobjectxy(OLIVER,6,52)
    TeleportCamera(5,52)
    fadescreen(FADE_FROM_BLACK)
    return
}

script TheGangsAllHere_ThisIsOffice_Script{
    lock
    faceplayer
    msgbox(format("This is Lucrezia's office. After you."),,SPEAKER_OLIVER)
    closemessage

    call(TheGangsAllHere_WalkHallway_Script)
    goto(TheGangsAllHere_ComeOnIn_Dialogue)
}

script TheGangsAllHere_ComeOnIn_Dialogue{
    setvar(VAR_SHARPRISESPIRE_CONFERENCE_STATE,POST_THE_GANGS_ALL_HERE)
    playbgm(MUS_RG_ENCOUNTER_DEOXYS,FALSE)
    playse(SE_PIN)

    applymovement(LUCREZIA,Common_Movement_ExclamationMark)
    waitmovement(LUCREZIA)
    applymovement(LUCREZIA, moves(walk_right * 2, walk_up * 3, walk_left, face_up, step_end))

    msgbox(format("{PLAYER}! Oliver! Come on in, I just finished an email! Glad you're both here, and excellent timing that you ran into each other."),,SPEAKER_LUCREZIA)
    closemessage

    msgbox(format("As you asked, I have your package from Emrys."),,SPEAKER_OLIVER)
    closemessage

    applymovement(PLAYER,Common_Movement_FaceRight)
    applymovement(OLIVER,Common_Movement_FaceLeft)
    waitmovement(PLAYER)

    msgbox(format("I thought you said you had a delivery for the C.E.O.?"),,SPEAKER_ALICIA)

    msgbox(format("I did. I see Lucrezia neglected to reveal her full title...?"),,SPEAKER_OLIVER)

    goto(TheGangsAllHere_NotIntentionally_Dialogue)
    end
}

script TheGangsAllHere_NotIntentionally_Dialogue{
    msgbox(format("Not intentionally! I was just so swept up in your battle!"),,SPEAKER_LUCREZIA)
    reveal(REVEAL_LUCREZIA)

    if(var(VAR_SHARPRISE_SPIRE_PRODUCTION_STATE) == POST_ALWAYS_WATCHING_WAZOWSKI){
        msgbox(format("Oliver, please tell me you'll go back and watch Ramesh's highlight reel later, it was fantastic."))

    msgbox(format("I heard wonderful things on the way over here, but I'll be certain to observe later."),,SPEAKER_OLIVER)
    }

    msgbox(format("In any case, your package, Lucrezia."))

    applymovement(OLIVER,Common_Movement_WalkInPlaceDown)
    waitmovement(OLIVER)

    bufferitemname(STR_VAR_1,ITEM_DRAGONIUM_Z)
    msgbox(format("Ah, my {STR_VAR_1}, wonderful.\nI've also got something for you, {PLAYER}! Let me see your phone..."),,SPEAKER_LUCREZIA)
    closemessage

    goto(TheGangsAllHere_GivePrestoApp_Script)
    end
}

script TheGangsAllHere_GivePrestoApp_Script{
    applymovement(PLAYER,Common_Movement_FaceDown)
    applymovement(LUCREZIA, moves(walk_left, face_up, walk_in_place_up, step_end))
    waitmovement(LUCREZIA)
    call(EventScript_InstallPrestoApp)
    goto(TheGangsAllHere_GivePrestoApp_Dialogue)
    end
}

script TheGangsAllHere_GivePrestoApp_Dialogue{
    msgbox(format("As a member of the SharpRise family, I've just granted you special access to Presto's drone delivery service. If you select that app while you're outdoors, you can have items delievered to you instantly! I'm excited to see how you use our tools."),,SPEAKER_LUCREZIA)

    applymovement(OLIVER,Common_Movement_FacePlayer)
    waitmovement(OLIVER)
    applymovement(PLAYER,Common_Movement_FaceRight)

    msgbox(format("Not to place any undue pressure on you, but we personally reviewed all of the applicants to our little program. You were exceptionally popular among the team, so we're very excited to see you perform well!"),,SPEAKER_OLIVER)

    msgbox(format("Oh. Wow! That means a lot. I'll absolutely put it to use!"),,SPEAKER_ALICIA)
    closemessage

    applymovement(PLAYER,Common_Movement_FaceDown)
    msgbox(format("We always make sure our Trainers have an edge on the competition!"),,SPEAKER_LUCREZIA)

    goto(TheGangsAllHere_EndScene_Dialogue)
}

script TheGangsAllHere_EndScene_Dialogue{
    msgbox(format("Time for my seventh Boomers coffee for the day. Shall we, Oliver?"))
    closemessage

    applymovement(OLIVER,Common_Movement_FaceDown)

    msgbox(format("We shall."),,SPEAKER_OLIVER)

    applymovement(OLIVER,Common_Movement_FacePlayer)
    applymovement(PLAYER,Common_Movement_FaceRight)

    msgbox(format("Feel free to continue your tour without us, {PLAYER}. Greatly appreciate your help in the battles. Hopefully supporting me wasn't too exhausting!"),,SPEAKER_OLIVER)

    if(var(VAR_SHARPRISESPIRE_CONFERENCE_STATE) != POST_ALWAYS_WATCHING_WAZOWSKI){
        msgbox(format("Ah, and if you haven't already visited Ramesh, go do so. They're on the Production Floor."))
    }

    closemessage

    applymovement(PLAYER, moves(delay_16 * 2, face_up, step_end))
    applymovement(LUCREZIA, moves(walk_right, walk_up * 8, step_end))
    applymovement(OLIVER, moves(walk_up * 8, step_end))
    waitmovement(OLIVER)
    fadedefaultbgm
    removeobject(LUCREZIA)
    removeobject(OLIVER)
    addvar(VAR_STORYLINE_STATE,1)
    setvar(VAR_TEMP_0,0)
    endcutscene
    call(TryAutoSave)
    call(AaandWereBack_Monologue)
    release
    end
}

movement TheGangsAllHere_OliverWalkToPlayer_Movement{
    face_right
    step_end
}

movement TheGangsAllHere_ThisIsOffice_Movement{
    walk_right * 3
    step_end
}

