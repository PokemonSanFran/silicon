// Thanks for choosing Lumi Cab!
// The base fare's $710. You want a ride?
//  	Y/N/something else
//
//  Got it. And where are we headed?
//  	fly menu
 // We're going to X placem with the stuff and the things?
//  	Y/N
//  Then here we go!
//
//  Okay kid, here's your stop
//
//  let's see your total will be $2000
//
//  Hey just a minute there friend!
//  You don't even have enough for the fare?!
//  I come out to earn a living, don't I?
//  So why you gotta tax me like this?
//  	singlebattle
//
//  Ain't you strong one?
//  Too strong for me!
//
// Now I won't even have a copper to send back to my family in Hoenn...
// Thanks a lot, pal.

script Taxi_Interact_Dialogue
{
    lock

    call_if_quest_reward(QUEST_TAXICABTURNAROUND,Quest_Taxicabturnaround_Reward_Dialogue)
    call(Quest_Taxicabturnaround_CheckNextStage_Script)

    callnative(BufferTaxiBaseFare)
    msgbox(gText_Taxi_ThanksForChoosing)
    call(Taxi_ConstructMenu_Script)
    dynmultistack(0,0,TRUE,3,TRUE,1,NULL)

    switch(var(VAR_RESULT)){
        case 0: goto(Taxi_Refuse_Dialogue)
        case 1: goto(Taxi_ChooseLocation_Dialogue)
        case 2: goto(Quest_Taxicabturnaround_AskPokemon_Dialogue)
    }
}

script Taxi_ConstructMenu_Script
{
    dynmultipush("No.",NO)
    dynmultipush("Yes!",YES)

    specialvar(VAR_0x8008,Quest_Taxicabturnaround_IsSubquestComplete)
    returnqueststate(QUEST_TAXICABTURNAROUND)

    if ((var(VAR_RESULT) > QUEST_MENU_UNLOCK_QUEST) && (!var(VAR_0x8008)))
    {
        dynmultipush("Something else.",2)
    }
    return
}

script Taxi_Refuse_Dialogue
{
    msgbox(gText_Taxi_ChangedYourMind)
    closemessage
    release
    end
}

script Taxi_ChooseLocation_Dialogue
{
    setflag(FLAG_TEMP_1)
    callnative(Script_OpenTaxi)
    waitstate
    goto_if_unset(FLAG_TEMP_1,Taxi_StartBattle_Dialogue)
    goto(Taxi_Refuse_Dialogue)
    release
    end
}

script Taxi_StartBattle_Dialogue{
    msgbox(format("{STR_VAR_1}, here we go!"))
    fadescreen(FADE_TO_BLACK)
    showmoneybox(0,0)
    callnative(Taxi_BufferDestinationMapName)
    msgbox(gText_Taxi_HereWeAre)
    msgbox(gText_Taxi_WhatDoYouMean)
    closemessage
    hidemoneybox
    trainerbattle_no_intro(TRAINER_CABBIE_HALE,gText_Taxi_AintYouStrong)
    msgbox(gText_Taxi_NowIWont)
    closemessage
    callnative(WarpTaxiAfterCutscene)
    closemessage
    return
}

text gText_Taxi_ThanksForChoosing{
    format("Thanks for choosing Taxicab! The base fare is ¥{STR_VAR_1}, do you want a ride?")
}
text gText_Taxi_ChangedYourMind{
    format("Changed your mind? That's fine. We'll be here!")
}
text gText_Taxi_HereWeAre{
    format("Okay kid, here's your stop, {STR_VAR_1}.")
}
text gText_Taxi_ItllCost{
    format("That'll be ¥{STR_VAR_2}.")
}
text gText_Taxi_WhatDoYouMean{
    format("Hey, just a minute there friend! You don't even have enough for the fare?! I come out to earn a living, don't I? So why you gotta tax me like this?")
}
text gText_Taxi_AintYouStrong{
    format("Well aren't you a strong one? Too strong for me!")
}
text gText_Taxi_NowIWont{
    format("Now I won't make enough money to send back home to my family in Hoenn...")
}
