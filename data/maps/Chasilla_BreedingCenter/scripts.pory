const BREEDINGEGG = 11

mapscripts Chasilla_BreedingCenter_MapScripts
{
}

script BreedingCenter_Script_SignifyEggReady
{
    if (flag(FLAG_PENDING_DAYCARE_EGG)) {
        setobjectmovementtype(BREEDINGEGG,MOVEMENT_TYPE_FACE_RIGHT)
    }
}

script BreedingCenter_Script_StartMonManagement
{
    lock
    faceplayer
    call(BreedingCenter_Dialogue_IntroPackage)
    if (!flag(FLAG_TEMP_11))
    {
        msgbox(format("Welcome back to the Chasilla Breeding Center, {PLAYER}!"),,SPEAKER_BREEDINGPARENT)
        closemessage
    }

    goto(BreedingCenter_Script_BuildCoreMenu)
    release
    end
}

script BreedingCenter_Script_HandleMainMenu
{
    setflag(FLAG_TEMP_11)
    goto(BreedingCenter_Script_BuildCoreMenu)
}

script BreedingCenter_Script_BuildCoreMenu
{
    specialvar(VAR_RESULT,GetDaycareState)
    switch(var(VAR_RESULT))
    {
        case DAYCARE_NO_MONS:
            goto(BreedingCenter_Script_ShowNoMonsMenu)
        case DAYCARE_EGG_WAITING:
            goto(BreedingCenter_Dialogue_EggWaiting)
        case DAYCARE_ONE_MON:
            call(BreedingCenter_Script_AddDepositToMenu)
            call(BreedingCenter_Script_GenerateOneParentReport)
        case DAYCARE_TWO_MONS:
            call(BreedingCenter_Script_GenerateOneParentReport)
            call(BreedingCenter_Script_GenerateTwoParentReport)
    }
    goto(BreedingCenter_Script_HandleCoreMenu)
}

script BreedingCenter_Dialogue_EggWaiting
{
    msgbox(format("Our records indicate that you have an Egg available for pickup! Talk to our Egg manager outside."),,SPEAKER_BREEDINGPARENT)
    closemessage
    release
    end
}

script BreedingCenter_Script_HandleCoreMenu
{
    call(BreedingCenter_Script_AddNeverMindToMenu)
    call(BreedingCenter_Script_AddInfoToMenu)
    dynmultistack(0, 0, FALSE, 3, TRUE, 0, NULL)
    msgbox(format("What can we help you with today?"),,SPEAKER_BREEDINGPARENT)

    switch (var(VAR_RESULT))
    {
        case 3: goto(BreedingCenter_Dialogue_ExplainBreedingCenter)
        case 2: goto(BreedingCenter_Script_DepositMonAtDaycare)
        case 0: goto(BreedingCenter_Script_WithdrawMon)
        case 1: goto(BreedingCenter_Script_WithdrawMon)
        case MULTI_B_PRESSED: goto(BreedingCenter_Dialogue_NeverMind)
    }
}

script BreedingCenter_Dialogue_IntroPackage
{
    if (flag(FLAG_RECEIVED_BREEDING_CENTER_WELCOME_KIT))
    {
        return
    }

    msgbox(format("Welcome to the Chasilla Breeding Center! Our state of the art Pokémon breeding facilities are open to the public."),,SPEAKER_BREEDINGPARENT)
    msgbox(format("Since its your first visit, we have a welcome package for you!"),,SPEAKER_BREEDINGPARENT)

    call(BreedingCenter_Script_CheckSpaceAndErrorMessage)
    msgbox(format("Hopefully this welcome helps you breed the best Pokémon possible!"),,SPEAKER_BREEDINGPARENT)
    closemessage
    call(BreedingCenter_Script_GiveWelcomeKit)
    msgbox(format("If you need more info, any of the staff here is happy to help."),,SPEAKER_BREEDINGPARENT)
    goto(BreedingCenter_Script_StartMonManagement)
}

script BreedingCenter_Script_CheckSpaceAndErrorMessage
{
    callnative(DoesPlayerHaveSpaceForWelcomeKit)
    if (var(VAR_RESULT) == FALSE)
    {
        msgbox(format("Our welcome kit has a quite a bit in it, and your bag appears to be full. Come back once you've made some room!"),,SPEAKER_BREEDINGPARENT)
        closemessage
        end
    }
    setflag(FLAG_RECEIVED_BREEDING_CENTER_WELCOME_KIT)
    setflag(FLAG_TEMP_11)
}

script BreedingCenter_Script_GiveWelcomeKit
{
    giveitem(ITEM_EVERSTONE, 2)
    giveitem(ITEM_DESTINY_KNOT, 2)
    giveitem(ITEM_POWER_WEIGHT,2)
    giveitem(ITEM_POWER_BRACER,2)
    giveitem(ITEM_POWER_BELT,2)
    giveitem(ITEM_POWER_LENS,2)
    giveitem(ITEM_POWER_BAND,2)
    giveitem(ITEM_POWER_ANKLET,2)
}

script BreedingCenter_Script_ShowNoMonsMenu
{
    msgbox(format("Are you ready to get started?"),,SPEAKER_BREEDINGPARENT)

    dynmultichoice(0,0,FALSE,3,0,NULL,
        "Deposit",
        "Info",
        "Never Mind",
    )

    switch (var(VAR_RESULT))
    {
        case 0: goto(BreedingCenter_Script_DepositMonAtDaycare)
        case 1: goto(BreedingCenter_Dialogue_ExplainBreedingCenter)
        case 2: goto(BreedingCenter_Dialogue_NeverMind)
        case MULTI_B_PRESSED: goto(BreedingCenter_Dialogue_NeverMind)
    }
}

script BreedingCenter_Dialogue_NeverMind
{
    msgbox(format("Changed your mind? You're always welcome back {PLAYER}."),,SPEAKER_BREEDINGPARENT)
    closemessage
    release
    end
}

script BreedingCenter_Dialogue_ExplainBreedingCenter
{
    msgbox(format("Here, Pokémon Trainers of can leave up to two Pokémon for us to raise. If two compatible parents are left in our care, they will evantually create an Egg. Our state of the art breeding facilities allow you to edit the DNA of the unhatched Pokémon, allowing you to customize it to your liking. As a bonus, every step you take out there will give your Pokémon here one Experience Point. Unlike other regions, we only charge fees for when Eggs are picked up."),,SPEAKER_BREEDINGPARENT)
    goto(BreedingCenter_Script_HandleMainMenu)
}

script BreedingCenter_Script_DepositMonAtDaycare
{
    specialvar(VAR_RESULT,CountPartyAliveNonEggMons)
    goto_if_lt(VAR_RESULT,2,BreedingCenter_Dialogue_NotEnoughMons)

    msgbox(format("Which Pokémon do you want to leave in our care?"),,SPEAKER_BREEDINGPARENT)
    closemessage
    fadescreen(FADE_TO_BLACK)
    special(ChooseSendDaycareMon)
    waitstate
    goto_if_eq(VAR_0x8004,PARTY_NOTHING_CHOSEN,BreedingCenter_Dialogue_NeverMind)

    specialvar(VAR_0x8005,GetSelectedMonNicknameAndSpecies)
    waitse
    playmoncry(VAR_0x8005,CRY_MODE_NORMAL)
    msgbox(format("We'll take good care of {STR_VAR_1}!"),,SPEAKER_BREEDINGPARENT)
    waitmoncry
    special(StoreSelectedPokemonInDaycare)
    callnative(UpdateFollowingPokemon)
    incrementgamestat(GAME_STAT_USED_DAYCARE)

    goto(BreedingCenter_Script_HandleMainMenu)
    //call(BreedingCenter_Dialogue_PostDepositMessage)
    //call(BreedingCenter_Dialogue_AskDepositAgain)
}

script BreedingCenter_Dialogue_AskDepositAgain
{
    specialvar(VAR_RESULT,GetDaycareState)
    if (var(VAR_RESULT) != DAYCARE_ONE_MON)
    {
        return
    }

    msgbox(format("Would you like us to raise another Pokémon?"),MSGBOX_YESNO,SPEAKER_BREEDINGPARENT)
    if (var(VAR_RESULT) != YES)
    {
        return
    }
    goto(BreedingCenter_Script_DepositMonAtDaycare)
}

script BreedingCenter_Dialogue_PostDepositMessage
{
    specialvar(VAR_RESULT,GetDaycareState)
    switch(var(VAR_RESULT))
    {
        default:
            msgbox(format("We're always here to help."),,SPEAKER_BREEDINGPARENT)
        case DAYCARE_ONE_MON:
            msgbox(format("When you've chosen another Pokémon to leave with us, we'll be here!"),,SPEAKER_BREEDINGPARENT)
        case DAYCARE_TWO_MONS:
            msgbox(format("If your Pokémon are discovered with an Egg, you'll get a Notification on your phone."),,SPEAKER_BREEDINGPARENT)
    }
    call(BreedingCenter_Script_HandleMainMenu)
}

script BreedingCenter_Dialogue_NotEnoughMons
{
    msgbox(format("If I take a Pokémon from you, you won't have any healthy Pokémon left!"),,SPEAKER_BREEDINGPARENT)
    call(BreedingCenter_Script_HandleMainMenu)
}

script BreedingCenter_Script_GenerateOneParentReport
{
    setvar(VAR_0x8004,0)
    specialvar(VAR_0x8007,GetNumLevelsGainedFromDaycare)
    if (!flag(FLAG_TEMP_11) && (var(VAR_0x8007) != 0))
    {
        msgbox(Route117_PokemonDayCare_Text_YourMonHasGrownXLevels,,SPEAKER_BREEDINGPARENT)
    }
    specialvar(VAR_RESULT,GetNumLevelsGainedFromDaycare)
    dynmultipush("Withdraw {STR_VAR_1}",0)
}

script BreedingCenter_Script_GenerateTwoParentReport
{
    setvar(VAR_0x8004,1)
    specialvar(VAR_0x8007,GetNumLevelsGainedFromDaycare)
    if (!flag(FLAG_TEMP_11) && (var(VAR_0x8007) != 0))
    {
        msgbox(Route117_PokemonDayCare_Text_YourMonHasGrownXLevels,,SPEAKER_BREEDINGPARENT)
    }
    specialvar(VAR_RESULT,GetNumLevelsGainedFromDaycare)
    dynmultipush("Withdraw {STR_VAR_1}",1)
}

script BreedingCenter_Script_AddTwoParentToMenu
{
}

script BreedingCenter_Script_AddNeverMindToMenu
{
    dynmultipush("Never Mind",MULTI_B_PRESSED)
}

script BreedingCenter_Script_AddDepositToMenu
{
    dynmultipush("Deposit",2)
}

script BreedingCenter_Script_WithdrawMon
{
	copyvar(VAR_0x8004, VAR_RESULT)
    call(BreedingCenter_Script_CheckRoomInformPlayer)
	specialvar(VAR_RESULT, TakePokemonFromDaycare)
    msgbox(format("Here's your {STR_VAR_1}!"),,SPEAKER_BREEDINGPARENT)
	playmoncry(VAR_RESULT, CRY_MODE_NORMAL)
    if (var(VAR_0x8009) != MON_GIVEN_TO_PARTY)
    {
        bufferboxname(STR_VAR_1, VAR_PC_BOX_TO_SEND_MON)
        bufferspeciesname(STR_VAR_2, VAR_RESULT)
        msgbox(format("{PLAYER} took {STR_VAR_2} from the Breeding Center stored it in {STR_VAR_1}."))
    }
    else
    {
        bufferspeciesname(STR_VAR_2, VAR_RESULT)
        msgbox(format("{PLAYER} took {STR_VAR_2} from the Breeding Center and added it to their party."))
    }
    waitmoncry
    goto(BreedingCenter_Script_HandleMainMenu)
}

script BreedingCenter_Script_CheckRoomInformPlayer
{
    callnative(Script_IsPlayerPartyAndPokemonStorageFull)
    if (var(VAR_RESULT) == FALSE)
    {
        return
    }
    msgbox(format("It appears your party and storage boxes don't have room to take back your {STR_VAR_1}."),,SPEAKER_BREEDINGPARENT)
    msgbox(format("Please make room and come back!"),,SPEAKER_BREEDINGPARENT)
    goto(BreedingCenter_Script_HandleMainMenu)
}

text BreedingCenter_Text_Welcome
{
    format("Welcome to the Chasilla Breeding Center! Our state of the art Pokémon breeding facilities are open to the public.")
}

text BreedingCenter_Text_SinceItsYour
{
    format("Since its your first visit, we have a welcome package for you!")
}

text BreedingCenter_Text_OurWelcomeKit
{
    format("Our welcome kit has a quite a bit in it, and your bag appears to be full. Come back once you've made some room!")
}

text BreedingCenter_Text_HopefullyThisWelcomePackage
{
    format("Hopefully this welcome helps you breed the best Pokémon possible!")
}

text BreedingCenter_Text_IfYouNeedMoreInfo
{
    format("If you need more info, any of the staff here is happy to help.")
}

text BreedingCenter_Text_WhatCanWe
{
    format("What can we help you with today?")
}

text BreedingCenter_Text_AreYouReady
{
    format("Are you ready to get started?")
}

text BreedingCenter_Text_WelcomeBack
{
    format("Welcome back to the Chasilla Breeding Center, {PLAYER}!")
}

text BreedingCenter_Text_OurRecordsIndicate
{
    format("Our records indicate that you have an Egg available for pickup! Talk to our Egg manager outside.")
}

text BreedingCenter_Text_ChangedYourMind
{
    format("Changed your mind? You're always welcome back {PLAYER}.")
}

text BreedingCenter_Text_WhichPokemonWouldYou
{
    format("Which Pokémon do you want to leave in our care?")
}

text BreedingCenter_Text_WellTakeGood
{
    format("We'll take good care of {STR_VAR_1}!")
}

text BreedingCenter_Text_WouldYouLikeUsToRaiseAnother
{
    format("Would you like us to raise another Pokémon?")
}

text BreedingCenter_Text_AlwaysHereToHelp
{
    format("We're always here to help.")
}

text BreedingCenter_Text_ComeBackIfYou
{
    format("When you've chosen another Pokémon to leave with us, we'll be here!")
}

text BreedingCenter_Text_YoullGetANotiifcation
{
    format("If your Pokémon are discovered with an Egg, you'll get a Notification on your phone.")
}

text BreedingCenter_Text_YouDontSeemToHaveEnough
{
    format("If I take a Pokémon from you, you won't have any healthy Pokémon left!")
}

text BreedingCenter_Text_IsAPlace
{
    format("Here, Pokémon Trainers of can leave up to two Pokémon for us to raise. If two compatible parents are left in our care, they will evantually create an Egg. Our state of the art breeding facilities allow you to edit the DNA of the unhatched Pokémon, allowing you to customize it to your liking. As a bonus, every step you take out there will give your Pokémon here one Experience Point. Unlike other regions, we only charge fees for when Eggs are picked up.")
}

text BreedingCenter_Text_HeresYour
{
    format("Here's your {STR_VAR_1}!")
}
text BreedingCenter_Text_ItAppears
{
    format("It appears your party and storage boxes don't have room to take back your {STR_VAR_1}.")
}
text BreedingCenter_Text_PleaseMake
{
    format("Please make room and come back!")
}

text BreedingCenter_Text_PlayerTookBack
{
    format("{PLAYER} took {STR_VAR_2} from the Breeding Center and added it to their party.")
}

text BreedingCenter_Text_PlayerTookBackBox
{
    format("{PLAYER} took {STR_VAR_2} from the Breeding Center stored it in {STR_VAR_1}.")
}

text BreedingCenter_Text_PlayerTookEgg
{
    format("{PLAYER} added the {STR_VAR_1} Egg their party.")
}

text BreedingCenter_Text_PlayerTookEggBox
{
    format("{PLAYER} stored the {STR_VAR_1} Egg in {STR_VAR_2}.")
}

script BreedingCenter_Script_StartEggManagement
{
    lock
    faceplayer
    goto(BreedingCenter_Script_BuildEggMenu)
    release
    end
}

script BreedingCenter_Script_BuildEggMenu
{
    specialvar(VAR_RESULT,GetDaycareState)
    switch(var(VAR_RESULT))
    {
        case DAYCARE_EGG_WAITING:
            goto(BreedingCenter_Script_EggPickUpIntro)
        case DAYCARE_ONE_MON:
            call(BreedingCenter_Script_AddCheckParentsToMenu)
        case DAYCARE_TWO_MONS:
            call(BreedingCenter_Script_AddCheckParentsToMenu)
    }
    msgbox(format("I'm the Egg Manager here!"),,SPEAKER_BREEDINGEGG)
    goto(BreedingCenter_Script_HandleEggManagement)
}

script BreedingCenter_Script_AddCheckParentsToMenu
{
    dynmultipush("Check Pokémon",DAYCARE_ONE_MON)
}

script BreedingCenter_Script_AddInfoToMenu
{
    dynmultipush("Info",3)
}

script BreedingCenter_Script_HandleEggManagement
{
    call(BreedingCenter_Script_AddNeverMindToMenu)
    call(BreedingCenter_Script_AddInfoToMenu)
    msgbox(format("What can we help you with today?"),,SPEAKER_BREEDINGEGG)

    dynmultistack(0, 0, FALSE, 3, TRUE, 0, NULL)
    switch (var(VAR_RESULT))
    {
        case DAYCARE_ONE_MON: goto(BreedingCenter_Dialogue_DescribeMons)
        case 3: goto(BreedingCenter_Dialogue_InformPlayer)
        case MULTI_B_PRESSED: goto(BreedingCenter_Dialogue_EggMenuNeverMind)
    }
}

script BreedingCenter_Dialogue_EggMenuNeverMind
{
    specialvar(VAR_RESULT,GetDaycareState)
    switch(var(VAR_RESULT))
    {
        case DAYCARE_NO_MONS:
            msgbox(format("Talk to me once you've left a Pokémon with our Parent Manager."),,SPEAKER_BREEDINGEGG)
        case DAYCARE_ONE_MON:
            msgbox(format("Hopefully we'll find an Egg with your Pokemon soon."),,SPEAKER_BREEDINGEGG)
        case DAYCARE_TWO_MONS:
            msgbox(format("Hopefully we'll find an Egg with your Pokemon soon."),,SPEAKER_BREEDINGEGG)
        case DAYCARE_EGG_WAITING:
            msgbox(format("We'll hold onto this Egg until you come back. We might discover more!"),,SPEAKER_BREEDINGEGG)
    }
    closemessage
    release
    end
}

script BreedingCenter_Dialogue_InformPlayer
{
    callnative(Script_AreEffortValuesEnabled)
    msgbox(format("You can leave up to two Pokémon with our attendant inside. If your Pokémon are compatible, we'll evantually discover them with an Egg, which you can pick up from me. We'll hold up to 10 Eggs for you."),,SPEAKER_BREEDINGEGG)

    bufferspeciesname(STR_VAR_2,SPECIES_GYARADOS)
    bufferspeciesname(STR_VAR_3,SPECIES_MAGIKARP)
    msgbox(format("If you are a {STR_VAR_2} Plan member, while your Egg is waiting to be picked up, we will store your Eggs in our proprietary incubator. If you are not a {STR_VAR_2} Plan member, we invite you to apply for a {STR_VAR_2} Plan membership."),,SPEAKER_BREEDINGEGG)
    msgbox(format("While on the incubator, the kinetic energy you generate before you recieve the Egg will carry over towards hatching the Egg once you pick it up."),,SPEAKER_BREEDINGEGG)
    if (var(VAR_RESULT))
    {
        msgbox(format("The incubator also diffuses a specific vitamin solution through the Egg’s membrane, so your Pokémon’s Effort Values have been optimized. Of course, you can change them yourself too."),,SPEAKER_BREEDINGEGG)
    }

    callnative(Script_AreIndividualValuesDisabled)
    if (!var(VAR_RESULT))
    {
        msgbox(format("Finally, I specialize in DNA manipulation of unhatched Pokémon, and I can edit the Individual Values of those Eggs to your liking."),,SPEAKER_BREEDINGEGG)
    }

    callnative(BufferEggPrices)
    bufferspeciesname(STR_VAR_2,SPECIES_GYARADOS)
    bufferspeciesname(STR_VAR_3,SPECIES_MAGIKARP)

    if (flag(FLAG_TIMELINE_TRUE))
    {
        msgbox(format("As a {STR_VAR_3} Plan member,  you are responsible for the full cost of our services. Your out of pocket cost to pick up an Egg is ¥{STR_VAR_1}."),,SPEAKER_BREEDINGEGG)
        if (!var(VAR_RESULT))
        {
            callnative(BufferStatPrices)
            msgbox(format("As a {STR_VAR_3} Plan member, you are responsible for the full cost of our services. For every Individual Values you change, the cost is ¥{STR_VAR_1}."),,SPEAKER_BREEDINGEGG)
        }
    }
    else
    {
        msgbox(format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. Your out of pocket cost to pick up an Egg is ¥{STR_VAR_1}."),,SPEAKER_BREEDINGEGG)
        if (!var(VAR_RESULT))
        {
            callnative(BufferStatPrices)
            msgbox(format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. If you use our gene editing services, the cost for every Individual Values you change is ¥{STR_VAR_1}."),,SPEAKER_BREEDINGEGG)
        }
    }

    closemessage

    specialvar(VAR_RESULT,GetDaycareState)
    if(var(VAR_RESULT) == DAYCARE_NO_MONS)
    {
        goto(BreedingCenter_Dialogue_EggMenuNeverMind)
    }
    goto(BreedingCenter_Script_BuildEggMenu)
}

script BreedingCenter_Dialogue_DescribeMons
{
    special(GetDaycareMonNicknames)
    specialvar(VAR_RESULT,GetDaycareState)

    if(var(VAR_RESULT) == DAYCARE_ONE_MON)
    {
        msgbox(format("You left {STR_VAR_1} here. It's condition is stable, but without a partner, they won't generate an Egg."),,SPEAKER_BREEDINGEGG)
    }
    else
    {
        call(BreedingCenter_Dialogue_DescribeCompatibility)
    }
    closemessage
	release
	end
}

script BreedingCenter_Dialogue_DescribeCompatibility
{
    callnative(Script_GetDaycareCompatibilityScoreFromSave)
    switch(var(VAR_RESULT))
    {
        case PARENTS_INCOMPATIBLE:
            msgbox(format("You left {STR_VAR_1} and {STR_VAR_2} here. They have zero compatibility - we won't discover an Egg with them."),,SPEAKER_BREEDINGEGG)
        case PARENTS_LOW_COMPATIBILITY:
            msgbox(format("You left {STR_VAR_1} and {STR_VAR_2} here. They have low compatibility - it will take a while to discover an Egg."),,SPEAKER_BREEDINGEGG)
        case PARENTS_MED_COMPATIBILITY:
            msgbox(format("You left {STR_VAR_1} and {STR_VAR_2} here. They have average compatibility - we'll discover an Egg soon enough."),,SPEAKER_BREEDINGEGG)
        case PARENTS_MAX_COMPATIBILITY:
            msgbox(format("You left {STR_VAR_1} and {STR_VAR_2} here. They have extremely high compatibility - we'll discover an Egg any second now!"),,SPEAKER_BREEDINGEGG)
    }
    goto(BreedingCenter_Script_BuildEggMenu)
}

script BreedingCenter_Script_EggPickUpIntro
{
    callnative(BufferUnhatchedMonEgg)
    msgbox(format("We discovered an Egg with Pokémon you left with us. Our data suggests that it will hatch into a {STR_VAR_1}."),,SPEAKER_BREEDINGEGG)
    goto(BreedingCenter_Script_EggPickUpMenu)
}

script BreedingCenter_Script_EggPickUpMenu
{
    msgbox(format("What would you like to do with it?"),,SPEAKER_BREEDINGEGG)
    call(BreedingCenter_Script_AddTakeToMenu)
    call(BreedingCenter_Script_AddSeeOrEditToMenu)
    call(BreedingCenter_Script_AddDiscardToMenu)
    call(BreedingCenter_Script_AddResetToMenu)
    call(BreedingCenter_Script_AddCancelEggToMenu)

    dynmultistack(0, 0, FALSE, 3, TRUE, 0, NULL)

    switch (var(VAR_RESULT))
    {
        case 0: goto(BreedingCenter_Script_TakeEggSequence)
        case 1: goto(BreedingCenter_Script_ViewEgg)
        case 2: goto(BreedingCenter_Script_EditEgg)
        case 3: goto(BreedingCenter_Script_AskDiscardEgg)
        case 4: goto(BreedingCenter_Script_AskResetEgg)
        case MULTI_B_PRESSED: goto(BreedingCenter_Dialogue_EggMenuNeverMind)
    }
}

script BreedingCenter_Script_CheckSpaceForEgg
{
    callnative(Script_IsPlayerPartyAndPokemonStorageFull)

    if (var(VAR_RESULT) == FALSE)
    {
        return
    }

    msgbox(format("It appears your party and storage boxes don't have room to take this Egg!"),,SPEAKER_BREEDINGEGG)
    msgbox(format("Please make room and come back!"),,SPEAKER_BREEDINGEGG)
    closemessage
    release
    end
}

script BreedingCenter_Script_TakeEggSequence
{
    call(BreedingCenter_Script_CheckSpaceForEgg)
    showmoneybox(0,0)
    callnative(Script_CalculateAndBufferEggPickupCost)
    callnative(Script_GetDaycareCostCode)
    bufferspeciesname(STR_VAR_2,SPECIES_GYARADOS)
    bufferspeciesname(STR_VAR_3,SPECIES_MAGIKARP)

    switch(var(VAR_RESULT))
    {
        case SILICON_DAYCARE_NO_DISCOUNT_YES_STATS:
            msgbox(format("Our records indicate that you are no longer a {STR_VAR_2} Plan member. Your plan has been downgraded to the {STR_VAR_3} Plan, which does not include any discounts. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}, which includes the cost of the Individual Values you changed. Do you still want it?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
        case SILICON_DAYCARE_NO_DISCOUNT_NO_STATS:
            msgbox(format("Our records indicate that you are no longer a {STR_VAR_2} Plan member. Your plan has been downgraded to the {STR_VAR_3} Plan, which does not include any discounts. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}. Do you still want it?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
        case SILICON_DAYCARE_YES_DISCOUNT_YES_STATS:
            msgbox(format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. The out of pocket cost to pick up this Egg is ¥{STR_VAR_1}, which includes the cost of the Individual Values you changed. Do you still want it?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
        case SILICON_DAYCARE_YES_DISCOUNT_NO_STATS:
            msgbox(format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}. Do you still want it?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
    }

    if(var(VAR_RESULT) == YES)
    {
        call(BreedingCenter_Script_CheckPlayerMoney)
        variableremovemoney(VAR_0x8005)
        updatemoneybox
        playse(SE_SHOP)
        waitse
        hidemoneybox
        msgbox(format("Here's your Egg!"),,SPEAKER_BREEDINGEGG)
        goto(BreedingCenter_Script_GiveEggToPlayer)
    }
    else
    {
        hidemoneybox
        goto(BreedingCenter_Script_EggPickUpMenu)
    }
}

script BreedingCenter_Script_GiveEggToPlayer
{
    closemessage
    callnative(BufferUnhatchedMonEgg)
    callnative(GiveEggFromDaycare_Silicon)
    cueobjectplayer(BREEDINGEGG)
    playfanfare(MUS_LEVEL_UP)
    if (var(VAR_0x8009) != MON_GIVEN_TO_PARTY)
    {
        bufferboxname(STR_VAR_2, VAR_PC_BOX_TO_SEND_MON)
        msgbox(format("{PLAYER} stored the {STR_VAR_1} Egg in {STR_VAR_2}."))
    }
    else
    {
        msgbox(format("{PLAYER} added the {STR_VAR_1} Egg their party."))
    }
    closemessage
    waitfanfare
    goto(BreedingCenter_Script_CheckRemainingEggs)
}

script BreedingCenter_Script_CheckPlayerMoney
{
    variablecheckmoney(VAR_0x8005)
    if (var(VAR_RESULT))
    {
        return
    }

    msgbox(format("You don't have enough funds right now."),,SPEAKER_BREEDINGEGG)
    msgbox(format("We'll hold onto this Egg until you come back. We might discover more!"),,SPEAKER_BREEDINGEGG)
    hidemoneybox
    closemessage
    release
    end
}

script BreedingCenter_Script_ViewEgg
{
    msgbox(format("Here's what will hatch from the Egg."),,SPEAKER_BREEDINGEGG)
    closemessage

    fadescreen(FADE_TO_BLACK)
    callnative(ViewEggContents)
    waitstate

    goto(BreedingCenter_Script_EggPickUpMenu)
}

script BreedingCenter_Script_EditEgg
{
    msgbox(format("Here's what will hatch from the Egg."),,SPEAKER_BREEDINGEGG)
    msgbox(format("If you like, you can change its Individual Values."),,SPEAKER_BREEDINGEGG)
    closemessage
    fadescreen(FADE_TO_BLACK)
    callnative(EditEggContents)
    waitstate

    goto(BreedingCenter_Script_EggPickUpMenu)
}

script BreedingCenter_Script_AskResetEgg
{
    callnative(BufferUnhatchedMonEgg)
    msgbox(format("Do you want to reset the changes you've made to the {STR_VAR_1} Egg?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)

    if (var(VAR_RESULT) == YES)
    {
        callnative(ResetUnhatchedMonEgg)
        msgbox(format("Your {STR_VAR_1} Egg's Individual Values has been reset back to when it was first discovered."),,SPEAKER_BREEDINGEGG)
    }
    goto(BreedingCenter_Script_EggPickUpMenu)
}

script BreedingCenter_Script_AskDiscardEgg
{
    callnative(BufferUnhatchedMonEgg)
    msgbox(format("Do you want to leave your {STR_VAR_1} Egg? We'll keep it, but won't be able to give it you later."),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)

    if (var(VAR_RESULT) == YES)
    {
        goto(BreedingCenter_Script_DeleteEgg)
    }
    else
    {
        goto(BreedingCenter_Script_EggPickUpMenu)
    }
}

script BreedingCenter_Script_CheckRemainingEggs
{
    callnative(Script_CountNumberOfEggs)
    if (var(VAR_0x8004) != 0)
    {
        goto(BreedingCenter_Script_AskMoreEggs)
    }
    closemessage
    release
    end
}

script BreedingCenter_Script_DeleteEgg
{
    callnative(RemoveEggFromDaycare_Silicon)
    msgbox(format("Thank you for contributing to our research!"),,SPEAKER_BREEDINGEGG)
    goto(BreedingCenter_Script_CheckRemainingEggs)
}

script BreedingCenter_Script_AskMoreEggs
{
    buffernumberstring(STR_VAR_1,VAR_0x8004)
    if (var(VAR_0x8004) == 1)
    {
        msgbox(format("You have {STR_VAR_1} more Egg with us. Do you want to discuss them now?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
    }
    else
    {
        msgbox(format("You have {STR_VAR_1} more Eggs with us. Do you want to discuss them now?"),MSGBOX_YESNO,SPEAKER_BREEDINGEGG)
    }

    if (var(VAR_RESULT) == YES)
    {
        goto(BreedingCenter_Script_EggPickUpIntro)
    }

    closemessage
    release
    end
}

script BreedingCenter_Dialogue_EggNeverMind
{
    msgbox(format("Changed your mind? You're always welcome back {PLAYER}."),,SPEAKER_BREEDINGPARENT)
    closemessage
    release
    end
}

script BreedingCenter_Script_AddTakeToMenu
{
    dynmultipush("Take Egg",0)
}

script BreedingCenter_Script_AddSeeToMenu
{
    dynmultipush("View Egg",1)
}

script BreedingCenter_Script_AddSeeEditToMenu
{
    dynmultipush("View / Edit Egg",2)
}

script BreedingCenter_Script_AddDiscardToMenu
{
    dynmultipush("Discard Egg",3)
}

script BreedingCenter_Script_AddResetToMenu
{
    callnative(Script_CountChangedIndividualValues)
    if (var(VAR_RESULT) == 0)
    {
        return
    }

    dynmultipush("Reset Egg Stats",4)
}

script BreedingCenter_Script_AddCancelEggToMenu
{
    dynmultipush("Never Mind",MULTI_B_PRESSED)
}

script BreedingCenter_Script_AddSeeOrEditToMenu
{
    callnative(Script_AreIndividualValuesDisabled)
    call_if_eq(VAR_RESULT,FALSE,BreedingCenter_Script_AddSeeEditToMenu)
    call_if_eq(VAR_RESULT,TRUE,BreedingCenter_Script_AddSeeToMenu)
}

script BreedingCenter_Script_AskPlayerGene
{
    callnative(Script_AreIndividualValuesDisabled)
    if (var(VAR_RESULT))
    {
        closemessage
        release
        end
    }

    msgbox(format("Which one of your Pokémon should I work on?"),,SPEAKER_BREEDINGGENE)
    closemessage

    special(ChoosePartyMon)
    waitstate
    goto_if_eq(VAR_0x8004,PARTY_NOTHING_CHOSEN,BreedingCenter_Dialogue_NeverMind)

    callnative(BufferMonNicknameOrEggName)
    msgbox(format("Let's help {STR_VAR_1} reach their full potential."),,SPEAKER_BREEDINGGENE)
    closemessage
    fadescreen(FADE_TO_BLACK)
    callnative(EditPokemonIndividualValues)
    waitstate
    callnative(CompareOldNewIndividualValues)
    goto(BreedingCenter_Script_ChargeForStatChanges)
}

script BreedingCenter_Script_ChargeForStatChanges
{
    if (var(VAR_0x8005) == 0)
    {
        goto(BreedingCenter_Dialogue_GeneNeverMind)
    }
    showmoneybox(0,0)
    callnative(Script_GetGeneEditingCostCode)
    if (var(VAR_RESULT) == SILICON_DAYCARE_YES_DISCOUNT_YES_STATS)
    {
        msgbox(format("Thanks to your affiliation with SharpRise, changing these Individual Values will cost ¥{STR_VAR_2}. Do you still want to do it?"),MSGBOX_YESNO,SPEAKER_BREEDINGGENE)
    }
    else
    {
        msgbox(format("Without the SharpRise discount, changing these Individual Values will cost ¥{STR_VAR_2}. Do you still want to do it?"),MSGBOX_YESNO,SPEAKER_BREEDINGGENE)
    }

    if(var(VAR_RESULT) == YES)
    {
        call(BreedingCenter_Script_GeneEditCheckPlayerMoney)
        goto(BreedingCenter_Script_RemoveFundsFinishGene)
    }
    else
    {
        goto(BreedingCenter_Dialogue_RevertChanges)
    }
}

script BreedingCenter_Dialogue_GeneNeverMind
{
    msgbox(format("Changed your mind? You're always welcome back {PLAYER}."),,SPEAKER_BREEDINGGENE)
    closemessage
    release
    end
}

script BreedingCenter_Script_RemoveFundsFinishGene
{
    variableremovemoney(VAR_0x8005)
    updatemoneybox
    playse(SE_SHOP)
    waitse
    hidemoneybox
    callnative(FinalizeIndividualValueChanges)
    msgbox(format("Your changes are complete. Let me know if you want to change any more of your Pokémon."),,SPEAKER_BREEDINGEGG)
    closemessage
    release
    end
}

script BreedingCenter_Script_GeneEditCheckPlayerMoney
{
    variablecheckmoney(VAR_0x8005)
    if (var(VAR_RESULT))
    {
        return
    }
    msgbox(format("You don't have enough funds right now."),,SPEAKER_BREEDINGGENE)
    goto(BreedingCenter_Dialogue_RevertChanges)
}

script BreedingCenter_Dialogue_RevertChanges
{
    hidemoneybox
    msgbox(format("Understood. I'll revert your Pokémon back to its original state."),,SPEAKER_BREEDINGGENE)
    closemessage
    release
    end
}

script BreedingCenter_Script_StartGeneTherapy
{
    lock
    faceplayer
    msgbox(format("I specialize in Pokémon gene therapy. I help optimize a Pokemon’s Individual Values to make sure each Pokémon is performing at their peak potential."),,SPEAKER_BREEDINGGENE)
    goto(BreedingCenter_Script_AskPlayerGene)
    closemessage
    release
    end
}

text BreedingCenter_Text_ExplainEgg
{
    format("You can leave up to two Pokémon with our attendant inside. If your Pokémon are compatible, we'll evantually discover them with an Egg, which you can pick up from me. We'll hold up to 10 Eggs for you.")
}

text BreedingCenter_Text_ExplainEggEdit
{
    format("Finally, I specialize in DNA manipulation of unhatched Pokémon, and I can edit the Individual Values of those Eggs to your liking.")
}

text BreedingCenter_Text_ExplainIncubatorPremise
{
    format("If you are a {STR_VAR_2} Plan member, while your Egg is waiting to be picked up, we will store your Eggs in our proprietary incubator. If you are not a {STR_VAR_2} Plan member, we invite you to apply for a {STR_VAR_2} Plan membership.")
}

text BreedingCenter_Text_ExplainIncubatorStepsWhenTrue
{
    format("While on the incubator, the kinetic energy you generate before you recieve the Egg will carry over towards hatching the Egg once you pick it up.")
}

text BreedingCenter_Text_ExplainEVPerksWhenTrue
{
    format("The incubator also diffuses a specific vitamin solution through the Egg’s membrane, so your Pokémon’s Effort Values have been optimized. Of course, you can change them yourself too.")
}

text BreedingCenter_Text_ExplainEggsFeesGold
{
    format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. Your out of pocket cost to pick up an Egg is ¥{STR_VAR_1}.")
}

text BreedingCenter_Text_ExplainEggEditFeesGold
{
    format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. If you use our gene editing services, the cost for every Individual Values you change is ¥{STR_VAR_1}.")
}

text BreedingCenter_Text_ExplainEggsFeesBronze
{
    format("As a {STR_VAR_3} Plan member,  you are responsible for the full cost of our services. Your out of pocket cost to pick up an Egg is ¥{STR_VAR_1}.")
}

text BreedingCenter_Text_ExplainEggEditFeesBronze
{
    format("As a {STR_VAR_3} Plan member, you are responsible for the full cost of our services. For every Individual Values you change, the cost is ¥{STR_VAR_1}.")
}

text BreedingCenter_Text_ImTheEgg
{
    format("I'm the Egg Manager here!")
}

text BreedingCenter_Text_TalkToMe
{
    format("Talk to me once you've left a Pokémon with our Parent Manager.")
}

text BreedingCenter_Text_HopefullyWeFind
{
    format("Hopefully we'll find an Egg with your Pokemon soon.")
}

text BreedingCenter_Text_OneMon
{
    format("You left {STR_VAR_1} here. It's condition is stable, but without a partner, they won't generate an Egg.")
}

text BreedingCenter_Text_ZeroCompat
{
    format("You left {STR_VAR_1} and {STR_VAR_2} here. They have zero compatibility - we won't discover an Egg with them.")
}

text BreedingCenter_Text_LowCompat
{
    format("You left {STR_VAR_1} and {STR_VAR_2} here. They have low compatibility - it will take a while to discover an Egg.")
}

text BreedingCenter_Text_MediumCompat
{
    format("You left {STR_VAR_1} and {STR_VAR_2} here. They have average compatibility - we'll discover an Egg soon enough.")
}

text BreedingCenter_Text_HighCompat
{
    format("You left {STR_VAR_1} and {STR_VAR_2} here. They have extremely high compatibility - we'll discover an Egg any second now!")
}

text BreedingCenter_Text_WeDiscoveredAMonEgg
{
    format("We discovered an Egg with Pokémon you left with us. Our data suggests that it will hatch into a {STR_VAR_1}.")
}

text BreedingCenter_Text_WhatWouldYouLikeTo
{
    format("What would you like to do with it?")
}

text BreedingCenter_Text_HeresWhatWillHatch
{
    format("Here's what will hatch from the Egg.")
}

text BreedingCenter_Text_IfYouLikeYou
{
    format("If you like, you can change its Individual Values.")
}

text BreedingCenter_Text_YouDontHaveSpace
{
    format("It appears your party and storage boxes don't have room to take this Egg!")
}

text BreedingCenter_Text_AreYouCertainYouDon
{
    format("Do you want to leave your {STR_VAR_1} Egg? We'll keep it, but won't be able to give it you later.")
}

text BreedingCenter_Text_DoYouWantToReset
{
    format("Do you want to reset the changes you've made to the {STR_VAR_1} Egg?")
}

text BreedingCenter_Text_WeveResetThe
{
    format("Your {STR_VAR_1} Egg's Individual Values has been reset back to when it was first discovered.")
}

text BreedingCenter_Text_ThankYouForContributingTo
{
    format("Thank you for contributing to our research!")
}

text BreedingCenter_Text_ThanksToYourAffiliationWith
{
    format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}. Do you still want it?")
}

text BreedingCenter_Text_ThanksToYourAffiliationWithEdit
{
    format("As a valued {STR_VAR_2} Plan member, 99% of your fees are covered. The out of pocket cost to pick up this Egg is ¥{STR_VAR_1}, which includes the cost of the Individual Values you changed. Do you still want it?")
}

text BreedingCenter_Text_WithoutTheSharpRiseDiscount
{
    format("Our records indicate that you are no longer a {STR_VAR_2} Plan member. Your plan has been downgraded to the {STR_VAR_3} Plan, which does not include any discounts. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}. Do you still want it?")
}

text BreedingCenter_Text_WithoutTheSharpRiseDiscountEdit
{
    format("Our records indicate that you are no longer a {STR_VAR_2} Plan member. Your plan has been downgraded to the {STR_VAR_3} Plan, which does not include any discounts. Your out of pocket cost to pick up this Egg is ¥{STR_VAR_1}, which includes the cost of the Individual Values you changed. Do you still want it?")
}

text BreedingCenter_Text_YouDontHaveEnough
{
    format("You don't have enough funds right now.")
}

text BreedingCenter_Text_HeresYourEgg
{
    format("Here's your Egg!")
}

text BreedingCenter_Text_YouHaveMoreEggs
{
    format("You have {STR_VAR_1} more Eggs with us. Do you want to discuss them now?")
}

text BreedingCenter_Text_YouHaveMoreEgg
{
    format("You have {STR_VAR_1} more Egg with us. Do you want to discuss them now?")
}

text BreedingCenter_Text_ComeBack
{
    format("We'll hold onto this Egg until you come back. We might discover more!")
}

text BreedingCenter_Text_WhichPokemonDoYouWant
{
    format("Which one of your Pokémon should I work on?")
}

text BreedingCenter_Text_LetsGet
{
    format("Let's help {STR_VAR_1} reach their full potential.")
}

text BreedingCenter_Text_GeneEditNoDiscount
{
    format("Without the SharpRise discount, changing these Individual Values will cost ¥{STR_VAR_2}. Do you still want to do it?")
}
text BreedingCenter_Text_GeneEditYesDiscount
{
    format("Thanks to your affiliation with SharpRise, changing these Individual Values will cost ¥{STR_VAR_2}. Do you still want to do it?")
}
text BreedingCenter_Text_YourChangesAreDone
{
    format("Your changes are complete. Let me know if you want to change any more of your Pokémon.")
}
text BreedingCenter_Text_IllRevertTheChanges
{
    format("Understood. I'll revert your Pokémon back to its original state.")
}
text BreedingCenter_Text_GeneTherapyIntro
{
    format("I specialize in Pokémon gene therapy. I help optimize a Pokemon’s Individual Values to make sure each Pokémon is performing at their peak potential.")
}
