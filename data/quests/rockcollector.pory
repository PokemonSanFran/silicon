//gText_Quest_Rockcollector_
//Quest_Rockcollector_

script Quest_Rockcollector_Event_Script{
    goto_if_quest_complete(QUEST_ROCKCOLLECTOR,Quest_Rockcollector_Complete_Dialogue)
    goto_if_quest_reward(QUEST_ROCKCOLLECTOR,Quest_Rockcollector_Reward_Dialogue)
    goto_if_quest_active(QUEST_ROCKCOLLECTOR,Quest_Rockcollector_Active_Dialogue)
}

script Quest_Rockcollector_StartQuest_Dialogue{
    msgbox(format("You're back! How's the trolley treating you?"))
    msgbox(format("I have a tiny issue though... that promotion from before, where I gave you the stone? It's actually doing surprisingly well! But I'm running low on stones now. There's a secret spot where I usually get them from, but I sprained my ankle, so I can't go myself."))
    msgbox(format("Can I trust you? Head down there and grab one of each stone, and bring them back to me. That should hold me over until my ankle heals..."))
    special(Quest_Rockcollector_CheckTakenStoneFlags)
    if (var(VAR_RESULT)){
        goto(Quest_Rockcollector_Secret_Dialogue)
    }else{
        goto(Quest_Rockcollector_Reveal_Dialogue)
    }
}

script Quest_Rockcollector_Reveal_Dialogue{
    startquest(QUEST_ROCKCOLLECTOR)
    msgbox(format("If you go behind the terminal in CurenoPort, you can get underground and there are stones down there already."))
    closemessage
    release
    end
}

script Quest_Rockcollector_Secret_Dialogue{
    msgbox(format("You've already been down there? Well, that's embarassing... I should hide the entrance better. Do you still have the stones? Can I have them instead?"),MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO){
        startquest(QUEST_ROCKCOLLECTOR)
        goto(Quest_Rockcollector_Refuse_Dialogue)
    }

    special(Quest_Rockcollector_TakeAllStonesFromBag)
    goto(Quest_Rockcollector_Reward_Dialogue)
}

script Quest_Rockcollector_Active_Dialogue{
    msgbox(format("Did you bring me some Evolution Stones? Are you sure I can take them??"),MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO){
        goto(Quest_Rockcollector_Refuse_Dialogue)
    }

    setvar(VAR_TEMP_1,0)
    setvar(VAR_TEMP_2,0)

    special(Quest_Rockcollector_CheckSubquestTakeStone)
    call(Quest_Rockcollector_Progress_Dialogue)
    call(Quest_Rockcollector_GoToReward_Script)
}

script Quest_Rockcollector_Refuse_Dialogue{
    msgbox(format("Oh okay, well let me know when you've got stones for me!"))
    closemessage
    release
    end
}

script Quest_Rockcollector_Progress_Dialogue{
    switch (var(VAR_TEMP_1)){
        case 0: msgbox(format("Looks like you haven't managed to get down there and grab some stones!"))
        case 1: msgbox(format("All right, great job finding this {STR_VAR_1}."))
        case 2: msgbox(format("Wow, these stones are great!"))
    }

    if (var(VAR_TEMP_2) > 0){
        msgbox(format("Huh, there's no dust on this stone. Did it really come from underground?"))
    }
    return
}

script Quest_Rockcollector_GoToReward_Script{
    special(Quest_Rockcollector_CountRemainingSubquests)

    if (var(VAR_RESULT) == 0){
        msgbox(format("Okay, I've got one of each stone."))
        goto(Quest_Rockcollector_Reward_Dialogue)
    }elif(var(VAR_RESULT) < 10){
        msgbox(format("I think you've got... {STR_VAR_1} to go!"))
    }
        closemessage
        release
        end
}

script Quest_Rockcollector_Reward_Dialogue{
    msgbox(format("Thank you so much for your help! I'm not a Trainer, but somebody left this on the trolley recently. I guess you can use it!"))
    giveitem(ITEM_TM64)
    completequest(QUEST_ROCKCOLLECTOR)
    release
    end
}

script Quest_Rockcollector_Complete_Dialogue{
    random(2)
    if (flag(FLAG_TEMP_1)){
        setvar(VAR_RESULT,0)
    }

    switch (var(VAR_RESULT)){
        case 0: msgbox(format("Keep on riding those trolleys, {PLAYER}!"))
        case 1:
                setflag(FLAG_TEMP_1)
                msgbox(format("You know, if you go back down there tomorrow, you might find more stones..."))
    }
    closemessage
    release
    end
}

// PSF TODO cave with evolution stones should be a tiny maze with the mining mini game on the walls available all the time, and a randomly spawning evolution stone on the ground, once a day. once a stone is picked up off the ground, it doesn't respawn until quest is completed
//
// trolley dude should give you the mining kit and let you keep it if you finish

text gText_Quest_Rockcollector_YoureBack{
    format("You're back! How's the trolley treating you?")
}
text gText_Quest_Rockcollector_IHaveATiny{
    format("I have a tiny issue though... that promotion from before, where I gave you the stone? It's actually doing surprisingly well! But I'm running low on stones now. There's a secret spot where I usually get them from, but I sprained my ankle, so I can't go myself.")
}
text gText_Quest_Rockcollector_CanITrustYou{
    format("Can I trust you? Head down there and grab one of each stone, and bring them back to me. That should hold me over until my ankle heals...")
}
text gText_Quest_Rockcollector_IfYouGoBehind{
    format("If you go behind the terminal in CurenoPort, you can get underground and there are stones down there already.")
}
text gText_Quest_Rockcollector_Awesome{
    format("All right, great job finding this {STR_VAR_1}.")
}
text gText_Quest_Rockcollector_WowTheseStones{
    format("Wow, these stones are great!")
}
text gText_Quest_Rockcollector_OnlyXMore{
    format("I think you've got... {STR_VAR_1} to go!")
}
text gText_Quest_Rockcollector_HowGoes{
    format("Looks like you haven't managed to get down there and grab some stones!")
}
text gText_Quest_Rockcollector_Huh{
    format("Huh, there's no dust on this stone. Did it really come from underground?")
}
text gText_Quest_Rockcollector_YouKnowIf{
    format("You know, if you go back down there tomorrow, you might find more stones...")
}
text gText_Quest_Rockcollector_AreYouSure{
    format("Did you bring me some Evolution Stones? Are you sure I can take them??")
}

text gText_Quest_Rockcollector_OhOkayWell{
    format("Oh okay, well let me know when you've got stones for me!")
}
text gText_Quest_Rockcollector_YouveAlready{
    format("You've already been down there? Well, that's embarassing... I should hide the entrance better. Do you still have the stones? Can I have them instead?")
}
text gText_Quest_Rockcollector_OkayIveGot{
    format("Okay, I've got one of each stone.")
}
text gText_Quest_Rockcollector_ThankYouSoMuch{
    format("Thank you so much for your help! I'm not a Trainer, but somebody left this on the trolley recently. I guess you can use it!")
}
text gText_Quest_Rockcollector_KeepOnRiding{
    format("Keep on riding those trolleys, {PLAYER}!")
}
