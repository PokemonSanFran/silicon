const PLAYER = OBJ_EVENT_ID_PLAYER
const SHINZO = 1
const FLAG_HIDE_SHINZO = FLAG_TEMP_1
//script Quest_HybridCulture

script Quest_HybridCulture_HideLeader_Script{

    if (var(VAR_SHINZO_STATE) != QUEST_1_COMPLETE){
        setflag(FLAG_HIDE_SHINZO)
    }
}

script Quest_HybridCulture_CheckSubquests_Script{

    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_HYBRIDCULTURE)

    if (!var(VAR_RESULT)){
        return
    }

    setvar(VAR_TEMP_0,0)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_1)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_2)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_3)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    if (var(VAR_TEMP_0) == QUEST_HYBRIDCULTURE_SUB_COUNT){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_HYBRIDCULTURE)
    }

    return
}

script Quest_HybridCulture_Event_Script{
    lock
        call(Quest_HybridCulture_CheckSubquests_Script)
        returnqueststate(QUEST_HYBRIDCULTURE)

        switch(var(VAR_RESULT)){
            case QUEST_MENU_SET_ACTIVE:
                goto(Quest_HybridCulture_Active_Dialogue)
            case QUEST_MENU_SET_REWARD: goto(Quest_HybridCulture_Reward_Dialogue)
            case QUEST_MENU_COMPLETE_QUEST: goto(Quest_HybridCulture_Complete_Dialogue)
            default: goto(Quest_HybridCulture_StartQuest_Dialogue)
        }
}

script Quest_HybridCulture_StartQuest_Dialogue{
    msgbox(format("Hey, thanks again for your help in HodouCity. That was a rough situation, but was a wake up call. That's not anything like what I want to be."),MSGBOX_NPC,SPEAKER_SHINZO)
    msgbox(format("I think I need to learn more about the culture of Resido."),MSGBOX_NPC,SPEAKER_SHINZO)


    msgbox(format("Have you started with just ToraTown? Maybe find the best parts of your culture and see how they're represented here."),MSGBOX_NPC,SPEAKER_ALICIA)


    msgbox(format("I haven't yet. But perhaps that's a good idea."),MSGBOX_NPC,SPEAKER_SHINZO)
    msgbox(format("Can you help with that? You're a traveling Trainer, so you'll see more of ToraTown than I will. What are the great parts of ToraTown culture?"),MSGBOX_NPC,SPEAKER_SHINZO)

    startquest(QUEST_HYBRIDCULTURE)
    end
}

script Quest_HybridCulture_Active_Dialogue{
    msgbox(format("Have you had any luck learning about ToraTown culture?"),MSGBOX_NPC,SPEAKER_SHINZO)

    end
}

script Quest_HybridCulture_Reward_Dialogue{
    msgbox(format("Oh! What did you learn?"),MSGBOX_NPC,SPEAKER_SHINZO)
    closemessage


    fadescreen(FADE_TO_BLACK)
    msgbox(format("{PLAYER} explained what they learned about ToraTown's food, traditions, and fashion."))
    closemessage
    fadescreen(FADE_FROM_BLACK)

    msgbox(format("Thank you. In a way, it is beautiful to see my country appreciated in this new way. I'm glad the people here love my culture as much I do. Please, take this!"),MSGBOX_NPC,SPEAKER_SHINZO)
    closemessage


    call(Quest_HybridCulture_GiveReward_Script)

    completequest(QUEST_HYBRIDCULTURE)
    setvar(VAR_SHINZO_STATE,QUEST_2_COMPLETE)
    setflag(FLAG_FACILITY_UNLOCK_SHINZO)
    end
}

script Quest_HybridCulture_GiveReward_Script{
    giveitem(ITEM_MASTER_BALL,1)
    return
}

script Quest_HybridCulture_Complete_Dialogue{
    msgbox(format("I think for now, I'll go back to my Gym. Now that you're stronger, I'd love to battle you again!"),MSGBOX_NPC,SPEAKER_SHINZO)
    closemessage


    applymovement(SHINZO,Quest_HybridCulture_WalkBackIntoGym_Movement)
    waitmovement(SHINZO)
    opendoor(17,6)
    waitdooranim
    applymovement(SHINZO,Common_Movement_WalkUp2)
    waitmovement(SHINZO)
    //delay(16)
    playse(SE_EXIT)
    removeobject(SHINZO)
    waitse
    closedoor(17,6)
    end
}

movement Quest_HybridCulture_WalkBackIntoGym_Movement{
    walk_down
    walk_right * 2
    face_up
    step_end
}
