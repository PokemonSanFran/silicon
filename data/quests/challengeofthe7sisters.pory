//Quest_Challengeof7Sisters_

const TINA = 1
const PAUL = 2
const JON = 3
const BRADLEY = 4
const JO = 5
const HANNAH = 6
const RACHEL = 7
const PLAYER = OBJ_EVENT_ID_PLAYER

const CHALLENGE = 0
const INFO = 1
const CANCEL = 2

const SS_RANK = 0
const S_RANK = 1
const A_RANK = 2
const B_RANK = 3
const C_RANK = 4
const D_RANK = 5
const E_RANK = 6
const FAIL = 7

const FLAG_CHALLENGE_FAINTED = FLAG_TEMP_1

script Quest_Challengeof7Sisters_Welcome_Dialogue{
    msgbox(format("Welcome to the Seven Sisters Challenge!"),MSGBOX_NPC,SPEAKER_SEVENSISTERS_TINA)
        goto(Quest_Challengeof7Sisters_ChallengeMenu_Dialogue)
}

script Quest_Challengeof7Sisters_ChallengeMenu_Dialogue{
    msgbox(format("Are you ready for the toughest challenge in Resido?"),,SPEAKER_SEVENSISTERS_TINA)
    dynmultichoice(0,0,FALSE,3,0,NULL, "I challenge you!", "Info / Record", "Never mind.")

        switch (var(VAR_RESULT)){
            case CHALLENGE: goto(Quest_Challengeof7Sisters_StartChallenge_Dialogue)
            case INFO: goto(Quest_Challengeof7Sisters_Explain_Dialogue)
            case CANCEL: goto(Quest_Challengeof7Sisters_Cancel_Dialogue)
        }
    end
}

script Quest_Challengeof7Sister_RankNumberToString_Script{
    switch (var(VAR_0x8006)){
        case SS_RANK: bufferstring(STR_VAR_2,"SS")
        case S_RANK: bufferstring(STR_VAR_2,"S")
        case A_RANK: bufferstring(STR_VAR_2,"A")
        case B_RANK: bufferstring(STR_VAR_2,"B")
        case C_RANK: bufferstring(STR_VAR_2,"C")
        case D_RANK: bufferstring(STR_VAR_2,"D")
        case E_RANK: bufferstring(STR_VAR_2,"E")
        case FAIL: bufferstring(STR_VAR_2,"Fail")
    }
    return
}

script Quest_Challengeof7Sisters_ConvertLastVars_Script{

    buffernumberstring(STR_VAR_1,VAR_QUEST_CHALLENGEOFTHE7SISTERS_LAST_RECORD)

        copyvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD,VAR_QUEST_CHALLENGEOFTHE7SISTERS_LAST_RECORD)
        specialvar(VAR_0x8006,ConvertChallengeTurnsToRank)
        call(Quest_Challengeof7Sister_RankNumberToString_Script)
        return
}

script Quest_Challengeof7Sisters_ConvertBestVars_Script{
        buffernumberstring(STR_VAR_1,VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD)
        copyvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD,VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD)
        specialvar(VAR_0x8006,ConvertChallengeTurnsToRank)
        call(Quest_Challengeof7Sister_RankNumberToString_Script)
        return
}

script Quest_Challengeof7Sisters_Explain_Dialogue{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_7)
        if(var(VAR_RESULT)){

            call(Quest_Challengeof7Sisters_ConvertLastVars_Script)
                msgbox(format("Your last record was {STR_VAR_1} turns, for an {STR_VAR_2} Rank."),,SPEAKER_SEVENSISTERS_TINA)
            call(Quest_Challengeof7Sisters_ConvertBestVars_Script)
                msgbox(format("Your best record is {STR_VAR_1} turns, for an {STR_VAR_2} Rank."),,SPEAKER_SEVENSISTERS_TINA)
                msgbox(format("I think you know how this works. Shall we get started?"),MSGBOX_YESNO,SPEAKER_SEVENSISTERS_TINA)
                if (var(VAR_RESULT) == YES){
                    goto(Quest_Challengeof7Sisters_StartChallenge_Dialogue)
                }
        }

    msgbox(format("You'll battle the seven of us amazing sisters, back to back."),,SPEAKER_SEVENSISTERS_TINA)
        msgbox(format("You can only bring one Pokémon with you, and there's no breaks in between."),,SPEAKER_SEVENSISTERS_TINA)
        msgbox(format("The goal is to win in as few turns as possible."),,SPEAKER_SEVENSISTERS_TINA)
        msgbox(format("If you can beat all of us, you get a rank depending on how many turns it took over all 7 battles. The first time you achieve a rank, you get a prize!"),,SPEAKER_SEVENSISTERS_TINA)
        msgbox(format("Would you like to play?"),MSGBOX_YESNO,SPEAKER_SEVENSISTERS_TINA)

        if (var(VAR_RESULT) == YES){
            goto(Quest_Challengeof7Sisters_StartChallenge_Dialogue)
        }else{
            goto(Quest_Challengeof7Sisters_ChallengeMenu_Dialogue)
        }
}

script Quest_Challengeof7Sisters_Cancel_Dialogue{
    msgbox(format("Hope to hear from you soon!"),,SPEAKER_SEVENSISTERS_TINA)
        closemessage
        release
        end
}

script Quest_Challengeof7Sisters_StartChallenge_Dialogue{
    call(Quest_Challengeof7Sisters_CheckPartySize_Script)
        msgbox(format("Let's get started then!"),,SPEAKER_SEVENSISTERS_TINA)
        closemessage
        setvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD, 0)
        call(Quest_Challengeof7Sisters_StartQuestIfNotStarted_Script)
        call(Quest_Challengeof7Sisters_WalkToBattlefield_Script)
        goto(Quest_Challengeof7Sisters_Battle1_Script)
}

script Quest_Challengeof7Sisters_CheckPartySize_Script{
    specialvar(VAR_RESULT,CheckPartyHasOneMon)
        if (!var(VAR_RESULT)){
            goto(Quest_Challengeof7Sisters_PartyTooLarge_Dialogue)
        }
    return
}

script Quest_Challengeof7Sisters_PartyTooLarge_Dialogue{
    msgbox(format("Oh wait a second... you have more than one Pokémon with you. Deposit the rest in the PC, and talk to me when you're ready!"),,SPEAKER_SEVENSISTERS_TINA)
        closemessage
        release
        end
}

script Quest_Challengeof7Sisters_WalkToBattlefield_Script{
    applymovement(TINA, moves(walk_left * 1, walk_down * 2, face_left, step_end))
        applymovement(PLAYER, moves(walk_left * 6, walk_down * 2, face_right, step_end))
        waitmovement(PLAYER)
        return
}

script Quest_Challengeof7Sisters_StartQuestIfNotStarted_Script{
    returnqueststate(QUEST_CHALLENGEOFTHE7SISTERS)

        if (var(VAR_RESULT) < QUEST_MENU_SET_ACTIVE){
            setvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_LAST_RECORD,0)
            setvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD,0)
            startquest(QUEST_CHALLENGEOFTHE7SISTERS)
        }
    return
}

script Quest_Challengeof7Sisters_Battle1_Script{
    msgbox(format("I'm first up!"),,SPEAKER_SEVENSISTERS_TINA)
        closemessage
        applymovement(PLAYER,Common_Movement_WalkRight)
        applymovement(TINA,Common_Movement_WalkLeft)
        waitmovement(TINA)
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_TINA,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle2_Script)
}

script Quest_Challengeof7Sisters_SwapTinaForPaul_Script{
    applymovement(TINA, moves(walk_right * 2, walk_down * 2, face_up))
        delay(8)
        applymovement(PAUL, moves(walk_up * 2, walk_left, step_end))
        waitmovement(PAUL)
        return
}

script Quest_Challengeof7Sisters_Battle2_Script{
    call(Quest_Challengeof7Sisters_SwapTinaForPaul_Script)
    msgbox(format("I'm second!"),,SPEAKER_SEVENSISTERS_PAUL)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_PAUL,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle3_Script)
}

script Quest_Challengeof7Sisters_SwapPaulForJonScript{
    applymovement(PAUL, moves(walk_right, walk_down * 2, face_up))
        delay(8)
        applymovement(JON, moves(walk_up * 2, face_left, step_end))
        waitmovement(JON)

}

script Quest_Challengeof7Sisters_Battle3_Script{
    call(Quest_Challengeof7Sisters_SwapPaulForJonScript)
    msgbox(format("I'm third."),,SPEAKER_SEVENSISTERS_JON)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_JON,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle4_Script)
}

script Quest_Challengeof7Sisters_SwapJonForBradleyScript{
    applymovement(JON, moves(walk_down * 2, face_up, step_end))
        delay(8)
        applymovement(BRADLEY, moves(walk_up * 2, face_left, step_end))
        waitmovement(BRADLEY)

}

script Quest_Challengeof7Sisters_Battle4_Script{
    call(Quest_Challengeof7Sisters_SwapJonForBradleyScript)
    msgbox(format("Fourth is me!"),,SPEAKER_SEVENSISTERS_BRADLEY)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_BRADLEY,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle5_Script)
}

script Quest_Challengeof7Sisters_SwapBradleyForJoScript{
    applymovement(BRADLEY, moves(walk_down * 2, face_up, step_end))
        delay(8)
        applymovement(JO, moves(walk_up, walk_right, walk_up, face_left, step_end))
        waitmovement(JO)

}

script Quest_Challengeof7Sisters_Battle5_Script{
    call(Quest_Challengeof7Sisters_SwapBradleyForJoScript)
    msgbox(format("Ready for fifth?"),,SPEAKER_SEVENSISTERS_JO)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_JO,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle6_Script)
}

script Quest_Challengeof7Sisters_SwapJoForHannahScript{
    applymovement(JO, moves(walk_down, walk_left, walk_down, face_up, step_end))
        delay(8)
        applymovement(HANNAH, moves(walk_up, walk_right * 3, walk_up, face_left, step_end))
        waitmovement(HANNAH)

}

script Quest_Challengeof7Sisters_Battle6_Script{
    call(Quest_Challengeof7Sisters_SwapJoForHannahScript)
    msgbox(format("Tired? This is sixth."),,SPEAKER_SEVENSISTERS_HANNAH)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_HANNAH,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_Battle7_Script)
}

script Quest_Challengeof7Sisters_SwapHannahForRachelScript{
    applymovement(HANNAH, moves(walk_down, walk_left * 3, walk_down, face_up, step_end))
        delay(8)
        applymovement(RACHEL, moves(walk_up, walk_right * 3, walk_up, walk_right, face_left, step_end))
        waitmovement(RACHEL)
}

script Quest_Challengeof7Sisters_Battle7_Script{
    call(Quest_Challengeof7Sisters_SwapHannahForRachelScript)
    msgbox(format("Last one!"),,SPEAKER_SEVENSISTERS_RACHEL)
        closemessage
        trainerbattle_continue_after_lose(TRAINER_SEVENSISTERS_RACHEL,gText_Challengeof7Sisters_IDidMyBest)
        call(Quest_Challengeof7Sisters_AssessBattleOutcome_Script)
        goto(Quest_Challengeof7Sisters_ChallengeOver_Dialogue)
}

script Quest_Challengeof7Sisters_AssessBattleOutcome_Script{
    specialvar(VAR_RESULT,GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON){
            return
        }else{
            call(Quest_Challengeof7Sisters_MarkLoss_Script)
            goto(Quest_Challengeof7Sisters_ChallengeOver_Dialogue)
        }
    return
}

script Quest_Challengeof7Sisters_MarkLoss_Script{
    clearflag(FLAG_CHALLENGE_FAINTED)
    setflag(FLAG_CHALLENGE_FAINTED)
    return
}

script Quest_Challengeof7Sisters_WarpEverybodyBackToPlace_Script{
    fadescreen(FADE_TO_BLACK)
        setobjectxy(PLAYER,11,3)
        TeleportCamera(11,3)
        applymovement(PLAYER,Common_Movement_FaceLeft)
        setobjectxy(TINA,10,3)
        setobjectxyperm(TINA,10,3)
        setobjectxyperm(PAUL,9,7)
        setobjectxyperm(JON,8,7)
        setobjectxyperm(BRADLEY,7,7)
        setobjectxyperm(JO,6,7)
        setobjectxyperm(HANNAH,5,7)
        setobjectxy(RACHEL,4,7)
        setobjectxyperm(RACHEL,4,7)
        applymovement(TINA,Common_Movement_FaceRight)
        applymovement(PAUL,Common_Movement_FaceUp)
        applymovement(JON,Common_Movement_FaceUp)
        applymovement(BRADLEY,Common_Movement_FaceUp)
        applymovement(JO,Common_Movement_FaceUp)
        applymovement(HANNAH,Common_Movement_FaceUp)
        applymovement(RACHEL,Common_Movement_FaceUp)
        fadescreen(FADE_FROM_BLACK)
}

script Quest_Challengeof7Sisters_ChallengeOver_Dialogue{
    lock
    call(Quest_Challengeof7Sisters_WarpEverybodyBackToPlace_Script)
    goto(Quest_Challengeof7Sisters_JudgePerformance_Script)
}

script Quest_Challengeof7Sisters_JudgePerformance_Script{
    lock
    copyvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_LAST_RECORD,VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD)

    if (var(VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD) > value(VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD) || var(VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD) == 0){
    copyvar(VAR_QUEST_CHALLENGEOFTHE7SISTERS_BEST_RECORD,VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD)
    }

    specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)

    special(HealPlayerParty)
    switch (var(VAR_RESULT)){
        case FAIL: goto(Quest_Challengeof7Sisters_Failed_Dialogue)
        default: goto(Quest_Challengeof7Sisters_Completed_Dialogue)
    }
}

script Quest_Challengeof7Sisters_Failed_Dialogue{
    msgbox(format("I'm sorry, but you fainted before you could beat all of us. Try again next time!"),,SPEAKER_SEVENSISTERS_TINA)
    msgbox(format("Hope to hear from you soon!"),,SPEAKER_SEVENSISTERS_TINA)
    closemessage
    release
    end
}

script Quest_Challengeof7Sisters_Completed_Dialogue{
    specialvar(VAR_0x8006,ConvertChallengeTurnsToRank)
    call(Quest_Challengeof7Sister_RankNumberToString_Script)
    buffernumberstring(STR_VAR_1,VAR_QUEST_CHALLENGEOFTHE7SISTERS_CURRENT_RECORD)
    msgbox(format("Congratulations! Your streak of {STR_VAR_1} got you the {STR_VAR_2} Rank!"),,SPEAKER_SEVENSISTERS_TINA)
    call(Quest_Challengeof7Sisters_GivePrizesAndComplete_Script)
    msgbox(format("It was fun to battle you! Maybe we'll battle again someday."),,SPEAKER_SEVENSISTERS_TINA)
    closemessage
    release
    end
}

script Quest_Challengeof7Sisters_GivePrizesAndComplete_Script{
    call(Quest_Challengeof7Sisters_RankCheck1_Script)
    call(Quest_Challengeof7Sisters_RankCheck2_Script)
    call(Quest_Challengeof7Sisters_RankCheck3_Script)
    call(Quest_Challengeof7Sisters_RankCheck4_Script)
    call(Quest_Challengeof7Sisters_RankCheck5_Script)
    call(Quest_Challengeof7Sisters_RankCheck6_Script)
    call(Quest_Challengeof7Sisters_RankCheck7_Script)
    return
}

script Quest_Challengeof7Sisters_RankCheck1_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_1)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) == SS_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_1)
                call(Quest_Challengeof7Sisters_CompleteQuestIfNotCompleted_Script)
                giveitem(ITEM_FIRE_GEM)
            }
    }
    return
}

script Quest_Challengeof7Sisters_RankCheck2_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_2)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= S_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_2)
                giveitem(ITEM_WATER_GEM)
            }
    }
    return
}
script Quest_Challengeof7Sisters_RankCheck3_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_3)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= A_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_3)
                giveitem(ITEM_ELECTRIC_GEM)
            }
    }
    return
}
script Quest_Challengeof7Sisters_RankCheck4_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_4)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= B_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_4)
                giveitem(ITEM_GRASS_GEM)
            }
    }
    return
}
script Quest_Challengeof7Sisters_RankCheck5_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_5)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= C_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_5)
                giveitem(ITEM_ICE_GEM)
            }
    }
    return
}
script Quest_Challengeof7Sisters_RankCheck6_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_6)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= D_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_6)
                giveitem(ITEM_PSYCHIC_GEM)
            }
    }
    return
}
script Quest_Challengeof7Sisters_RankCheck7_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_7)

    if(!var(VAR_RESULT)){
        specialvar(VAR_RESULT,ConvertChallengeTurnsToRank)
            if (var(VAR_RESULT) <= E_RANK){
                subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CHALLENGEOFTHE7SISTERS,SUB_QUEST_7)
                giveitem(ITEM_POKE_BALL)
            }
    }
    return
}

script Quest_Challengeof7Sisters_CompleteQuestIfNotCompleted_Script{
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_CHALLENGEOFTHE7SISTERS)
        if (var(VAR_RESULT)){
            completequest(QUEST_CHALLENGEOFTHE7SISTERS)
        }
    return
}

movement Quest_Challengeof7Sisters_RachelFromBattlefield_Movement{
    walk_down
    step_end
}

text gText_Challengeof7Sisters_IDidMyBest{
    format("I did my best. I have no regrets!")
}
