const PLAYER = OBJ_EVENT_ID_PLAYER
const BIKER_A = 3 // Pioca Bridge
const BIKER_B = 4 // Route 98
const BIKER_C = 1 // Cureno Port
const BIKER_D = 2 // Cureno Port
const FLAG_HIDE_BIKER_A = FLAG_TEMP_5
const FLAG_HIDE_BIKER_B = FLAG_TEMP_6

//Quest_AngelDelivery

script AngelDelivery_Script_HideBikers
{
    returnqueststate(QUEST_ANGELDELIVERY)

    if (var(VAR_RESULT) >= QUEST_MENU_SET_ACTIVE)
    {
        setflag(FLAG_HIDE_BIKER_A)
    }

    if (var(VAR_RESULT) >= QUEST_MENU_SET_REWARD)
    {
        setflag(FLAG_HIDE_BIKER_B)
    }
    return
}

script AngelDelivery_Script_CheckItemSpace
{
    lock
    faceplayer
    checkitemspace(ITEM_QUEST_ANGELDELIVERY_1,1)
    if(!var(VAR_RESULT))
    {
        goto(AngelDelivery_Dialogue_BagFull)
    }

    checkitemspace(ITEM_QUEST_ANGELDELIVERY_2,1)
    if(!var(VAR_RESULT))
    {
        goto(AngelDelivery_Dialogue_BagFull)
    }

    goto(AngelDelivery_Dialogue_StartQuest)
}

script AngelDelivery_Dialogue_BagFull
{
    msgbox(format("Hey, could you help me- Oh. Your bag is too full to help me. Maybe I can hop on my one good leg to the nearest town and find somebody else…"),,SPEAKER_ANGELDELIVERYBIKERA)
        closemessage
        release
        end
}

script AngelDelivery_Dialogue_StartQuest
{
    msgbox(format("Hey, could you help me out?"),,SPEAKER_ANGELDELIVERYBIKERA)
        msgbox(format("I’m doing my initiation into the Gear Grind Gang and I have to deliver something to somebody on Route 98."),,SPEAKER_ANGELDELIVERYBIKERA)
        msgbox(format("When I was starting off, I ran over a Pokémon, fell off my bike, and broke my leg."),,SPEAKER_ANGELDELIVERYBIKERA)
        msgbox(format("And if I don’t get this package there in time, I’m not allowed to join!"),,SPEAKER_ANGELDELIVERYBIKERA)
        msgbox(format("Can you make the delivery for me instead? You can even use my top of the line Bike!"),,SPEAKER_ANGELDELIVERYBIKERA)
        closemessage
    cueobjectplayer(BIKER_A)
        giveitem(ITEM_QUEST_ANGELDELIVERY_1, 1)
        giveitem(ITEM_QUEST_ANGELDELIVERY_2, 1)
        msgbox(format("I should probably go to a hospital…"),,SPEAKER_ANGELDELIVERYBIKERA)
        closemessage
        call(AngelDelivery_Script_BikerALeaves)
        startquest(QUEST_ANGELDELIVERY)
        release
        end
}

script AngelDelivery_Script_BikerALeaves
{
    getobjectfacingdirection(PLAYER, VAR_TEMP_1)
        switch(var(VAR_TEMP_1))
        {
            case DIR_NORTH:
                applymovement(BIKER_A, Quest_AngelDelivery_Movement_BikerAWalksAway_North_1)
                turnobject(PLAYER, DIR_EAST)
                waitmovement(0)
                applymovement(BIKER_A, Quest_AngelDelivery_Movement_BikerAWalksAway_North_2)
                turnobject(PLAYER, DIR_SOUTH)
            case DIR_SOUTH:
            case DIR_EAST:
            case DIR_WEST:
                applymovement(BIKER_A, Quest_AngelDelivery_Movement_BikerAWalksAway)
                turnobject(PLAYER, DIR_SOUTH)
        }
    waitmovement(PLAYER)
    removeobject(BIKER_A)
}

script AngelDelivery_Script_GesturePlayerTakeItem
{
    getobjectfacingdirection(PLAYER, VAR_TEMP_1)
    cueobject(PLAYER,VAR_TEMP_1)
    waitmovement(0)
    removeitem(ITEM_QUEST_ANGELDELIVERY_2, 1)
}

script AngelDelivery_Dialogue_Active
{
    msgbox(format("Are you AngeldeliverybikerB?"),,SPEAKER_ALICIA)
    msgbox(format("Who wants to know?"),,SPEAKER_ANGELDELIVERYBIKERB)
    closemessage
    call(AngelDelivery_Script_GesturePlayerTakeItem)
    msgbox(format("Isn’t that AngeldeliverybikerA’s?"),,SPEAKER_ANGELDELIVERYBIKERB)
    msgbox(format("They broke their foot trying to get here. They wanted to prove that they were serious, so they even gave me their bike to come bring this to you."))
    msgbox(format("Woah, that’s hardcore! Are they okay?"),,SPEAKER_ANGELDELIVERYBIKERB)
    msgbox(format("They said they were going to the hospital."))
    msgbox(format("Wow."),,SPEAKER_ANGELDELIVERYBIKERB)
    msgbox(format("Well, you have a bike… And you’ve completed the initiation by finishing the challenge and giving me the Blue Card…"),,SPEAKER_ANGELDELIVERYBIKERB)
    msgbox(format("Sounds like you’ve earned a spot on Gear Grind Gang!"),,SPEAKER_ANGELDELIVERYBIKERB)
    msgbox(format("I’m really just a Trainer…"))
    buffermapname(STR_VAR_1,MAP_CURENO_PORT)
    msgbox(format("At least let us upgrade your bike! If you head to the HQ in {STR_VAR_1}, we can get that tuned up for you."),,SPEAKER_ANGELDELIVERYBIKERB)
    questmenu(QUEST_MENU_SET_REWARD, QUEST_ANGELDELIVERY)
}

script AngelDelivery_Dialogue_Reward
{
    bufferitemname(STR_VAR_1,ITEM_QUEST_ANGELDELIVERY_2)
    msgbox(format("The {STR_VAR_1} is actually from the Johto region. Our leader is a big weeb, and it reminds him of one of his favorite animé."),,SPEAKER_ANGELDELIVERYBIKERB)
}

script AngelDelivery_Dialogue_Inactive
{
    msgbox(format("Man, he’s not gonna pass at this rate…"),,SPEAKER_ANGELDELIVERYBIKERB, TAIL_WHISPER)
}

script AngelDelivery_Script_InteractBikerB
{
    lock
    faceplayer
    returnqueststate(QUEST_ANGELDELIVERY)
    switch(var(VAR_RESULT))
    {
        case QUEST_MENU_SET_ACTIVE:
            call(AngelDelivery_Dialogue_Active)
        case QUEST_MENU_SET_REWARD:
            call(AngelDelivery_Dialogue_Reward)
        default:
            call(AngelDelivery_Dialogue_Inactive)
    }
    release
    end
}

script AngelDelivery_Dialogue_PreBattle
{
    applymovement(BIKER_C, AngelDelivery_Movement_MoveBikerC)
    applymovement(BIKER_D, AngelDelivery_Movement_MoveBikerD)
    waitmovement(0)
    call(AngelDelivery_Dialogue_FirstTime)
    msgbox(format("And now for the final stage of your onboarding…"),,SPEAKER_ANGELDELIVERYBIKERC)
    msgbox(format("Prove that you’re tough enough to hang!"),,SPEAKER_ANGELDELIVERYBIKERD)
    goto(AngelDelivery_Script_StartBattle)
}

script AngelDelivery_Dialogue_FirstTime
{
    //PSF TODO fix bug where when on a bike and going inside, player is forced off bike
    checktrainerdiscovered(TRAINER_ANGELDELIVERY_BIKERC)
    if (var(VAR_RESULT) == TRUE)
    {
        return
    }
    msgbox(format("Ah, are you PLAYER? AngeldeliverybikerB told us you were coming."),,SPEAKER_ANGELDELIVERYBIKERC)
    msgbox(format("Congrats on completing your initiation! Insane that you were able to just fill in for AngeldeliverybikerA with no training."),,SPEAKER_ANGELDELIVERYBIKERD)
}

script AngelDelivery_Script_StartBattle
{
    closemessage
    trainerbattle_two_trainers(TRAINER_ANGELDELIVERY_BIKERC,format("One of us, one of us!"),TRAINER_ANGELDELIVERY_BIKERD,format("You're the real deal, {PLAYER}."))
    goto(AngelDelivery_Script_PostBattle)
}

script AngelDelivery_Script_CircleBikers
{
    applymovement(BIKER_C, Quest_AngelDelivery_BikersCDWalkToTheCenter)
    applymovement(BIKER_D, Quest_AngelDelivery_BikersCDWalkToTheCenter)
    applymovement(PLAYER, Quest_AngelDelivery_PlayerWalksToTheCenter)
    waitmovement(0)
    delay(15)
    applymovement(BIKER_C, AngelDelivery_Movement_BikerCCirclesAroundThePlayer)
    applymovement(BIKER_D, AngelDelivery_Movement_BikerDCirclesAroundThePlayer)
    applymovement(PLAYER, AngelDelivery_Movement_PlayerLooksAtTheBikers)
    waitmovement(0)
    msgbox(format("Gotta get to that 48:16 gear ratio…"),,SPEAKER_ANGELDELIVERYBIKERC, TAIL_WHISPER)
    msgbox(format("This saddle height is too low, they’re not aero enough…"),,SPEAKER_ANGELDELIVERYBIKERD, TAIL_WHISPER)
    closemessage
    applymovement(BIKER_C, AngelDelivery_Movement_BikerCCirclesAroundThePlayer)
    applymovement(BIKER_D, AngelDelivery_Movement_BikerDCirclesAroundThePlayer)
    applymovement(PLAYER, AngelDelivery_Movement_PlayerLooksAtTheBikers)
    waitmovement(0)
    msgbox(format("Has this chain ever been lubed?"),,SPEAKER_ANGELDELIVERYBIKERC, TAIL_WHISPER)
    msgbox(format("And gotta hook up the nitro…"),,SPEAKER_ANGELDELIVERYBIKERD, TAIL_WHISPER)
    closemessage
    applymovement(BIKER_C, AngelDelivery_Movement_BikerCCirclesAroundThePlayer_End)
    applymovement(BIKER_D, AngelDelivery_Movement_BikerDCirclesAroundThePlayer_End)
    applymovement(PLAYER, AngelDelivery_Movement_PlayerLooksAtTheBikers)
    waitmovement(0)
    closemessage
}

script AngelDelivery_Script_PostBattle
{
    setflag(FLAG_SPEED_DEMON)
    msgbox(format("You passed the last test!"),,SPEAKER_ANGELDELIVERYBIKERC)
    msgbox(format("But of course, you can’t be riding around at normie speed!"),,SPEAKER_ANGELDELIVERYBIKERD)
    closemessage
    call(AngelDelivery_Script_CircleBikers)
    msgbox(format("All done. Your bike should be way faster at top speed now!"),,SPEAKER_ANGELDELIVERYBIKERC)
    msgbox(format("Pedal power!"),,SPEAKER_ANGELDELIVERYBIKERD)
    msgbox(format("You're officially one of the Gear Grind Gang. You’re welcome to come to this clubhouse whenever you like."),,SPEAKER_ANGELDELIVERYBIKERD)
    completequest(QUEST_ANGELDELIVERY)
    release
    end
}

movement Quest_AngelDelivery_Movement_BikerAWalksAway_North_1 {
    walk_slow_right * 2
    step_end
}

movement Quest_AngelDelivery_Movement_BikerAWalksAway_North_2 {
    walk_slow_down * 7
    step_end
}

movement Quest_AngelDelivery_Movement_BikerAWalksAway {
    walk_slow_down * 6
    step_end
}

movement AngelDelivery_Movement_MoveBikerC {
    walk_right
    face_down
    step_end
}

movement AngelDelivery_Movement_MoveBikerD {
    walk_left
    face_down
    step_end
}

movement Quest_AngelDelivery_BikersCDWalkToTheCenter {
    walk_up * 3
    face_down
    step_end
}

movement Quest_AngelDelivery_PlayerWalksToTheCenter {
    walk_up * 3
    step_end
}

movement AngelDelivery_Movement_BikerCCirclesAroundThePlayer {
    walk_left
    walk_down * 2
    walk_right * 2
    walk_up * 2
    walk_left
    face_right
    step_end
}

movement AngelDelivery_Movement_BikerDCirclesAroundThePlayer {
    walk_left * 2
    walk_down * 2
    walk_right * 2
    walk_up * 2
    face_left
    step_end
}

movement AngelDelivery_Movement_BikerCCirclesAroundThePlayer_End {
    walk_left
    walk_down * 2
    walk_right * 2
    walk_up * 2
    walk_left
    face_down
    step_end
}

movement AngelDelivery_Movement_BikerDCirclesAroundThePlayer_End {
    walk_left * 2
    walk_down * 2
    walk_right * 2
    walk_up * 2
    face_down
    step_end
}

movement AngelDelivery_Movement_PlayerLooksAtTheBikers {
    face_left
    delay_16 * 3
    face_down
    delay_16 * 3
    face_right
    delay_16 * 2
    face_up
    step_end
}

