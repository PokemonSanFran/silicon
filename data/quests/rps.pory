const SPEAKER_MONTY = SPEAKER_BLAINE
const MONTY_CAPHE = 20

const SPEAKER_MONTYOPP = SPEAKER_DIMU
const MONTYOPP = 21

const SPEAKER_PLAYER = SPEAKER_ALICIA
const PLAYER = OBJ_EVENT_ID_PLAYER

const FLAG_HIDE_MONTY_OPP = FLAG_TEMP_5

script RPS_Script_ControlMontyOppVisibility
{
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_RPS)
    if(var(VAR_RESULT))
    {
        setflag(FLAG_HIDE_MONTY_OPP)
    }
}

script RPS_Script_SaveAndDeleteParty
{
    special(SavePlayerParty)
    callnative(ZeroPlayerPartyMons)
}

script RPS_Script_GiveRiggedMon
{
    call(RPS_Script_SaveAndDeleteParty)
    givemon(species=SPECIES_HITMONCHAN,level=20,item=0,abilityNum=2,shinyMode=SHINY_MODE_ALWAYS,hpIv=7,atkIv=9,defIv=1,spAtkIv=9,spDefIv=3,move1=MOVE_UPPER_HAND,move2=MOVE_MACH_PUNCH,move3=MOVE_FOCUS_PUNCH)
}

script RPS_Script_GiveFairMon
{
    call(RPS_Script_SaveAndDeleteParty)
    givemon(species=SPECIES_HITMONCHAN,level=20,item=ITEM_PUNCHING_GLOVE,abilityNum=1,shinyMode=SHINY_MODE_ALWAYS,hpIv=31,atkIv=31,defIv=31,spAtkIv=31,spDefIv=31,move1=MOVE_UPPER_HAND,move2=MOVE_MACH_PUNCH,move3=MOVE_FOCUS_PUNCH,nature=NATURE_JOLLY,hpEv=16,atkEv=236,speedEv=248)
}

script RPS_Script_ResetPlayer
{
    callnative(ZeroPlayerPartyMons)
    special(LoadPlayerParty)
}

script RPS_Script_StreakReset
{
    setvar(VAR_QUEST_RPS_STREAK_COUNT,0)
}

script RPS_Script_IncrementStreak
{
    addvar(VAR_QUEST_RPS_STREAK_COUNT,1)
}

script RPS_Script_DecrementStreak
{
    subvar(VAR_QUEST_RPS_STREAK_COUNT,1)
}

script RPS_Script_GetWager
{
    specialvar(VAR_0x8004, Quest_RPS_CalculateWager)
}

script RPS_Script_QuestInactive
{
    call_if_quest_inactive(QUEST_RPS,RPS_Dialogue_StartQuest)
    call_if_quest_active(QUEST_RPS,RPS_Script_InteractMontyQuestActive)
    call_if_quest_active(QUEST_RESTOREZENZUGYM,RestoreZenzuIsland_Dialogue_RecruitMonty)
    call_if_quest_complete(QUEST_RPS,RPS_Dialogue_PostQuestInteract)
}

script RPS_Dialogue_StartQuest
{
    msgbox(format("You beat me again Monty! I can't keep losing money like this. My wife will have my head!"),MSGBOX_DEFAULT,SPEAKER_MONTYOPP)
    msgbox(format("Hueh heuh heuh, don't worry, you'll taste victory soon enough MontyOpp. Just come back when your pockets are a little heavier."),MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    call (RPS_Script_MontyOppLeaveScreen)
    waitmovement(MONTY_CAPHE)
    applymovement (MONTY_CAPHE, RPS_Movement_MontyFacePlayer)
    msgbox(format("Heyo! You there! New around these parts?"),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox(format("Who, me?"),MSGBOX_AUTOCLOSE,SPEAKER_PLAYER)
    msgbox(format("Hueh heuh heuh, yeah you!"),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox(format("You ever hear of Monty's Mach-Focus-Hand?"),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox(format("No, can't say that I have!"),MSGBOX_AUTOCLOSE,SPEAKER_PLAYER)
    msgbox(format("Hueh heuh heuh, well I'm Monty, and I made it up. of course you haven't."),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox(format("Or learned it in Kanto..."),MSGBOX_AUTOCLOSE,SPEAKER_MONTY,TAIL_WHISPER)
    closemessage
    startquest(QUEST_RPS)
}

script RPS_Script_InteractMontyQuestActive
{
    lock
    showmoneybox(0,0)
    call(RPS_Script_GetWager)
    buffernumberstring(STR_VAR_2 ,VAR_0x8004)
    msgbox(format("Ready to give it a shot?"), MSGBOX_NPC, SPEAKER_MONTY)

    if(!var(VAR_RESULT))
    {
        goto(RPS_Dialogue_NotEnoughMoney)
    }

    msgbox(format("Let's put some stakes on it. ¥{STR_VAR_2}, winner takes the pot."),MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    msgbox(format("Hueh hueh hueh, I know a hotshot Trainer like you has ¥{STR_VAR_2} laying around, right?"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    msgbox(format("Here's the lowdown:"),MSGBOX_DEFAULT,SPEAKER_MONTY)

    hidemoneybox
    call(RPS_Script_GiveRiggedMon)
    buffermovename(STR_VAR_1,MOVE_UPPER_HAND)
    buffermovename(STR_VAR_2,MOVE_MACH_PUNCH)
    msgbox(format("{STR_VAR_1} trumps {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    buffermovename(STR_VAR_2,MOVE_FOCUS_PUNCH)
    buffermovename(STR_VAR_1,MOVE_MACH_PUNCH)
    msgbox(format("{STR_VAR_1} beats {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    buffermovename(STR_VAR_2,MOVE_UPPER_HAND)
    buffermovename(STR_VAR_1,MOVE_FOCUS_PUNCH)
    msgbox(format("{STR_VAR_1} wins out over {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    msgbox(format("If your Pokémon faints, you lose."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    bufferspeciesname(STR_VAR_1,SPECIES_HITMONCHAN)
    msgbox(format("Easy, yeah? Let's see how lucky you are. You can even use my lucky {STR_VAR_1}!"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    closemessage
    trainerbattle_continue_after_lose(TRAINER_MONTY,format("Hueh hueh hueh, sometimes you just have to make your own luck."))
    call(RPS_Script_JudgeBattleOutcome)
}

script RPS_Dialogue_NotEnoughMoney
{
    msgbox(format("Ah, it's no fun without a little wager. Come back with ¥{STR_VAR_2}"), , SPEAKER_MONTY)
    closemessage
    hidemoneybox
    releaseall
    end
}
script RPS_Script_JudgeBattleOutcome
{
    call(RPS_Script_ResetPlayer)
    specialvar(VAR_RESULT,GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(RPS_Dialogue_QuestComplete)
    }
    goto(RPS_Dialogue_LostQuestBattle)
}

script RPS_Dialogue_QuestComplete
{
    msgbox(format("My luck ran out. I'm shocked."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You weren't ever lucky to begin with!"), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("He? watch your mouth, what are you implying..."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    bufferitemname(STR_VAR_1,ITEM_PUNCHING_GLOVE)
    bufferstring(STR_VAR_1,format("Google Glass"))
    msgbox(format("My {STR_VAR_2} can see everything about your Pokémon. I can see that your Pokémon was stronger than mine and holding {STR_VAR_1}."), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("..."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You got me! Hueh hueh hueh, kids these days are too high tech to fool. Maybe I need to target a... less savvy demographic."), MSGBOX_DEFAULT SPEAKER_MONTY)
    msgbox(format("Wait,you tageted me specifically?"), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("Hueh hueh hueh, of course! I don't want to con region natives, but I have no problem ripping off techie transplants. You're so gullible!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You know, when I started hustling, I was just trying to get my house back. But now? I'm just good at getting people to hand over their cash!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("But for now, I'm not goin anywhere. {PLAYER}, anytime you want a rematch, I'll be here."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    showmoneybox(0, 0)
    addmoney(QUEST_RPS_BASE_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Next time, I'll play fair, hueh hueh hueh."), , SPEAKER_MONTY)
    hidemoneybox
    closemessage
    completequest(QUEST_RPS)
    release
    end
}

script RPS_Dialogue_LostQuestBattle
{
    msgbox(format("Tough break kid."), , SPEAKER_MONTY )
    showmoneybox(0, 0)
    removemoney(QUEST_RPS_BASE_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Better luck next time!"), , SPEAKER_MONTY )
    hidemoneybox
    closemessage
}

script RPS_Dialogue_PostQuestInteract
{
    lock
    faceplayer
    goto(RPS_Script_JudgeStreak)
}

script RPS_Script_JudgeStreak
{
    switch(var(VAR_QUEST_RPS_STREAK_COUNT))
    {
        default: goto(RPS_Dialogue_ContinueStreak)
        case 0: goto(RPS_Dialogue_ZeroStreak)
        case QUEST_RPS_MAX_STREAK_LENGTH: goto(RPS_Dialogue_CompletedStreak)
        case QUEST_RPS_GIVE_REWARD: goto(RPS_Dialogue_CheckRewardSpace)
        case QUEST_RPS_REWARD_GIVEN: goto(RPS_Dialogue_PostStreak)
    }
}

script RPS_Dialogue_ZeroStreak
{
    msgbox(format("Hey {PLAYER}!"),MSGBOX_DEFAULT,SPEAKER_MONTY)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREZENZUGYM,SUB_QUEST_1)
    if(var(VAR_RESULT))
    {
        msgbox(format("This production is somethin' else! So many moving parts. But ol' Monty is putting his own spin on this here fundraising."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    }

    msgbox(format("How lucky are you feeling today?"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    callnative(Quest_RPS_CheckWager)
    if(var(VAR_RESULT))
    {
        goto(RPS_Dialogue_AskFirstStreak)
    }
    goto(RPS_Dialogue_CannotAffordWager)
}

script RPS_Dialogue_AskFirstStreak
{
    showmoneybox (0,0)
    buffernumberstring(STR_VAR_1,QUEST_RPS_WAGER)
    msgbox(format("How about another go? We can make interesting with ¥{STR_VAR_1}."), MSGBOX_YESNO, SPEAKER_MONTY)
        if (var(VAR_RESULT) == YES)
        {
            hidemoneybox
            closemessage
            goto(RPS_Script_StartWagerBattle)
        }
    goto(RPS_Dialogue_DenyFirstWagerBattle)
}

script RPS_Dialogue_CannotAffordWager
{
    msgbox(format("Hueh hueh hueh, you look more broke than me! Come back when your luck has changed."), MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    closemessage
    releaseall
    end
}


script RPS_Script_JudgeWagerBattleOutcome
{
    call(RPS_Script_ResetPlayer)
    specialvar(VAR_RESULT,GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(RPS_Dialogue_WonWagerBattle)
    }
    goto(RPS_Dialogue_LostWagerBattle)
}

script RPS_Script_StartWagerBattle
{
    call (RPS_Script_GiveFairMon)
    msgbox(format("Hueh hueh hueh, all right! Mach! Focus! Hand!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    closemessage
    trainerbattle_continue_after_lose(TRAINER_MONTY,format("Well what do you know? Luck has finally turned against me."))
    call(RPS_Script_JudgeWagerBattleOutcome)
}

script RPS_Dialogue_WonWagerBattle
{
    call(RPS_Script_IncrementStreak)
    call(RPS_Script_JudgeStreak)
}

script RPS_Dialogue_LostWagerBattle
{
    call(RPS_Script_GetWager)
    call (RPS_Script_StreakReset)
    msgbox(format("Hueh hueh hueh, victory is mine this time!"), , SPEAKER_MONTY)
    showmoneybox(0,0)
    removemoney(QUEST_RPS_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Pleasure doing business with ya. Holler if you're up for another round."), ,SPEAKER_MONTY)
    waitbuttonpress
    hidemoneybox
    closemessage
    releaseall
    end
}

script RPS_Dialogue_DenyFirstWagerBattle
{
    msgbox(format("I promise I'm playing it straight this time!"), MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    hidemoneybox
    closemessage
    releaseall
    end
}

script RPS_Script_MontyOppLeaveScreen
{
    applymovement(MONTYOPP, Movement_Monty_OppRunOutScreen)
    waitmovement(MONTYOPP)
    removeobject(MONTYOPP)
    return
}

script RPS_Dialogue_ContinueStreak
{
    showmoneybox(0,0)
    call(RPS_Script_GetWager)
    buffernumberstring(STR_VAR_2 ,VAR_0x8004)
    msgbox(format("You feeling brave? Hueh hueh hueh, let's go for double or nothing! ¥{STR_VAR_2} against Old Man Monty!"), MSGBOX_YESNO, SPEAKER_MONTY)
    if(var(VAR_RESULT)==YES)
    {
        hidemoneybox
        closemessage
        goto(RPS_Script_StartWagerBattle)
    }
    goto(RPS_Script_DenyWagerBattle)
}

script RPS_Script_DenyWagerBattle
{
    call(RPS_Script_DecrementStreak)
    msgbox(format("You're no fun!"), , SPEAKER_MONTY)
    showmoneybox(0,0)
    callnative(Quest_RPS_PayWager)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Come back if you change your mind."), , SPEAKER_MONTY)
    hidemoneybox
    closemessage
    call(RPS_Script_StreakReset)
    releaseall
    end
}

script RPS_Dialogue_CheckRewardSpace
{
    checkitemspace(ITEM_LOADED_DICE, 1)
        if (!var(VAR_RESULT))
        {
            msgbox(format("Got a lil' sumthin' for ya. Make sure  you've got space in your bag."), MSGBOX_DEFAULT, SPEAKER_PLAYER)
                closemessage
                releaseall
                end
        }
    goto(RPS_Dialogue_GiveItem)
}

script RPS_Dialogue_GiveItem
{
    giveitem(ITEM_LOADED_DICE)
        call (RPS_Script_IncrementStreak)
        msgbox(format("They're not exactly lucky, but they're a gift from me."), MSGBOX_DEFAULT, SPEAKER_MONTY)
        msgbox(format("I'm calling it quits on Mach-Focus-Hand. Gotta turn over a new leaf. Thanks for showing me the way, {PLAYER}."), MSGBOX_DEFAULT, SPEAKER_MONTY)
        msgbox(format("I didn't do anything!"), MSGBOX_DEFAULT, SPEAKER_PLAYER)
        closemessage
        release
        end
}

script RPS_Dialogue_CompletedStreak
{
    call(RPS_Script_IncrementStreak)
    msgbox(format("Okay okay, I'm tapped out, you got me."),,SPEAKER_MONTYOPP)
    showmoneybox(0,0)
    callnative(Quest_RPS_PayWager)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Maybe it's time I hung up my hustler's hat."),,SPEAKER_MONTYOPP)
    hidemoneybox
    closemessage
    call(RPS_Script_StreakReset)
    goto(RPS_Dialogue_CheckRewardSpace)
}

script RPS_Dialogue_PostStreak
{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREZENZUGYM,SUB_QUEST_1)
    if(var(VAR_RESULT))
    {
        msgbox(format("Hueh hueh, I think this studio gig really suits me!"),,SPEAKER_MONTY)
        msgbox(format("I'm still taking money from folks with heavy pockets... but now we're fueling this here production! I'm havin' a great time usin' my hustlin' powers for good."),,SPEAKER_MONTY)
        msgbox(format("Thanks for setting me up!"),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    }
    else
    {
        msgbox(format("Might be done with Mach-Focus-Hand... I think I need to find a bigger hustle, something more fufilling."),MSGBOX_AUTOCLOSE,SPEAKER_MONTYOPP)
    }
}

movement Monty_FacePlayer
{
    face_player
}

movement Monty_Opp_Left
{
    face_left
    step_end
}

movement Movement_Monty_OppRunOutScreen
{
    face_up
    walk_fast_up *2
    walk_fast_left *3
    walk_fast_up *4
    step_end
}

movement RPS_Movement_MontyFacePlayer
{
   face_player
   step_end
}
