const SPEAKER_MONTY = SPEAKER_BLAINE
const MONTY_CAPHE = 20

const SPEAKER_MONTYOPP = SPEAKER_DIMU
const MONTYOPP = 21

const SPEAKER_PLAYER = SPEAKER_ALICIA
const PLAYER = OBJ_EVENT_ID_PLAYER

const FLAG_HIDE_MONTY_OPP = FLAG_TEMP_5

script RPS_Script_ControlMontyOppVisibility
{
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_RPS)
    if(var(VAR_RESULT))
    {
        setflag(FLAG_HIDE_MONTY_OPP)
    }
}

script RPS_Script_SaveAndDeleteParty
{
    special(SavePlayerParty)
    callnative(ZeroPlayerPartyMons)
}

script RPS_Script_GiveRiggedMon
{
    call(RPS_Script_SaveAndDeleteParty)
    givemon(species=SPECIES_HITMONCHAN,level=20,item=0,abilityNum=2,shinyMode=SHINY_MODE_ALWAYS,hpIv=7,atkIv=9,defIv=1,spAtkIv=9,spDefIv=3,move1=MOVE_UPPER_HAND,move2=MOVE_MACH_PUNCH,move3=MOVE_FOCUS_PUNCH)
}

script RPS_Script_GiveFairMon
{
    call(RPS_Script_SaveAndDeleteParty)
    givemon(species=SPECIES_HITMONCHAN,level=20,item=ITEM_PUNCHING_GLOVE,abilityNum=1,shinyMode=SHINY_MODE_ALWAYS,hpIv=31,atkIv=31,defIv=31,spAtkIv=31,spDefIv=31,move1=MOVE_UPPER_HAND,move2=MOVE_MACH_PUNCH,move3=MOVE_FOCUS_PUNCH,nature=NATURE_JOLLY,hpEv=16,atkEv=236,speedEv=248)
}

script RPS_Script_ResetPlayer
{
    callnative(ZeroPlayerPartyMons)
    special(LoadPlayerParty)
}

script RPS_Script_StreakReset
{
    setvar(VAR_QUEST_RPS_STREAK_COUNT,0)
}

script RPS_Script_IncrementStreak
{
    addvar(VAR_QUEST_RPS_STREAK_COUNT,1)
}

script RPS_Script_DecrementStreak
{
    subvar(VAR_QUEST_RPS_STREAK_COUNT,1)
}

script RPS_Script_GetWager
{
    specialvar(VAR_0x8004, Quest_RPS_CalculateWager)
}

script RPS_Script_QuestInactive
{
    call_if_quest_inactive(QUEST_RPS,RPS_Dialogue_StartQuest)
    call_if_quest_active(QUEST_RPS,RPS_Script_InteractMontyQuestActive)
    call_if_quest_active(QUEST_RESTOREZENZUGYM,RestoreZenzuIsland_Dialogue_RecruitMonty)
    call_if_quest_complete(QUEST_RPS,RPS_Dialogue_PostQuestInteract)
}

script RPS_Dialogue_StartQuest
{
    msgbox (RPS_Text_InteractWithMonty,MSGBOX_DEFAULT,SPEAKER_MONTYOPP)
    msgbox (RPS_Text_Huehue,MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    call (RPS_Script_MontyOppLeaveScreen)
    waitmovement(MONTY_CAPHE)
    applymovement (MONTY_CAPHE, RPS_Movement_MontyFacePlayer)
    msgbox (RPS_Text_Heyo,MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox (RPS_Text_Who,MSGBOX_AUTOCLOSE,SPEAKER_PLAYER)
    msgbox (RPS_Text_YeahYou,MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox (RPS_Text_YouEverHear,MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox (RPS_Text_No,MSGBOX_AUTOCLOSE,SPEAKER_PLAYER)
    msgbox (RPS_Text_wellimMonty,MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    msgbox (RPS_Text_ORlearned,MSGBOX_AUTOCLOSE,SPEAKER_MONTY,TAIL_WHISPER)
    closemessage
    startquest(QUEST_RPS)
}

script RPS_Script_InteractMontyQuestActive
{
    lock
    showmoneybox(0,0)
    call(RPS_Script_GetWager)
    buffernumberstring(STR_VAR_2 ,VAR_0x8004)
    msgbox(format("Ready to give it a shot?"), MSGBOX_NPC, SPEAKER_MONTY)

    if(!var(VAR_RESULT))
    {
        goto(RPS_Dialogue_NotEnoughMoney)
    }

    msgbox(format("Let's put some stakes on it. ¥{STR_VAR_2}, winner takes the pot."),MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    msgbox(format("Hueh hueh hueh, I know a hotshot Trainer like you has ¥{STR_VAR_2} laying around, right?"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    msgbox(format("Here's the lowdown:"),MSGBOX_DEFAULT,SPEAKER_MONTY)

    hidemoneybox
    call(RPS_Script_GiveRiggedMon)
    buffermovename(STR_VAR_1,MOVE_UPPER_HAND)
    buffermovename(STR_VAR_2,MOVE_MACH_PUNCH)
    msgbox(format("{STR_VAR_1} trumps {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    buffermovename(STR_VAR_2,MOVE_FOCUS_PUNCH)
    buffermovename(STR_VAR_1,MOVE_MACH_PUNCH)
    msgbox(format("{STR_VAR_1} beats {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    buffermovename(STR_VAR_2,MOVE_UPPER_HAND)
    buffermovename(STR_VAR_1,MOVE_FOCUS_PUNCH)
    msgbox(format("{STR_VAR_1} wins out over {STR_VAR_2}."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    msgbox(format("If your Pokémon faints, you lose."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    bufferspeciesname(STR_VAR_1,SPECIES_HITMONCHAN)
    msgbox(format("Easy, yeah? Let's see how lucky you are. You can even use my lucky {STR_VAR_1}!"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    closemessage
    trainerbattle_continue_after_lose(TRAINER_MONTY, RPS_Text_MakeYoureOwnLuck)
    call(RPS_Script_JudgeBattleOutcome)
}

script RPS_Dialogue_NotEnoughMoney
{
    msgbox (RPS_Text_AhItso, , SPEAKER_MONTY)
    closemessage
    hidemoneybox
    releaseall
    end
}
script RPS_Script_JudgeBattleOutcome
{
    call(RPS_Script_ResetPlayer)
    specialvar(VAR_RESULT,GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(RPS_Dialogue_QuestComplete)
    }
    goto(RPS_Dialogue_LostQuestBattle)
}

script RPS_Dialogue_QuestComplete
{
    msgbox(format("My luck ran out. I'm shocked."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You weren't ever lucky to begin with!"), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("He? watch your mouth, what are you implying..."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    bufferitemname(STR_VAR_1,ITEM_PUNCHING_GLOVE)
    bufferstring(STR_VAR_1,format("Google Glass"))
    msgbox(format("My {STR_VAR_2} can see everything about your Pokémon. I can see that your Pokémon was stronger than mine and holding {STR_VAR_1}."), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("..."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You got me! Hueh hueh hueh, kids these days are too high tech to fool. Maybe I need to target a... less savvy demographic."), MSGBOX_DEFAULT SPEAKER_MONTY)
    msgbox(format("Wait,you tageted me specifically?"), MSGBOX_DEFAULT, SPEAKER_PLAYER)
    msgbox(format("Hueh hueh hueh, of course! I don't want to con region natives, but I have no problem ripping off techie transplants. You're so gullible!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("You know, when I started hustling, I was just trying to get my house back. But now? I'm just good at getting people to hand over their cash!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    msgbox(format("But for now, I'm not goin anywhere. {PLAYER}, anytime you want a rematch, I'll be here."), MSGBOX_DEFAULT, SPEAKER_MONTY)
    showmoneybox(0, 0)
    addmoney(QUEST_RPS_BASE_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Next time, I'll play fair, hueh hueh hueh."), , SPEAKER_MONTY)
    hidemoneybox
    closemessage
    completequest(QUEST_RPS)
    release
    end
}

script RPS_Dialogue_LostQuestBattle
{
    msgbox(format("Tough break kid."), , SPEAKER_MONTY )
    showmoneybox(0, 0)
    removemoney(QUEST_RPS_BASE_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Better luck next time!"), , SPEAKER_MONTY )
    hidemoneybox
    closemessage
}

script RPS_Dialogue_PostQuestInteract
{
    lock
    faceplayer
    goto(RPS_Script_JudgeStreak)
}

script RPS_Script_JudgeStreak
{
    switch(var(VAR_QUEST_RPS_STREAK_COUNT))
    {
        default: goto(RPS_Dialogue_ContinueStreak)
        case 0: goto(RPS_Dialogue_ZeroStreak)
        case QUEST_RPS_MAX_STREAK_LENGTH: goto(RPS_Dialogue_CompletedStreak)
        case QUEST_RPS_GIVE_REWARD: goto(RPS_Dialogue_CheckRewardSpace)
        case QUEST_RPS_REWARD_GIVEN: goto(RPS_Dialogue_PostStreak)
    }
}

script RPS_Dialogue_ZeroStreak
{
    msgbox(format("Hey {PLAYER}!"),MSGBOX_DEFAULT,SPEAKER_MONTY)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREZENZUGYM,SUB_QUEST_1)
    if(var(VAR_RESULT))
    {
        msgbox(format("This production is somethin' else! So many moving parts. But ol' Monty is putting his own spin on this here fundraising."),MSGBOX_DEFAULT,SPEAKER_MONTY)
    }

    msgbox(format("How lucky are you feeling today?"),MSGBOX_DEFAULT,SPEAKER_MONTY)
    callnative(Quest_RPS_CheckWager)
    if(var(VAR_RESULT))
    {
        goto(RPS_Dialogue_AskFirstStreak)
    }
    goto(RPS_Dialogue_CannotAffordWager)
}

script RPS_Dialogue_AskFirstStreak
{
    showmoneybox (0,0)
    buffernumberstring(STR_VAR_1,QUEST_RPS_WAGER)
    msgbox (RPS_Text_AnotherGo, MSGBOX_YESNO, SPEAKER_MONTY)
        if (var(VAR_RESULT) == YES)
        {
            hidemoneybox
            closemessage
            goto(RPS_Script_StartWagerBattle)
        }
    goto(RPS_Dialogue_DenyFirstWagerBattle)
}

script RPS_Dialogue_CannotAffordWager
{
    msgbox(format("Hueh hueh hueh, you look more broke than me! Come back when your luck has changed."), MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    closemessage
    releaseall
    end
}


script RPS_Script_JudgeWagerBattleOutcome
{
    call(RPS_Script_ResetPlayer)
    specialvar(VAR_RESULT,GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(RPS_Dialogue_WonWagerBattle)
    }
    goto(RPS_Dialogue_LostWagerBattle)
}

script RPS_Script_StartWagerBattle
{
    call (RPS_Script_GiveFairMon)
    msgbox(format("Hueh hueh hueh, all right! Mach! Focus! Hand!"), MSGBOX_DEFAULT, SPEAKER_MONTY)
    closemessage
    trainerbattle_continue_after_lose(TRAINER_MONTY, RPS_Text_WellWhat)
    call(RPS_Script_JudgeWagerBattleOutcome)
}

script RPS_Dialogue_WonWagerBattle
{
    call(RPS_Script_IncrementStreak)
    call(RPS_Script_JudgeStreak)
}

script RPS_Dialogue_LostWagerBattle
{
    call(RPS_Script_GetWager)
    call (RPS_Script_StreakReset)
    msgbox (RPS_Text_VictoryIsMine, , SPEAKER_MONTY)
    showmoneybox(0,0)
    removemoney(QUEST_RPS_WAGER)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Pleasure doing business with ya. Holler if you're up for another round."), ,SPEAKER_MONTY)
    waitbuttonpress
    hidemoneybox
    closemessage
    releaseall
    end
}

script RPS_Dialogue_DenyFirstWagerBattle
{
    msgbox (RPS_Text_IPromise, MSGBOX_AUTOCLOSE, SPEAKER_MONTY)
    hidemoneybox
    closemessage
    releaseall
    end
}

script RPS_Script_MontyOppLeaveScreen
{
    applymovement(MONTYOPP, Movement_Monty_OppRunOutScreen)
    waitmovement(MONTYOPP)
    removeobject(MONTYOPP)
    return
}

script RPS_Dialogue_ContinueStreak
{
    showmoneybox(0,0)
    call(RPS_Script_GetWager)
    buffernumberstring(STR_VAR_2 ,VAR_0x8004)
    msgbox (RPS_Text_FeelingBrave, MSGBOX_YESNO, SPEAKER_MONTY)
    if(var(VAR_RESULT)==YES)
    {
        hidemoneybox
        closemessage
        goto(RPS_Script_StartWagerBattle)
    }
    goto(RPS_Script_DenyWagerBattle)
}

script RPS_Script_DenyWagerBattle
{
    call(RPS_Script_DecrementStreak)
    msgbox (RPS_Text_NoFun, , SPEAKER_MONTY)
    showmoneybox(0,0)
    callnative(Quest_RPS_PayWager)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox (RPS_Text_ComeBack, , SPEAKER_MONTY)
    hidemoneybox
    closemessage
    call(RPS_Script_StreakReset)
    releaseall
    end
}

script RPS_Dialogue_CheckRewardSpace
{
    checkitemspace(ITEM_LOADED_DICE, 1)
        if (!var(VAR_RESULT))
        {
            msgbox (RPS_Text_GotALil, MSGBOX_DEFAULT, SPEAKER_PLAYER)
                closemessage
                releaseall
                end
        }
    goto(RPS_Dialogue_GiveItem)
}

script RPS_Dialogue_GiveItem
{
    giveitem(ITEM_LOADED_DICE)
        call (RPS_Script_IncrementStreak)
        msgbox (RPS_Text_NotExactlyLucky, MSGBOX_DEFAULT, SPEAKER_MONTY)
        msgbox (RPS_Text_ImCalling, MSGBOX_DEFAULT, SPEAKER_MONTY)
        msgbox (RPS_Text_IDidNot, MSGBOX_DEFAULT, SPEAKER_PLAYER)
        closemessage
        release
        end
}

script RPS_Dialogue_CompletedStreak
{
    call(RPS_Script_IncrementStreak)
    msgbox(format("Okay okay, I'm tapped out, you got me."),,SPEAKER_MONTYOPP)
    showmoneybox(0,0)
    callnative(Quest_RPS_PayWager)
    waitbuttonpress
    playse(SE_SHOP)
    updatemoneybox
    msgbox(format("Maybe it's time I hung up my hustler's hat."),,SPEAKER_MONTYOPP)
    hidemoneybox
    closemessage
    call(RPS_Script_StreakReset)
    goto(RPS_Dialogue_CheckRewardSpace)
}

script RPS_Dialogue_PostStreak
{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREZENZUGYM,SUB_QUEST_1)
    if(var(VAR_RESULT))
    {
        msgbox(format("Hueh hueh, I think this studio gig really suits me!"),,SPEAKER_MONTY)
        msgbox(format("I'm still taking money from folks with heavy pockets... but now we're fueling this here production! I'm havin' a great time usin' my hustlin' powers for good."),,SPEAKER_MONTY)
        msgbox(format("Thanks for setting me up!"),MSGBOX_AUTOCLOSE,SPEAKER_MONTY)
    }
    else
    {
        msgbox(format("Might be done with Mach-Focus-Hand... I think I need to find a bigger hustle, something more fufilling."),MSGBOX_AUTOCLOSE,SPEAKER_MONTYOPP)
    }
}

movement Monty_FacePlayer
{
    face_player
}

movement Monty_Opp_Left
{
    face_left
    step_end
}

movement Movement_Monty_OppRunOutScreen
{
    face_up
    walk_fast_up *2
    walk_fast_left *3
    walk_fast_up *4
    step_end
}

movement RPS_Movement_MontyFacePlayer
{
   face_player
   step_end
}

text RPS_Text_MontyOpp
{
    format("I lost to Monty again today…  I need to stop going but I can't resist the gamble!")
}
text RPS_Text_InteractWithMonty
{
    format("You beat me again Monty! I can't keep losing money like this. My wife will have my head!")
}
text RPS_Text_Huehue
{
    format("Hueh heuh heuh, don't worry, you'll taste victory soon enough MontyOpp. Just come back when your pockets are a little heavier.")
}
text RPS_Text_Heyo
{
    format("Heyo! You there! New around these parts?")
}
text RPS_Text_Who
{
    format("Who, me?")
}
text RPS_Text_YeahYou
{
    format("Hueh heuh heuh, yeah you!")
}
text RPS_Text_YouEverHear
{
    format("You ever hear of Monty's Mach-Focus-Hand?")
}
text RPS_Text_No
{
    format("No, can't say that I have!")
}
text RPS_Text_wellimMonty
{
    format("Hueh heuh heuh, well I'm Monty, and I made it up. of course you haven't.")
}
text RPS_Text_ORlearned
{
    format("Or learned it in Kanto...")
}

text RPS_Text_Readyto
{
    format("Ready to give it a shot?")
}

text RPS_Text_AhItso
{
    format("Ah, it's no fun without a little wager. Come back with ¥{STR_VAR_2}")
}
//Quest Active and Player Money >199
text RPS_Text_Letsput
{
    format("Let's put some stakes on it. ¥{STR_VAR_2}, winner takes the pot.")
}
text RPS_Text_hotshot
{
    format("Hueh hueh hueh, I know a hotshot Trainer like you has ¥{STR_VAR_2} laying around, right?")
}
text RPS_Text_lowdown
{
    format("Here's the lowdown:")
}
text RPS_Text_UpperHand
{
    format("{STR_VAR_1} trumps {STR_VAR_2}.")
}
text RPS_Text_Machbeats
{
    format("{STR_VAR_1} beats {STR_VAR_2}.")
}
text RPS_Text_FocusPunchWin
{
    format("{STR_VAR_1} wins out over {STR_VAR_2}.")
}
text RPS_Text_ifpokemonfaints
{
    format("If your Pokémon faints, you lose.")
}
text RPS_Text_EasyYeah
{
    format("Easy, yeah? Let's see how lucky you are. You can even use my lucky {STR_VAR_1}!")
}
// PLayer Wins
text RPS_Text_WellWhat
{
    format("Well what do you know? Luck has finally turned against me.")
}
// Player Loses
text RPS_Text_MakeYoureOwnLuck
{
    format("Hueh hueh hueh, sometimes you just have to make your own luck.")
}
//Post Battle Player Loss
text RPS_Text_ToughBreak
{
    format("Tough break kid.")
}

text RPS_Text_NextTime
{
    format("Better luck next time!")
}
//Post Battle Win
text RPS_Text_Luckranout
{
    format("My luck ran out. I'm shocked.")
}
text RPS_Text_YouWerent
{
    format("You weren't ever lucky to begin with!")
}
text RPS_Text_implying
{
    format("He? watch your mouth, what are you implying...")
}
text RPS_Text_MyGoogleGlass
{
    format("My {STR_VAR_2} can see everything about your Pokémon. I can see that your Pokémon was stronger than mine and holding {STR_VAR_1}.")
}
text RPS_Text_Suspensive
{
    format("...")
}
text RPS_Text_YouGotMe
{
    format("You got me! Hueh hueh hueh, kids these days are too high tech to fool. Maybe I need to target a... less savvy demographic.")
}
text RPS_Text_WaitYou
{
    format("Wait,you tageted me specifically?")
}
text RPS_Text_OfCurse
{
    format("Hueh hueh hueh, of course! I don't want to con region natives, but I have no problem ripping off techie transplants. You're so gullible!")
}
text RPS_Text_WhenIStarted
{
    format("You know, when I started hustling, I was just trying to get my house back. But now? I'm just good at getting people to hand over their cash!")
}
text RPS_Text_ButForNow
{
    format("But for now, I'm not goin anywhere. {PLAYER}, anytime you want a rematch, I'll be here.")
}
text RPS_Text_Next
{
     format("Next time, I'll play fair, hueh hueh hueh.")
}
// Interact Monty with 0 streak
text RPS_Text_HeyU
{
    format("Hey {PLAYER}!")
}

text RPS_Text_ThisProduction
{
    format("This production is somethin' else! So many moving parts. But ol' Monty is putting his own spin on this here fundraising.")
}

text RPS_Text_FeelingLucky
{
    format("How lucky are you feeling today?")
}
// Interact Monty  with 0 streak but Player has <$100
text RPS_Text_LookMoreBroke
{
    format("Hueh hueh hueh, you look more broke than me! Come back when your luck has changed.")
}
// Interact Monty whit 0 strak but player has >$99
text RPS_Text_AnotherGo
{
    format("Hoy about another go? We can make interesting with ¥{STR_VAR_1}.")
}
// Yes/No menu appears
// No
text RPS_Text_IPromise
{
    format("I promise I'm playing it straight this time!")
}
//yes (RPGGiveGairMon runs)
text RPS_Text_AllRight
{
    format("Hueh hueh hueh, all right! Mach! Focus! Hand!")
}
//Post Battle Player Loss
text RPS_Text_VictoryIsMine
{
    format("Hueh hueh hueh, victory is mine this time!")
}
// Money box appears /Player loses x money/ Money sounds plays Money box disappears.
text RPS_Text_Pleasure
{
    format("Pleasure doing business with ya. Holler if you're up for another round.")
}
//Post Battle Player Win/ ResetRPStreak runs/IncrementRPSStreak runs/CalcRPSWager runs
//Player Does Not Have Enough to Wager
text RPS_Text_LoveToDouble
{
    format("Hueh hueh hueh, I'd love to double down but it looks like that's all you've got. Come back when you're ready to take another shot at ol' Monty.")
}
//Money box appears/ Player gainsx/ Money sound Plays/ MoneyBox disappears
//Player Has Enough to wager
text RPS_Text_FeelingBrave
{
    format("You feeling brave? Hueh hueh hueh, let's go for double or nothing! ¥{STR_VAR_2} against Old Man Monty!")
}
// Yes / No menu appears
//Yes
text RPS_Text_Right
{
    format("All right! Mach! Focus! Hand!")
}
//No / ResetRPStreak runs / Money Box appears / Player gains XX / Money sounds plays / Money Box disappears.
text RPS_Text_NoFun
{
    format("You're no fun!")
}

text RPS_Text_ComeBack
{
    format("Come back if you change your mind.")
}
// Post Battle Player Win Streak is 10
text RPS_Text_Imtapped
{
    format("Okay okay, I'm tapped out, you got me.")
}
//Money box appears/ gains $XX /Money Sounds plays/ Money box disappears.
text RPS_Text_IHungUp
{
    format("Maybe it's time I hung up my hustler's hat.")
}
// RPSStreakIncrement runs.
//No Room for 1 Loaded Dice
text RPS_Text_GotALil
{
    format("Got a lil' sumthin' for ya. Make sure  you've got space in your bag.")
}
// Player Has Room For 1 Loadded Dice /Interact Monty When  RPSStreak is 11 /RPSStreakIncrement runs/ Player recives Loaded Dice
text RPS_Text_NotExactlyLucky
{
    format("They're not exactly lucky, but they're a gift from me.")
}
text RPS_Text_ImCalling
{
    format("I'm calling it quits on Mach-Focus-Hand. Gotta turn over a new leaf. Thanks for showing me the way, {PLAYER}.")
}
text RPS_Text_IDidNot
{
    format("I didn't do anything!")
}
// Interact Monty When RPSStreak is 12
text RPS_Text_MightBeDone
{
    format("Might be done with Mach-Focus-Hand... I think I need to find a bigger hustle, something more fufilling.")
}

text RPS_Text_YouKnowIThink
{
    format("Hueh hueh, I think this studio gig really suits me!")
}

text RPS_Text_TakingMoneyFrom
{
    format("I'm still taking money from folks with heavy pockets... but now we're fueling this here production! I'm havin' a great time usin' my hustlin' powers for good.")
}

text RPS_Text_ThanksFor
{
    format("Thanks for setting me up!")
}
