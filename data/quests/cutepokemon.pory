const MELISSA_PERLACIA_INSIDE = 1
const MELISSA_PERLACIA_OUTSIDE = 11
const FLAG_HIDE_MELISSA_PERLACIA_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_PERLACIA_OUTSIDE = FLAG_TEMP_5

const MELISSA_QIU_INSIDE = 2
const MELISSA_QIU_OUTSIDE = 9
const FLAG_HIDE_MELISSA_QIU_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_QIU_OUTSIDE = FLAG_TEMP_5

const MELISSA_CRESALTA_INSIDE = 1
const MELISSA_CRESALTA_OUTSIDE = 10
const FLAG_HIDE_MELISSA_CRESALTA_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_CRESALTA_OUTSIDE = FLAG_TEMP_2

const MELISSA_HALAI_OUTSIDE = 23
const FLAG_HIDE_MELISSA_HALAI_OUTSIDE = FLAG_TEMP_6

script CutePokemon_Script_DebugGiveMon
{
    lock
    callnative(DebugQuest_CutePokemon_GiveMon)
    release
    end
}

script CutePokemon_Script_HideMelissaPerlaciaInside
{
    setflag(FLAG_HIDE_MELISSA_PERLACIA_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaPerlaciaOutside
{
    setflag(FLAG_HIDE_MELISSA_PERLACIA_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaPerlaciaOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_PERLACIA)
    {
        return
    }
    call(CutePokemon_Script_HideMelissaPerlaciaOutside)
}

script CutePokemon_Script_ControlMelissaPerlaciaInsideVisibility
{

	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_STATE_NOT_STARTED)
	{
        setobjectxyperm(MELISSA_PERLACIA_INSIDE,4,4)
        return
	}
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Script_HideMelissaPerlaciaInside)
}

script CutePokemon_Dialogue_GreetingPerlacia
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_PERLACIA)
	lock
	addobject(MELISSA_PERLACIA_INSIDE)
	applymovement(MELISSA_PERLACIA_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_PERLACIA_INSIDE)
	msgbox(CutePokemon_Text_WelcomeShortStaffed,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

movement CutePokemon_WalkToPlayer_Movement
{
	walk_down
}

script CutePokemon_Script_PerlaciaSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_inactive(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_FirstInteraction)
	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_PerlaciaGuess)
}

script CutePokemon_Dialogue_FirstInteraction
{
	addvar(QUEST_CUTEPOKEMON,1)
	msgbox(CutePokemon_Text_AreYouFree,,SPEAKER_MELISSA)

		dynmultichoice(
				1,1,TRUE,2,0,NULL,
				CutePokemon_Text_YeahSure,
				CutePokemon_Text_YeahIGuess
				)

	startquest(QUEST_CUTEPOKEMON)
	goto(CutePokemon_Dialogue_PerlaciaGuess)
}

script CutePokemon_Script_SelectOptionCheckPokemon
{
	dynmultichoice(
			1,1,FALSE,2,1,NULL,
			CutePokemon_Text_OkayBye,
			CutePokemon_Text_IHaveYourPokemon,
			)

	if (var(VAR_RESULT) != YES)
	{
		closemessage
		release
		end
	}
	msgbox(CutePokemon_Text_ThatWasFast,MSGBOX_NPC,SPEAKER_MELISSA)

	special(ChoosePartyMon)
	waitstate

	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
	{
		msgbox(CutePokemon_Text_NoMonBobaInstead,,SPEAKER_MELISSA)
		closemessage
		release
		end
	}

	callnative(CheckCutePokemonAndSetHint)
}

script CutePokemon_Dialogue_PerlaciaGuess
{
	lock
	faceplayer
	callnative(BufferEiscueHeightPlusOne)

	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_EISCUE_HINT_0:
			msgbox(CutePokemon_Text_IveBeenLookingIce,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_1:
			msgbox(CutePokemon_Text_BringIceShort,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_2:
			msgbox(CutePokemon_Text_BringIceShortTrapped,,SPEAKER_MELISSA)
	}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_PerlaciaCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_PerlaciaCheck
{
	callnative(BufferEiscueHeightPlusOne)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_EISCUE_HINT_0:
			msgbox(CutePokemon_Text_ThisIsFine,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_1:
			msgbox(CutePokemon_Text_ShorterThanHeight,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_2:
			msgbox(CutePokemon_Text_ShorterAndHeight,,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_PerlaciaAsk)
	}
	return
}

script CutePokemon_Script_PerlaciaAsk
{
	askplayermon(CutePokemon_Text_ThatsItThankYou,CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_1)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_EISCUE_HINT_2)
	msgbox(CutePokemon_Text_IKnewYoudBe,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_SubquestComplete
{
	msgbox(CutePokemon_Text_IHaveToGetBack,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_CheckItemSpace
{
	msgbox(CutePokemon_Text_ThisTimeIHave,,SPEAKER_MELISSA)
		closemessage
		release
		end
}

script CutePokemon_Script_HideMelissaQiuInside
{
    setflag(FLAG_HIDE_MELISSA_QIU_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaQiuOutside
{
    setflag(FLAG_HIDE_MELISSA_QIU_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaQiuInsideVisibility
{
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_EISCUE_HINT_2)
	{
		setobjectxyperm(MELISSA_QIU_INSIDE,4,4)
        return
	}

    call_if_subquest_not_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Script_HideMelissaQiuInside)
    call_if_gt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_QIU,CutePokemon_Script_HideMelissaQiuInside)

}

script CutePokemon_Script_ControlMelissaQiuOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_QIU)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaQiuOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_QIU,CutePokemon_Script_HideMelissaQiuOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Script_HideMelissaQiuOutside)
}

script CutePokemon_Dialogue_GreetingQiu
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_QIU)
	lock
	addobject(MELISSA_QIU_INSIDE)
	applymovement(MELISSA_QIU_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_QIU_INSIDE)
	msgbox(CutePokemon_Text_WelcomeToShop,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_RecognizeGoOutsideQiu
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_QIU)
	lock
    bufferspeciesname(STR_VAR_1,SPECIES_EISCUE)
    buffermapname(STR_VAR_2,MAP_PERLACIA_CITY)
	msgbox(CutePokemon_Text_DidntYouHelpEiscue,MSGBOX_NPC,SPEAKER_MELISSA)
	closemessage
	applymovement(MELISSA_QIU_INSIDE,CutePokemon_WalkToExit_Movement)
	fadescreen(FADE_TO_BLACK)
	removeobject(MELISSA_QIU_INSIDE)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

movement CutePokemon_WalkToExit_Movement
{
	walk_up * 5
}

script CutePokemon_Script_QiuSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_ASK_BATTLE_QIU)
	{
		call(CutePokemon_Script_BattleQiu)
	}

	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_QiuGuess)
}

script CutePokemon_Script_BattleQiu
{
	msgbox(CutePokemon_Text_DoYouMind,,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_QIU,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? Iâ€™m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_QIU_COMPLETE)
	msgbox(CutePokemon_Text_DoYouHaveTime,,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuess
			)
	goto(CutePokemon_Dialogue_QiuGuess)
}

script CutePokemon_Dialogue_QiuGuess
{
	lock
	faceplayer

	callnative(BufferCorsolaWeightPlusOne)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_CORSOLA_HINT_0:
            msgbox(CutePokemon_Text_MyPartnerAskedPink,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_1:
            msgbox(CutePokemon_Text_PinkAndWeighs,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_2:
            msgbox(CutePokemon_Text_PinkWeighsPacks,,SPEAKER_MELISSA)
	}

        checkitemspace(ITEM_QUEST_CUTEPOKEMON_1,1)
		if (var(VAR_RESULT) == NO)
		{
			call(CutePokemon_Dialogue_CheckItemSpace)
		}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_QiuCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_QiuCheck
{
	callnative(BufferCorsolaWeightPlusOne)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_CORSOLA_HINT_0:
			msgbox(CutePokemon_Text_ThisIsntPink,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_1:
			msgbox(CutePokemon_Text_PinkWrongWeight,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_2:
			msgbox(CutePokemon_Text_PinkWeightPacks,,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_QiuAsk)
	}
	return
}

script CutePokemon_Script_QiuAsk
{
    bufferspeciesname(STR_VAR_2,SPECIES_CORSOLA)
	askplayermon(CutePokemon_Text_MyPartnerWillBeExcited,CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_2)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_CORSOLA_HINT_2)
	msgbox(CutePokemon_Text_IKnewYoudBe,,SPEAKER_MELISSA)
	msgbox(CutePokemon_Text_ThanksForAllHelp,,SPEAKER_MELISSA)
	closemessage
	cueobjectplayer(MELISSA_QIU_OUTSIDE)
	giveitem(ITEM_QUEST_CUTEPOKEMON_1,1)
	release
	end
}

script CutePokemon_Script_HideMelissaCresaltaInside
{
    setflag(FLAG_HIDE_MELISSA_CRESALTA_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaCresaltaOutside
{
    setflag(FLAG_HIDE_MELISSA_CRESALTA_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaCresaltaInsideVisibility
{
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_CORSOLA_HINT_2)
	{
		setobjectxyperm(MELISSA_CRESALTA_INSIDE,4,4)
	}

    call_if_subquest_not_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Script_HideMelissaCresaltaInside)
    call_if_gt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_CRESALTA,CutePokemon_Script_HideMelissaCresaltaInside)
}

script CutePokemon_Script_ControlMelissaCresaltaOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_CRESALTA)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaCresaltaOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA,CutePokemon_Script_HideMelissaCresaltaOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_3,CutePokemon_Script_HideMelissaCresaltaOutside)
}

script CutePokemon_Dialogue_GreetingCresalta
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_CRESALTA)
	lock
	addobject(MELISSA_CRESALTA_INSIDE)
	applymovement(MELISSA_CRESALTA_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_CRESALTA_INSIDE)
	msgbox(CutePokemon_Text_WelcomeToShop,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_RecognizeGoOutsideCresalta
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA)
	lock
    bufferspeciesname(STR_VAR_1,SPECIES_CORSOLA)
    buffermapname(STR_VAR_2,MAP_QIU_VILLAGE)
	msgbox(CutePokemon_Text_DidntYouHelpCorsola,,SPEAKER_MELISSA)
	closemessage
	applymovement(MELISSA_CRESALTA_INSIDE,CutePokemon_WalkToExit_Movement)
	fadescreen(FADE_TO_BLACK)
	removeobject(MELISSA_CRESALTA_INSIDE)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

script CutePokemon_Script_CresaltaSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA)
	{
		call(CutePokemon_Script_BattleCresalta)
	}

	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_3,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_CresaltaGuess)
}

script CutePokemon_Script_BattleCresalta
{
	msgbox(CutePokemon_Text_DoYouMind,,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_CRESALTA,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? Iâ€™m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_CRESALTA_COMPLETE)
	msgbox(CutePokemon_Text_DoYouHaveTime,,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuessElipsis
			)
	goto(CutePokemon_Dialogue_CresaltaGuess)
}

script CutePokemon_Dialogue_CresaltaGuess
{
	lock
	faceplayer
    bufferabilityname(STR_VAR_1,ABILITY_LEVITATE)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_DUSKULL_HINT_0:
            msgbox(CutePokemon_Text_ThisMonLevitate,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_1:
            msgbox(CutePokemon_Text_MonLevitateAndEvolve,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_2:
            msgbox(CutePokemon_Text_LevitateEvolveCloak,,SPEAKER_MELISSA)
	}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_CresaltaCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_CresaltaCheck
{
    bufferabilityname(STR_VAR_1,ABILITY_LEVITATE)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_DUSKULL_HINT_0:
			msgbox(CutePokemon_Text_ThisCantLevitate,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_1:
			msgbox(CutePokemon_Text_CuteLevitateNeedsEvolve,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_2:
			msgbox(CutePokemon_Text_CuteLeviitteEvolveNeedsCloak,,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_CresaltaAsk)
	}
	return
}

script CutePokemon_Script_CresaltaAsk
{
	askplayermon(CutePokemon_Text_ThanksForFinding,CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_3)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_HALAI)
	msgbox(CutePokemon_Text_ThanksForBeingAHomie,,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Script_HideMelissaHalaiOutside
{
    setflag(FLAG_HIDE_MELISSA_HALAI_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaHalaiOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_HALAI)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaHalaiOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_DUSKULL_HINT_2,CutePokemon_Script_HideMelissaHalaiOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_4,CutePokemon_Script_HideMelissaHalaiOutside)
}

script CutePokemon_Script_HalaiSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
    goto_if_eq(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_HALAI,CutePokemon_Script_BattleHalai)
	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_4,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_HalaiGuess)
}

script CutePokemon_Script_BattleHalai
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_HALAI)
	msgbox(CutePokemon_Text_WeHaveToBattle,,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_HALAI,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? Iâ€™m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_HALAI_COMPLETE)
	msgbox(CutePokemon_Text_GoodWorkPlayer,,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuessElipsis
			)
	goto(CutePokemon_Dialogue_HalaiGuess)
}

script CutePokemon_Dialogue_HalaiGuess
{
	lock
	faceplayer

    buffermovename(STR_VAR_1,MOVE_ACID_SPRAY)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_0:
            msgbox(CutePokemon_Text_CanBeBredDragon,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_1:
            msgbox(CutePokemon_Text_CanBeBredDragonAcidSpray,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_2:
            msgbox(CutePokemon_Text_CanBeBredDragonAcidSprayHasMohawk,,SPEAKER_MELISSA)
	}

	checkitemspace(ITEM_QUEST_CUTEPOKEMON_2,1)
		if (var(VAR_RESULT) == NO)
		{
			call(CutePokemon_Dialogue_CheckItemSpace)
		}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_HalaiCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_HalaiCheck
{
    buffermovename(STR_VAR_1,MOVE_ACID_SPRAY)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_0:
			msgbox(CutePokemon_Text_DidYouEvenRead,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_1:
			msgbox(CutePokemon_Text_CuteDragonsNeedsAcid,,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_2:
			msgbox(CutePokemon_Text_CuteDragonAcidSprayNeedsMOhawk,,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_HalaiAsk)
	}
	return
}

script CutePokemon_Script_HalaiAsk
{
	special(BufferMonNickname)
	askplayermon(CutePokemon_Text_ThatsItThankYouForFinding,CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)

	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_4)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_CORRECT)
	msgbox(CutePokemon_Text_ThanksForBeingAealOneIMadeThis,,SPEAKER_MELISSA)
	closemessage
	cueobjectplayer(MELISSA_QIU_OUTSIDE)
	giveitem(ITEM_QUEST_CUTEPOKEMON_2,1)
	completequest(QUEST_CUTEPOKEMON)
    setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	release
	end
}

script CutePokemon_Dialogue_Rematch
{
	if(var(VAR_CUTE_POKEMON_GUESS) != VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	{
		msgbox(CutePokemon_Text_DoYouHaveTimeToBattle,,SPEAKER_MELISSA)
		closemessage
		trainerbattle_no_intro(TRAINER_MELISSA_REMATCH,CutePokemon_Text_INeedToTrain)
		//PSF TODO need battle slide for every loss "Honestly? Iâ€™m surprised."
		setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	}

	msgbox(CutePokemon_Text_PostBattleBoba,,SPEAKER_MELISSA)
	closemessage
	release
	end
}
text CutePokemon_Text_WelcomeShortStaffed
{
	format("Welcome to The Boba Shop! Sorry, weâ€™re short staffed today!")
}
text CutePokemon_Text_AreYouFree
{
	format("Hey, are you free? Iâ€™m about to run to my next shift across Resido. Do you have time to help me find a cute PokÃ©mon? I keep picking up shifts, because everybodyâ€™s burnt out.")
}
text CutePokemon_Text_IveBeenLookingIce
{
	format("Iâ€™ve been looking for this cute PokÃ©mon. Itâ€™s an Ice-type. Can you catch one for me? But only if you have time. You can say no!")
}
text CutePokemon_Text_BringIceShort
{
	format("Bring the cute Ice-type PokÃ©mon thatâ€™s shorter than {STR_VAR_1}. Iâ€™ve gotta go back to work!")
}
text CutePokemon_Text_BringIceShortTrapped
{
	format("Bring the cute Ice-type PokÃ©mon thatâ€™s shorter than {STR_VAR_1} and is trapped in its own head. Iâ€™ve gotta go back to work!")
}
text CutePokemon_Text_ThatWasFast
{
	format("That was fast! Show me!")
}
text CutePokemon_Text_NoMonBobaInstead
{
	format("Oh, you donâ€™t have a PokÃ©mon? Do you want to order a boba drink instead?")
}
text CutePokemon_Text_ThisIsFine
{
	format("This is fineâ€¦ but it's not an Ice-type like I asked for.")
}
text CutePokemon_Text_ShorterThanHeight
{
	format("Oh! This is a cute PokÃ©mon! But the one I have in mind is shorter than {STR_VAR_1}.")
}
text CutePokemon_Text_ShorterAndHeight
{
	format("Oh! This is a cute PokÃ©mon! But the one I have in mind feels trapped in its own head.")
}
text CutePokemon_Text_ThatsItThankYou
{
	format("Thatâ€™s it! Thank you for finding that PokÃ©mon! You saved me hella time. Are you sure I can have your {STR_VAR_1}?")
}
text CutePokemon_Text_CanYouBringOne
{
	format("Awwâ€¦ Well, can you bring me one I can keep?")
}
text CutePokemon_Text_AwwThatsSweet
{
	format("Aww, thatâ€™s sweet of you, but you donâ€™t have any other PokÃ©mon!")
}
text CutePokemon_Text_IKnewYoudBe
{
	format("Hell yeah! I knew youâ€™d be a real one!")
}
text CutePokemon_Text_IHaveToGetBack
{
	format("I have to get back to work, do you want some boba?")
}
text CutePokemon_Text_ThisTimeIHave
{
	format("This time, I have something for you in exchange for the right PokÃ©mon. Come back when you've made room!")
}
text CutePokemon_Text_WelcomeToShop
{
	format("Welcome to The Boba Shop!")
}
text CutePokemon_Text_DidntYouHelpEiscue
{
	format("Wait, didnâ€™t you help me find {STR_VAR_1} in {STR_VAR_2}? Iâ€™ve trained with him a bit since you gave him to me. Iâ€™m about to go on my break. Letâ€™s battle outside!")
}
text CutePokemon_Text_DoYouMind
{
	format("Do you mind if I smoke?")
}
text CutePokemon_Text_INeedToTrain
{
	format("gg. I need to train these guys more!")
}
text CutePokemon_Text_DoYouHaveTime
{
	format("Good work {PLAYER}. Do you, by chance, have time to help me find another cute PokÃ©mon?")
}
text CutePokemon_Text_MyPartnerAskedPink
{
	format("My partner asked me to find this pink PokÃ©mon for him, but I have too many shifts to find the time to catch one.")
}
text CutePokemon_Text_PinkAndWeighs
{
	format("Hey, did you ever get the chance to get that pink PokÃ©mon that weighs less than {STR_VAR_1}?")
}
text CutePokemon_Text_PinkWeighsPacks
{
	format("Hey, did you ever get the chance to get that pink PokÃ©mon that weighs less than {STR_VAR_1} and travels in packs?")
}
text CutePokemon_Text_DontHaveOrderInside
{
	format("Oh, you donâ€™t have a PokÃ©mon? Do you want to order a boba drink inside instead?")
}
text CutePokemon_Text_ThisIsntPink
{
	format("This PokÃ©mon isnâ€™t pink.")
}
text CutePokemon_Text_PinkWrongWeight
{
	format("While this PokÃ©mon is cute and pink, the one I had in mind weighs less than {STR_VAR_1}.")
}
text CutePokemon_Text_PinkWeightPacks
{
	format("Oh! This is a cute PokÃ©mon! But the one I have in mind is for the girls, and travels in packs.")
}
text CutePokemon_Text_MyPartnerWillBeExcited
{
	format("Thatâ€™s it! My partner will be excited for {STR_VAR_2}, and you saved me hella time, thanks! Are you sure I can have {STR_VAR_1}?")
}
text CutePokemon_Text_ThanksForAllHelp
{
	format("Thanks for all your help so far. Youâ€™re really saving me as I waste away at these jobs. Here's something for your help!")
}
text CutePokemon_Text_DidntYouHelpCorsola
{
	format("Wait, didnâ€™t you help me find {STR_VAR_1} in {STR_VAR_2}? Iâ€™ve trained with her a bit since you gave her to me. Iâ€™m about to go on my break. Letâ€™s battle outside!")
}
text CutePokemon_Text_ThisMonLevitate
{
    format("I'm looking for a cute PokÃ©mon with the Ability {STR_VAR_1}.")
}
text CutePokemon_Text_MonLevitateAndEvolve
{
	format("Did you ever find that cute PokÃ©mon with the Ability {STR_VAR_1} and can evolve?")
}
text CutePokemon_Text_LevitateEvolveCloak
{
	format("Did you ever find that cute PokÃ©mon with the Ability {STR_VAR_1}, can evolve and wears a cloak?")
}
text CutePokemon_Text_ThisCantLevitate
{
	format("Dude, this PokÃ©mon doesn't have {STR_VAR_1}.")
}
text CutePokemon_Text_CuteLevitateNeedsEvolve
{
	format("While this PokÃ©mon is cute and has {STR_VAR_1}, the one I had in mind can evolve.")
}
text CutePokemon_Text_CuteLeviitteEvolveNeedsCloak
{
	format("Oh, I forgot that this PokÃ©mon has {STR_VAR_1} and evolve, but the one I had in mind is like, wearing a cloak.")
}
text CutePokemon_Text_ThanksForFinding
{
	format("Thatâ€™s it! Thank you for finding that PokÃ©mon! You saved me hella time. Isnâ€™t it cute AF? Are you sure I can have {STR_VAR_1}?")
}
text CutePokemon_Text_ThanksForBeingAHomie
{
	format("Hell yeah! Thanks for being a homie.")
}
text CutePokemon_Text_WeHaveToBattle
{
	format("Hey {PLAYER}. We have to battle because the game designer said so.")
}
text CutePokemon_Text_GoodWorkPlayer
{
	format("Good work {PLAYER}. Do you, by chance, have time to help me find one last cute PokÃ©mon?")
}
text CutePokemon_Text_CanBeBredDragon
{
	format("I'm looking for a cute PokÃ©mon that can be bred with dragons.")
}
text CutePokemon_Text_CanBeBredDragonAcidSpray
{
	format("Hey, {PLAYER}, did you come across a cute PokÃ©mon that can breed with dragons and learn the move {STR_VAR_1}?")
}
text CutePokemon_Text_CanBeBredDragonAcidSprayHasMohawk
{
	format("Hey, {PLAYER}, did you come across a cute PokÃ©mon that can breed with dragons and learn the move {STR_VAR_1} that basically has a mohawk?")
}
text CutePokemon_Text_DidYouEvenRead
{
	format("Dude, did you even read the PokÃ©dex entry?")
}
text CutePokemon_Text_CuteDragonsNeedsAcid
{
	format("While this PokÃ©mon is cute and can breed with dragons, the PokÃ©mon I had in mind can learn the move {STR_VAR_1}.")
}
text CutePokemon_Text_CuteDragonAcidSprayNeedsMOhawk
{
	format("Oh, I forgot about this one. The cute PokÃ©mon I had in mind has a mohawk. Or looks like it has a mohawkâ€¦? I donâ€™t know.")
}
text CutePokemon_Text_ThatsItThankYouForFinding
{
	format("Thatâ€™s it! Thank you for finding that PokÃ©mon! You saved me hella time. Are you sure I can have {STR_VAR_1}?")
}
text CutePokemon_Text_ThanksForBeingAealOneIMadeThis
{
	format("Hell yeah! Thanks for being a real one and helping me find all these PokÃ©mon. Take this, I made this myself on break!")
}
text CutePokemon_Text_DoYouHaveTimeToBattle
{
	format("Hey {PLAYER}. Thanks again for helping me catch those cute PokÃ©mon. Do you have time to battle?")
}
text CutePokemon_Text_PostBattleBoba
{
	format("Wanna grab some post battle boba?")
}
text CutePokemon_Text_OkayBye
{
	format("Okay, bye.")
}

text CutePokemon_Text_YeahSure
{
	format("Yeah, sure!")
}

text CutePokemon_Text_YeahIGuess
{
	format("Yeah, I guess!")
}

text CutePokemon_Text_IHaveYourPokemon
{
	format("I have your PokÃ©mon.")
}

text CutePokemon_Text_YeahIGuessElipsis
{
	format("Yeah, I guessâ€¦")
}
