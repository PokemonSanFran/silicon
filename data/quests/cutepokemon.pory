const MELISSA_PERLACIA_INSIDE = 1
const MELISSA_PERLACIA_OUTSIDE = 11
const FLAG_HIDE_MELISSA_PERLACIA_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_PERLACIA_OUTSIDE = FLAG_TEMP_5

const MELISSA_QIU_INSIDE = 2
const MELISSA_QIU_OUTSIDE = 9
const FLAG_HIDE_MELISSA_QIU_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_QIU_OUTSIDE = FLAG_TEMP_5

const MELISSA_CRESALTA_INSIDE = 1
const MELISSA_CRESALTA_OUTSIDE = 10
const FLAG_HIDE_MELISSA_CRESALTA_INSIDE = FLAG_TEMP_1
const FLAG_HIDE_MELISSA_CRESALTA_OUTSIDE = FLAG_TEMP_2

const MELISSA_HALAI_OUTSIDE = 23
const FLAG_HIDE_MELISSA_HALAI_OUTSIDE = FLAG_TEMP_6

script CutePokemon_Script_DebugGiveMon
{
    lock
    callnative(DebugQuest_CutePokemon_GiveMon)
    release
    end
}

script CutePokemon_Script_HideMelissaPerlaciaInside
{
    setflag(FLAG_HIDE_MELISSA_PERLACIA_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaPerlaciaOutside
{
    setflag(FLAG_HIDE_MELISSA_PERLACIA_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaPerlaciaOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_PERLACIA)
    {
        return
    }
    call(CutePokemon_Script_HideMelissaPerlaciaOutside)
}

script CutePokemon_Script_ControlMelissaPerlaciaInsideVisibility
{

	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_STATE_NOT_STARTED)
	{
        setobjectxyperm(MELISSA_PERLACIA_INSIDE,4,4)
        return
	}
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Script_HideMelissaPerlaciaInside)
}

script CutePokemon_Dialogue_GreetingPerlacia
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_PERLACIA)
	lock
	addobject(MELISSA_PERLACIA_INSIDE)
	applymovement(MELISSA_PERLACIA_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_PERLACIA_INSIDE)
	msgbox(format("Welcome to The Boba Shop! Sorry, we’re short staffed today!"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

movement CutePokemon_WalkToPlayer_Movement
{
	walk_down
}

script CutePokemon_Script_PerlaciaSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_inactive(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_FirstInteraction)
	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_PerlaciaGuess)
}

script CutePokemon_Dialogue_FirstInteraction
{
	addvar(QUEST_CUTEPOKEMON,1)
	msgbox(format("Hey, are you free? I’m about to run to my next shift across Resido. Do you have time to help me find a cute Pokémon? I keep picking up shifts, because everybody’s burnt out."),,SPEAKER_MELISSA)

		dynmultichoice(
				1,1,TRUE,2,0,NULL,
				CutePokemon_Text_YeahSure,
				CutePokemon_Text_YeahIGuess
				)

	startquest(QUEST_CUTEPOKEMON)
	goto(CutePokemon_Dialogue_PerlaciaGuess)
}

script CutePokemon_Script_SelectOptionCheckPokemon
{
	dynmultichoice(
			1,1,FALSE,2,1,NULL,
			CutePokemon_Text_OkayBye,
			CutePokemon_Text_IHaveYourPokemon,
			)

	if (var(VAR_RESULT) != YES)
	{
		closemessage
		release
		end
	}
	msgbox(format("That was fast! Show me!"),MSGBOX_NPC,SPEAKER_MELISSA)

	special(ChoosePartyMon)
	waitstate

	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
	{
		msgbox(format("Oh, you don’t have a Pokémon? Do you want to order a boba drink instead?"),,SPEAKER_MELISSA)
		closemessage
		release
		end
	}

	callnative(CheckCutePokemonAndSetHint)
}

script CutePokemon_Dialogue_PerlaciaGuess
{
	lock
	faceplayer
	callnative(BufferEiscueHeightPlusOne)

	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_EISCUE_HINT_0:
			msgbox(format("I’ve been looking for this cute Pokémon. It’s an Ice-type. Can you catch one for me? But only if you have time. You can say no!"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_1:
			msgbox(format("Bring the cute Ice-type Pokémon that’s shorter than {STR_VAR_1}. I’ve gotta go back to work!"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_2:
			msgbox(format("Bring the cute Ice-type Pokémon that’s shorter than {STR_VAR_1} and is trapped in its own head. I’ve gotta go back to work!"),,SPEAKER_MELISSA)
	}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_PerlaciaCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_PerlaciaCheck
{
	callnative(BufferEiscueHeightPlusOne)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_EISCUE_HINT_0:
			msgbox(format("This is fine… but it's not an Ice-type like I asked for."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_1:
			msgbox(format("Oh! This is a cute Pokémon! But the one I have in mind is shorter than {STR_VAR_1}."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_EISCUE_HINT_2:
			msgbox(format("Oh! This is a cute Pokémon! But the one I have in mind feels trapped in its own head."),,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_PerlaciaAsk)
	}
	return
}

script CutePokemon_Script_PerlaciaAsk
{
	askplayermon(format("That’s it! Thank you for finding that Pokémon! You saved me hella time. Are you sure I can have your {STR_VAR_1}?"),CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_1)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_EISCUE_HINT_2)
	msgbox(format("Hell yeah! I knew you’d be a real one!"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_SubquestComplete
{
	msgbox(format("I have to get back to work, do you want some boba?"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_CheckItemSpace
{
	msgbox(format("This time, I have something for you in exchange for the right Pokémon. Come back when you've made room!"),,SPEAKER_MELISSA)
		closemessage
		release
		end
}

script CutePokemon_Script_HideMelissaQiuInside
{
    setflag(FLAG_HIDE_MELISSA_QIU_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaQiuOutside
{
    setflag(FLAG_HIDE_MELISSA_QIU_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaQiuInsideVisibility
{
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_EISCUE_HINT_2)
	{
		setobjectxyperm(MELISSA_QIU_INSIDE,4,4)
        return
	}

    call_if_subquest_not_complete(QUEST_CUTEPOKEMON,SUB_QUEST_1,CutePokemon_Script_HideMelissaQiuInside)
    call_if_gt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_QIU,CutePokemon_Script_HideMelissaQiuInside)

}

script CutePokemon_Script_ControlMelissaQiuOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_QIU)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaQiuOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_QIU,CutePokemon_Script_HideMelissaQiuOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Script_HideMelissaQiuOutside)
}

script CutePokemon_Dialogue_GreetingQiu
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_QIU)
	lock
	addobject(MELISSA_QIU_INSIDE)
	applymovement(MELISSA_QIU_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_QIU_INSIDE)
	msgbox(format("Welcome to The Boba Shop!"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_RecognizeGoOutsideQiu
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_QIU)
	lock
    bufferspeciesname(STR_VAR_1,SPECIES_EISCUE)
    buffermapname(STR_VAR_2,MAP_PERLACIA_CITY)
	msgbox(format("Wait, didn’t you help me find {STR_VAR_1} in {STR_VAR_2}? I’ve trained with him a bit since you gave him to me. I’m about to go on my break. Let’s battle outside!"),MSGBOX_NPC,SPEAKER_MELISSA)
	closemessage
	applymovement(MELISSA_QIU_INSIDE,CutePokemon_WalkToExit_Movement)
	fadescreen(FADE_TO_BLACK)
	removeobject(MELISSA_QIU_INSIDE)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

movement CutePokemon_WalkToExit_Movement
{
	walk_up * 5
}

script CutePokemon_Script_QiuSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_ASK_BATTLE_QIU)
	{
		call(CutePokemon_Script_BattleQiu)
	}

	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_QiuGuess)
}

script CutePokemon_Script_BattleQiu
{
	msgbox(format("Do you mind if I smoke?"),,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_QIU,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? I’m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_QIU_COMPLETE)
	msgbox(format("Good work {PLAYER}. Do you, by chance, have time to help me find another cute Pokémon?"),,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuess
			)
	goto(CutePokemon_Dialogue_QiuGuess)
}

script CutePokemon_Dialogue_QiuGuess
{
	lock
	faceplayer

	callnative(BufferCorsolaWeightPlusOne)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_CORSOLA_HINT_0:
            msgbox(format("My partner asked me to find this pink Pokémon for him, but I have too many shifts to find the time to catch one."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_1:
            msgbox(format("Hey, did you ever get the chance to get that pink Pokémon that weighs less than {STR_VAR_1}?"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_2:
            msgbox(format("Hey, did you ever get the chance to get that pink Pokémon that weighs less than {STR_VAR_1} and travels in packs?"),,SPEAKER_MELISSA)
	}

        checkitemspace(ITEM_QUEST_CUTEPOKEMON_1,1)
		if (var(VAR_RESULT) == NO)
		{
			call(CutePokemon_Dialogue_CheckItemSpace)
		}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_QiuCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_QiuCheck
{
	callnative(BufferCorsolaWeightPlusOne)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_CORSOLA_HINT_0:
			msgbox(format("This Pokémon isn’t pink."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_1:
			msgbox(format("While this Pokémon is cute and pink, the one I had in mind weighs less than {STR_VAR_1}."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_CORSOLA_HINT_2:
			msgbox(format("Oh! This is a cute Pokémon! But the one I have in mind is for the girls, and travels in packs."),,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_QiuAsk)
	}
	return
}

script CutePokemon_Script_QiuAsk
{
    bufferspeciesname(STR_VAR_2,SPECIES_CORSOLA)
	askplayermon(format("That’s it! My partner will be excited for {STR_VAR_2}, and you saved me hella time, thanks! Are you sure I can have {STR_VAR_1}?"),CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_2)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_CORSOLA_HINT_2)
	msgbox(format("Hell yeah! I knew you’d be a real one!"),,SPEAKER_MELISSA)
	msgbox(format("Thanks for all your help so far. You’re really saving me as I waste away at these jobs. Here's something for your help!"),,SPEAKER_MELISSA)
	closemessage
	cueobjectplayer(MELISSA_QIU_OUTSIDE)
	giveitem(ITEM_QUEST_CUTEPOKEMON_1,1)
	release
	end
}

script CutePokemon_Script_HideMelissaCresaltaInside
{
    setflag(FLAG_HIDE_MELISSA_CRESALTA_INSIDE)
    return
}

script CutePokemon_Script_HideMelissaCresaltaOutside
{
    setflag(FLAG_HIDE_MELISSA_CRESALTA_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaCresaltaInsideVisibility
{
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_CORSOLA_HINT_2)
	{
		setobjectxyperm(MELISSA_CRESALTA_INSIDE,4,4)
	}

    call_if_subquest_not_complete(QUEST_CUTEPOKEMON,SUB_QUEST_2,CutePokemon_Script_HideMelissaCresaltaInside)
    call_if_gt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_CRESALTA,CutePokemon_Script_HideMelissaCresaltaInside)
}

script CutePokemon_Script_ControlMelissaCresaltaOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_CRESALTA)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaCresaltaOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA,CutePokemon_Script_HideMelissaCresaltaOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_3,CutePokemon_Script_HideMelissaCresaltaOutside)
}

script CutePokemon_Dialogue_GreetingCresalta
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_CRESALTA)
	lock
	addobject(MELISSA_CRESALTA_INSIDE)
	applymovement(MELISSA_CRESALTA_INSIDE,CutePokemon_WalkToPlayer_Movement)
	waitmovement(MELISSA_CRESALTA_INSIDE)
	msgbox(format("Welcome to The Boba Shop!"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Dialogue_RecognizeGoOutsideCresalta
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA)
	lock
    bufferspeciesname(STR_VAR_1,SPECIES_CORSOLA)
    buffermapname(STR_VAR_2,MAP_QIU_VILLAGE)
	msgbox(format("Wait, didn’t you help me find {STR_VAR_1} in {STR_VAR_2}? I’ve trained with her a bit since you gave her to me. I’m about to go on my break. Let’s battle outside!"),,SPEAKER_MELISSA)
	closemessage
	applymovement(MELISSA_CRESALTA_INSIDE,CutePokemon_WalkToExit_Movement)
	fadescreen(FADE_TO_BLACK)
	removeobject(MELISSA_CRESALTA_INSIDE)
	fadescreen(FADE_FROM_BLACK)
	release
	end
}

script CutePokemon_Script_CresaltaSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
	if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_ASK_BATTLE_CRESALTA)
	{
		call(CutePokemon_Script_BattleCresalta)
	}

	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_3,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_CresaltaGuess)
}

script CutePokemon_Script_BattleCresalta
{
	msgbox(format("Do you mind if I smoke?"),,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_CRESALTA,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? I’m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_CRESALTA_COMPLETE)
	msgbox(format("Good work {PLAYER}. Do you, by chance, have time to help me find another cute Pokémon?"),,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuessElipsis
			)
	goto(CutePokemon_Dialogue_CresaltaGuess)
}

script CutePokemon_Dialogue_CresaltaGuess
{
	lock
	faceplayer
    bufferabilityname(STR_VAR_1,ABILITY_LEVITATE)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_DUSKULL_HINT_0:
            msgbox(format("I'm looking for a cute Pokémon with the Ability {STR_VAR_1}."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_1:
            msgbox(format("Did you ever find that cute Pokémon with the Ability {STR_VAR_1} and can evolve?"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_2:
            msgbox(format("Did you ever find that cute Pokémon with the Ability {STR_VAR_1}, can evolve and wears a cloak?"),,SPEAKER_MELISSA)
	}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_CresaltaCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_CresaltaCheck
{
    bufferabilityname(STR_VAR_1,ABILITY_LEVITATE)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_DUSKULL_HINT_0:
			msgbox(format("Dude, this Pokémon doesn't have {STR_VAR_1}."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_1:
			msgbox(format("While this Pokémon is cute and has {STR_VAR_1}, the one I had in mind can evolve."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_DUSKULL_HINT_2:
			msgbox(format("Oh, I forgot that this Pokémon has {STR_VAR_1} and evolve, but the one I had in mind is like, wearing a cloak."),,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_CresaltaAsk)
	}
	return
}

script CutePokemon_Script_CresaltaAsk
{
	askplayermon(format("That’s it! Thank you for finding that Pokémon! You saved me hella time. Isn’t it cute AF? Are you sure I can have {STR_VAR_1}?"),CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)
	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_3)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_HALAI)
	msgbox(format("Hell yeah! Thanks for being a homie."),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

script CutePokemon_Script_HideMelissaHalaiOutside
{
    setflag(FLAG_HIDE_MELISSA_HALAI_OUTSIDE)
    return
}

script CutePokemon_Script_ControlMelissaHalaiOutsideVisibility
{
    if (var(VAR_CUTE_POKEMON_GUESS) == VAR_CUTE_POKEMON_HALAI)
    {
        return
    }

    call_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Script_HideMelissaHalaiOutside)
    call_if_lt(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_DUSKULL_HINT_2,CutePokemon_Script_HideMelissaHalaiOutside)
    call_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_4,CutePokemon_Script_HideMelissaHalaiOutside)
}

script CutePokemon_Script_HalaiSelectDialogue
{
	lock
	faceplayer
	goto_if_quest_complete(QUEST_CUTEPOKEMON,CutePokemon_Dialogue_Rematch)
    goto_if_eq(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_GREETING_HALAI,CutePokemon_Script_BattleHalai)
	goto_if_subquest_complete(QUEST_CUTEPOKEMON,SUB_QUEST_4,CutePokemon_Dialogue_SubquestComplete)
	goto(CutePokemon_Dialogue_HalaiGuess)
}

script CutePokemon_Script_BattleHalai
{
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_ASK_BATTLE_HALAI)
	msgbox(format("Hey {PLAYER}. We have to battle because the game designer said so."),,SPEAKER_MELISSA)
	closemessage
	trainerbattle_no_intro(TRAINER_MELISSA_HALAI,CutePokemon_Text_INeedToTrain)
	//PSF TODO need battle slide for every loss "Honestly? I’m surprised."
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_BATTLE_HALAI_COMPLETE)
	msgbox(format("Good work {PLAYER}. Do you, by chance, have time to help me find one last cute Pokémon?"),,SPEAKER_MELISSA)

	dynmultichoice(
			1,1,TRUE,2,0,NULL,
			CutePokemon_Text_YeahSure,
			CutePokemon_Text_YeahIGuessElipsis
			)
	goto(CutePokemon_Dialogue_HalaiGuess)
}

script CutePokemon_Dialogue_HalaiGuess
{
	lock
	faceplayer

    buffermovename(STR_VAR_1,MOVE_ACID_SPRAY)
	switch(var(VAR_CUTE_POKEMON_GUESS))
	{
	default:
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_0:
            msgbox(format("I'm looking for a cute Pokémon that can be bred with dragons."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_1:
            msgbox(format("Hey, {PLAYER}, did you come across a cute Pokémon that can breed with dragons and learn the move {STR_VAR_1}?"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_2:
            msgbox(format("Hey, {PLAYER}, did you come across a cute Pokémon that can breed with dragons and learn the move {STR_VAR_1} that basically has a mohawk?"),,SPEAKER_MELISSA)
	}

	checkitemspace(ITEM_QUEST_CUTEPOKEMON_2,1)
		if (var(VAR_RESULT) == NO)
		{
			call(CutePokemon_Dialogue_CheckItemSpace)
		}

	call(CutePokemon_Script_SelectOptionCheckPokemon)
	call(CutePokemon_Script_HalaiCheck)
	closemessage
	release
	end
}

script CutePokemon_Script_HalaiCheck
{
    buffermovename(STR_VAR_1,MOVE_ACID_SPRAY)
	switch(var(VAR_TEMP_0))
	{
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_0:
			msgbox(format("Dude, did you even read the Pokédex entry?"),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_1:
			msgbox(format("While this Pokémon is cute and can breed with dragons, the Pokémon I had in mind can learn the move {STR_VAR_1}."),,SPEAKER_MELISSA)
		case VAR_CUTE_POKEMON_SCRAFTY_HINT_2:
			msgbox(format("Oh, I forgot about this one. The cute Pokémon I had in mind has a mohawk. Or looks like it has a mohawk…? I don’t know."),,SPEAKER_MELISSA)
		default: goto(CutePokemon_Script_HalaiAsk)
	}
	return
}

script CutePokemon_Script_HalaiAsk
{
	special(BufferMonNickname)
	askplayermon(format("That’s it! Thank you for finding that Pokémon! You saved me hella time. Are you sure I can have {STR_VAR_1}?"),CutePokemon_Text_CanYouBringOne,CutePokemon_Text_AwwThatsSweet,SPEAKER_MELISSA)

	subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CUTEPOKEMON,SUB_QUEST_4)
	setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_CORRECT)
	msgbox(format("Hell yeah! Thanks for being a real one and helping me find all these Pokémon. Take this, I made this myself on break!"),,SPEAKER_MELISSA)
	closemessage
	cueobjectplayer(MELISSA_QIU_OUTSIDE)
	giveitem(ITEM_QUEST_CUTEPOKEMON_2,1)
	completequest(QUEST_CUTEPOKEMON)
    setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	release
	end
}

script CutePokemon_Dialogue_Rematch
{
	if(var(VAR_CUTE_POKEMON_GUESS) != VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	{
		msgbox(format("Hey {PLAYER}. Thanks again for helping me catch those cute Pokémon. Do you have time to battle?"),,SPEAKER_MELISSA)
		closemessage
		trainerbattle_no_intro(TRAINER_MELISSA_REMATCH,CutePokemon_Text_INeedToTrain)
		//PSF TODO need battle slide for every loss "Honestly? I’m surprised."
		setvar(VAR_CUTE_POKEMON_GUESS,VAR_CUTE_POKEMON_REMATCH_COMPLETE)
	}

	msgbox(format("Wanna grab some post battle boba?"),,SPEAKER_MELISSA)
	closemessage
	release
	end
}

text CutePokemon_Text_CanYouBringOne
{
    format("Aww… Well, can you bring me one I can keep?")
}
text CutePokemon_Text_AwwThatsSweet
{
    format("Aww, that’s sweet of you, but you don’t have any other Pokémon!")
}
text CutePokemon_Text_INeedToTrain
{
    format("gg. I need to train these guys more!")
}
text CutePokemon_Text_OkayBye
{
    format("Okay, bye.")
}

text CutePokemon_Text_YeahSure
{
    format("Yeah, sure!")
}

text CutePokemon_Text_YeahIGuess
{
    format("Yeah, I guess!")
}

text CutePokemon_Text_IHaveYourPokemon
{
    format("I have your Pokémon.")
}

text CutePokemon_Text_YeahIGuessElipsis
{
    format("Yeah, I guess…")
}

