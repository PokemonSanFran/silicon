//Quest_InstallNatureProbes_

const PLAYER = OBJ_EVENT_ID_PLAYER
const LIIDIA = 2

script Quest_InstallNatureProbes_Interact_Liidia_Script
{
	lock
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
	returnqueststate(QUEST_INSTALLNATUREPROBES)

	switch (var(VAR_RESULT))
	{
		case QUEST_MENU_SET_ACTIVE: goto(Quest_InstallNatureProbes_Active_Dialogue)
		case QUEST_MENU_SET_REWARD: goto(Quest_InstallNatureProbes_Reward_Cutscene)
		default: goto(Quest_InstallNatureProbes_StartQuest_Dialogue)
	}
}

script Quest_InstallNatureProbes_StartQuest_Dialogue
{
	msgbox(format("Geez, I gotta work late tonight if I'm going to finish these reports and the install...\pActually, you're a Trainer! Trainers are exploring places and shit, right?"), MSGBOX_NPC,SPEAKER_DEFAULT)
	msgbox(format("Can you help me with something? I don't think it'll put you too far out of your way. I need to install three probes in LeaverraForest for tracking Pokémon movement. One in the deepest part of the forest, one up on the tallest hill, and one over by the lakeshore. Installing is really easy, you just clip them to the sign posts that we've already got set up there."), MSGBOX_NPC,SPEAKER_DEFAULT)
	msgbox(format("You've got a PokéDex? Great. I can upgrade it with the Resido Habitat Mode if you help me out."), MSGBOX_NPC,SPEAKER_DEFAULT)

	startquest(QUEST_INSTALLNATUREPROBES)
	release
	end
}

script Quest_InstallNatureProbes_Active_Dialogue
{
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
	msgbox(format("What probes did you already install in LeaverraForest?"),MSGBOX_NPC,SPEAKER_DEFAULT)

    if (var(VAR_TEMP_1) == 0){
        msgbox(format("One probe goes in the deepest part of the forest."),MSGBOX_NPC,SPEAKER_DEFAULT)
    }

    if (var(VAR_TEMP_2) == 0){
        msgbox(format("One probe goes on the tallest hill."),MSGBOX_NPC,SPEAKER_DEFAULT)
    }

    if (var(VAR_TEMP_3) == 0){
        msgbox(format("One probe goes by the lake shore."),MSGBOX_NPC,SPEAKER_DEFAULT)
    }
}

script Quest_InstallNatureProbes_InstallForestProbe_Script{
    lock

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_1)
    if(var(VAR_RESULT)){
        msgbox(format("There is a probe attached to the sign post."),MSGBOX_SIGN)
        release
        end
    }

    msgbox(format("Yeah, this seems to match the description... deepest part of the forest."),,SPEAKER_ALICIA)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_1)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(format("{PLAYER} installed the probe in the forest."))
    waitse
    waitmessage

    release
    return
}
script Quest_InstallNatureProbes_InstallHillProbe_Script{
    lock

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_2)
    if(var(VAR_RESULT)){
        msgbox(format("There is a probe attached to the sign post."),MSGBOX_SIGN)
        release
        end
    }
    msgbox(format("This is as good as a hill as I'm going to find, right?"),,SPEAKER_ALICIA)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_2)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(format("{PLAYER} installed the probe on the hill."))
    waitse
    waitmessage

    release
    end
}
script Quest_InstallNatureProbes_InstallShoreProbe_Script{
    lock
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_3)
    if(var(VAR_RESULT)){
        msgbox(format("There is a probe attached to the sign post."),MSGBOX_SIGN)
        release
        end
    }

    msgbox(format("Just gotta make sure its attached correctly..."),,SPEAKER_ALICIA)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_3)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(format("{PLAYER} installed the probe by the shore."))
    waitse
    waitmessage

    release
    end
}

script Quest_InstallNatureProbes_CheckCompletionRate_Script
{
    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_INSTALLNATUREPROBES)

    if (!var(VAR_RESULT)){
        release
        return
    }

	setvar(VAR_TEMP_0, 0)
	setvar(VAR_TEMP_1, 0)
	setvar(VAR_TEMP_2, 0)
	setvar(VAR_TEMP_3, 0)

	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_1)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_1, 1)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_2)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_2, 1)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_3)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_3, 1)
	}

	if (var(VAR_TEMP_0) == 3)
	{
		questmenu(QUEST_MENU_SET_REWARD, QUEST_INSTALLNATUREPROBES)
	}
}

script Quest_InstallNatureProbes_Reward_Cutscene
{
    msgbox(format("Wow! You got all of them in? Let's test that..."),MSGBOX_NPC,SPEAKER_DEFAULT)

    applymovement(LIIDIA,Common_Movement_FaceAwayPlayer)
    waitmovement(LIIDIA)

    playse(SE_PC_LOGIN)
    waitse

    applymovement(LIIDIA,Common_Movement_FacePlayer)
    waitmovement(LIIDIA)
    msgbox(format("The tracking is working! You did great, thank you so much. Could I see your Pokédex please? Oh, this isn't the Residomodel... Orre? Fascinating. It should work the same..."),MSGBOX_NPC,SPEAKER_DEFAULT)
    closemessage

    msgbox(format("Okay, done!"),MSGBOX_NPC,SPEAKER_DEFAULT)
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("{PLAYER}'s Pokédex was upgraded with Habitat Mode!"))
    waitmessage
    waitfanfare
    //PSF TODO find a different rewards for this app and update the text
    closemessage

    playse(SE_POKENAV_CALL)
    waitse

    msgbox(format("That's my boss, let me just answer that."),,SPEAKER_LIIDIA)
    applymovement(LIIDIA,Common_Movement_FaceAwayPlayer)
    waitmovement(LIIDIA)

    msgbox(format("Go for Liidia!"),,,PHONE_ON)
    closemessage
    delay(32)
    msgbox(format("...I understand."),,,PHONE_ON)
    closemessage

    playse(SE_POKENAV_HANG_UP)
    waitse
    applymovement(LIIDIA,Common_Movement_FacePlayer)
    waitmovement(LIIDIA)

    msgbox(format("He thanked me... and then told me that my job was redundant. He told I'm fired, and have to leave the premises immediately."),,SPEAKER_LIIDIA)
    msgbox(format("I realize now, I think you put me out of a job. How could I be so... naive?"),,SPEAKER_LIIDIA)
    closemessage

    applymovement(LIIDIA,Quest_InstallNatureProbes_LeaveStation_Movement)
    applymovement(PLAYER,Quest_InstallNatureProbes_WatchLiidiaLeave_Movement)

    waitmovement(LIIDIA)
    removeobject(LIIDIA)
    completequest(QUEST_INSTALLNATUREPROBES)
    release
    end
}

movement Quest_InstallNatureProbes_LeaveStation_Movement{
    walk_right
    walk_down * 7
    step_end
}
movement Quest_InstallNatureProbes_WatchLiidiaLeave_Movement{
    delay_16
    face_right
    face_down
    step_end
}
