const PLAYER = OBJ_EVENT_ID_PLAYER
const CHARLOTTE = 1
const ARMANDO = 2

//gText_Armandostraining_
//Quest_Armandostraining_

script Quest_Armandostraining_StartQuest_Script{
        msgbox(format("Okay, let's take a crack at these..."),,SPEAKER_CHARLOTTE)
        closemessage
        applymovement(CHARLOTTE,Quest_Armandostraining_WalkToRobots)
        fadescreen(FADE_TO_BLACK)
        startquest(QUEST_ARMANDOSTRAINING)
        call(EnterTheMaster_RepositionCharlotte_Script)
        fadescreen(FADE_FROM_BLACK)
        end
}

script Quest_Armandostraining_Interact_Charlotte_Script{
    lock
    msgbox(format("Okay, what if I send out this Pokémon first..."),,SPEAKER_CHARLOTTE,TAIL_WHISPER)
    release
    end
}

script Quest_Armandostraining_GiveMons_Script{
    //special(SavePlayerParty)
    return
}

script Quest_Armandostraining_ChooseMons_Script{
    //choose_mons
    //special(ReducePlayerPartyToSelectedMons)
    return
}

script Quest_Armandostraining_Battle_Setup_Script{
    call(Quest_Armandostraining_GiveMons_Script)
    call(Quest_Armandostraining_ChooseMons_Script)
    return
}

script Quest_Armandostraining_Interact_Robot_Script{
    lock
    faceplayer
    msgbox(format("Hello {PLAYER}. Which scenario would you like to run?"))

    dynmultichoice(1,1,TRUE,4,0,NULL,
        "Scenario 1",
        "Scenario 2",
        "Scenario 3",
        "Scenario 4",
        "Never mind.",
    )

    switch (var(VAR_RESULT)){
        case 0:goto(Quest_Armandostraining_Battle_Robot1_Script)
        case 1:goto(Quest_Armandostraining_Battle_Robot2_Script)
        case 2:goto(Quest_Armandostraining_Battle_Robot3_Script)
        case 3:goto(Quest_Armandostraining_Battle_Robot4_Script)
    }
    release
    end
}

script Quest_Armandostraining_CheckSubquest_Script{
    if (!var(VAR_RESULT)){
        return
    }

    msgbox(format("You've already completed this puzzle. Are you going to try again?"),MSGBOX_YESNO,SPEAKER_OLIVER)

    if (var(VAR_RESULT) == NO){
        goto(Quest_Armandostraining_Interact_Robot_Script)
    }
    return
}

script Quest_Armandostraining_Battle_Robot1_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_1)
    call(Quest_Armandostraining_CheckSubquest_Script)

    setvar(VAR_TEMP_0,1)
    call(Quest_Armandostraining_Battle_Setup_Script)
    trainerbattle_continue_after_lose(TRAINER_ARMANDO_ROBOT_1,gText_Armandostraining_BeepBoop)
    msgbox(format("Excellent work!"))
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARMANDOSTRAINING,SUB_QUEST_1)
    call(Quest_Quest_Armandostraining_CheckReward_Script)
    release
    end
}

script Quest_Armandostraining_Battle_Robot2_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_2)
    call(Quest_Armandostraining_CheckSubquest_Script)

    setvar(VAR_TEMP_0,2)
    call(Quest_Armandostraining_Battle_Setup_Script)
    trainerbattle_continue_after_lose(TRAINER_ARMANDO_ROBOT_2,gText_Armandostraining_BeepBoop)
    msgbox(format("Excellent work!"))
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARMANDOSTRAINING,SUB_QUEST_2)
    call(Quest_Quest_Armandostraining_CheckReward_Script)
    release
    end
}

script Quest_Armandostraining_Battle_Robot3_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_3)
    call(Quest_Armandostraining_CheckSubquest_Script)

    setvar(VAR_TEMP_0,3)
    call(Quest_Armandostraining_Battle_Setup_Script)
    trainerbattle_continue_after_lose(TRAINER_ARMANDO_ROBOT_3,gText_Armandostraining_BeepBoop)
    msgbox(format("Excellent work!"))
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARMANDOSTRAINING,SUB_QUEST_3)
    call(Quest_Quest_Armandostraining_CheckReward_Script)
    release
    end
}

script Quest_Armandostraining_Battle_Robot4_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_4)
    call(Quest_Armandostraining_CheckSubquest_Script)

    setvar(VAR_TEMP_0,4)
    call(Quest_Armandostraining_Battle_Setup_Script)
    trainerbattle_continue_after_lose(TRAINER_ARMANDO_ROBOT_4,gText_Armandostraining_BeepBoop)
    msgbox(format("Excellent work!"))
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARMANDOSTRAINING,SUB_QUEST_4)
    call(Quest_Quest_Armandostraining_CheckReward_Script)
    release
    end
}

script Quest_Quest_Armandostraining_CheckReward_Script{
    setvar(VAR_TEMP_0,0)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_1)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_2)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_3)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARMANDOSTRAINING,SUB_QUEST_4)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    if (var(VAR_TEMP_0) == QUEST_ARMANDOSTRAINING_SUB_COUNT){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_ARMANDOSTRAINING)
        goto(Quest_ArmandoTraining_CompleteQuest_Script)
    }
    return
}

script Quest_Armandostraining_Interact_CharlotteRobot_Script{
    lock
    msgbox(format("This robot is for Charlotte."),MSGBOX_AUTOCLOSE)
    release
    end
}

script Quest_Armandostraining_SetUpCompletion_Script{
    fadescreen(FADE_TO_BLACK)
    setobjectxy(CHARLOTTE,9,16)
    setobjectxyperm(CHARLOTTE,9,16)
    setobjectxy(PLAYER,10,16)
    applymovement(PLAYER,Common_Movement_FaceUp)
    TeleportCamera(10,16)
    fadescreen(FADE_FROM_BLACK)
    return
}

script Quest_ArmandoTraining_CompleteQuest_Script{
    call(Quest_Armandostraining_SetUpCompletion_Script)
    setvar(VAR_LEAVERRA_FOREST_STATE,FINISHED_ARMANDO_PUZZLES)
    applymovement(ARMANDO,Common_Movement_FaceDown)
    waitmovement(ARMANDO)
    msgbox(format("Well, it's been quite a week. Impressed you were both able to clear those puzzles so quickly."),,SPEAKER_ARMANDO)
    msgbox(format("A week?!?"),,SPEAKER_CHARLOTTE,TAIL_SHOUT,EMOTE_SHOCK)
    msgbox(format("It feels like maybe a day at most!"),,SPEAKER_ALICIA,,EMOTE_SHOCK)
    msgbox(format("Yeah, the forest has that effect on people..."),,SPEAKER_ARMANDO)
    completequest(QUEST_ARMANDOSTRAINING)
    msgbox(format("Either way, good job. Go ahead and rest -"),,SPEAKER_ARMANDO)
    closemessage
    end
}
text gText_Armandostraining_OkayLetsACrack{
    format("Okay, let's take a crack at these...")
}
text gText_Armandostraining_OkayWhatIfISendOut{
    format("Okay, what if I send out this Pokémon first...")
}
text gText_Armandostraining_RobotQuestion{
    format("Hello {PLAYER}. Which scenario would you like to run?")
}
text gText_Armandostraining_ExcellentWorkPlayer{
    format("Excellent work!")
}
text gText_Armandostraining_ThisRobotIsFor{
    format("This robot is for Charlotte.")
}
text gText_Armandostraining_BeepBoop{
    format("Beep boop.")
}
text gText_Armandostraining_WellItsBeenQuite_Script{
    format("Well, it's been quite a week. Impressed you were both able to clear those puzzles so quickly.")
}
text gText_Armandostraining_AWeek{
    format("A week?!?")
}
text gText_Armandostraining_ItFeelsLike{
    format("It feels like maybe a day at most!")
}
text gText_Armandostraining_YeahTheForest{
    format("Yeah, the forest has that effect on people...")
}
text gText_Armandostraining_EitherWay{
    format("Either way, good job. Go ahead and rest -")
}

text gText_Armandostraining_Youvealready{
    format("You've already completed this puzzle. Are you going to try again?")
}

movement Quest_Armandostraining_WalkToRobots{
    walk_up
    walk_left * 5
    walk_up * 6
    step_end
}

movement Quest_Armandostraining_WalkAway{
    walk_down * 2
    step_end
}
