const PLAYER = OBJ_EVENT_ID_PLAYER
//PSF TODO
//make cutscene of ocean floor with a sparkle
//make a bullshit maze
//put a shiny floatzel at the end of maze
//give HM for Dive

//Quest_Allthatglittersmightbegold_
//gText_Allthatglittersmightbegold_

script Quest_Allthatglittersmightbegold_StartSparkle_Script{
    returnqueststate(QUEST_ALLTHATGLITTERSMIGHTBEGOLD)

        if (var(VAR_RESULT) != QUEST_MENU_COMPLETE_QUEST){
            dofieldeffectsparkle(45,66,0)
        }
    return
}

script Quest_Allthatglittersmightbegold_Interact_Elior_Script{
    lock
        faceplayer
        returnqueststate(QUEST_ALLTHATGLITTERSMIGHTBEGOLD)

        switch (var(VAR_RESULT)){
            case QUEST_MENU_SET_ACTIVE:
                goto(Quest_Allthatglittersmightbegold_Active_Dialogue)
            case QUEST_MENU_COMPLETE_QUEST:
                    goto(Quest_Allthatglittersmightbegold_Completed_Dialogue)
            case QUEST_MENU_SET_REWARD:
                        goto(Quest_Allthatglittersmightbegold_Reward_Dialogue)
            default:
                            goto(Quest_Allthatglittersmightbegold_StartQuest_Dialogue)
        }
}

script Quest_Allthatglittersmightbegold_StartQuest_Dialogue{
    msgbox(format("Hey there, young one. You look like you're up for a challenge! My boat here and I, we go fishing out in the Bay every weekend. My newphew got me one of them fancy underside camera for my boat, and I was watching it and... well come look!"),MSGBOX_NPC,SPEAKER_ELIOR,,)
        closemessage
        goto(Quest_Allthatglittersmightbegold_BeforeVideo_Script)
}

script Quest_Allthatglittersmightbegold_BeforeVideo_Script{
    setflag(FLAG_QUEST_ALLTHATGLITTERS_DEFEATED_POKEMON)
        setflag(FLAG_SPAWN_INVISIBLE)
        setflag(FLAG_HIDE_MAP_NAME_POPUP)
        setvar(VAR_QUEST_GLITTERS_GOLD_STATE,NOT_SHOWN_PICTURE)
        fadeblackandsetremovewarpfadeflag
        warpsilent(MAP_ROUTE_D_UNDERWATER,45,66)
}

script Quest_Allthatglittersmightbegold_ShowVideo_Dialogue{
    setvar(VAR_QUEST_GLITTERS_GOLD_STATE,SHOWN_PICTURE)
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
        setvar(VAR_TEMP_1,0)
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
        delay(30)
        msgbox(format("See that there sparkle? You know there used to be a big old gold rush around these parts. I betcha that's there some gold!"),,SPEAKER_ELIOR,,EMOTE_SHOCK)
        closemessage
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
        clearflag(FLAG_QUEST_ALLTHATGLITTERS_DEFEATED_POKEMON)
        clearflag(FLAG_SPAWN_INVISIBLE)
        delay(30)
        warpsilent(MAP_ROUTE_D,40,61)
}

script Quest_Allthatglittersmightbegold_AfterVideo_Dialogue{
    setvar(VAR_QUEST_GLITTERS_GOLD_STATE,QUEST_ASSIGNED)
        clearflag(FLAG_HIDE_MAP_NAME_POPUP)
        applymovement(PLAYER,Common_Movement_FaceUp)
        msgbox(format("I'm too old to go on adventures anymore, but I'll cut you a deal. I'll lend you my diving equipment, and if you go down and get it for me, we'll split it... you keep 70%!"),,SPEAKER_ELIOR,,)
        startquest(QUEST_ALLTHATGLITTERSMIGHTBEGOLD)
        msgbox(format("Holla at me when you're ready to head down."),,SPEAKER_ELIOR,,)
        release
        end
}

script Quest_Allthatglittersmightbegold_Active_Dialogue{
    msgbox(format("You heading down there?"),MSGBOX_YESNO,SPEAKER_ELIOR,,)
    msgbox(format("Okay! I put a beacon down there for you, just talk to it when you're ready to come back, and I'll pull ya up."),,SPEAKER_ELIOR,,)
    closemessage

        warpsilent(MAP_ROUTE_D_UNDERWATER,42,19)
}


script Quest_Allthatglittersmightbegold_CoreEncounter_Script{
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
    startmenu_savescreen
        faceplayer
        setflag(FLAG_FORCE_SHINY)
        setwildbattle(SPECIES_FLOATZEL, 60, ITEM_NONE)
        playmoncry(SPECIES_FLOATZEL, CRY_MODE_ENCOUNTER)
        waitmoncry
        playse(SE_SHINY)
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
        waitse
        delay(40)
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        closemessage
        dowildbattle
        clearflag(FLAG_FORCE_SHINY)
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch (var (VAR_RESULT)){
            case B_OUTCOME_CAUGHT:
                setvar(VAR_QUEST_GLITTERS_GOLD_STATE,CAUGHT_SHINY)
            case B_OUTCOME_WON:
                    setvar(VAR_QUEST_GLITTERS_GOLD_STATE,DEFEATED_SHINY)
                        call(Quest_Allthatglittersmightbegold_RemoveFloatzel_Script)
        }
    release
        end
}

script Quest_Allthatglittersmightbegold_RemoveFloatzel_Script{
        call(Quest_Allthatglittersmightbegold_StartSparkle_Script)
    fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        msgbox(format("Wait, was that the sparkle? Let me go tell Elior..."),,SPEAKER_ALICIA,TAIL_THOUGHT,EMOTE_SHOCK)
        questmenu(QUEST_MENU_SET_REWARD,QUEST_ALLTHATGLITTERSMIGHTBEGOLD)
        closemessage
        return
}

script Quest_Allthatglittersmightbegold_ReturnSurface_Script{
    msgbox(format("Do you want to return to Elior on RouteD?"),MSGBOX_YESNO)
        closemessage
        if (var(VAR_RESULT) == YES){
            setvar(VAR_QUEST_GLITTERS_GOLD_STATE,POST_DIVE)
            warpsilent(MAP_ROUTE_D,40,61)
        }
}

script Quest_Allthatglittersmightbegold_CheckSuccess_Script{
    lock
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(PLAYER)
    setvar(VAR_QUEST_GLITTERS_GOLD_STATE,QUEST_ASSIGNED)
    goto_if_quest_reward(QUEST_ALLTHATGLITTERSMIGHTBEGOLD,Quest_Allthatglittersmightbegold_Reward_Dialogue)
    goto(Quest_Allthatglittersmightbegold_EmptyHanded_Dialogue)
}

script Quest_Allthatglittersmightbegold_EmptyHanded_Dialogue{
    msgbox(format("Couldn't find it yet? No matter, I'm sure its down there somewhere."),,SPEAKER_ELIOR,,EMOTE_SWEAT)
    closemessage
    end
}

script Quest_Allthatglittersmightbegold_Reward_Dialogue{
    msgbox(format("You got the treasure?!? Let me see! ...Oh. It was just a Pokémon? "),,SPEAKER_ELIOR,,EMOTE_HAPPY)
        if (var(VAR_QUEST_GLITTERS_GOLD_STATE) == CAUGHT_SHINY){
            msgbox(format("At least you caught it, I hope its strong as it is sparkly!"),,SPEAKER_ELIOR,,)
        }else{
            msgbox(format("A shame you didn't catch it!"),,SPEAKER_ELIOR,,EMOTE_SAD)
        }
    msgbox(format("I was thinking while you were down there, I'm too old for that diving nonsense. Let me teach you something..."),,SPEAKER_ELIOR,,)
        call(Quest_Allthatglittersmightbegold_GiveReward_Script)
        setflag(FLAG_UNLOCKED_DIVE)
        completequest(QUEST_ALLTHATGLITTERSMIGHTBEGOLD)
        closemessage
        end
}

script Quest_Allthatglittersmightbegold_GiveReward_Script{
    playfanfare(MUS_OBTAIN_ITEM)
        msgbox(format("Elior taught your Pokémon how to Dive!"))
        additem(ITEM_HM08)
        waitmessage
        waitfanfare
        msgbox(format("Okay! If you have a Pokémon with you that can learn Dive, you can explore the ocean floor. Maybe you'll find treasure after all!"),,SPEAKER_ELIOR,,)
}

script Quest_Allthatglittersmightbegold_Completed_Dialogue{
    msgbox(format("If you find something truly amazing down there, let me know about it! I'll trade you more gear for a fun story."),MSGBOX_NPC,SPEAKER_ELIOR,,EMOTE_HAPPY)
}
