//PSF TODO Implement actual item effect for ITEM_SHERBET_SURPRISE
//PSF TODO Modify additem script to check for berries and mark this quest as complete if player has the berries

const PLAYER = OBJ_EVENT_ID_PLAYER
const CASHIER = 1
const PATRON = 2
const POKEMON = 3

script Quest_SmoothieCrafting_HidePatron_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_SMOOTHIECRAFTING)
        if (var(VAR_RESULT)){
            setflag(FLAG_TEMP_1)
        }
}

script Quest_SmoothieCrafting_Event_Script{
    msgbox(format("Welcome to the Marble Slab!"),MSGBOX_NPC)

    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_SMOOTHIECRAFTING)
    if (var(VAR_RESULT)){
        goto(Chasilla_IceCreamShop_Cashier_Event_Script)
    }

    call(Quest_SmoothieCrafting_CheckBerries_Script)
    goto_if_quest_reward(QUEST_SMOOTHIECRAFTING,Quest_SmoothieCrafting_TakeBerries_Dialogue)
    goto_if_quest_active(QUEST_SMOOTHIECRAFTING,Quest_SmoothieCrafting_GiveRecipe_Dialogue)
}

script Quest_SmoothieCrafting_Debug_Script{
    additem(ITEM_PECHA_BERRY,3)
    additem(ITEM_NANAB_BERRY,2)
    additem(ITEM_RABUTA_BERRY,1)
    msgbox(format("now you have all the items you need."))
//PSF TODO remove when ready
    return
}

script Quest_SmoothieCrafting_GiveRecipe_Dialogue{
        call(Quest_SmoothieCrafting_Debug_Script)
        msgbox(format("We're actually not serving customers yet, today is just our soft open! If you want to try our signature Sherbet Surprise, bring in 3 Pecha Berries, 2 Nanab Berries, and 1 Rabuta Berry!"),MSGBOX_NPC)
        end
}

script Quest_SmoothieCrafting_TakeBerries_Dialogue{
        lock
        msgbox(format("Sick, you brought the berries! Okay, I'll make this.... and here you go!"),MSGBOX_NPC)
        closemessage

        call(Quest_SmoothieCrafting_TakeBerries_Script)
        giveitem(ITEM_SHERBET_SURPRISE,6)

        applymovement(PATRON, moves(walk_faster_up, walk_fast_left * 4, face_up, step_end))
        waitmovement(PATRON)
        applymovement(CASHIER, moves(lock_facing_direction, walk_right, step_end))
        waitmovement(CASHIER)

        msgbox(format("What the hell are you serving us? What is going on?"))
        msgbox(format("What do you mean? Did your Pokémon not like their treat?"))
        msgbox(format("My Pokémon got weaker after eating it! What gives?"))
        msgbox(format("Yeah! We clean out your Pokémon bodies so you can toughen them up the way they should be! That's what our ad said, right?"))
        msgbox(format("...I can't believe this shit! I wanted to toughen my team up! Ugh!"))
        closemessage

        applymovement(PATRON, moves(walk_right * 4, walk_down, step_end))
        waitmovement(PATRON)
        call(Quest_SmoothieCrafting_RemovePokemon_Script)
        applymovement(PATRON, moves(walk_down * 3, walk_left * 4, face_down, step_end))
        waitmovement(PATRON)
        playse(SE_EXIT)
        removeobject(PATRON)
        applymovement(CASHIER, moves(lock_facing_direction, walk_left, step_end))
        waitmovement(CASHIER)
        release
        completequest(QUEST_SMOOTHIECRAFTING)
        call_if_quest_not_active(QUEST_BREAKTHEINTERNET,Quest_Breaktheinternet_CountQuestsAndProgress_Script)
        end
}

script Quest_SmoothieCrafting_RemovePokemon_Script{
    playse(SE_BALL_OPEN)
        waitse
        setflag(FLAG_TEMP_1)
        removeobject(POKEMON)
}

script Quest_SmoothieCrafting_TakeBerries_Script{
    removeitem(ITEM_PECHA_BERRY,3)
    removeitem(ITEM_NANAB_BERRY,2)
    removeitem(ITEM_RABUTA_BERRY,1)
}

script Quest_SmoothieCrafting_CheckBerries_Script{
    specialvar(VAR_RESULT,Quest_SmoothieCrafting_CheckNeededItems)
        if (var(VAR_RESULT)){
            questmenu(QUEST_MENU_SET_REWARD,QUEST_SMOOTHIECRAFTING)
        }
    return
}

