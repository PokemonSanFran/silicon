//Quest_ContractorPorygon_

script Quest_ContractorPorygon_Debug_Script{
    addvar(VAR_TEMP_0,1)
    additem(ITEM_LAVA_COOKIE,1)
    additem(ITEM_RAGE_CANDY_BAR,1)
    additem(ITEM_PEWTER_CRUNCHIES,1)
    end
}

script Quest_ContractorPorygon_StartQuest_Dialogue{
    lock
    faceplayer
    msgbox(format("hey we're a startup. the league asked us to replicate the work of Cinnabar Pokemon Lab and create Porygon... and its evolutions... in one work"))
    msgbox(format("its a tough task. we haven't eaten or slept in about 90 hours"))
    msgbox(format("when we do get hungry, we're not gonna have time to get food. could you swing by later and grab food for us? we'll tell you what we want."))
    closemessage
    startquest(QUEST_CONTRACTORPORYGON)
    release
    end
}
script Quest_ContractorPorygon_Subquest1_Ineligible_Dialogue{
    msgbox(format("want to get out this up-grade.... and i'm so close! after this block of code, I'll eat..."),MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)

    end
}
script Quest_ContractorPorygon_Subquest2_Ineligible_Dialogue{
    msgbox(format("they want me to replicate the dubious disc? that's why its DUBIOUS!"),MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)

    end
}
script Quest_ContractorPorygon_Subquest3_Ineligible_Dialogue{
    msgbox(format("not really hungry right now... maybe later though. you're probably wondering why we couldn't just order delievery, right? well, we're testing the Porygon on our phones! can't use them for anything else while the tests are running, haha."),MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)

    end
}
script Quest_ContractorPorygon_Subquest1_Request_Dialogue{
    lock
    faceplayer
    msgbox(format("okay, good stopping point!"))
    msgbox(format("could i get some dumplings? dumpling king in the qiuvillage is the best spot for them."))

    checkitem(ITEM_PEWTER_CRUNCHIES,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest1_Thanks_Dialogue)
    }
    closemessage

    release
    end
}
script Quest_ContractorPorygon_Subquest2_Request_Dialogue{
    lock
    faceplayer
    msgbox(format("i think the disc is done...."))
    msgbox(format("i hear you were getting us food, is that still a thing? Yeah? can I get a salad from Sweetgreeen? It's in Chasilla."))

    checkitem(ITEM_LAVA_COOKIE,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest2_Thanks_Dialogue)
    }
    closemessage

    release
    end
}
script Quest_ContractorPorygon_Subquest3_Request_Dialogue{
    lock
    faceplayer
    msgbox(format("the progress they're making is fucking insane! are the prototypes working well?"))
    msgbox(format("i didn't realize how hungry i was gonna be"))

    checkitem(ITEM_RAGE_CANDY_BAR,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Reward_Dialogue)
    }
    closemessage

    release
    end
}

script Quest_ContractorPorygon_GivePorygon_Script{

    switch(var(VAR_LAST_TALKED)){
        case 2:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON)
            givemon(SPECIES_PORYGON,1,ITEM_EVIOLITE ,ITEM_POKE_BALL ,NATURE_MODEST,1,0,0,0,0,0,0,,,,,,,MOVE_TRI_ATTACK,MOVE_NONE,MOVE_NONE,MOVE_NONE)
        case 1:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON_Z)
            givemon(SPECIES_PORYGON_Z,47,ITEM_DUBIOUS_DISC ,ITEM_POKE_BALL ,NATURE_BOLD,0,132,0,252,72,52,0,31,31,31,31,31,31,MOVE_NASTY_PLOT,MOVE_HYPER_BEAM,MOVE_EERIE_IMPULSE,MOVE_RECOVER)
        case 3:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON2)
            givemon(SPECIES_PORYGON2,26,ITEM_UPGRADE ,ITEM_POKE_BALL ,NATURE_QUIET,1,252,0,160,0,16,80,,,,,,,MOVE_TRICK_ROOM,MOVE_NONE,MOVE_NONE,MOVE_NONE)
    }

        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(format("You recieved a {STR_VAR_2}!"))
        waitmessage
        waitfanfare
}

script Quest_ContractorPorygon_Subquest1_Thanks_Dialogue{
    lock
    faceplayer
    msgbox(format("score! salad. thank you so much! i just finished this, give is a try."))
    closemessage
    call(Quest_ContractorPorygon_GivePorygon_Script)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)
    release
    end
}
script Quest_ContractorPorygon_Subquest2_Thanks_Dialogue{
    lock
    faceplayer
    msgbox(format("yesssss dumplings this is gonna be so good. here, take this Pokemon."))
    closemessage
    call(Quest_ContractorPorygon_GivePorygon_Script)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    release
    end
}
script Quest_ContractorPorygon_Subquest1_Script{
    if (var(VAR_STORYLINE_STATE) < STORY_FERRY_FIXED){
        goto(Quest_ContractorPorygon_Subquest1_Ineligible_Dialogue)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)

    if (!var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest1_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Focused_Dialogue)
    }
}
script Quest_ContractorPorygon_Subquest2_Script{
    if (var(VAR_STORYLINE_STATE) < STORY_EAST_RESIDO_COMPLETE){
        goto(Quest_ContractorPorygon_Subquest2_Ineligible_Dialogue)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)

    if (!var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest2_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Focused_Dialogue)
    }
}
script Quest_ContractorPorygon_Subquest3_Script{

    setvar(VAR_TEMP_2,0)

    if (var(VAR_STORYLINE_STATE) > STORY_ASSIGNED_FRANK_QUESTS){
        addvar(VAR_TEMP_2,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)
        if (var(VAR_RESULT)){
            addvar(VAR_TEMP_2,1)
        }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)
        if (var(VAR_RESULT)){
            addvar(VAR_TEMP_2,1)
        }

    if (var(VAR_TEMP_2) == 3){
        goto(Quest_ContractorPorygon_Subquest3_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Subquest3_Ineligible_Dialogue)
    }
}

script Quest_ContractorPorygon_HandleMoney_Script{
	showmoneybox(0, 0)
    waitbuttonpress
    addmoney(200000)
    playse(SE_SHOP)
    updatemoneybox(0)
    waitse
    waitbuttonpress
    hidemoneybox
    closemessage
    return
}

script Quest_ContractorPorygon_Reward_Dialogue{
    msgbox(format("hellllll yes curry! UGH this shit is gonna slap."))
    msgbox(format("This cash should cover the cost of lunch, right??"))
    call(Quest_ContractorPorygon_HandleMoney_Script)
    msgbox(format("And for helping us, you can have this too. You're a beta tester!"))
    call(Quest_ContractorPorygon_GivePorygon_Script)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_3)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    completequest(QUEST_CONTRACTORPORYGON)
    release
    end
}
script Quest_ContractorPorygon_Completed_Dialogue{
    msgbox(format("I think we're gonna make the deadline!"),MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    end
}

script Quest_ContractorPorygon_Focused_Dialogue{
    lock
    msgbox(format("Hmm....."))
    closemessage
    release
    end
}
script Quest_ContractorPorygon_Interact_Shan_Script{
	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest3_Script)
	        case QUEST_MENU_COMPLETE_QUEST: goto(Quest_ContractorPorygon_Completed_Dialogue)
	        default: goto(Quest_ContractorPorygon_StartQuest_Dialogue)
	}
}
script Quest_ContractorPorygon_Interact_Evan_Script{
    	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest1_Script)
	        default: goto(Quest_ContractorPorygon_Focused_Dialogue)
	}
}
script Quest_ContractorPorygon_Interact_Nathan_Script{
    	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest2_Script)
	        default: goto(Quest_ContractorPorygon_Focused_Dialogue)
	}
}
