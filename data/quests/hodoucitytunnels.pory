const PLAYER = OBJ_EVENT_ID_PLAYER
const FLAG_HIDE_TREASURE = FLAG_TEMP_1

// PSF TODO it should be "obvious" that there is more tunnel behind the treasure where player can go to get encounters

script Quest_Hodoucitytunnels_Script_TryHideTreasure
{
    checkitem(ITEM_QUEST_HODOUTUNNELS_TREASURE,1)
    if (var(VAR_RESULT))
    {
        goto(Quest_Hodoucitytunnels_Script_HideTreasure)
    }

    goto_if_quest_reward(QUEST_HODOUTUNNELS,Quest_Hodoucitytunnels_Script_HideTreasure)
    goto_if_quest_complete(QUEST_HODOUTUNNELS,Quest_Hodoucitytunnels_Script_HideTreasure
)
}

script Quest_Hodoucitytunnels_Script_HideTreasure
{
    setflag(FLAG_HIDE_TREASURE)
}

script Quest_HodouCityTunnels_Event_Script
{
    lock
    faceplayer
    goto_if_quest_complete(QUEST_HODOUTUNNELS,Quest_Hodoucitytunnels_Dialogue_Complete)
    goto_if_quest_reward(QUEST_HODOUTUNNELS,Quest_Hodoucitytunnels_Dialogue_Reward)
    goto_if_quest_active(QUEST_HODOUTUNNELS,Quest_Hodoucitytunnels_Dialogue_Active)

    msgbox(format("I've lived a long life, but there are still some things that continue to haunt me. Did you know that I served in the war 50 years ago? While I was wounded during my service, the military accidentally reported me as missing in action!"),,SPEAKER_TUNNELSPERSON)
    msgbox(format("When I came back from duty, my home had been sold. The government offered a pittance in compensation for the mistake, but no amount of money could replace the treasures of that house. I'd give anything to have it back."),,SPEAKER_TUNNELSPERSON)
    closemessage
    releaseall
    end
}

script Quest_Hodoucitytunnels_Dialogue_Active
{
    callnative(Quest_Hodoutunnels_GetVariable_TalkedToElder_Script)
    if (var(VAR_RESULT))
    {
        goto(Quest_Hodoucitytunnels_Dialogue_ActiveContinue)
    }

    msgbox(format("Ah, so you've heard the buzz! Are you interested in the treasure or the super strong Pokémon?"),,SPEAKER_TUNNELSPERSON)
    msgbox(format("Both!"),,SPEAKER_ALICIA)
    msgbox(format("Age has caught up with me and I can't retrieve it myself. But if you find it I'll gladly share the reward with you!"),,SPEAKER_TUNNELSPERSON)
    goto(Quest_Hodoucitytunnels_Dialogue_ActiveContinue)
}
script Quest_Hodoucitytunnels_Dialogue_ActiveContinue
{
    callnative(Quest_Hodoutunnels_GetVariable_TalkedToElder_Script)
    if (var(VAR_RESULT))
    {
        checkitem(ITEM_QUEST_HODOUTUNNELS_TREASURE,1)
        if (var(VAR_RESULT))
        {
            goto(Quest_Hodoucitytunnels_Dialogue_GiveTreasure)
        }
    }

    msgbox(format("The treasure is protected by a red scroll, kept in a building in the city. I hope it's still untouched..."),,SPEAKER_TUNNELSPERSON)

    callnative(Quest_Hodoutunnels_GetVariable_TalkedToElder_Script)
    if (var(VAR_RESULT) == FALSE)
    {
        checkitem(ITEM_QUEST_HODOUTUNNELS_TREASURE,1)
        if (var(VAR_RESULT))
        {
            goto(Quest_Hodoucitytunnels_Script_SecretEnding)
        }
    }
    callnative(Quest_Hodoutunnels_SetVariable_TalkedToElder)

    closemessage
    release
    end
}

script Quest_Hodoucitytunnels_Dialogue_GiveTreasure
{
    removeitem(ITEM_QUEST_HODOUTUNNELS_TREASURE,1)
    msgbox(format("I guess I found it, but I need a key."),,SPEAKER_ALICIA)
    closemessage
    cueobjectplayer(LOCALID_TUNNELSPERSON)
    msgbox(format("Tunnelsperson used a key to open the box."))
    msgbox(format("You had the key the entire time?"),,SPEAKER_ALICIA,TAIL_SHOUT,EMOTE_CONFUSE)
    msgbox(format("I must apologize for misleading you."),,SPEAKER_TUNNELSPERSON)
    msgbox(format("That place... it was once my home. I used to retreat to that crawl space for peace and quiet, and to keep my valuables safe."),,SPEAKER_TUNNELSPERSON)
    msgbox(format(" I was sent off to the war 50 years ago, and when I returned the government had seized and sold my home. I thought my treasure was lost forever!"),,SPEAKER_TUNNELSPERSON)

    msgbox(format("I tried to explain the situation to the cultists that had taken over, but of course they wouldn't listen to me. There was nobody strong enough to help me… until one of the local kids posted that zap for me."),,SPEAKER_TUNNELSPERSON)
    msgbox(format("I tried to come here and explain the situation, but those cultists had completely taken over and there was nobody strong enough to help me… until you!"),,SPEAKER_TUNNELSPERSON)
    msgbox(format("My family is gone, and these photos are all I have left."),,SPEAKER_TUNNELSPERSON)
    msgbox(format("Thank you, from the bottom of my heart."),,SPEAKER_TUNNELSPERSON)
    questmenu(QUEST_MENU_SET_REWARD,QUEST_HODOUTUNNELS)
    goto(Quest_Hodoucitytunnels_Dialogue_Reward)
}

script Quest_Hodoucitytunnels_Dialogue_Reward
{
    checkitemspace(ITEM_QUEST_HODOUTUNNELS_REWARD,1)
    if (var(VAR_RESULT) == FALSE)
    {
        msgbox(format("Your bag is full. Come see me once you have space."),,SPEAKER_TUNNELSPERSON)
        closemessage
        release
        end
    }
    msgbox(format("Please, take this as a token of my gratitude. It's the least I can do."),,SPEAKER_TUNNELSPERSON)
    closemessage
    cueobjectplayer(LOCALID_TUNNELSPERSON)
    giveitem(ITEM_QUEST_HODOUTUNNELS_REWARD,1)
    completequest(QUEST_HODOUTUNNELS)
    call_if_quest_not_active(QUEST_BREAKTHEINTERNET,Quest_Breaktheinternet_CountQuestsAndProgress_Script)
    release
    end
}

script Quest_Hodoucitytunnels_Dialogue_Complete
{
    msgbox(format("When I first tried to get back into the house I caught a glimpse of my old wall scroll over their shoulder. I was amazed they kept it!"),,SPEAKER_TUNNELSPERSON)
    msgbox(format("There is a poem written on it. Would you like to hear it?"),MSGBOX_YESNO,SPEAKER_TUNNELSPERSON)

    if (var(VAR_RESULT) == NO)
    {
        msgbox(format("Being a Trainer is a full time job!"),,SPEAKER_TUNNELSPERSON)
        closemessage
        release
        end
    }

    msgbox(format("She jumps off the swing, lazily stretching her slender hands."),,SPEAKER_TUNNELSPERSON)
    msgbox(format("The dew is heavy on the thin flower branch, a light sweat seeps through her shirt."),,SPEAKER_TUNNELSPERSON)
    msgbox(format("Seeing a guest, she runs away in her socks, her golden hair pin slipping off. "),,SPEAKER_TUNNELSPERSON)
    msgbox(format("She leaves in embarrassment, yet pauses at the door to look back, and sniffs the green plums."),,SPEAKER_TUNNELSPERSON)
        closemessage
        release
        end
}

script Quest_Hodoucitytunnels_Script_SecretEnding
{
    removeitem(ITEM_QUEST_HODOUTUNNELS_TREASURE,1)
    bufferitemname(STR_VAR_1,ITEM_QUEST_HODOUTUNNELS_TREASURE)
    msgbox(format("Red scroll... is this {STR_VAR_1} yours?"),,SPEAKER_ALICIA)
    closemessage
    cueobjecttalking(PLAYER)
    msgbox(format("Yes! My photos! Oh, I never thought I'd see these again. Thank you so much!"),,SPEAKER_TUNNELSPERSON,TAIL_SHOUT,EMOTE_SHOCK)
    closemessage
    questmenu(QUEST_MENU_SET_REWARD,QUEST_HODOUTUNNELS)
    goto(Quest_Hodoucitytunnels_Dialogue_Reward)
}
