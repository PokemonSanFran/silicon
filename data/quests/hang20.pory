const PLAYER = OBJ_EVENT_ID_PLAYER
const KAUNA = 6
const FLAG_HIDE_KAUNA = FLAG_TEMP_6
//Quest_Hang20_

script Quest_Hang20_ResetCount_Script
{
    setvar(VAR_DEFEATED_CRESALTA_VISTA_COUNT,0)
    return
}

script Quest_Hang20_HideLeader_Script
{
	if (var(VAR_KAUNA_STATE) != BATTLE_1_COMPLETE)
	{
		setflag(FLAG_HIDE_KAUNA)
	}
}

script Quest_Hang20_Debug_Script
{
    addvar(VAR_TEMP_0,1)
    setvar(VAR_DEFEATED_CRESALTA_VISTA_COUNT,47)
    //PSF TODO this count needs to reset if the user
    //changes the pokemon in their party
    //uses a healing item
    callnative(CountDefeatedCresaltaVista)
    release
}

script Quest_Hang20_Interact_Script
{
    lock
	faceplayer
	returnqueststate(QUEST_HANG20)

	switch(var(VAR_RESULT))
	{
		case QUEST_MENU_SET_ACTIVE: goto(Quest_Hang20_Active_Dialogue)
		case QUEST_MENU_SET_REWARD: goto(Quest_Hang20_Reward_Dialogue)
		default: goto(Quest_Hang20_StartQuest_Dialogue)
	}
}

script Quest_Hang20_CheckPreCompletion
{
    if (var(VAR_DEFEATED_CRESALTA_VISTA_COUNT) > 29){
        goto(Quest_Hang20_Secret_Dialogue)
    }
    return
}

script Quest_Hang20_StartQuest_Dialogue
{

    call(Quest_Hang20_CheckPreCompletion)

    msgbox(format("Yoooo {PLAYER}, right? You're not Resido born, right?"))

    msgbox(format("Yeah, I'm from Orre."))

    msgbox(format("Oh damn, that's land locked, right?"))

    msgbox(format("We're touching the sea, but not anything you can surf in, unfortunately."))

    msgbox(format("No waves? Damn that sucks!"))
    msgbox(format("You tryna learn how to ride the waves?"))

    msgbox(format("Oh, I've never really given it a shot. Is it hard?"))

    msgbox(format("It's tough, so we gotta make sure you're ready."))
    msgbox(format("Wait, beating you doesn't make me tough enough?"))
    msgbox(format("I'll teach you to Surf if you can defeat 30 Pokémon here in a row. No healing items, no accessing the PC, no leaving CresaltaVista."))

    closemessage
    startquest(QUEST_HANG20)
    release
    end
}

script Quest_Hang20_Active_Dialogue
{
	if (var(VAR_DEFEATED_CRESALTA_VISTA_COUNT) > 28)
	{
		msgbox(format("Keep that energy!"),MSGBOX_NPC)
	}
	elif (var(VAR_DEFEATED_CRESALTA_VISTA_COUNT) > 20)
	{
		msgbox(format("Maybe you're tough after all!"),MSGBOX_NPC)
	}
	elif (var(VAR_DEFEATED_CRESALTA_VISTA_COUNT) > 10)
	{
		msgbox(format("I saw you kicking ass out there!"),MSGBOX_NPC)
	}
	else
	{
		msgbox(format("I'll teach you to Surf if you can defeat 30 Pokémon here in a row. No healing items, no accessing the PC, no leaving CresaltaVista."),MSGBOX_NPC)
	}
	end
}

script Quest_Hang20_Reward_Dialogue
{
    msgbox(format("Okay, beating me was no fluke, you're the real deal. Take this!"))
    giveitem(ITEM_HM03,1)

    msgbox(format("If you have a Pokémon in your party that can learn Surf, you can just walk onto the waves and handle it, no problem. Watch!"))
    closemessage
    applymovement(KAUNA,Quest_Hang20_SurfAway_Movement)
    completequest(QUEST_HANG20)
    waitmovement(KAUNA)
    removeobject(KAUNA)
    release
    end
}

script Quest_Hang20_Secret_Dialogue
{
    msgbox(format("Hey, you're {PLAYER}, right? Killer waves, right? Not as good as back home, but still sick."))
    msgbox(format("Here to catch some waves?"))
    msgbox(format("I'm from Orre! We're landlocked, so I never learned to Surf."))
    msgbox(format("Word? It's mad easy to learn though, hold on..."))
    giveitem(ITEM_HM03,1)
    msgbox(format("If you've got a Pokémon that can learn Surf, you can just walk onto the waves now, no problem! Check it!"))
    closemessage
    applymovement(KAUNA,Quest_Hang20_SurfAway_Movement)
    waitmovement(KAUNA)
    removeobject(KAUNA)
    questmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_HANG20)
    release
    end
}

movement Quest_Hang20_SurfAway_Movement
{
    walk_up * 3
    walk_right * 5
    walk_up * 3
    step_end
}

