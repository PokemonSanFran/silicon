//Quest_ArtisanBalls1_
//Quest_ArtisanBalls2_
//Quest_ArtisanBalls3_
const PLAYER = OBJ_EVENT_ID_PLAYER
const TRAINER_A = 1
const TRAINER_B = 1
const TRAINER_C = 2
const SILPH = 2
const SERGEY = 1

script Quest_ArtisanBalls1_DebugA_Script{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_1)
    msgbox("you caught a pokemon in a fast ball.")
    release
    end
}
script Quest_ArtisanBalls1_DebugB_Script{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_1)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_2)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_3)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_4)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_5)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_6)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS1,SUB_QUEST_7)
    questmenu(QUEST_MENU_SET_REWARD,QUEST_ARTISANBALLS1)
    msgbox(format("you caught 7 pokemon in 7 different balls"))
    release
    end
}
script Quest_ArtisanBalls2_DebugA_Script{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS2,SUB_QUEST_1)
    msgbox(format("you recruited one person"))
    release
    end
}
script Quest_ArtisanBalls2_DebugB_Script{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS2,SUB_QUEST_1)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS2,SUB_QUEST_2)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_ARTISANBALLS2,SUB_QUEST_3)
    call(Quest_ArtisanBalls2_CheckReward_Script)
    msgbox(format("you recruited all the people"))
        fadeblackandsetremovewarpfadeflag
    warp(MAP_TORA_TOWN,4,16)
    release
    end
}
script Quest_ArtisanBalls3_DebugA_Script{
    questmenu(QUEST_MENU_SET_REWARD,QUEST_ARTISANBALLS3)
    msgbox(format("you defeated a gym leader while you had a ball"))
    release
    end
}

script Quest_ArtisanBalls3_HideSilph_Script {
	questmenu(QUEST_MENU_CHECK_REWARD, QUEST_ARTISANBALLS3)
	if (!var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_4)
	}
}

script Quest_ArtisanBalls2_HideEmployees_Script {
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_1)
	if (!var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_1)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_2)
	if (!var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_2)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_3)
	if (!var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_3)
	}

	questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS3)
	if (var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_1)
		setflag(FLAG_TEMP_2)
		setflag(FLAG_TEMP_3)
	}
}

script Quest_ArtisanBalls1_CheckStatus_Script {
	returnqueststate(QUEST_ARTISANBALLS1)

	switch (var(VAR_RESULT))
	{
		case QUEST_MENU_SET_ACTIVE:
			goto(Quest_ArtisanBalls1_Active_Dialogue)
		case QUEST_MENU_SET_REWARD:
			goto(Quest_ArtisanBalls1_Reward_Dialogue)
		default:
			goto(Quest_ArtisanBalls1_Start_Dialogue)
	}
}

script Quest_ArtisanBalls1_Start_Dialogue {
	msgbox(format("I make craft PokéBalls. I'm tired of working for somebody else, and want to open a shop selling my Balls! Would you be willing to test my PokeBalls?"), MSGBOX_NPC,SPEAKER_SERGEY)
	call(Quest_ArtisanBalls1_GiveFirstSet)
	startquest(QUEST_ARTISANBALLS1)
    msgbox(format("That set has one of each: Fast, Level, Lure, Love, Friend and a Moon Ball. The books explain how to best use them. Come back if you need more PokeBalls."),MSGBOX_NPC,SPEAKER_SERGEY)
    end
}

script Quest_ArtisanBalls1_GiveFirstSet {
	additem(ITEM_FAST_BALL, 1)
	additem(ITEM_LEVEL_BALL, 1)
	additem(ITEM_LURE_BALL, 1)
	additem(ITEM_HEAVY_BALL, 1)
	additem(ITEM_LOVE_BALL, 1)
	additem(ITEM_FRIEND_BALL, 1)
	additem(ITEM_MOON_BALL, 1)
	playfanfare(MUS_OBTAIN_ITEM)
	msgbox(format("{PLAYER} obtained a set of craft PokéBalls!"))
	waitmessage
	waitfanfare
	return
}


script Quest_ArtisanBalls1_Active_Dialogue {

    msgbox(format("How is progress going?"), MSGBOX_NPC,SPEAKER_SERGEY)
    call(Quest_ArtisanBalls1_During_ReplenishBalls_Script)

    if (var(VAR_TEMP_0) > 0)
    {
        msgbox(format("Just made those today. Let me know how they do!"), MSGBOX_NPC,SPEAKER_SERGEY)
        end
    }
    msgbox(format("Those balls can be tricky to use! You gotta make sure you're picking the right targets."), MSGBOX_NPC,SPEAKER_SERGEY)
    end
    }

script Quest_ArtisanBalls1_During_ReplenishBalls_Script {

    setvar(VAR_TEMP_0,0)
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_1)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_FAST_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_FAST_BALL, 1)
			addvar(VAR_TEMP_0, 1)

		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_2)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_LEVEL_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_LEVEL_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_3)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_LURE_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_LURE_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_4)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_HEAVY_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_HEAVY_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_5)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_LOVE_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_LOVE_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_6)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_FRIEND_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_FRIEND_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS1, SUB_QUEST_7)
	if (!var(VAR_RESULT))
	{
		checkitem(ITEM_MOON_BALL, 1)
		if (!var(VAR_RESULT)) {
			giveitem(ITEM_MOON_BALL, 1)
			addvar(VAR_TEMP_0, 1)
		}
	}
	return
}

script Quest_ArtisanBalls1_Reward_Dialogue {
	msgbox(format("So they all work well? Fantastic. Thank you so much for your help! Time to start reaching out and really selling them. Take this as a thanks!"), MSGBOX_NPC,SPEAKER_SERGEY)
	call(Quest_ArtisanBalls1_GiveReward_Script)
	completequest(QUEST_ARTISANBALLS1)
	end
}

script Quest_ArtisanBalls1_GiveReward_Script {
	giveitem(ITEM_CHERISH_BALL, 1)
    setvar(VAR_TEMP_5,1)
}

script Quest_ArtisanBalls2_HideTrainerFalse_Script{
	if (!var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_C)
	}
	return
}
script Quest_ArtisanBalls2_HideTrainerTrue_Script{
	if (var(VAR_RESULT))
	{
		setflag(FLAG_TEMP_C)
	}
	return
}

script Quest_ArtisanBalls2_CheckActiveAndHide_Script{
	questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_ARTISANBALLS2)
	call(Quest_ArtisanBalls2_HideTrainerFalse_Script)
	return
}


script Quest_ArtisanBalls2_CheckTrainerA_Script{
	call(Quest_ArtisanBalls2_CheckActiveAndHide_Script)
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_1)
	call(Quest_ArtisanBalls2_HideTrainerTrue_Script)
	return
}

script Quest_ArtisanBalls2_CheckTrainerB_Script{
	call(Quest_ArtisanBalls2_CheckActiveAndHide_Script)
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_2)
	call(Quest_ArtisanBalls2_HideTrainerTrue_Script)
	return
}

script Quest_ArtisanBalls2_CheckTrainerC_Script{
	call(Quest_ArtisanBalls2_CheckActiveAndHide_Script)
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_3)
	call(Quest_ArtisanBalls2_HideTrainerTrue_Script)
	return
}

script Quest_ArtisanBalls2_ShopQuestChoiceChoosePrompt_Script{
    if (var(VAR_RESULT)){
        msgbox(format("Hey {PLAYER}! Are you here to buy, or do you have an update?"),MSGBOX_YESNO)
    }else{
	msgbox(format("Hey {PLAYER}! Are you here to buy, or do you wanna hear the latest news with the business?"), MSGBOX_YESNO)
    }
    return
}


script Quest_ArtisanBalls2_ShopQuestChoice_Script {
	//PSF TODO  this should not be a yes or no question, it should be "I want to shop" or "I want to hear the latest"

    faceplayer
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_ARTISANBALLS2)
    call(Quest_ArtisanBalls2_ShopQuestChoiceChoosePrompt_Script)

	if (var(VAR_RESULT) == YES)
	{
		goto(Quest_ArtisanBalls2_GenerateDailyBall_Script)
	} else
	{
		goto(Quest_ArtisanBalls2_During_ProgressCheck_Script)
	}
}

script Quest_ArtisanBalls2_GenerateDailyBall_Script {
	random(6)

	//PSF TODO change this random to happen once daily
	copyvar(VAR_TEMP_1, VAR_RESULT)

	switch (var(VAR_RESULT))
	{
		case 0:
			bufferitemname(STR_VAR_3, ITEM_FAST_BALL)
		case 1:
			bufferitemname(STR_VAR_3, ITEM_LEVEL_BALL)
		case 2:
			bufferitemname(STR_VAR_3, ITEM_LURE_BALL)
		case 3:
			bufferitemname(STR_VAR_3, ITEM_HEAVY_BALL)
		case 4:
			bufferitemname(STR_VAR_3, ITEM_LOVE_BALL)
		case 5:
			bufferitemname(STR_VAR_3, ITEM_FRIEND_BALL)
		case 6:
			bufferitemname(STR_VAR_3, ITEM_MOON_BALL)
	}
    goto(Quest_ArtisanBalls2_AskRandomBall_Script)
}

script Quest_ArtisanBalls2_AskRandomBall_Script {
	showmoneybox(0, 0)
	msgbox(format("I made a small batch this morning. Want {STR_VAR_3} for ¥1300?"), MSGBOX_YESNO)

	if (var(VAR_RESULT) == YES)
	{
		call(Quest_ArtisanBalls2_BuyRandomBall_Script)
	} else
	{
		call(Quest_ArtisanBalls2_DeclineRandomBall_Script)
	}
	hidemoneybox
	release
	end
}

script Quest_ArtisanBalls2_BuyRandomBall_Script {
	removemoney(1300, 0)
	updatemoneybox(0)
	switch (var(VAR_TEMP_1))
	{
		case 0:
			giveitem(ITEM_FAST_BALL, 1)
		case 1:
			giveitem(ITEM_LEVEL_BALL, 1)
		case 2:
			giveitem(ITEM_LURE_BALL, 1)
		case 3:
			giveitem(ITEM_HEAVY_BALL, 1)
		case 4:
			giveitem(ITEM_LOVE_BALL, 1)
		case 5:
			giveitem(ITEM_FRIEND_BALL, 1)
		case 6:
			giveitem(ITEM_MOON_BALL, 1)
	}
	return
}

script Quest_ArtisanBalls2_DeclineRandomBall_Script {
	msgbox(format("I'll be here if you change your mind."), MSGBOX_NPC,SPEAKER_SERGEY)
	return
}

script Quest_ArtisanBalls2_During_ProgressCheck_Script {
	returnqueststate(QUEST_ARTISANBALLS2)
	switch (var(VAR_RESULT))
	{
		case QUEST_MENU_SET_ACTIVE:
			goto(Quest_ArtisanBalls2_Active_Dialogue)
		case QUEST_MENU_SET_REWARD:
			goto(Quest_ArtisanBalls2_Reward_Dialogue)
		default:
			goto(Quest_ArtisanBalls2_Start_Dialogue)
	}
}


script Quest_ArtisanBalls2_Start_Dialogue {
	msgbox(format("Things are going well, and I think I'm ready to expand."), MSGBOX_NPC,SPEAKER_SERGEY)

	msgbox(format("That's amazing! Congrats Sergey!"), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("I need help though. There's a lot of work to do, so I'm going to need to hire people... I think I need an office manager, customer service rep, and a marketing manger. Could you help me find some people? I want to hire people that really use and understand my products. There's gotta be Trainers out there that are using my Balls, right?"), MSGBOX_NPC,SPEAKER_SERGEY)
	startquest(QUEST_ARTISANBALLS2)
	end
}


script Quest_ArtisanBalls2_Active_Dialogue {
    setvar(VAR_TEMP_0,0)
    call(Quest_ArtisanBalls2_CheckReward_Script)

    if(var(VAR_TEMP_0) > 0){
        msgbox(format("Wow! People are slowly trickling in... I think we can actually pull this off!"),MSGBOX_NPC,SPEAKER_SERGEY)
    }else{
        msgbox(format("Finding help is tough, isn't it?"), MSGBOX_NPC,SPEAKER_SERGEY)
    }
    end
}

script Quest_ArtisanBalls2_TrainerA_Event_Script{
	trainerbattle_single(TRAINER_ARTISANBALLS2_A,format("You'd think office management and Pokémon management would be similar..."),format("Not even close!"), Quest_ArtisanBalls2_TrainerA_PostBattle_Script)
}

script Quest_ArtisanBalls2_TrainerA_PostBattle_Script{
	lock
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_ARTISANBALLS2, SUB_QUEST_1)
    call(Quest_ArtisanBalls2_CheckReward_Script)

	msgbox(format("Where did you get those PokéBalls?"), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("My girlfriend got them for me as a present! I guess there's some craft seller making them."), MSGBOX_NPC,SPEAKER_SERGEY)

	msgbox(format("You mentioned Office Management, right? The guy who made those balls is actually looking to hire somebody for that. You should talk to him, he's in ToraTown."), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("Wait, really? I absolutely need a new gig, and that sounds amazing! Thank you so much."), MSGBOX_NPC,SPEAKER_SERGEY)

	fadescreen(FADE_TO_BLACK)
	removeobject(TRAINER_A)
	fadescreen(FADE_FROM_BLACK)

	release
	end
}

script Quest_ArtisanBalls2_TrainerB_Event_Script{
	trainerbattle_single(TRAINER_ARTISANBALLS2_B, format("My personal brand is the most important thing!"), format("I hope gettind defeated doesn't hurt my brand..."), Quest_ArtisanBalls2_TrainerB_PostBattle_Script)
}

script Quest_ArtisanBalls2_TrainerB_PostBattle_Script{
	lock
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_ARTISANBALLS2, SUB_QUEST_2)
    call(Quest_ArtisanBalls2_CheckReward_Script)

	msgbox(format("Where did you get those PokéBalls?"), MSGBOX_NPC,SPEAKER_ALICIA)

	msgbox(format("I saw the collection on Buzzr and wanted to try them out. Why?"), MSGBOX_NPC,SPEAKER_SERGEY)

	msgbox(format("You said you were into branding, right? The guy who made those balls is actually looking to hire somebody for that. You should talk to him, he's in ToraTown."), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("Oh man, what? I gotta get over there right now!"), MSGBOX_NPC,SPEAKER_SERGEY)

	fadescreen(FADE_TO_BLACK)
	removeobject(TRAINER_B)
	fadescreen(FADE_FROM_BLACK)

	release
	end
}

script Quest_ArtisanBalls2_TrainerC_Event_Script{
	trainerbattle_single(TRAINER_ARTISANBALLS2_C, format("I miss helping people. Serving is my passion!"), format("Did I help you today?"), Quest_ArtisanBalls2_TrainerC_PostBattle_Script)
}

script Quest_ArtisanBalls2_TrainerC_PostBattle_Script{
	lock
	subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_ARTISANBALLS2, SUB_QUEST_3)
    call(Quest_ArtisanBalls2_CheckReward_Script)

	msgbox(format("Where did you get those PokéBalls?"), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("I met the vendor a local business trade show!"), MSGBOX_NPC,SPEAKER_SERGEY)

	msgbox(format("If you like helping people, the guy who made those Balls is looking for a customer service rep. You should go to ToraTown and talk to him!"), MSGBOX_NPC,SPEAKER_ALICIA)


	msgbox(format("That would be amazing! I'll head over there now."), MSGBOX_NPC,SPEAKER_SERGEY)

	fadescreen(FADE_TO_BLACK)
	removeobject(TRAINER_C)
	fadescreen(FADE_FROM_BLACK)

	release
	end
}

script Quest_ArtisanBalls2_CheckReward_Script{

	setvar(VAR_TEMP_0, 0)

	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_1)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
	}

	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_2)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
	}

	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_ARTISANBALLS2, SUB_QUEST_3)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
	}

	if (var(VAR_TEMP_0) == 3)
	{
		questmenu(QUEST_MENU_SET_REWARD, QUEST_ARTISANBALLS2)
	}
    return
}

script Quest_ArtisanBalls2_Reward_Dialogue {
    msgbox(format("I think I have the staff to finally expand out. It's all thanks to you. I was able to do a limited run of a new ball, and I want you to have some!"),MSGBOX_NPC,SPEAKER_SERGEY)
    call(Quest_ArtisanBalls2_GiveReward_Script)
    completequest(QUEST_ARTISANBALLS2)
    end
}

script Quest_ArtisanBalls2_GiveReward_Script {
    giveitem(ITEM_DREAM_BALL,5)
    setvar(VAR_TEMP_5,1)
}

script Quest_ArtisanBalls3_ShopQuestChoice_Script {
    faceplayer
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_ARTISANBALLS3)
    call(Quest_ArtisanBalls2_ShopQuestChoiceChoosePrompt_Script)

	if (var(VAR_RESULT) == YES)
	{
		goto(Quest_ArtisanBalls3_GenerateMart_Script)
	} else
	{
		goto(Quest_ArtisanBalls3_DuringProgressCheck_Script)
	}
}

script Quest_ArtisanBalls3_GenerateMart_Script {
    lock
    faceplayer
    msgbox(format("What PokéBalls do you need today?"))
	waitmessage
	pokemart(ArtisanBallsShop)
    msgbox(format("Thanks again!"),MSGBOX_NPC,SPEAKER_SERGEY)
	release
}

//PSF TODO set apriballs sell price to 0, but buy price is 1300

mart ArtisanBallsShop{
	ITEM_FAST_BALL
	ITEM_LEVEL_BALL
	ITEM_LURE_BALL
	ITEM_HEAVY_BALL
	ITEM_LOVE_BALL
	ITEM_FRIEND_BALL
	ITEM_MOON_BALL
}

script Quest_ArtisanBalls3_DuringProgressCheck_Script {
	returnqueststate(QUEST_ARTISANBALLS3)
	switch (var(VAR_RESULT))
	{
        case QUEST_MENU_SET_REWARD:
            goto(Quest_ArtisanBalls3_Reward_Dialogue)
		case QUEST_MENU_SET_ACTIVE:
			goto(Quest_ArtisanBalls3_Active_Dialogue)
		default:
			goto(Quest_ArtisanBalls3_Start_Dialogue)
	}
}

script Quest_ArtisanBalls3_Start_Dialogue {
    msgbox(format("Business has been crazy! The new staff really has been amazing, we've got a good product, and I'm doing what I love."),MSGBOX_NPC,SPEAKER_SERGEY)
    msgbox(format("I've been talking to some Silph Co. executives too. They want to acquire us! I want to see if we can do a big marketing push soon so I can keep showing them growth. Could you do me one last favor? Go defeat one of the Gym Leaders in Resido using a Pokémon in one of those Balls."),MSGBOX_NPC,SPEAKER_SERGEY)
    startquest(QUEST_ARTISANBALLS3)
    release
    end
}

//PSF TODO When battling any of the Gym Leaders, check if the player has a pokemon in their party with the riht ball. if they do AND they win the battle, mark ArtisanQuest3 as complete

script Quest_ArtisanBalls3_Active_Dialogue {
    msgbox(format("I can't wait to see your battle!"),MSGBOX_NPC,SPEAKER_SERGEY)
    end
}

script Quest_ArtisanBalls3_Reward_Dialogue {
    goto(Quest_ArtisanBalls3_DebugA_Script)
}

script Quest_ArtisanBalls3_Reward_Cutscene{
    addvar(VAR_TEMP_3,99)
   questmenu(QUEST_MENU_CHECK_REWARD,QUEST_ARTISANBALLS3)

    if (!var(VAR_RESULT)){
        release
        end
    }

    lock
    applymovement(SILPH,Quest_ArtisanBalls3_LeaveRoom_Movement)
    waitmovement(SILPH)
    playse(SE_EXIT)
    removeobject(SILPH)

    applymovement(SERGEY,Quest_ArtisanBalls3_RunToDoor_Movement)
    applymovement(PLAYER,Common_Movement_FaceLeft)
    waitmovement(SERGEY)

    msgbox(format("Did you hear that?!?!?"),MSGBOX_DEFAULT)
    msgbox(format("No, but it sounds like something good!"),MSGBOX_DEFAULT,SPEAKER_ALICIA)


    msgbox(format("They want to acquire us! We're getting a huge buyout, and we get to keep working at Silph as part of their new craft division! This was all thanks to you, {PLAYER}. I couldn't have asked for more. They gave us this to celebrate the acquisition, but I want you to have it."),MSGBOX_DEFAULT)
    call(Quest_ArtisanBalls3_GiveReward_Script)
    completequest(QUEST_ARTISANBALLS3)
    end
}

script Quest_ArtisanBalls3_GiveReward_Script {
    giveitem(ITEM_MASTER_BALL,1)
    setvar(VAR_TEMP_5,1)
}

script Quest_ArtisanBalls3_Post_Dialogue{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARTISANBALLS3)

    if(var(VAR_RESULT)){

    lock
    msgbox(format("The door is locked, but there is a sign on the door: This location has been closed. Please direct all questions to support at silph.co. Thank you for choosing Silph Co.!\pThere is another note below it. Do you want to read it?"),MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES){
    msgbox(format("To my loyal customers,\NSilph laid us all off. They bought my company, took the patents, and laid us all off. I feel so... used. Thank you for your support as we grew and thank you for enjoying our products. Special thanks to {PLAYER}, who always believed in us.\p- Sergey"),MSGBOX_SIGN)
    }

    }
    release
    end
}

script Quest_ArtisanBalls_BetweenStates_Dialogue {
    msgbox(format("I'm so excited for what will come next!"),MSGBOX_NPC,SPEAKER_SERGEY)
    end
}
script Quest_ArtisanBalls3_ChangeDoorMetatile_Script {
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_ARTISANBALLS3)

    if(var(VAR_RESULT)){
        setmetatile(4,15,0x014,TRUE)
    }

    return
}

script Quest_ArtisanBalls_ReadBook_Dialogue{
    msgbox(format("Which craft Poké Ball would you like to read about?"))
    dynmultipush("Level Ball", ITEM_LEVEL_BALL)
    dynmultipush("Lure Ball", ITEM_LURE_BALL)
    dynmultipush("Moon Ball", ITEM_MOON_BALL)
    dynmultipush("Friend Ball", ITEM_FRIEND_BALL)
    dynmultipush("Love Ball", ITEM_LOVE_BALL)
    dynmultipush("Fast Ball", ITEM_FAST_BALL)
    dynmultipush("Heavy Ball", ITEM_HEAVY_BALL)
    dynmultipush("Close the book.", ITEMS_COUNT)
    dynmultistack(0, 0, FALSE, 5, TRUE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)
    closemessage

    switch(var(VAR_RESULT)){
        case ITEM_LURE_BALL: msgbox(format("The Lure Ball works best in a battle that was initated by fishing."))
        case ITEM_FAST_BALL: msgbox(format("Pokémon that are naturally very fast will be more easily captured with the Fast Ball."))
        case ITEM_LEVEL_BALL: msgbox(format("The Level Ball is most successful when the user's Pokémon is a higher level than the wild Pokémon being captured."))
        case ITEM_HEAVY_BALL: msgbox(format("The Heavy Ball is less successful on light Pokémon and more successful on Heavy Pokémon!"))
        case ITEM_LOVE_BALL: msgbox(format("The Love Ball works best when the user's Pokémon is the opposite gender of the wild Pokémon being captured."))
        case ITEM_FRIEND_BALL: msgbox(format("Pokémon captured with the Friend Ball will initally be more friendly to the Trainer that captured it."))
        case ITEM_MOON_BALL: msgbox(format("If the Pokémon being captured is part of an evolutionary family that uses a Moon Stone to evolve, the Moon Ball will be more effective."))
        //PSF TODO Why doesn't pressing B work here?
        case MULTI_B_PRESSED:
        case ITEMS_COUNT: end
    }
    goto(Quest_ArtisanBalls_ReadBook_Dialogue)
}

text Quest_ArtisanBalls1_CraftBallList{
    "Level Ball$"
    "Lure Ball$"
    "Moon Ball$"
    "Friend Ball$"
    "Love Ball$"
    "Fast Ball$"
    "Heavy Ball$"
    "Cancel$"
    "$"
}

movement Quest_ArtisanBalls3_LeaveRoom_Movement{
    walk_down
    walk_right
    walk_down
    step_end
}
movement Quest_ArtisanBalls3_RunToDoor_Movement{
    walk_faster_down * 2
    walk_faster_right
    walk_faster_down
    face_right
    jump_in_place_right * 2
    step_end
}
